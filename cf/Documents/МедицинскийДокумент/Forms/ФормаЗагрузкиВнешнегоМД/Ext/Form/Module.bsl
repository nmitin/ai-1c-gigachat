#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	ВосстановитьСостояниеКнопокДинПоля();
	ОтборыСписковКлиентСервер.УстановитьЭлементОтбораСписка(ЭтотОбъект.ДинамическоеПолеМКБ10Избранное,"Владелец",Пользователи.ТекущийПользователь());
	
	ПоляОформления_ = "Наименование, Код";
	ОформленияВидовОбъектов.НастроитьУсловноеОформлениеСписка(
		ЭтотОбъект, 
		"ДинамическоеПолеМКБ10", 
		"ИспользуетсяВОМС",
		ПоляОформления_, 
		"РекомендуемыеЭлементы"
	);

	ОформленияВидовОбъектов.РазместитьЛегендуОформленияВФорме(
		ЭтотОбъект, 
		Элементы.ГруппаОформление, 
		"РекомендуемыеЭлементы"
	);
	
	Если ТипЗнч(ЭтотОбъект.Параметры.МедицинскаяКартаИлиПациент) = Тип("СправочникСсылка.МедицинскиеКарты") Тогда
		ЭтотОбъект.МедицинскаяКарта = ЭтотОбъект.Параметры.МедицинскаяКартаИлиПациент;
		ЭтотОбъект.Пациент = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ЭтотОбъект.МедицинскаяКарта, "Пациент");
	Иначе
		ЭтотОбъект.Пациент = ЭтотОбъект.Параметры.МедицинскаяКартаИлиПациент;
	КонецЕсли;
	
	ЭтотОбъект.ДатаМД = ТекущаяДатаСеанса();
	
	Элементы.НадписьПредупреждение.Видимость = Ложь;
	Элементы.ФормаЗагрузитьЕщеОдинДокумент.Видимость = Ложь;
	
	Если ЭтотОбъект.Параметры.Свойство("МедицинскийДокументОбъект") Тогда
		// Форма открывается в ФМД
		ОбщегоНазначенияФМДСервер.ОбработкаФормированияПриСозданииНаСервере(
			Отказ,
			ЭтотОбъект,
			"Подключаемый_ВыполнитьКомандуНавигации"
		);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	ПрименитьФильтр();
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ЗагрузитьДокумент(Команда)
	
	Если ЭтотОбъект.ПроверитьЗаполнение() Тогда

		Файл_ = Новый Файл(ЭтотОбъект.ПутьКФайлу);
		
		Если Файл_.Существует() Тогда
			ДД_ = Новый ДвоичныеДанные(ЭтотОбъект.ПутьКФайлу);
			
			СоздатьМедицинскийДокумент(ДД_, Файл_.ИмяБезРасширения, УбратьТочкуИзРасширения(Файл_.Расширение));
			ОповеститьОЗаписиНового(ЭтотОбъект.СозданныйМедицинскийДокумент);
			
			ЭтотОбъект.Модифицированность = Ложь;
		Иначе
			
			Сообщить("Файл не найден.");
		КонецЕсли;

	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура Выбрать(Команда)
	Если ЭтотОбъект.ТолькоПросмотр Тогда
		Возврат;
	КонецЕсли;
	
	Диагнозы_ = Новый Массив;
	Если Элементы.КомандаИзбранное.Пометка = Истина Тогда
		Для Каждого Строка_ Из Элементы.ДинамическоеПолеМКБ10Избранное.ВыделенныеСтроки Цикл
			Диагнозы_.Добавить(Элементы.ДинамическоеПолеМКБ10Избранное.ДанныеСтроки(Строка_).ЭлементМКБ10);
		КонецЦикла;
	Иначе
		Для Каждого Строка_ Из Элементы.ДинамическоеПолеМКБ.ВыделенныеСтроки Цикл
			Диагнозы_.Добавить(Строка_);
		КонецЦикла;
	КонецЕсли;
	
	Если Диагнозы_.Количество() > 0 Тогда
		ДобавитьДиагнозы(Диагнозы_);	
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьИерархию(Команда)
	КнопкаИерахия = Элементы.УстановитьИерархию;
	КнопкаИерахия.Пометка = НЕ КнопкаИерахия.Пометка;
	ЗапомнитьСостояниеКнопокДинПоля();
	ПрименитьФильтр();
КонецПроцедуры

&НаКлиенте
Процедура КомандаИзбранное(Команда)
	КнопкаИзбранное = Элементы.КомандаИзбранное;
	КнопкаИзбранное.Пометка = НЕ КнопкаИзбранное.Пометка;
	ЗапомнитьСостояниеКнопокДинПоля();
	ПрименитьФильтр();
КонецПроцедуры

&НаКлиенте
Процедура КомандаНастроитьИзбранное(Команда)
	П_ = Новый Структура;
	П_.Вставить("Отбор", Новый Структура("Владелец", ПользователиКлиент.ТекущийПользователь()));
	ОписаниеОповещения_ = Новый ОписаниеОповещения("ПослеНастройкиИзбранного", ЭтотОбъект);
	ОткрытьФорму("Справочник.МКБ10Избранное.Форма.ФормаСписка",
					П_,
					ЭтотОбъект,,,,
					ОписаниеОповещения_,
					РежимОткрытияОкнаФормы.БлокироватьОкноВладельца
				);
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОформленияВидовОбъектовОткрытие(Команда) Экспорт
	
	ОформленияВидовОбъектов.ОткрытьЛегендуОформления(ЭтотОбъект, Команда);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьЕщеОдинДокумент(Команда)
	ОчиститьПараметрыЗагружаемогоДокумента();
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ВыполнитьКомандуНавигации(Команда)
	
	ОбщегоНазначенияФМДКлиент.ВыполнитьКомандуПереходаНавигационнойПанели(ЭтотОбъект, Команда);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиЭлементовФормы

&НаКлиенте
Процедура ДинамическоеПолеМКБ10Выбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	Если ЭтотОбъект.ТолькоПросмотр = Ложь Тогда
		Если Элементы.КомандаИзбранное.Пометка = Истина Тогда
			ДобавитьДиагноз(Элементы.ДинамическоеПолеМКБ10Избранное.ДанныеСтроки(ВыбраннаяСтрока).ЭлементМКБ10);
		Иначе
			ДобавитьДиагноз(ВыбраннаяСтрока);
		КонецЕсли;
		
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ФильтрКодПриИзменении(Элемент)
	ЭтотОбъект.ФильтрКод = ОбщиеМеханизмыКлиентСервер.ЗаменитьСимволНаАнглийский(Элемент.ТекстРедактирования);
	ПрименитьФильтр();
КонецПроцедуры

&НаКлиенте
Процедура ФильтрНаименованиеПриИзменении(Элемент)
	ПрименитьФильтр();
КонецПроцедуры

&НаКлиенте
Процедура ПациентПриИзменении(Элемент)
	ЭтотОбъект.МедицинскаяКарта = Неопределено;
КонецПроцедуры

&НаКлиенте
Процедура МедицинскаяКартаПриИзменении(Элемент)
	Если ЗначениеЗаполнено(ЭтотОбъект.МедицинскаяКарта) Тогда
		МедицинскаяКартаПриИзмененииНаСервере();
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ПутьКФайлуНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)

	ПослеПодключенияРасширения_ = Новый ОписаниеОповещения("ВыборФайлаВнешнегоМДПродолжение", ЭтотОбъект);
	ФайловаяСистемаКлиент.ПодключитьРасширениеДляРаботыСФайлами(ПослеПодключенияРасширения_); 

КонецПроцедуры

&НаКлиенте
Процедура ПутьКФайлуПриИзменении(Элемент)

	ПроверитьФорматВыбранногоФайла();
	
КонецПроцедуры

&НаКлиенте
Процедура МедицинскаяКартаНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	
	ПараметрыФормы_ = Новый Структура();
	Если ЗначениеЗаполнено(ЭтотОбъект.Пациент) Тогда
		ПараметрыФормы_.Вставить("НачальноеЗначениеВыбора", ЭтотОбъект.Пациент);
	КонецЕсли;
	
	РегистратураКлиент.ВыбратьПациентаКарту(Неопределено, Элемент, Ложь, Истина, ПараметрыФормы_);
КонецПроцедуры

&НаКлиенте
Процедура ДиагнозыПоМКБ10ПриИзменении(Элемент)
	ЭтотОбъект.КоличествоДиагнозов = ЭтотОбъект.ДиагнозыПоМКБ10.Количество();
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура МедицинскаяКартаПриИзмененииНаСервере()
	ЭтотОбъект.Пациент = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ЭтотОбъект.МедицинскаяКарта, "Пациент");
КонецПроцедуры

&НаКлиенте
// Обработчик завершения подключения расширения работы с файлами.
Процедура ВыборФайлаВнешнегоМДПродолжение(РасширениеПодключено, ДополнительныеПараметры) Экспорт
	
	Если Не РасширениеПодключено Тогда
		ПоказатьПредупреждение(, НСтр("ru = 'Не установлено расширение для работы с 1С:Предприятием. Действие не доступно.'"));
		Возврат;
	КонецЕсли;

	Оповещение_ = Новый ОписаниеОповещения(
		"ВыборФайлаВнешнегоМДПродолжениеПослеЗакрытияДиалога", ЭтотОбъект
	);
	Диалог_ = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Открытие);
	Диалог_.МножественныйВыбор = Ложь;
	Диалог_.Заголовок = СообщенияПользователю.Получить("Общие_ВыборФайла");
	Диалог_.Фильтр = СообщенияПользователю.Получить("Общие_ВсеФайлыРасширение");
	Диалог_.Показать(Оповещение_);
	
КонецПроцедуры

&НаКлиенте
// Обработчик выбора файла внешнего МД.
Процедура ВыборФайлаВнешнегоМДПродолжениеПослеЗакрытияДиалога(ВыбранныеФайлы, ДополнительныеПараметры) Экспорт

	Если ТипЗнч(ВыбранныеФайлы) <> Тип("Массив") Или ВыбранныеФайлы.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	ЭтотОбъект.ПутьКФайлу = ВыбранныеФайлы[0];
	
	Если ЭтотОбъект.ТипВыбранногоФайла = ПредопределенноеЗначение("Перечисление.ФорматФайлаДляРЭМД.CDA") Тогда
		// Если до выбора нового файла поля были заполнены по данным CDA очистим их.
		ЭтотОбъект.ДатаМД = ТекущаяДата();
		ЭтотОбъект.ЗаголовокМД = Неопределено;
		ЭтотОбъект.ТипМД = Неопределено;
	КонецЕсли;
	
	ПроверитьФорматВыбранногоФайла();

КонецПроцедуры

&НаКлиенте
Процедура ПроверитьФорматВыбранногоФайла()
	
	ВидимостьПредупреждения_ = Ложь;
	
	Если ЗначениеЗаполнено(ЭтотОбъект.ПутьКФайлу) Тогда
	
		Файл_ = Новый Файл(ЭтотОбъект.ПутьКФайлу);
		
		Если Файл_.Существует() Тогда
		
			ЭтотОбъект.ТипВыбранногоФайла = ОпределитьТипФайла(Файл_);
			
			Если ЭтотОбъект.ТипВыбранногоФайла <> ПредопределенноеЗначение("Перечисление.ФорматФайлаДляРЭМД.PDF")
				И ЭтотОбъект.ТипВыбранногоФайла <> ПредопределенноеЗначение("Перечисление.ФорматФайлаДляРЭМД.CDA")
			Тогда
				ВидимостьПредупреждения_ = Истина;
			КонецЕсли;


			Если ЭтотОбъект.ТипВыбранногоФайла = ПредопределенноеЗначение("Перечисление.ФорматФайлаДляРЭМД.CDA") Тогда
				ЗаполнитьФормуДаннымиИзCDA();
			КонецЕсли;

			Если МедицинаРегион.ДоступенОбменСРегионом() Тогда
				
				ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "НадписьПредупреждение", "Видимость", ВидимостьПредупреждения_);

			КонецЕсли;

		Иначе
			
			Сообщить("Файл не найден.");
		КонецЕсли;
	КонецЕсли;
	
	
КонецПроцедуры

&НаСервере
Функция СоздатьМедицинскийДокумент(Знач ДвоичныеДанныеФайла, Знач ИмяФайлаБезРасширения, Знач РасширениеБезТочки)

	МедицинскийДокумент_ = Документы.МедицинскийДокумент.СоздатьДокумент();
	МедицинскийДокумент_.Дата = ТекущаяДатаСеанса();
	МедицинскийДокумент_.ДатаМД = ЭтотОбъект.ДатаМД;
	МедицинскийДокумент_.МедицинскаяКарта = ЭтотОбъект.МедицинскаяКарта;
	МедицинскийДокумент_.Пациент = ЭтотОбъект.Пациент;
	МедицинскийДокумент_.Ответственный = Пользователи.ТекущийПользователь();
	МедицинскийДокумент_.ПолученОтМО = ЭтотОбъект.ВнешняяОрганизация;
	МедицинскийДокумент_.Заголовок = ЭтотОбъект.ЗаголовокМД;
	МедицинскийДокумент_.УсловияОказанияПомощи = ОпределитьУсловияОказанияПомощи();
	МедицинскийДокумент_.ИзВнешнегоИсточника = Истина;
	
	Для Каждого СтрокаДиагноза_ Из ЭтотОбъект.ДиагнозыПоМКБ10 Цикл
		НоваяСтрокаТЧДиагнозов_ = МедицинскийДокумент_.ДиагнозыПоМКБ10.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрокаТЧДиагнозов_, СтрокаДиагноза_);
		НоваяСтрокаТЧДиагнозов_.Действует = Истина;
	КонецЦикла;
	
	МедицинскийДокумент_.ДополнительныеСвойства.Вставить("ЗаписатьДажеЕслиКартаЗакрыта", Истина);
	МедицинскийДокумент_.ДополнительныеСвойства.Вставить("СтатусТелаДокумента", ПредопределенноеЗначение("Перечисление.СтатусыМедицинскихДокументов.Готов"));
	МедицинскийДокумент_.ДополнительныеСвойства.Вставить("УслугиНеВыполнены", Ложь);
	МедицинскийДокумент_.ДополнительныеСвойства.Вставить("ОтключитьКонтрольНаличияВыполненныхУслуг", Истина);
	
	ФайлСсылка_ = Документы.МедицинскийДокумент.ПолучитьCDAДокумент(МедицинскийДокумент_, Истина,, ИмяФайлаБезРасширения);
	ФайлСсылка_ = Документы.МедицинскийДокумент.ЗаписатьТелоМД(ФайлСсылка_, ДвоичныеДанныеФайла,
						Документы.МедицинскийДокумент.ВыбратьВладельцаДляCDA(МедицинскийДокумент_, МедицинскийДокумент_.Пациент),
						,,
						ИмяФайлаБезРасширения,
						?(ЭтотОбъект.ТипВыбранногоФайла = Перечисления.ФорматФайлаДляРЭМД.CDA, "cda", РасширениеБезТочки)
					);
					
	Документы.МедицинскийДокумент.УстановитьCDAДокумент(МедицинскийДокумент_, ФайлСсылка_,,,, ЭтотОбъект.ТипМД);

	МедицинскийДокумент_.Записать(РежимЗаписиДокумента.Проведение);
	
	ЭтотОбъект.СозданныйМедицинскийДокумент = МедицинскийДокумент_.Ссылка;
	
	НоваяСтрокаТЗДокументов_ = ЭтотОбъект.ЗагруженныеДокументы.Добавить();
	НоваяСтрокаТЗДокументов_.СозданныйДокумент = МедицинскийДокумент_.Ссылка;
	НоваяСтрокаТЗДокументов_.ПутьКФайлу = ЭтотОбъект.ПутьКФайлу;
	
	ЭтотОбъект.КоличествоЗагруженныхДокументов = ЭтотОбъект.ЗагруженныеДокументы.Количество();
	
	ЭтотОбъект.Элементы.ФормаЗагрузитьДокумент.Доступность = Ложь;
	Элементы.ФормаЗагрузитьЕщеОдинДокумент.Видимость = Истина;
	
	
	Сообщить("Документ загружен.");
	
	Если МедицинаРегион.ДоступенОбменСРегионом() Тогда
		Если ЗначениеЗаполнено(ЭтотОбъект.ТипМД)
			И (ЭтотОбъект.ТипВыбранногоФайла = Перечисления.ФорматФайлаДляРЭМД.PDF
				Или ЭтотОбъект.ТипВыбранногоФайла = Перечисления.ФорматФайлаДляРЭМД.CDA)
		Тогда
			// Поместим в справочник ЭМД для отправки в региональную ИЭМК.
			ФедеральныеВебСервисыРЭМД.ПоместитьДокументВХранилищеЭМД(
				МедицинскийДокумент_.Пациент,
				ДвоичныеДанныеФайла,
				Новый Массив,
				Неопределено,
				Неопределено,
				ФайлСсылка_,
				ЭтотОбъект.ТипВыбранногоФайла,
				Неопределено
			);
		КонецЕсли;
	КонецЕсли;
	
КонецФункции

Функция ОпределитьУсловияОказанияПомощи()

	УсловияОказанияПомощи_ = Перечисления.УсловияМедицинскойПомощи.АмбулаторноПоликлиническая;
	
	Если ЗначениеЗаполнено(ЭтотОбъект.МедицинскаяКарта) Тогда
		УсловияОказанияПомощи_ = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ЭтотОбъект.МедицинскаяКарта, "ТипМедицинскойКарты.УсловияОказанияПомощи");
	КонецЕсли;
	
	Возврат УсловияОказанияПомощи_;
КонецФункции

&НаКлиенте
Функция УбратьТочкуИзРасширения(Расширение)
	
	Длина_ = стрДлина(Расширение);
	Если Длина_ > 0 Тогда
		Если Сред(Расширение, 1, 1) = "." Тогда
			Если Длина_ > 1 Тогда
				Возврат Прав(Расширение, Длина_ - 1);
			Иначе
				Возврат "";
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	Возврат Расширение;
	
КонецФункции

&НаКлиенте
Процедура ОчиститьПараметрыЗагружаемогоДокумента()

	Элементы.ФормаЗагрузитьЕщеОдинДокумент.Видимость = Ложь;
	ЭтотОбъект.Элементы.ФормаЗагрузитьДокумент.Доступность = Истина;
	Элементы.НадписьПредупреждение.Видимость = Ложь;
	ЭтотОбъект.Модифицированность = Ложь;
	
	ЭтотОбъект.ПутьКФайлу = Неопределено;
	ЭтотОбъект.ТипМД = Неопределено;
	ЭтотОбъект.ЗаголовокМД = Неопределено;
	ЭтотОбъект.ПутьКФайлу = Неопределено;
	ЭтотОбъект.СозданныйМедицинскийДокумент = Неопределено;
	ЭтотОбъект.ТипВыбранногоФайла = Неопределено;
	ЭтотОбъект.ДиагнозыПоМКБ10.Очистить();
	ЭтотОбъект.КоличествоДиагнозов = 0;
	
	Если ЭтотОбъект.ТипВыбранногоФайла = ПредопределенноеЗначение("Перечисление.ФорматФайлаДляРЭМД.CDA") Тогда
		// Если поле заполнено по данным CDA очистим его.
		ЭтотОбъект.ДатаМД = ТекущаяДата();
	КонецЕсли;
	
	ЭтотОбъект.ТипВыбранногоФайла = Неопределено;
	
КонецПроцедуры

&НаКлиенте
Функция ОпределитьТипФайла(Знач Файл)

	РасширениеИмениФайла = ВРег(Файл.Расширение);
	
	Если РасширениеИмениФайла = ".PDF" Тогда
		
		Возврат ПредопределенноеЗначение("Перечисление.ФорматФайлаДляРЭМД.PDF");
		
	ИначеЕсли РасширениеИмениФайла = ".XML" Или РасширениеИмениФайла = ".CDA" Тогда
	
		Попытка
			ТекстовыйДокумент_ = Новый ТекстовыйДокумент;
			ТекстовыйДокумент_.Прочитать(Файл.ПолноеИмя);
			ТекстCDA_ = ТекстовыйДокумент_.ПолучитьТекст();
		Исключение
			ТекстCDA_ = Неопределено;
		КонецПопытки;
		
		Если ТекстCDA_ <> Неопределено И СтрНайти(ТекстCDA_, "ClinicalDocument") > 0 И СтрНайти(ТекстCDA_, "urn:hl7-org:v3") > 0 Тогда
			Возврат ПредопределенноеЗначение("Перечисление.ФорматФайлаДляРЭМД.CDA");
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат Неопределено;
КонецФункции

&НаКлиенте
Процедура ЗаполнитьФормуДаннымиИзCDA()

	ТекстовыйДокумент_ = Новый ТекстовыйДокумент;
	ТекстовыйДокумент_.Прочитать(ЭтотОбъект.ПутьКФайлу, КодировкаТекста.UTF8);
	ТекстCDA_ = ТекстовыйДокумент_.ПолучитьТекст();

	ЗаполнитьФормуДаннымиИзCDAНаСервере(ТекстCDA_);

КонецПроцедуры

&НаСервере
Процедура ЗаполнитьФормуДаннымиИзCDAНаСервере(Знач ТекстCDA)

	ДанныеCDA_ = ФедеральныеВебСервисыРазборСЭМД.ПолучитьДанныеИзСЭМД(ТекстCDA, "Шапка");
	
	Если ЗначениеЗаполнено(ДанныеCDA_["КодТипаДокумента"]) И ЗначениеЗаполнено(ДанныеCDA_["КодСправочникаТипаДокумента"]) Тогда
		ЭтотОбъект.ТипМД = Справочники.СправочникиФНСИ.СсылкаНаОбъектМетаданныхПоСтруктуреКодированныхПолей(
			Новый Структура("code, codeSystem", ДанныеCDA_["КодТипаДокумента"], ДанныеCDA_["КодСправочникаТипаДокумента"]),
			Ложь
		);
		
		ЭтотОбъект.ЗаголовокМД = ДанныеCDA_["Заголовок"];
		
		Если ЗначениеЗаполнено(ДанныеCDA_["ДокументДата"]) Тогда
			ЭтотОбъект.ДатаМД = ДанныеCDA_["ДокументДата"];
		КонецЕсли;
	КонецЕсли;

КонецПроцедуры

#Область Диагнозы

&НаСервере
Процедура ДобавитьДиагнозы(Диагнозы)
	КопированиеДиагнозов_ = Ложь;
	Если ТипЗнч(Диагнозы) = Тип("ДанныеФормыКоллекция") Тогда
		КопированиеДиагнозов_ = Истина;
	КонецЕсли;
	
	Для Каждого Диагноз_ Из Диагнозы Цикл
		Если КопированиеДиагнозов_ Тогда
			ДобавитьДиагноз(Диагноз_.МКБ10, Диагноз_.СтепеньОбоснованностиДиагноза);
		Иначе
			ДобавитьДиагноз(Диагноз_);
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры

&НаСервере
Процедура ДобавитьДиагноз(МКБ10, СтепеньОбоснованностиДиагноза = Неопределено)

	Отбор_ = Новый Структура(
		"МКБ10", 
		МКБ10
	);
	
	ДанныеМКБ10_ = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(МКБ10, "Код,Наименование,ДополнительныйКод");
	
	Если ЭтотОбъект.ДиагнозыПоМКБ10.Количество() = 0
		И СтрНайти(ДанныеМКБ10_.Код, "*") = 0 И СтрНайти(ДанныеМКБ10_.ДополнительныйКод, "*") = 0
	Тогда
		ТипДиагноза_ = Перечисления.ТипыДиагнозовПациента.ОсновноеЗаболевание;
	Иначе
		ТипДиагноза_ = Перечисления.ТипыДиагнозовПациента.СопутствующееЗаболевание;
	КонецЕсли;
	
	Если Справочники.МКБ10.ДоступенДляВыбора(МКБ10, ТипДиагноза_,, Истина) = Ложь Тогда
		// не доступен
	ИначеЕсли ЭтотОбъект.ДиагнозыПоМКБ10.НайтиСтроки(Отбор_).Количество() = 0 Тогда
		НоваяЗапись_ = ЭтотОбъект.ДиагнозыПоМКБ10.Добавить();
		
		ЭтотОбъект.КоличествоДиагнозов = ЭтотОбъект.ДиагнозыПоМКБ10.Количество();
		
		НоваяЗапись_.МКБ10 = МКБ10;
		НоваяЗапись_.КлиническийДиагноз = ДанныеМКБ10_.Наименование;
		НоваяЗапись_.ТипДиагноза = ТипДиагноза_;
		
		Если Не СтепеньОбоснованностиДиагноза = Неопределено Тогда
			НоваяЗапись_.СтепеньОбоснованностиДиагноза = СтепеньОбоснованностиДиагноза;
		Иначе
			Если НЕ ДиагнозыПоМКБ10.Количество() = 1 Тогда
				Если ЭтотОбъект.ДиагнозыПоМКБ10.Количество() > 1 Тогда
					НоваяЗапись_.СтепеньОбоснованностиДиагноза = ЭтотОбъект.ДиагнозыПоМКБ10[ЭтотОбъект.ДиагнозыПоМКБ10.Количество() - 2].СтепеньОбоснованностиДиагноза;
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
		
		ЭтотОбъект.Модифицированность = Истина;
	Иначе
		Параметры_ = Новый Структура("Элемент", МКБ10);
		СообщенияПользователю.Показать("МКБ10_ЭлементУжеИмеетсяВСписке", Параметры_);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗапомнитьСостояниеКнопокДинПоля() 
	Настройки = Новый Структура;	
	КнопкаОИ = Элементы.КомандаИзбранное;
	Настройки.Вставить("КомандаИзбранное", КнопкаОИ.Пометка);
	КнопкаИерахия = Элементы.УстановитьИерархию;
	Настройки.Вставить("УстановитьИерархию", КнопкаИерахия.Пометка);
	ЗапомнитьСостояниеКнопокДинПоля_Сервер(Настройки);
КонецПроцедуры

////
 // Процедура: ЗапомнитьСостояниеКнопокДинПоля
 //   Предназначена для сохранения состояний кнопок в хранилище общих настроек
 //
 // Параметры:
 //   СтруктуаНастроек
 //     Структура с именем и значением настройки
 ///
&НаСервереБезКонтекста
Процедура ЗапомнитьСостояниеКнопокДинПоля_Сервер(СтуктураНастроек) 
	ХранилищеОбщихНастроек.Сохранить(
		"ДиагнозыПоМКБ10",
		,
		СтуктураНастроек
	);
КонецПроцедуры

////
 // Процедуа: ВосстановитьСостояниеКнопокДинПоля
 //   Предназначена для восстановления состояний кнопок из хранилища общих настроек
 ///
&НаСервере
Процедура ВосстановитьСостояниеКнопокДинПоля() 
	Настройки = ХранилищеОбщихНастроек.Загрузить("ДиагнозыПоМКБ10");
	Если НЕ Настройки = Неопределено Тогда
		Для Каждого Настройка Из Настройки Цикл
			Попытка
				КнопкаФормы = Элементы[Настройка.Ключ];
				КнопкаФормы.Пометка = Настройка.Значение;
			Исключение
			КонецПопытки;
		КонецЦикла;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ПрименитьФильтр()
	
	ТекстЗапроса_ = ПолучитьЗапрос();
	ПараметрыЗапросаСтруктуой_ = ПолучитьПараметрыЗапросаСтруктурой();
	УстановитьПараметрыЗапроса(ТекстЗапроса_, ПараметрыЗапросаСтруктуой_);
	УстановитьВидимостьСписков();
	Элементы.ДинамическоеПолеМКБ.Обновить();
	Элементы.ДинамическоеПолеМКБ10Избранное.Обновить();
КонецПроцедуры

&НаКлиенте
Функция ПолучитьЗапрос()
	
	ТолькоИзбранное_ = Элементы.КомандаИзбранное.Пометка;
	ОтображатьВИерархии_ = Элементы.УстановитьИерархию.Пометка;
	
	Если Не ТолькоИзбранное_ Тогда
		Если Не ОтображатьВИерархии_ Тогда
			Результат_ =
				"ВЫБРАТЬ
				|	СправочникМКБ.Ссылка КАК Ссылка,
				|	ЛОЖЬ КАК ЭтоГруппа,
				|	СправочникМКБ.Код КАК Код,
				|	СправочникМКБ.Наименование КАК Наименование,
				|	СправочникМКБ.ИспользуетсяВОМС КАК ИспользуетсяВОМС
				|ИЗ
				|	Справочник.МКБ10 КАК СправочникМКБ
				|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.МКБ10 КАК МКБ10Иерархия
				|		ПО СправочникМКБ.Ссылка = МКБ10Иерархия.Родитель
				|ГДЕ
				|	СправочникМКБ.ПометкаУдаления = ЛОЖЬ
				|	И СправочникМКБ.ВАрхиве = ЛОЖЬ
				|	И СправочникМКБ.Код <> """"
				|	И (&МассивМКБНеУказан
				|			ИЛИ СправочникМКБ.Ссылка В (&МассивНайденыхМКБ))
				|	И (&НеИспользоватьСписокИерархии
				|			ИЛИ СправочникМКБ.Ссылка В ИЕРАРХИИ (&СписокИерархии))
				|	И МКБ10Иерархия.Ссылка ЕСТЬ NULL"
			;
		Иначе
			Результат_ =
				"ВЫБРАТЬ
				|	СправочникМКБ.Ссылка КАК Ссылка,
				|	ЛОЖЬ КАК ЭтоГруппа,
				|	СправочникМКБ.Код КАК Код,
				|	СправочникМКБ.Наименование КАК Наименование,
				|	СправочникМКБ.ИспользуетсяВОМС КАК ИспользуетсяВОМС
				|ИЗ
				|	Справочник.МКБ10 КАК СправочникМКБ
				|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ (ВЫБРАТЬ РАЗЛИЧНЫЕ
				|			ИерархияМКБ10.ДиагнозыИГруппы КАК Ссылка
				|		ИЗ
				|			Справочник.МКБ10 КАК СправочникМКБ
				|				ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ИерархияМКБ10 КАК ИерархияМКБ10
				|				ПО (ИерархияМКБ10.Ссылка = СправочникМКБ.Ссылка)
				|					И (СправочникМКБ.ПометкаУдаления = ЛОЖЬ)
				|					И (СправочникМКБ.ВАрхиве = ЛОЖЬ)
				|					И (&МассивМКБНеУказан
				|						ИЛИ СправочникМКБ.Ссылка В (&МассивНайденыхМКБ))
				|					И (&НеИспользоватьСписокИерархии
				|						ИЛИ СправочникМКБ.Ссылка В ИЕРАРХИИ (&СписокИерархии))) КАК КонтрольИерархии
				|		ПО СправочникМКБ.Ссылка = КонтрольИерархии.Ссылка"
			;
		КонецЕсли;
	Иначе
		Если Не ОтображатьВИерархии_ Тогда
			Результат_ =
				"ВЫБРАТЬ
				|	СправочникМКБ.Ссылка КАК Ссылка,
				|	ЛОЖЬ КАК ЭтоГруппа,
				|	СправочникМКБ.Код КАК Код,
				|	СправочникМКБ.Наименование КАК Наименование,
				|	СправочникМКБ.ЭлементМКБ10 КАК ЭлементМКБ10
				|ИЗ
				|	Справочник.МКБ10Избранное КАК СправочникМКБ
				|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.МКБ10 КАК СправочникМКБ10
				|		ПО СправочникМКБ.ЭлементМКБ10 = СправочникМКБ10.Ссылка
				|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.МКБ10 КАК МКБ10Иерархия
				|		ПО (СправочникМКБ10.Ссылка = МКБ10Иерархия.Родитель)
				|ГДЕ
				|	СправочникМКБ.ПометкаУдаления = ЛОЖЬ
				|	И СправочникМКБ10.ПометкаУдаления = ЛОЖЬ
				|	И СправочникМКБ10.ВАрхиве = ЛОЖЬ
				|	И СправочникМКБ10.Код <> """"
				|	И (&МассивМКБНеУказан
				|			ИЛИ СправочникМКБ.ЭлементМКБ10 В (&МассивНайденыхМКБ))
				|	И (&НеИспользоватьСписокИерархии
				|			ИЛИ СправочникМКБ.Ссылка В ИЕРАРХИИ (&СписокИерархии))
				|	И МКБ10Иерархия.Ссылка ЕСТЬ NULL"
			;
		Иначе
			Результат_ =
				"ВЫБРАТЬ
				|	СправочникМКБ.Ссылка КАК Ссылка,
				|	ЛОЖЬ КАК ЭтоГруппа,
				|	СправочникМКБ.Код КАК Код,
				|	СправочникМКБ.Наименование КАК Наименование,
				|	СправочникМКБ.ЭлементМКБ10 КАК ЭлементМКБ10
				|ИЗ
				|	Справочник.МКБ10Избранное КАК СправочникМКБ
				|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.МКБ10 КАК СправочникМКБ10
				|		ПО СправочникМКБ.ЭлементМКБ10 = СправочникМКБ10.Ссылка
				|			И (СправочникМКБ10.ПометкаУдаления = ЛОЖЬ)
				|			И (СправочникМКБ10.ВАрхиве = ЛОЖЬ)
				|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ (ВЫБРАТЬ РАЗЛИЧНЫЕ
				|			ИерархияМКБ10.ДиагнозыИГруппы КАК Ссылка
				|		ИЗ
				|			Справочник.МКБ10Избранное КАК СправочникМКБ
				|				ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ИерархияМКБ10Избранное КАК ИерархияМКБ10
				|				ПО (ИерархияМКБ10.Ссылка = СправочникМКБ.Ссылка)
				|					И (СправочникМКБ.ЭтоГруппа = ЛОЖЬ)
				|					И (СправочникМКБ.ПометкаУдаления = ЛОЖЬ)
				|					И (&МассивМКБНеУказан
				|						ИЛИ СправочникМКБ.ЭлементМКБ10 В (&МассивНайденыхМКБ))
				|					И (&НеИспользоватьСписокИерархии
				|						ИЛИ СправочникМКБ.Ссылка В ИЕРАРХИИ (&СписокИерархии))) КАК КонтрольИерархии
				|		ПО СправочникМКБ.Ссылка = КонтрольИерархии.Ссылка"
			;
		КонецЕсли;
	КонецЕсли;
	
	Возврат Результат_;
	
КонецФункции

&НаКлиенте
Функция ПолучитьПараметрыЗапросаСтруктурой()
		
	Результат_ = Новый Структура(
		"НеИспользоватьСписокИерархии,СписокИерархии,МассивМКБНеУказан,МассивНайденыхМКБ");
		
	Список_ = Новый Массив();
	Результат_.НеИспользоватьСписокИерархии = Истина;
	Результат_.СписокИерархии = Список_;

	Результат_.МассивНайденыхМКБ = ПоискДиагнозов(ЭтотОбъект.ФильтрНаименование, ЭтотОбъект.ФильтрКод);
	Результат_.МассивМКБНеУказан = (ИСТИНА
		И НЕ ЗначениеЗаполнено(ЭтотОбъект.ФильтрНаименование)
		И НЕ ЗначениеЗаполнено(ЭтотОбъект.ФильтрКод)
	);
	Возврат Результат_;
	
КонецФункции

&НаСервере
Процедура УстановитьПараметрыЗапроса(Запрос, ПараметрыЗапроса)
	КнопкаИерахия = Элементы.УстановитьИерархию;
	КнопкаСписка = Элементы.КомандаИзбранное;
	Если КнопкаСписка.Пометка = Ложь Тогда
		Список = ЭтотОбъект.ДинамическоеПолеМКБ10;
		СписокЭлемент = ЭтотОбъект.Элементы.ДинамическоеПолеМКБ;
	Иначе
		Список = ЭтотОбъект.ДинамическоеПолеМКБ10Избранное;
		СписокЭлемент = ЭтотОбъект.Элементы.ДинамическоеПолеМКБ10Избранное;
	КонецЕсли;
	Список.ТекстЗапроса = Запрос;
	// устанавливаем параметры запроса
	Для Каждого Парам из  ПараметрыЗапроса Цикл
		Список.Параметры.УстановитьЗначениеПараметра(Парам.Ключ,Парам.Значение);
	КонецЦикла;
	/// устанавливаем отобажение списка
	Если КнопкаИерахия.Пометка = Истина И НЕ СписокЭлемент.Отображение=ОтображениеТаблицы.Дерево Тогда 
		СписокЭлемент.Отображение=ОтображениеТаблицы.Дерево;
	ИначеЕсли КнопкаИерахия.Пометка = Ложь И НЕ СписокЭлемент.Отображение=ОтображениеТаблицы.Список Тогда 
		СписокЭлемент.Отображение=ОтображениеТаблицы.Список;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура УстановитьВидимостьСписков()
	Если Элементы.КомандаИзбранное.Пометка = Истина Тогда
		Если Элементы.ДинамическоеПолеМКБ.Видимость = Истина Тогда
			Элементы.ДинамическоеПолеМКБ.Видимость = Ложь;
		КонецЕсли;
		Если Элементы.ДинамическоеПолеМКБ10Избранное.Видимость = Ложь Тогда
			Элементы.ДинамическоеПолеМКБ10Избранное.Видимость = Истина;
		КонецЕсли;
	ИначеЕсли Элементы.КомандаИзбранное.Пометка = Ложь Тогда
		Если Элементы.ДинамическоеПолеМКБ10Избранное.Видимость = Истина Тогда
			Элементы.ДинамическоеПолеМКБ10Избранное.Видимость = Ложь;
		КонецЕсли;
		Если Элементы.ДинамическоеПолеМКБ.Видимость = Ложь Тогда
			Элементы.ДинамическоеПолеМКБ.Видимость = Истина;
		КонецЕсли;		
	КонецЕсли;	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПоискДиагнозов(Знач Наименование, Знач Код)
	Возврат Справочники.МКБ10.ПоискДиагнозов(Наименование, Код, ИСТИНА);
КонецФункции

&НаКлиенте
Процедура ПослеНастройкиИзбранного(Результат, ДополнительныеПараметры) Экспорт
	
	УстановитьВидимостьСписков();
	Элементы.ДинамическоеПолеМКБ10Избранное.Обновить();
КонецПроцедуры

#КонецОбласти

#КонецОбласти


