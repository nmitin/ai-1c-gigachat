#Область СлужебныеПроцедурыИФункции

////
 // Функция: ПолучитьСсылкуВизуализатора
 //   Возвращает ссылку на визуализатор медицинского документа.
 //
 // Параметры:
 //   СсылкаМД {ДокументСсылка.МедицинскийДокумент}
 //     Ссылка на медицинский документ.
 //
 // Возврат: {СправочникСсылка.ВизуализаторыМедицискихДокументов}
 //   Визуализатор, наиболее подходящий для данного документа.
 ///
&НаСервере
Функция ПолучитьСсылкуВизуализатора(СсылкаМД)
	Если Истина
		И ТипЗнч(СсылкаМД) = Тип("ДокументСсылка.МедицинскийДокумент")
		И ЗначениеЗаполнено(СсылкаМД)
	Тогда
		ШаблонМедицинскогоДокумента_ = Документы.МедицинскийДокумент.ПолучитьШаблонМедицинскогоДокумента(СсылкаМД);
	КонецЕсли;
	Возврат ШаблоныМедицинскихДокументов.ПолучитьСсылкуВизуализатора(ШаблонМедицинскогоДокумента_);
КонецФункции

////
 // Функция: ПолучитьТипВизуализатора
 //   Возвращает тип визуализатора медицинского документа.
 //
 // Параметры:
 //   СсылкаВизуализатор {СправочникСсылка.ВизуализаторыМедицинскихДокументов}
 //     Ссылка на визуализатор.
 //
 // Возврат: {Строка}
 //   Тип визуализатора
 ///
&НаСервере
Функция ПолучитьТипВизуализатора(СсылкаВизуализатор)
	Возврат Справочники.ВизуализаторыМедицинскихДокументов.ПолучитьТипВизуализатора(СсылкаВизуализатор);
КонецФункции

&НаСервере
Функция ПолучитьТелоМедицинскогоДокумента(СсылкаМД) 
	CDAДокумент_ = Документы.МедицинскийДокумент.ПолучитьCDAДокумент(СсылкаМД);
	Возврат Документы.МедицинскийДокумент.ПолучитьТелоМД(CDAДокумент_);
КонецФункции

&НаСервере
Функция ПолучитьШМДПоСсылкеМД(СсылкаМД)
	Возврат Документы.МедицинскийДокумент.ПолучитьШаблонМедицинскогоДокумента(СсылкаМД);
КонецФункции

&НаКлиенте
Функция ПолучитьШМД(СсылкаМД) 
	Перем СсылкаШМД;
	СсылкаШМД = ПолучитьШМДПоСсылкеМД(СсылкаМД);
	Если СсылкаШМД <> Неопределено Тогда
		Возврат СсылкаШМД;
	Иначе
		
	КонецЕсли;	
КонецФункции

&НаСервере
Процедура СброситьЗначениеШМД(Ссылка) 
	НачатьТранзакцию();
	Объект = Ссылка.ПолучитьОбъект();
	
	Для Каждого строкаТЧ_ Из Объект.CDAДокументы Цикл
		строкаТЧ_.Актуально = Ложь;
	КонецЦикла;
	
	Объект.Записать();
	ЗафиксироватьТранзакцию();
КонецПроцедуры

&НаСервере
Функция ПолучитьДокументПроведен(СсылкаМД)
	Возврат СсылкаМД.Проведен;
КонецФункции

&НаСервере
Функция ПолучитьТабличныйДокумент(СсылкаМД)
	Возврат ВизуализаторCDA.СформироватьТабличныйДокумент(СсылкаМД);
КонецФункции

&НаКлиенте 
Процедура УстановитьПараметрыФильтрации()
	ЭтотОбъект.Список.Параметры.УстановитьЗначениеПараметра("НеФильтроватьПоТипамМД", (ЭтотОбъект.ТипыМД.Количество() = 0));
	ЭтотОбъект.Список.Параметры.УстановитьЗначениеПараметра("СписокТиповМД", ЭтотОбъект.ТипыМД.ВыгрузитьЗначения());
	УстановитьПометкуКнопкеОткрытияФормыТиповМД();
КонецПроцедуры

&НаКлиенте
Процедура УстановитьПометкуКнопкеОткрытияФормыТиповМД()
	ФильтрПоТипамМДПрименяется_ = (ЭтотОбъект.ТипыМД.Количество() > 0);
	КнопкаТиповМД_ = ЭтотОбъект.Элементы.СписокОткрытьФормуТиповМД;
	КнопкаТиповМД_.Пометка = ФильтрПоТипамМДПрименяется_;
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКоманды.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	ЭтотОбъект.Список.Параметры.УстановитьЗначениеПараметра("НеФильтроватьПоТипамМД", Истина);
	ЭтотОбъект.Список.Параметры.УстановитьЗначениеПараметра("СписокТиповМД", Новый Массив);

КонецПроцедуры

&НаКлиенте
Процедура ОбработкаВыбора(ВыбранноеЗначение, ИсточникВыбора)
Если ИсточникВыбора.ИмяФормы = "Справочник.ТипыМД.Форма.ФормаВыбораСФлажками" Тогда
		ЭтотОбъект.ТипыМД.ЗагрузитьЗначения(ВыбранноеЗначение);
		УстановитьПараметрыФильтрации();
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	УстановитьПараметрыФильтрации();
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

// СтандартныеПодсистемы.ПодключаемыеКоманды
&НаКлиенте
Процедура Подключаемый_ВыполнитьКоманду(Команда)
	ПодключаемыеКомандыКлиент.ВыполнитьКоманду(ЭтотОбъект, Команда, Элементы.Список);
КонецПроцедуры

&НаСервере
Процедура Подключаемый_ВыполнитьКомандуНаСервере(Контекст, Результат) Экспорт
	ПодключаемыеКоманды.ВыполнитьКоманду(ЭтотОбъект, Контекст, Элементы.Список, Результат);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбновитьКоманды()
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Элементы.Список);
КонецПроцедуры
// Конец СтандартныеПодсистемы.ПодключаемыеКоманды

&НаКлиенте
Процедура ПоказатьМД(Команда)
	Если ЭтотОбъект.Элементы.Список.ТекущаяСтрока <> Неопределено Тогда
		СсылкаМД = ЭтотОбъект.Элементы.Список.ТекущиеДанные.Ссылка;
		ШаблоныМедицинскихДокументовКлиент.ПоказатьМД(
			,
			ПолучитьТелоМедицинскогоДокумента(СсылкаМД),
			СсылкаМД,
			ЭтотОбъект
		);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура РедактироватьМД(Команда)
	Перем СсылкаМД;
	
	Если ЭтотОбъект.Элементы.Список.ТекущаяСтрока <> Неопределено Тогда
		СсылкаМД = ЭтотОбъект.Элементы.Список.ТекущиеДанные.Ссылка;
		ШаблоныМедицинскихДокументовКлиент.ЗапуститьШМД(
			ПолучитьШМД(СсылкаМД),
			ПолучитьТелоМедицинскогоДокумента(СсылкаМД),
			СсылкаМД,
			ЭтотОбъект
		);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ТекстМД(Команда)
	Если ЭтотОбъект.Элементы.Список.ТекущаяСтрока <> Неопределено Тогда		
		СсылкаМД = ЭтотОбъект.Элементы.Список.ТекущиеДанные.Ссылка;		
		ОткрытьФорму(
			"ОбщаяФорма.ФормаРедактированияМД",
			Новый Структура("ТекстовоеСодержимое, ИдентификаторДокумента",
				ПолучитьТелоМедицинскогоДокумента(СсылкаМД),
				СсылкаМД
			)
		);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура СброситьШМД(Команда)
	Если ЭтотОбъект.Элементы.Список.ТекущаяСтрока <> Неопределено Тогда		
		СброситьЗначениеШМД(ЭтотОбъект.Элементы.Список.ТекущаяСтрока);
	КонецЕсли;	
КонецПроцедуры

#Если Не ВебКлиент Тогда
&НаКлиенте
Процедура СохранитьPDF(Команда)
	Если ЭтотОбъект.Элементы.Список.ТекущаяСтрока <> Неопределено Тогда		
		СсылкаМД = ЭтотОбъект.Элементы.Список.ТекущиеДанные.Ссылка;	
		Визуализатор_ = ПолучитьСсылкуВизуализатора(СсылкаМД);
		
		Если ПолучитьТипВизуализатора(Визуализатор_) = ПредопределенноеЗначение("Перечисление.ТипыВизуализаторов.XSLT") Тогда
			АдресФайла_ = ШаблоныМедицинскихДокументов.HTML2PDF(
				ШаблоныМедицинскихДокументов.CDA2HTML(
					ПолучитьТелоМедицинскогоДокумента(СсылкаМД),
					ПолучитьСсылкуВизуализатора(СсылкаМД),
					ШаблоныМедицинскихДокументовПереопределяемый.ОпределитьБазовыйАдрес(СсылкаМД),
					ПолучитьДокументПроведен(СсылкаМД),
					Истина
				)
			);
		Иначе
			ТабДок_ = ПолучитьТабличныйДокумент(СсылкаМД);
			Если ТабДок_ = Неопределено Тогда 
				Возврат;
			КонецЕсли;
			ТабДок_.Защита = Истина;
			ВремФайл_ = ПолучитьИмяВременногоФайла("pdf");
			ТабДок_.Записать(ВремФайл_, ТипФайлаТабличногоДокумента.PDF);
			ДДанные_ = Новый ДвоичныеДанные(ВремФайл_);
			АдресФайла_ = ПоместитьВоВременноеХранилище(ДДанные_);
		КонецЕсли;
		Если АдресФайла_ <> Неопределено Тогда
			НачатьПолучениеФайлаССервера(
				АдресФайла_,
				"medical.pdf"
			);
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры
#КонецЕсли

&НаКлиенте
Процедура СохранитьXML(Команда)
	Если ЭтотОбъект.Элементы.Список.ТекущаяСтрока = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	СсылкаМД_ = ЭтотОбъект.Элементы.Список.ТекущиеДанные.Ссылка;
	ПолучаемыеФайлы_ = ШаблоныМедицинскихДокументов.МассивФайловXML(
		ПолучитьТелоМедицинскогоДокумента(СсылкаМД_),
		ПолучитьСсылкуВизуализатора(СсылкаМД_)
	);
	
	НачатьПолучениеФайловССервера(ПолучаемыеФайлы_, Новый ПараметрыДиалогаПолученияФайлов("", Истина));
КонецПроцедуры

&НаКлиенте
Процедура ПечатьПакета(Команда)
	Если ЭтотОбъект.Элементы.Список.ВыделенныеСтроки.Количество() > 0 Тогда
		Тексты = Новый Массив;
		Для каждого Строка Из ЭтотОбъект.Элементы.Список.ВыделенныеСтроки Цикл
			Тексты.Добавить(ПолучитьТелоМедицинскогоДокумента(Строка));
		КонецЦикла;
		
		ШаблоныМедицинскихДокументовКлиент.ПечататьМД(Новый Массив(), Тексты, Истина, , , , ЭтотОбъект.Элементы.Список.ВыделенныеСтроки);
		
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьФормуТиповМД(Команда)
	ПараметрыФормы_ = Новый Структура(
		"СписокВыбранных", 
		ЭтотОбъект.ТипыМД
	);
	ОткрытьФорму(
		"Справочник.ТипыМД.Форма.ФормаВыбораСФлажками", 
		ПараметрыФормы_,
		ЭтотОбъект,
		ЭтотОбъект.УникальныйИдентификатор,
		,
		,
		,
		РежимОткрытияОкнаФормы.БлокироватьОкноВладельца
	);
КонецПроцедуры

&НаКлиенте
Процедура ОтменитьФильтрацию(Команда)
	ЭтотОбъект.ТипыМД.Очистить();
	УстановитьПараметрыФильтрации();
КонецПроцедуры

&НаКлиенте
Процедура АнализИИ(Команда)
	Если ЭтотОбъект.Элементы.Список.ТекущаяСтрока = Неопределено Тогда
		Сообщить("Выберите документ для анализа");
		Возврат;
	КонецЕсли;

	СсылкаМД = ЭтотОбъект.Элементы.Список.ТекущиеДанные.Ссылка;

	// Получаем результат анализа с сервера
	РезультатАнализа = ВыполнитьАнализИИНаСервере(СсылкаМД);

	Если РезультатАнализа.Успех Тогда
		// Открываем форму с результатом
		ПараметрыФормы = Новый Структура;
		ПараметрыФормы.Вставить("РезультатJSON", РезультатАнализа.Результат);
		ПараметрыФормы.Вставить("Заголовок", "Анализ ИИ: " + СсылкаМД);
		ПараметрыФормы.Вставить("ДанныеДокумента", РезультатАнализа.ДанныеДокумента);

		ОткрытьФорму("ОбщаяФорма.ФормаРезультатаИИ", ПараметрыФормы, ЭтотОбъект);
	Иначе
		Сообщить("Ошибка анализа: " + РезультатАнализа.Ошибка);
	КонецЕсли;
КонецПроцедуры

&НаСервере
Функция ВыполнитьАнализИИНаСервере(СсылкаМД)
	Результат = Новый Структура("Успех, Результат, Ошибка, ДанныеДокумента", Ложь, "", "", Неопределено);

	Попытка
		// Получаем данные документа (структура с текстом и метаданными)
		ДанныеДокумента = ПолучитьДанныеДляАнализаИИ(СсылкаМД);

		Если ПустаяСтрока(ДанныеДокумента.ТекстДляАнализа) Тогда
			Результат.Ошибка = "Нет данных для анализа (диагнозы или услуги не заполнены)";
			Возврат Результат;
		КонецЕсли;

		// Формируем системный промпт с требованием JSON
		СистемныйПромпт = "Ты - медицинский эксперт. Проанализируй соответствие назначенных услуг диагнозам пациента.
		|Ответ дай СТРОГО в формате JSON без дополнительного текста:
		|{
		|  ""summary"": ""Краткий вывод об анализе"",
		|  ""items"": [
		|    {
		|      ""service"": ""Название услуги"",
		|      ""status"": ""ok|warning|error"",
		|      ""comment"": ""Комментарий к услуге""
		|    }
		|  ],
		|  ""recommendations"": [""Рекомендация 1"", ""Рекомендация 2""]
		|}
		|Статусы: ok - услуга соответствует диагнозу, warning - возможно избыточна, error - не соответствует диагнозам.";

		// Отправляем запрос к GigaChat API
		ОтветИИ = ОтправитьЗапросGigaChat(ДанныеДокумента.ТекстДляАнализа, СистемныйПромпт);

		Если ОтветИИ.Успех Тогда
			Результат.Успех = Истина;
			Результат.Результат = ОтветИИ.Результат;
			Результат.ДанныеДокумента = ДанныеДокумента;
		Иначе
			Результат.Ошибка = ОтветИИ.Ошибка;
		КонецЕсли;

	Исключение
		Результат.Ошибка = ОписаниеОшибки();
	КонецПопытки;

	Возврат Результат;
КонецФункции

&НаСервере
Функция ПолучитьДанныеДляАнализаИИ(СсылкаМД)
	// Результат - структура с текстом и метаданными для отображения
	Результат = Новый Структура;
	Результат.Вставить("ТекстДляАнализа", "");
	Результат.Вставить("ЗаголовокДокумента", "");
	Результат.Вставить("Пациент", "");
	Результат.Вставить("ДатаДокумента", "");
	Результат.Вставить("Диагнозы", Новый Массив); // Массив структур {Тип, Код, Наименование}

	// Запрос для получения диагнозов и услуг
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	МД.Пациент.Наименование КАК Пациент,
		|	МД.Дата КАК ДатаДокумента,
		|	МД.Заголовок КАК Заголовок
		|ИЗ
		|	Документ.МедицинскийДокумент КАК МД
		|ГДЕ
		|	МД.Ссылка = &Ссылка
		|;
		|
		|ВЫБРАТЬ
		|	Диагнозы.ТипДиагноза КАК ТипДиагноза,
		|	Диагнозы.МКБ10.Код КАК КодМКБ,
		|	Диагнозы.МКБ10.Наименование КАК НаименованиеДиагноза
		|ИЗ
		|	Документ.МедицинскийДокумент.ДиагнозыПоМКБ10 КАК Диагнозы
		|ГДЕ
		|	Диагнозы.Ссылка = &Ссылка
		|;
		|
		|ВЫБРАТЬ
		|	Услуги.Номенклатура.Наименование КАК НаименованиеУслуги,
		|	Услуги.Номенклатура.Код КАК КодУслуги
		|ИЗ
		|	Документ.МедицинскийДокумент.НазначенныеУслуги КАК Услуги
		|ГДЕ
		|	Услуги.Ссылка = &Ссылка";

	Запрос.УстановитьПараметр("Ссылка", СсылкаМД);
	РезультатПакета = Запрос.ВыполнитьПакет();

	// Шапка документа
	ВыборкаШапка = РезультатПакета[0].Выбрать();
	ТекстДляАнализа = "";
	Если ВыборкаШапка.Следующий() Тогда
		Результат.Пациент = ВыборкаШапка.Пациент;
		Результат.ДатаДокумента = Формат(ВыборкаШапка.ДатаДокумента, "ДЛФ=D");
		Результат.ЗаголовокДокумента = ВыборкаШапка.Заголовок;

		ТекстДляАнализа = "МЕДИЦИНСКИЙ ДОКУМЕНТ" + Символы.ПС
			+ "Пациент: " + ВыборкаШапка.Пациент + Символы.ПС
			+ "Дата: " + Результат.ДатаДокумента + Символы.ПС
			+ "Заголовок: " + ВыборкаШапка.Заголовок + Символы.ПС + Символы.ПС;
	КонецЕсли;

	// Диагнозы
	ВыборкаДиагнозы = РезультатПакета[1].Выбрать();
	Если ВыборкаДиагнозы.Количество() > 0 Тогда
		ТекстДляАнализа = ТекстДляАнализа + "ДИАГНОЗЫ:" + Символы.ПС;
		Пока ВыборкаДиагнозы.Следующий() Цикл
			// Сохраняем для отображения
			ДанныеДиагноза = Новый Структура("Тип, Код, Наименование",
				Строка(ВыборкаДиагнозы.ТипДиагноза),
				ВыборкаДиагнозы.КодМКБ,
				ВыборкаДиагнозы.НаименованиеДиагноза);
			Результат.Диагнозы.Добавить(ДанныеДиагноза);

			ТекстДляАнализа = ТекстДляАнализа
				+ "- " + ВыборкаДиагнозы.ТипДиагноза + ": "
				+ ВыборкаДиагнозы.КодМКБ + " "
				+ ВыборкаДиагнозы.НаименованиеДиагноза + Символы.ПС;
		КонецЦикла;
		ТекстДляАнализа = ТекстДляАнализа + Символы.ПС;
	КонецЕсли;

	// Назначенные услуги
	ВыборкаУслуги = РезультатПакета[2].Выбрать();
	Если ВыборкаУслуги.Количество() > 0 Тогда
		ТекстДляАнализа = ТекстДляАнализа + "НАЗНАЧЕННЫЕ УСЛУГИ:" + Символы.ПС;
		Пока ВыборкаУслуги.Следующий() Цикл
			ТекстДляАнализа = ТекстДляАнализа
				+ "- [" + ВыборкаУслуги.КодУслуги + "] "
				+ ВыборкаУслуги.НаименованиеУслуги + Символы.ПС;
		КонецЦикла;
	КонецЕсли;

	Результат.ТекстДляАнализа = ТекстДляАнализа;
	Возврат Результат;
КонецФункции

&НаСервере
Функция ОтправитьЗапросGigaChat(ТекстДляАнализа, СистемныйПромпт)
	Результат = Новый Структура("Успех, Результат, Ошибка", Ложь, "", "");

	// Адрес API сервера GigaChat
	АдресСервера = "localhost";
	ПортСервера = 8000;

	Попытка
		// Формируем JSON запрос
		ПараметрыЗапроса = Новый Структура;
		ПараметрыЗапроса.Вставить("text", ТекстДляАнализа);
		ПараметрыЗапроса.Вставить("system_prompt", СистемныйПромпт);

		ЗаписьJSON = Новый ЗаписьJSON;
		ЗаписьJSON.УстановитьСтроку();
		ЗаписатьJSON(ЗаписьJSON, ПараметрыЗапроса);
		ТелоЗапроса = ЗаписьJSON.Закрыть();

		// HTTP запрос
		HTTPСоединение = Новый HTTPСоединение(АдресСервера, ПортСервера, , , , 120);
		HTTPЗапрос = Новый HTTPЗапрос("/analyze/text");
		HTTPЗапрос.УстановитьТелоИзСтроки(ТелоЗапроса, КодировкаТекста.UTF8);
		HTTPЗапрос.Заголовки.Вставить("Content-Type", "application/json");

		HTTPОтвет = HTTPСоединение.ОтправитьДляОбработки(HTTPЗапрос);

		Если HTTPОтвет.КодСостояния = 200 Тогда
			ЧтениеJSON = Новый ЧтениеJSON;
			ЧтениеJSON.УстановитьСтроку(HTTPОтвет.ПолучитьТелоКакСтроку(КодировкаТекста.UTF8));
			Ответ = ПрочитатьJSON(ЧтениеJSON);

			Результат.Успех = Истина;
			Результат.Результат = Ответ.result; // JSON строка
		Иначе
			Результат.Ошибка = "HTTP ошибка: " + HTTPОтвет.КодСостояния;
		КонецЕсли;

	Исключение
		Результат.Ошибка = "Ошибка подключения к серверу ИИ: " + ОписаниеОшибки();
	КонецПопытки;

	Возврат Результат;
КонецФункции

#КонецОбласти
