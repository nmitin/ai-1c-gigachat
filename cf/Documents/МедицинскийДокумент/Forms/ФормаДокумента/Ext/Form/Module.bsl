
#Область ПрограммныйИнтерфейс

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если ЗначениеЗаполнено(Объект.Ссылка) Тогда
		CDAДокумент_ = Документы.МедицинскийДокумент.ПолучитьCDAДокумент(Объект);
		ТелоМД_ = Документы.МедицинскийДокумент.ПолучитьТелоМД(CDAДокумент_);
		Если ТелоМД_ <> Неопределено И НЕ Объект.Ссылка.Пустая() Тогда
			СоздатьГруппуПросмотраДокумента();
			ДинПолеОтображениеМДПриСозданииНаСервере(Отказ, СтандартнаяОбработка);
		КонецЕсли;
	
		ЭтотОбъект.УслугиНеВыполнены = РегистрыСведений.СтатусыМедицинскихДокументов.ПолучитьРеквизитУслугиНеВыполнены(Объект.Ссылка);
		
		ЭтотОбъект.ФайлМД = Документы.МедицинскийДокумент.ПолучитьCDAДокумент(Объект, Истина);
	КонецЕсли;
	
	Если Не ЭтотОбъект.УслугиНеВыполнены Тогда
		Элементы.УслугиНеВыполнены.Видимость = Ложь;
	КонецЕсли;

	// Если ШМД не использует прикрепленные файлы, тогда незачем отображать
	// команду прикрепленные файлы. этим управляет параметризированная 
	// функциональная опция ОтображатьЗИиФДляМедицинскогоДокумента.
	ЭтотОбъект.УстановитьПараметрыФункциональныхОпцийФормы(
		Новый Структура("МедицинскийДокумент", 
			ЭтотОбъект.Объект.Ссылка
		)
	);
	
	ЭтотОбъект.МедицинскаяКартаИлиПациент = ВыбратьЗаполненное(Объект.МедицинскаяКарта, Объект.Пациент);
		
	Если ПолучитьФункциональнуюОпцию("ИспользоватьЭлектронныеПодписи") = Ложь Тогда
		Элементы.ГруппаЭлектронныеПодписи.Видимость = Ложь;
	Иначе
		ЗаполнитьСписокПодписей();
	КонецЕсли;

	КолонкиМассив = Новый Массив;
	Для Каждого ОписаниеКолонки Из РеквизитФормыВЗначение("ТаблицаПодписей").Колонки Цикл
		КолонкиМассив.Добавить(ОписаниеКолонки.Имя);
	КонецЦикла;
	ОписаниеКолонокТаблицыПодписей = Новый ФиксированныйМассив(КолонкиМассив);
	
	// ВерсионированиеОбъектов
	ВерсионированиеОбъектов.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец ВерсионированиеОбъектов
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКоманды.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	Если ЗначениеЗаполнено(Объект.Ссылка) Тогда
		
		Элемент_ = ВывестиСтатусРЭМД(Объект.Ссылка, Неопределено);
		
		Если Элемент_ = Неопределено Тогда
			// Сам МД отсутствует в справочнике ЭМД, скорее всего в ЭМД помещаются его файлы CDA.
			НомерСтроки_ = 1;
			Для Каждого строкаТЧ_ Из ЭтотОбъект.Объект.CDAДокументы Цикл
				Если строкаТЧ_.Актуально И ЗначениеЗаполнено(строкаТЧ_.ТелоМедДокумента) Тогда
					ВывестиСтатусРЭМД(строкаТЧ_.ТелоМедДокумента, НомерСтроки_);
				КонецЕсли;
				НомерСтроки_ = НомерСтроки_ + 1;
			КонецЦикла;
		КонецЕсли;
	КонецЕсли;
	
	Для Каждого строкаТЧ_ Из Объект.ЛекарственныеНазначения Цикл
		Если ЗначениеЗаполнено(строкаТЧ_.УИДСтандарта) Тогда
			строкаТЧ_.СтандартМедицинскойПомощи = УправлениеЗаказамиУслугами.ПолучитьСтандартМедицинскойПомощиПоУИД(строкаТЧ_.УИДСтандарта);
		КонецЕсли;
	КонецЦикла;
	
	Элементы.СтраницаНазначенияЛСВремяВыполнения.Видимость = ОбщиеМеханизмыПовтИсп.ИспользоватьСтационарныеУсловия();
	
	Элементы.ЛекарственныеНазначения.ПодчиненныеЭлементы.ЛекарственныеНазначенияПрепаратПациента.Видимость = 
		?(ПолучитьФункциональнуюОпцию("ИспользоватьПрепаратыПациента"), Истина, Ложь);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	УстановитьДоступностьКомандСпискаЭП();
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	// СтандартныеПодсистемы.ДатыЗапретаИзменения
	ДатыЗапретаИзменения.ОбъектПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.ДатыЗапретаИзменения
	
	Для Каждого строкаТЧ_ Из Объект.Исполнители Цикл
		Если ЗначениеЗаполнено(строкаТЧ_.УникальныйИдентификаторУслуги) Тогда
			Найденные_ = Объект.ВыполненныеУслуги.НайтиСтроки(Новый Структура("УникальныйИдентификаторУслуги, КлючСтрокиСоставаУслуги", строкаТЧ_.УникальныйИдентификаторУслуги, строкаТЧ_.КлючСтрокиСоставаУслуги));
			Если Найденные_.Количество() > 0 Тогда
				строкаТЧ_.Услуга = Найденные_[0].НоменклатураСоставаУслуги;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	// При клике по ссылке в документе файл открывается во внешнем приложении.
	Если ИмяСобытия = ОповещенияФорм.ЗапускФайлаВПриложении()
		И Источник = ЭтотОбъект 
	Тогда
		ШаблоныМедицинскихДокументовПереопределяемыйКлиент.ОтображениеМДОбработкаОповещения(ЭтотОбъект, ИмяСобытия, Параметр, Источник);
	КонецЕсли;
	// при сохранении документа обновляется просмотр в группе просмотра.
	Если ИмяСобытия = ОповещенияФорм.ПослеЗаписиВОбработкеФормированиеМедицинскихДокументов() 
		И Источник = ЭтотОбъект.ВладелецФормы
	Тогда
		ДинПолеОтображениеМД.ОтобразитьЗначениеДинПоля(ЭтотОбъект, ДинПолеОтображениеМДПолучитьДинПоле());
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиент.ПослеЗаписи(ЭтотОбъект, Объект, ПараметрыЗаписи);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура Подключаемый_СтатусРЭМДНажатие(Элемент)
	СтандартнаяОбработка = Ложь;
	ЧастиИмени_ = стрРазделить(Элемент.Имя, "_");
	Если ЧастиИмени_[1] = "МД" Тогда
		ДокументИнформационнойБазы_ = Объект.Ссылка;
	Иначе
		ДокументИнформационнойБазы_ = Объект.CDAДокументы[Число(ЧастиИмени_[1]) - 1].ТелоМедДокумента;
	КонецЕсли;
	ПараметрыФормы = Новый Структура("ДокументИнформационнойБазы", ДокументИнформационнойБазы_);
	ОткрытьФорму("Справочник.ФедеральныеВебСервисыЭМД.Форма.СписокПодписейДляРЭМД", ПараметрыФормы);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_СтатусРЭМДНажатиеПриОшибке(Элемент)

	ПоказатьЗначение(, Элемент.Заголовок);

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийКомандФормы

&НаКлиенте
Процедура Подписать(Команда)
	
	Если Модифицированность Тогда
		Если Не Записать() Тогда 
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	ПодписатьПродолжение(Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ПоставитьДополнительнуюПодпись(Команда)

	Если Модифицированность Тогда
		Если Не Записать() Тогда 
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	ПодписатьПродолжение(Ложь);
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьПодпись(Команда)
	АдресСертификата_ = Элементы.ТаблицаПодписей.ТекущиеДанные.АдресСертификата;
	ЭлектроннаяПодписьКлиент.ОткрытьСертификат(АдресСертификата_);
КонецПроцедуры

&НаКлиенте
Процедура Проверить(Команда)
	
	ВыполнятьПроверкуЭПНаСервере = ЭлектроннаяПодписьКлиент.ОбщиеНастройки().ПроверятьЭлектронныеПодписиНаСервере;
	
	ИдентификаторыПодписей_ = Новый Массив;
	
	ПодписиCades_ = Новый Массив;
	
	Для Каждого Элемент_ Из Элементы.ТаблицаПодписей.ВыделенныеСтроки Цикл
		ТекущаяСтрока_ = ТаблицаПодписей.НайтиПоИдентификатору(Элемент_);
		Если ЗначениеЗаполнено(ТекущаяСтрока_.ИдПодписи) Тогда
			ИдентификаторыПодписей_.Добавить(ТекущаяСтрока_.ИдПодписи);
		Иначе
			Структура_ = Новый Структура;
			Структура_.Вставить("Подпись", ТекущаяСтрока_.Подпись);
			Структура_.Вставить("ПорядковыйНомер", ТекущаяСтрока_.ПорядковыйНомер);
			ПодписиCades_.Добавить(Структура_);
		КонецЕсли;
	КонецЦикла;
	
	ПроверитьПодписиСЗаданнымиИдентификаторами(ИдентификаторыПодписей_);
	
	ПроверитьПодписиCades(ПодписиCades_);
	
КонецПроцедуры

&НаКлиенте
Процедура ПроверитьВсе(Команда)
	
	
	ВыполнятьПроверкуЭПНаСервере = ЭлектроннаяПодписьКлиент.ОбщиеНастройки().ПроверятьЭлектронныеПодписиНаСервере;
	
	ИдентификаторыПодписей_ = Новый Массив;
	
	ПодписиCades_ = Новый Массив;
	
	Для Каждого ТекущаяСтрока_ Из ЭтотОбъект.ТаблицаПодписей Цикл
		Если ЗначениеЗаполнено(ТекущаяСтрока_.ИдПодписи) Тогда
			ИдентификаторыПодписей_.Добавить(ТекущаяСтрока_.ИдПодписи);
		Иначе
			Структура_ = Новый Структура;
			Структура_.Вставить("Подпись", ТекущаяСтрока_.Подпись);
			Структура_.Вставить("ПорядковыйНомер", ТекущаяСтрока_.ПорядковыйНомер);
			ПодписиCades_.Добавить(Структура_);
		КонецЕсли;
	КонецЦикла;
	
	ПроверитьПодписиСЗаданнымиИдентификаторами(ИдентификаторыПодписей_);
	
	ПроверитьПодписиCades(ПодписиCades_);
	
КонецПроцедуры

&НаКлиенте
Процедура Сохранить(Команда)
	
	Если Элементы.ТаблицаПодписей.ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если Элементы.ТаблицаПодписей.ТекущиеДанные.Объект <> Неопределено И (НЕ Элементы.ТаблицаПодписей.ТекущиеДанные.Объект.Пустая()) Тогда
		ЭлектроннаяПодписьКлиент.СохранитьПодпись(Элементы.ТаблицаПодписей.ТекущиеДанные.АдресПодписи);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура Удалить(Команда)
	
	Обработчик = Новый ОписаниеОповещения("УдалитьЗавершение", ЭтотОбъект);
	
	ПоказатьВопрос(Обработчик, СообщенияПользователю.Получить("МедицинскийДокумент_УдалитьВыделенныеПодписиВопрос"), РежимДиалогаВопрос.ДаНет);
	
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьПодписьИзФайла(Команда)
	
	Обработчик = Новый ОписаниеОповещения("ДобавитьПодписьИзФайлаЗавершение", ЭтотОбъект);
	
	РаботаСЭПКлиент.ДобавитьПодписьИзФайла(Обработчик, Объект.Ссылка, УникальныйИдентификатор);

КонецПроцедуры

&НаКлиенте
Процедура ОткрытьФормирование(Команда)

	Файл_ = ШаблоныМедицинскихДокументовПоликлиника.ПолучитьФайлМедицинскогоДокумента(
		Объект.Ссылка
	);
	
	Если ЗначениеЗаполнено(Файл_) Тогда
		// Внешний МД.
		Отбор_ = Новый Структура("Ссылка", Файл_);
		Параметры_ = Новый Структура("Отбор, ТекущаяСтрока", Отбор_, Файл_);
		ОткрытьФорму("Обработка.РаботаСФайлами.Форма.ПрисоединенныеФайлы", Параметры_);
	Иначе
		Параметры_ = Новый Структура("МедицинскийДокумент", Объект.Ссылка);
		ОбщегоНазначенияФМДКлиент.ОткрытьОбработкуФормированияМД(Объект.Ссылка, Параметры_, ЭтотОбъект, Истина);
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ИсходныйТекстДокумента(Команда)
	СсылкаМД = ЭтотОбъект.Объект.Ссылка;		
	ОткрытьФорму(
		"ОбщаяФорма.ФормаРедактированияМД",
		Новый Структура("ТекстовоеСодержимое, ИдентификаторДокумента",
			ПолучитьТелоМедицинскогоДокумента(СсылкаМД),
			СсылкаМД
		)
	);
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьСЭМД(Команда)
	ТД_ = ЭтотОбъект.Элементы.CDAДокументы.ТекущиеДанные;
	Если ТД_ = Неопределено ИЛИ Не ЗначениеЗаполнено(ТД_.ТелоМедДокумента) Тогда
		Возврат;
	КонецЕсли;
	ТелоМД_ = ПолучитьТелоМДНаСервере(ТД_.ТелоМедДокумента);
	ШаблоныМедицинскихДокументовКлиент.ПоказатьМД(, ТелоМД_, ПредопределенноеЗначение("Документ.МедицинскийДокумент.ПустаяСсылка"),,,,ТД_.ШаблонМедицинскогоДокумента);
КонецПроцедуры

// СтандартныеПодсистемы.ПодключаемыеКоманды
&НаКлиенте
Процедура Подключаемый_ВыполнитьКоманду(Команда)
	ПодключаемыеКомандыКлиент.ВыполнитьКоманду(ЭтотОбъект, Команда, Объект);
КонецПроцедуры

&НаСервере
Процедура Подключаемый_ВыполнитьКомандуНаСервере(Контекст, Результат) Экспорт
	ПодключаемыеКоманды.ВыполнитьКоманду(ЭтотОбъект, Контекст, Объект, Результат);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбновитьКоманды()
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
КонецПроцедуры
// Конец СтандартныеПодсистемы.ПодключаемыеКоманды

#КонецОбласти

/// Клиентский обработчик для динамического поля формы
//
//   Передает обработку событий элементов динамического поля формы
//   динамическому полю формы.
//
// Параметры:
//   Элемент - ПолеФормы, ГруппаФормы, ТаблицаФормы, ДекорацияФормы -
//     Элемент, который инициировал событие.
//   Арг1 - Произвольный -
//     Необязательный параметр события.
//   Арг2 - Произвольный -
//     Необязательный параметр события.
//   Арг3 - Произвольный -
//     Необязательный параметр события.
//   Арг4 - Произвольный -
//     Необязательный параметр события.
//   Арг5 - Произвольный -
//     Необязательный параметр события.
///
&НаКлиенте
Процедура ДинПоле_КлиентскийОбработчик( // 6 параметров
	Элемент,
	Арг1 = Неопределено,
	Арг2 = Неопределено,
	Арг3 = Неопределено,
	Арг4 = Неопределено,
	Арг5 = Неопределено
)
// НачалоПроцедуры
	
	ДинамическоеПолеФормы.ОбработатьСобытие(
		ЭтотОбъект, Элемент, "ДинПоле_КлиентскийОбработчик", Арг1, Арг2, Арг3, Арг4, Арг5
	);
	
КонецПроцедуры

&НаСервере
Процедура СоздатьГруппуПросмотраДокумента()
	Если НЕ Объект.Ссылка.Пустая() Тогда
		ИдентификаторыДокументов_ = Новый Массив;
		ИдентификаторыДокументов_.Добавить(Объект.Ссылка);
		Параметры_ = Новый Структура(
	    	"ИдентификаторыДокументов, Визуализатор, ПолныйДокумент, ИменаОтображаемыхКнопок",
	    	ИдентификаторыДокументов_,
	    	Неопределено,
	    	Истина,
	    	"КнопкаСетка,КнопкаПолный,КнопкаПечать,КнопкаПечатьБезШапки,КнопкаПечатьУсеченного,КнопкаПараметрыСтраницы,КнопкаКолонтитулы,КнопкаИсходник,КнопкаСохранитьPDF,КнопкаСохранитьXML,КнопкаДокументСНовойСтраницы"
	    );
		ДинПолеОтображениеМДУстановитьПараметрыИнициализации(Параметры_);
	КонецЕсли;
КонецПроцедуры

&НаСервере
Функция ПолучитьТелоМедицинскогоДокумента(СсылкаМД) 
	CDAДокумент_ = Документы.МедицинскийДокумент.ПолучитьCDAДокумент(СсылкаМД);
	Возврат Документы.МедицинскийДокумент.ПолучитьТелоМД(CDAДокумент_);
КонецФункции

&НаСервере
Процедура ЗаполнитьСписокПодписей()

	CDAДокумент_ = Документы.МедицинскийДокумент.ПолучитьCDAДокумент(ЭтотОбъект.Объект);
	
	Если ЗначениеЗаполнено(CDAДокумент_) Тогда
		Набор_ = РегистрыСведений.ЭлектронныеПодписи.СоздатьНаборЗаписей();
		Набор_.Отбор.ПодписанныйОбъект.Установить(CDAДокумент_);
		Набор_.Прочитать();
		ЭлектронныеПодписи_ = Набор_.Выгрузить();
	Иначе
		ЭлектронныеПодписи_ = Неопределено;
	КонецЕсли;
	
	ТаблицаПодписей.Очистить();
	
	ТекстЗаголовка = СообщенияПользователю.Получить("МедицинскийДокумент_ЭлектронныеПодписи");
	Элементы.ГруппаЭлектронныеПодписи.Заголовок = ТекстЗаголовка;
	
	ДанныеПодписей_ = РаботаСЭП.ПолучитьДанныеПодписей(ЭтотОбъект.Объект.Ссылка);
	
	УдаляемыеСтроки_ = Новый Массив;
	Для Каждого ДанныеПодписи_ Из ДанныеПодписей_ Цикл
		НоваяСтрока = ТаблицаПодписей.Добавить();
		
		ЗаполнитьЗначенияСвойств(НоваяСтрока, ДанныеПодписи_);
		
		НоваяСтрока.ИндексКартинки = -1;
		
		Если ЗначениеЗаполнено(ДанныеПодписи_.Сертификат) Тогда
			ДвоичныеДанныеСертификата = Base64Значение(ДанныеПодписи_.Сертификат);
			НоваяСтрока.АдресСертификата = ПоместитьВоВременноеХранилище(
				ДвоичныеДанныеСертификата, ЭтотОбъект.УникальныйИдентификатор
			);
		КонецЕсли;
		
		Если ЭлектронныеПодписи_ <> Неопределено Тогда
			Найденные_ = ЭлектронныеПодписи_.НайтиСтроки(Новый Структура("ИдентификаторПодписи", ДанныеПодписи_.ИдПодписи));
			Если Найденные_.Количество() Тогда
				УдаляемыеСтроки_.Добавить(Найденные_[0]);
				Если Найденные_[0].ПодписьВерна = Истина Тогда
					Статус_ = НСтр("ru = 'Верна'");
				ИначеЕсли ЗначениеЗаполнено(Найденные_[0].ДатаПроверкиПодписи) Тогда
					Статус_ = НСтр("ru = 'Неверна'");
				Иначе
					Статус_ = "";
				КонецЕсли;
				
				НоваяСтрока.Статус = Статус_;
				НоваяСтрока.ДатаПроверкиПодписи = Найденные_[0].ДатаПроверкиПодписи;

			КонецЕсли;
		КонецЕсли;
		
	КонецЦикла;
	
	// Удалим из списка, полученного из регистра, те подписи, которые сформированы по формату XMLDSig
	Для Каждого Элемент_ из УдаляемыеСтроки_ Цикл
		ЭлектронныеПодписи_.Удалить(Элемент_);
	КонецЦикла;

	Если ЭлектронныеПодписи_ <> Неопределено Тогда
		Для Каждого ДанныеПодписи_ Из ЭлектронныеПодписи_ Цикл
			НоваяСтрока = ТаблицаПодписей.Добавить();
			
			ЗаполнитьЗначенияСвойств(НоваяСтрока, ДанныеПодписи_);
			
			НоваяСтрока.ИндексКартинки = -1;
			
			Если ДанныеПодписи_.Комментарий = "Основная" Тогда
				НоваяСтрока.Основная = Истина;
			КонецЕсли;
			
			Если ЗначениеЗаполнено(ДанныеПодписи_.Сертификат) Тогда
				НоваяСтрока.АдресСертификата = ПоместитьВоВременноеХранилище(
					ДанныеПодписи_.Сертификат.Получить(), ЭтотОбъект.УникальныйИдентификатор
				);
			КонецЕсли;
			
			НоваяСтрока.Подпись = ДанныеПодписи_.Подпись.Получить();
			
			Если ДанныеПодписи_.ПодписьВерна = Истина Тогда
				Статус_ = НСтр("ru = 'Верна'");
			ИначеЕсли ЗначениеЗаполнено(ДанныеПодписи_.ДатаПроверкиПодписи) Тогда
				Статус_ = НСтр("ru = 'Неверна'");
			Иначе
				Статус_ = "";
			КонецЕсли;
					
			НоваяСтрока.Статус = Статус_;
			НоваяСтрока.ДатаПроверкиПодписи = ДанныеПодписи_.ДатаПроверкиПодписи;
			
			СертификатКриптографии_ = Новый СертификатКриптографии(ДанныеПодписи_.Сертификат.Получить());
		
			СвойстваСубъекта_ = ЭлектроннаяПодпись.СвойстваСубъектаСертификата(СертификатКриптографии_);
			
			ВладелецСертификата_ = ""
				+ СвойстваСубъекта_.Фамилия + " "
				+ СвойстваСубъекта_.Имя + " "
				+ СвойстваСубъекта_.Отчество
			;
			
			СвойстваСертификата_ = ЭлектроннаяПодпись.СвойстваСертификата(СертификатКриптографии_);
			ПериодДействия_ = СтрШаблон(
				НСтр("ru = 'с %1 по %2'"),
				Формат(СвойстваСертификата_.ДатаНачала,    "ДЛФ=DT"),
				Формат(СвойстваСертификата_.ДатаОкончания, "ДЛФ=DT")
			);
			
			Комментарий_ = 
				СообщенияПользователю.Получить(
					"РаботаСЭП_ПодписаноЭлектроннойПодписьюАвтора",
					Новый Структура(
						"ФИО, Дата, Должность",
						ВладелецСертификата_,
						ДанныеПодписи_.ДатаПодписи,
						СвойстваСубъекта_.Должность
					)
				);
				
			Комментарий_ = Комментарий_ +
				Символы.Таб + СертификатКриптографии_.СерийныйНомер +
				Символы.Таб + СвойстваСубъекта_.ОбщееИмя + 
				Символы.Таб + ПериодДействия_;
				
			НоваяСтрока.Комментарий = Комментарий_;
		КонецЦикла;
	КонецЕсли;
	
	ТекстЗаголовка = СообщенияПользователю.Получить(
						"МедицинскийДокумент_ЭлектронныеПодписиКоличество",
						Новый Структура(
							"Количество",
							Формат(ТаблицаПодписей.Количество(), "ЧГ=")
						)
					 );
	Элементы.ГруппаЭлектронныеПодписи.Заголовок = ТекстЗаголовка;
	
	Если Не 0 = ЭтотОбъект.ТаблицаПодписей.Количество() Тогда
		Элементы.ГруппаЭлектронныеПодписи.Видимость = Истина;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПодписатьПродолжение(ПерваяПодпись = Ложь)
	Обработчик = Новый ОписаниеОповещения("ПодписатьПослеФормированияПодписи", ЭтотОбъект);
	РаботаСЭПКлиент.СформироватьПодписьМедицинскогоДокумента(Обработчик, Объект.Ссылка, ПерваяПодпись);
КонецПроцедуры

&НаКлиенте
// Вызывается после создания ЭП
//
// Параметры:
//  <Параметр1>  - <Тип.Вид> - <описание параметра>
//                 <продолжение описания параметра>
//  <Параметр2>  - <Тип.Вид> - <описание параметра>
//                 <продолжение описания параметра>
//
// Возвращаемое значение:
//   <Тип.Вид>   - <описание возвращаемого значения>
//
Процедура ПодписатьПослеФормированияПодписи(Результат, ПараметрыВыполнения) Экспорт
	Если Результат.Успех <> Истина Тогда
		Возврат;
	КонецЕсли;
	
	НаборДанных_ = Результат.НаборДанных;
	МассивПодписей_ = Новый Массив;
	
	СвойстваПодписи_ = Неопределено;
	
	Результат.Свойство("СвойстваПодписи", СвойстваПодписи_);
	
	Если ЗначениеЗаполнено(СвойстваПодписи_) Тогда
		ЭтоПерваяПодпись_ = Неопределено;
		Если ПараметрыВыполнения.Свойство("ПерваяПодпись", ЭтоПерваяПодпись_) Тогда
			Если ЭтоПерваяПодпись_ = Истина Тогда
				СвойстваПодписи_.Вставить("Комментарий", "Основная");
			КонецЕсли;
		КонецЕсли;
		МассивПодписей_.Добавить(СвойстваПодписи_);
	КонецЕсли;
	
	Для Каждого ДанныеПодписи_ Из НаборДанных_ Цикл 
		
		ДанныеXML_ = ОбщиеМеханизмыКлиентСервер.ДвоичныеДанныеВСтроку(ДанныеПодписи_.Данные);
		
		ЗаписатьПодписанноеТелоДокумента(
			ДанныеXML_, 
			ДанныеПодписи_.ТелоМДСсылка,
			Истина, 
			Истина, 
			МассивПодписей_, 
			Истина
		);
	КонецЦикла;
	
	ЭтотОбъект.Записать();
	
	ЭлектроннаяПодписьКлиент.ИнформироватьОПодписанииОбъекта(Объект.Ссылка);
	
	ЗаполнитьСписокПодписей();
	
	УстановитьДоступностьКомандСпискаЭП();
КонецПроцедуры

&НаСервере
Процедура ЗаписатьПодписанноеТелоДокумента(ТелоМД, CDAСсылка = Неопределено, ПодписаниеИлиУдаление = Истина, ПодписанЭП = Истина, МассивПодписей = Неопределено, ДобавитьПодписиВРегистр = Ложь)
	
	РаботаСЭП.ЗаписатьТелоДокумента(
		ЭтотОбъект.Объект.Ссылка, 
		CDAСсылка, 
		ТелоМД, 
		ПодписаниеИлиУдаление, 
		ПодписанЭП, 
		МассивПодписей, 
		, 
		ДобавитьПодписиВРегистр
	);
	
КонецПроцедуры

// Продолжение процедуры Проверить
&НаКлиенте
Процедура ПроверитьПодписиСЗаданнымиИдентификаторами(ИдентификаторыПодписей)
	
	Для Каждого ИдПодписи_ Из ИдентификаторыПодписей Цикл
		
		Оповещение_ = Новый ОписаниеОповещения(
			"ПослеПроверкиПодписи",
			ЭтотОбъект, 
			Новый Структура("ИдПодписи", ИдПодписи_)
		);
		
		РаботаСЭПКлиент.ПроверитьПодписьXMLDSigМедицинскогоДокумента(
			Оповещение_,
			ЭтотОбъект.Объект.Ссылка,
			ИдПодписи_
		);
		
	КонецЦикла;
	
	// Синтаксический контроль
	Если Ложь Тогда
		ПослеПроверкиПодписи(0, 0);
	КонецЕсли;
	
КонецПроцедуры

// Продолжение процедуры Проверить
&НаКлиенте
Процедура ПроверитьПодписиCades(МассивПодписей)
	
	Для Каждого ДанныеПодписи_ Из МассивПодписей Цикл
		
		Оповещение_ = Новый ОписаниеОповещения(
			"ПослеПроверкиПодписи",
			ЭтотОбъект, 
			ДанныеПодписи_
		);
		
		РаботаСЭПКлиент.ПроверитьПодписьCadesМедицинскогоДокумента(
			Оповещение_,
			ЭтотОбъект.Объект.Ссылка,
			ДанныеПодписи_.Подпись
		);
		
	КонецЦикла;
	
	// Синтаксический контроль
	Если Ложь Тогда
		ПослеПроверкиПодписи(0, 0);
	КонецЕсли;
	
КонецПроцедуры

// Продолжение процедуры ПроверитьОднуПодпись
&НаКлиенте
Процедура ПослеПроверкиПодписи(Результат, ДополнительныеПараметры) Экспорт
	
	ИдПодписи_ = Неопределено;
	Подпись_ = Неопределено;
	ПорядковыйНомер_ = Неопределено;
	Если ДополнительныеПараметры.Свойство("ИдПодписи", ИдПодписи_) Тогда
		Отбор_ = Новый Структура("ИдПодписи", ИдПодписи_);
	Иначе
		Отбор_ = ДополнительныеПараметры;
		ПорядковыйНомер_ = ДополнительныеПараметры.ПорядковыйНомер;
	КонецЕсли;
	
	Выгрузка_ = ЭтотОбъект.ТаблицаПодписей.НайтиСтроки(Отбор_);
	
	__ПРОВЕРКА__(1 = Выгрузка_.Количество(), "Ожидалась одна строка в таблице подписей при проверке.");
	
	ТекущиеДанные_ = Выгрузка_[0];
	
	ПодписьВерна_ = Ложь;
	Если Результат = Истина Тогда
		Статус_ = НСтр("ru = 'Верна'");
		ПодписьВерна_ = Истина;
	Иначе
		Статус_ = СтрШаблон(НСтр("ru = 'Неверна. %1'"), Результат);
	КонецЕсли;
	
	ТекущиеДанные_.Статус = Статус_;
	ТекущиеДанные_.ДатаПроверкиПодписи = ТекущаяДата();
	
	Найденные_ = ЭтотОбъект.Объект.CDAДокументы.НайтиСтроки(Новый Структура("Актуально", Истина));
	
	РаботаСЭП.ОбновитьИнформациюОПроверкиПодписи(Найденные_[0].ТелоМедДокумента, ИдПодписи_, ПорядковыйНомер_, Результат, ТекущиеДанные_.ДатаПроверкиПодписи);
	
КонецПроцедуры

&НаКлиенте
Процедура УдалитьЗавершение(Ответ, ПараметрыВыполнения) Экспорт
	Если Ответ = КодВозвратаДиалога.Нет Тогда
		Возврат;
	КонецЕсли;
	
	РеквизитПодписанИзменен = Ложь;
	УдалитьПодписиИОбновитьСписок(РеквизитПодписанИзменен);
	
	Если РеквизитПодписанИзменен Тогда
		ОповеститьОбИзменении(Объект.Ссылка);
		Прочитать();
	КонецЕсли;
	
	
	УстановитьДоступностьКомандСпискаЭП();
	
КонецПроцедуры

&НаСервере
Процедура УдалитьПодписиИОбновитьСписок(РеквизитПодписанИзменен)
	
	Если Не ЗначениеЗаполнено(ЭтотОбъект.Объект.Ссылка) Тогда
		Возврат;
	КонецЕсли;
	
	ИдентификаторыПодписей_ = Новый Массив;
	НомераПодписей_ = Новый Массив;
	
	Для Каждого Элемент Из Элементы.ТаблицаПодписей.ВыделенныеСтроки Цикл
		
		ДанныеСтроки_ = ТаблицаПодписей.НайтиПоИдентификатору(Элемент);
		Если ЗначениеЗаполнено(ДанныеСтроки_.ИдПодписи) Тогда
			ИдентификаторыПодписей_.Добавить(ДанныеСтроки_.ИдПодписи);
		Иначе
			НомераПодписей_.Добавить(ДанныеСтроки_.ПорядковыйНомер);
		КонецЕсли;
		
	КонецЦикла;
	
	Если ИдентификаторыПодписей_.Количество() = 0 И НомераПодписей_.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	УдаленныеПодписи_ = Неопределено;
	СоответствиеФайлCDA_ = РаботаСЭП.УдалитьПодписиМД(
		ЭтотОбъект.Объект.Ссылка,
		ИдентификаторыПодписей_,
		РеквизитПодписанИзменен,
		УдаленныеПодписи_,
		НомераПодписей_
	);
	
	Для Каждого КлючЗначение_ Из СоответствиеФайлCDA_ Цикл 
		CDAСсылка_ = КлючЗначение_.Ключ;
		ТелоМД_ = КлючЗначение_.Значение;
		Если Не ЗначениеЗаполнено(ТелоМД_) Тогда
			Продолжить;
		КонецЕсли;
		ЗаписатьПодписанноеТелоДокумента(
			ТелоМД_, 
			CDAСсылка_,
			Ложь, 
			Не РеквизитПодписанИзменен, 
			УдаленныеПодписи_
		);
	КонецЦикла;
	
	
	ЭтотОбъект.Записать();
	ЗаполнитьСписокПодписей();
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьДоступностьКомандСпискаЭП()
	
	ЕстьПодписи = (ТаблицаПодписей.Количество() <> 0);
	
	//Элементы.ТаблицаПодписейПроверить.Доступность = ЕстьПодписи;
	Элементы.ТаблицаПодписейПроверитьВсе.Доступность = ЕстьПодписи;
	Элементы.ТаблицаПодписейОткрытьСертификат.Доступность = ЕстьПодписи;
	Элементы.ТаблицаПодписейУдалить.Доступность = ЕстьПодписи;
	Элементы.ТаблицаПодписейСохранить.Доступность = ЕстьПодписи;
	
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьПодписьИзФайлаЗавершение(Результат, ПараметрыВыполнения) Экспорт
	Если Результат.Успех = Истина Тогда
		ЗаполнитьСписокПодписей();
		УстановитьДоступностьКомандСпискаЭП();
	КонецЕсли;
КонецПроцедуры

&НаСервере
Функция ВывестиСтатусРЭМД(ДокументИнформационнойБазы, Номер)

	Попытка
		ЕстьОшибка_ = Ложь;
		СтатусРЭМД_ = ФедеральныеВебСервисыРЭМД.ПолучитьСтатусРегистрацииДокументаВРЭМД(ДокументИнформационнойБазы);
		СтатусСтрокой_ = СтатусРЭМД_.СтатусСтрокой;
		
		Если ДокументИнформационнойБазы = Объект.Ссылка 
			И (стрНайти(СтатусРЭМД_.Описание, "Документ отсутствует в справочнике") > 0
				Или СтатусРЭМД_.КодСтатуса = "НеДляРЭМД")
		Тогда
			Возврат Неопределено;
		КонецЕсли;
	Исключение
		ЕстьОшибка_ = Истина;
		Инф_ = ИнформацияОбОшибке();
		СтатусСтрокой_ = КраткоеПредставлениеОшибки(Инф_);
		Сообщить(ПодробноеПредставлениеОшибки(Инф_));
	КонецПопытки;

	ДекорацияСтатусРЭМД_ = Элементы.Добавить("СтатусРЭМД_" + ?(ЗначениеЗаполнено(Номер),Формат(Номер,"ЧГ=0"), "МД"), Тип("ДекорацияФормы"), Элементы.ГруппаСписокСтатусов);
	ДекорацияСтатусРЭМД_.Вид = ВидДекорацииФормы.Надпись;
	ДекорацияСтатусРЭМД_.Шрифт = Новый Шрифт(ДекорацияСтатусРЭМД_.Шрифт,,, Ложь);
	ДекорацияСтатусРЭМД_.Гиперссылка = Истина;
	Если Не ЕстьОшибка_ Тогда
		ДекорацияСтатусРЭМД_.УстановитьДействие("Нажатие", "Подключаемый_СтатусРЭМДНажатие");
	Иначе
		ДекорацияСтатусРЭМД_.УстановитьДействие("Нажатие", "Подключаемый_СтатусРЭМДНажатиеПриОшибке");
	КонецЕсли;
	ДекорацияСтатусРЭМД_.Заголовок  = стрШаблон("%1: %2", ДокументИнформационнойБазы, СтатусСтрокой_);
	ДекорацияСтатусРЭМД_.Видимость  = Истина;
	
	Возврат ДекорацияСтатусРЭМД_;

КонецФункции


&НаСервереБезКонтекста
Функция ПолучитьТелоМДНаСервере(ТелоМДФайл)
	Возврат Документы.МедицинскийДокумент.ПолучитьТелоМД(ТелоМДФайл);
КонецФункции

#КонецОбласти

#Область ДинПолеОтображениеМД
#Область ПрограммныйИнтерфейс

&НаСервере
Процедура ДинПолеОтображениеМДУстановитьПараметрыИнициализации(ПараметрыИн)
	ДинамическоеПолеФормы.УстановитьРеквизит(ЭтотОбъект, ДинПолеОтображениеМДПолучитьДинПоле(), "ПараметрыИнициализации", ПараметрыИн);
КонецПроцедуры

&НаСервере
Процедура ДинПолеОтображениеМДПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	ДинПоле_ = ДинПолеОтображениеМДПолучитьДинПоле();
	ПараметрыПоля_ = ДинамическоеПолеФормы.ПолучитьРеквизит(ЭтотОбъект, ДинПоле_, "ПараметрыИнициализации");
	Если ПараметрыПоля_ = Неопределено Тогда
		ДинПолеОтображениеМДУстановитьПараметрыИнициализации(Параметры);
		ПараметрыПоля_ = ДинамическоеПолеФормы.ПолучитьРеквизит(ЭтотОбъект, ДинПоле_, "ПараметрыИнициализации");
	КонецЕсли;
	
	ДинПолеОтображениеМД.ПриСозданииНаСервере(ЭтотОбъект, ДинПоле_, ПараметрыПоля_);
КонецПроцедуры

&НаКлиенте
Процедура ДинПолеОтображениеМДТабличноеПолеОбработкаРасшифровки(Элемент, Расшифровка, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	ДинПолеОтображениеМД.ОбработкаРасшифровки(
		ЭтотОбъект, 
		ДинПолеОтображениеМДПолучитьДинПоле(), 
		Расшифровка
	);
КонецПроцедуры

&НаКлиенте
Процедура ДинПолеОтображениеМДПолеДокумента_ДокументСформирован(Элемент)
	ДинПоле_ = ДинПолеОтображениеМДПолучитьДинПоле();
	Если Элемент.Имя = "ДинПолеОтображениеМДПолеПечати" И ЗначениеЗаполнено(ДинамическоеПолеФормы.ПолучитьРеквизит(ЭтотОбъект, ДинПоле_, "ПечатьДокумента")) Тогда
		Документ_ = ДинамическоеПолеФормы.ПолучитьЭлемент(ЭтотОбъект, ДинПоле_, "ПолеПечати").Документ;
		Попытка
			Документ_.parentWindow.print()
		Исключение
			Попытка
				Документ_.defaultView.print()
			Исключение
				Попытка
					Принтер = Документ_.getElementById("print");
					Принтер.click();
				Исключение
					ВызватьИсключение("Невозможно распечатать документ.");
				КонецПопытки
			КонецПопытки;
		КонецПопытки;
	Иначе
		ДинПолеОтображениеМД.ПолеДокумента_ДокументСформирован(
			ЭтотОбъект,
			ДинПоле_,
			Элемент
		);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ДинПолеОтображениеМДПолеДокумента_ПриНажатии(Элемент, ДанныеСобытия, СтандартнаяОбработка)
	ДинПолеОтображениеМД.ПолеДокумента_ПриНажатии(
		ЭтотОбъект, 
		ДинПолеОтображениеМДПолучитьДинПоле(), 
		Элемент, 
		ДанныеСобытия, 
		СтандартнаяОбработка
	);
КонецПроцедуры

&НаКлиенте
Процедура ДинПолеОтображениеМДОбработчикКоманды(Команда)
	Параметры_ = Неопределено;
	ВызовСервераНеНужен_ = ДинПолеОтображениеМД.ОбработчикКоманды(
		ЭтотОбъект, 
		ДинПолеОтображениеМДПолучитьДинПоле(), 
		Команда, 
		Параметры_
	);
	
	Если Истина <> ВызовСервераНеНужен_ Тогда
		ДинПолеОтображениеМДОбработчикКомандыНаСервере(Команда.Имя, Параметры_);
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура ДинПолеОтображениеМДОбработчикКомандыНаСервере(ИмяКоманды, Параметры)
	ДинПолеОтображениеМД.ОбработчикКомандыНаСервере(
		ЭтотОбъект, 
		ДинПолеОтображениеМДПолучитьДинПоле(), 
		Команды.Найти(ИмяКоманды), 
		Параметры
	);
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция ДинПолеОтображениеМДПолучитьДинПоле()
	Возврат "ДинПолеОтображениеМД";
КонецФункции

&НаКлиенте
Функция СформироватьТабличныйДокументДляПакетаМДКлиент(Форма, ДинПоле, ПолныйДокумент, ВыводитьШапку, OutМакет = Неопределено, ВыводитьАвтосгенерированные = Истина) Экспорт
	Возврат СформироватьТабличныйДокументДляПакетаМД(ДинПоле, ПолныйДокумент, ВыводитьШапку, OutМакет, ВыводитьАвтосгенерированные);	
КонецФункции

&НаСервере
Функция СформироватьТабличныйДокументДляПакетаМД(ДинПоле, ПолныйДокумент, ВыводитьШапку, OutМакет = Неопределено, ВыводитьАвтосгенерированные = Истина)
	Возврат ДинПолеОтображениеМД.СформироватьТабличныйДокументДляПакетаМД(ЭтотОбъект, ДинПоле, ПолныйДокумент, ВыводитьШапку, OutМакет, ВыводитьАвтосгенерированные);
КонецФункции

#КонецОбласти


#КонецОбласти








