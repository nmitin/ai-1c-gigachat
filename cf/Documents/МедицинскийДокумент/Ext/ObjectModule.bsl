

#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОписаниеПеременных

Перем ЭтоПерепроведение; // Контроль перепроведения документа.

#КонецОбласти

#Область ОбработчикиСобытий

////
 // Процедура: ПередЗаписью
 //   Обработчик события ПередЗаписью документа.                
 //   Изменяет статусы услуг в регистре у услуг из табличной части ВыполненныеУслуги.
 //   Если документ перепроводится, то определяем какие строки были удалены из тч ВыполненныеУслуги
 //   и изменяем статус на исходный соответствующих услуг в регистре СтатусыУслуг.
 //
 // Параметры:
 //   Отказ {Булево}
 //     Отказ от отмены проведения.
 //   РежимЗаписи 
 //     Режим записи документа.
 //   РежимПроведения
 //     Режим проведения документа.
 ///
Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если ЭтотОбъект.ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Если ЭтотОбъект.ДополнительныеСвойства.Свойство("ОбработчикОбновленияНомеров")
		Или ЭтотОбъект.ДополнительныеСвойства.Свойство("ОбработчикОбновленияМД")
	Тогда
		Возврат;
	КонецЕсли;
	
	ЭтоПерепроведение = Ложь;
	Если РежимЗаписи = РежимЗаписиДокумента.Проведение 
		И ЗначениеЗаполнено(ЭтотОбъект.Ссылка)
		И ЭтотОбъект.Проведен
	Тогда
		ЭтоПерепроведение = Истина;
	КонецЕсли;
	
	Дата = ТекущаяДатаСеанса();
	Если ЗначениеЗаполнено(ЭтотОбъект.Ответственный) 
			И ЭтотОбъект.Ответственный <> Пользователи.ТекущийПользователь() 
			И НЕ ЭтотОбъект.ДополнительныеСвойства.Свойство("ПерепроведениеНаОснованииДокумента")
			И НЕ (Пользователи.РолиДоступны("ПравоИзменятьЧужиеМедицинскиеДокументы") 
				ИЛИ Пользователи.ЭтоПолноправныйПользователь()
			)
	Тогда
		Если НЕ Истина = ПользовательСредиИСполнителей(ЭтотОбъект.Исполнители, Пользователи.ТекущийПользователь()) Тогда 
			Отказ = Истина;
			
			Сообщить("Изменение документа разрешено только его автору");
		КонецЕсли;
	КонецЕсли;
	ЭтотОбъект.Ответственный = Пользователи.ТекущийПользователь();
	
	// Удалим пустые записи из ТЧ Исполнители.
	НайденныеПустыеСтрокиИсполнители_ = ЭтотОбъект.Исполнители.НайтиСтроки(Новый Структура("Исполнитель", Справочники.Сотрудники.ПустаяСсылка()));
	Для Каждого ПустаяСтрокаИсполнитель_ Из НайденныеПустыеСтрокиИсполнители_ Цикл
		ЭтотОбъект.Исполнители.Удалить(ПустаяСтрокаИсполнитель_);
	КонецЦикла;
	
	Если ЭтотОбъект.ПометкаУдаления И Не Ссылка.ПометкаУдаления Тогда
		ПодписанЭП_ = Ложь;
		Документы.МедицинскийДокумент.ПолучитьCDAДокумент(ЭтотОбъект, Ложь, ПодписанЭП_);
		
		Если ПодписанЭП_ = Истина И Не Пользователи.ЭтоПолноправныйПользователь() Тогда
			Отказ = Истина;
			СообщенияПользователю.Показать(
				"МедицинскийДокумент_УдалениеМедицинскогоДокументаНеДопускается",
				Новый Структура(
					"Ссылка",
					Ссылка
				)
			);
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	Если РежимЗаписи = РежимЗаписиДокумента.Проведение 
			И Не ЗначениеЗаполнено(ЭтотОбъект.ДатаМД) 
	Тогда
		Если ЭтотОбъект.ВыполненныеУслуги.Количество() > 0
			И ЗначениеЗаполнено(ЭтотОбъект.ВыполненныеУслуги[0].ДатаВыполнения)
		Тогда
			ЭтотОбъект.ДатаМД = ЭтотОбъект.ВыполненныеУслуги[0].ДатаВыполнения;
		Иначе
			ЭтотОбъект.ДатаМД = ЭтотОбъект.Дата;
		КонецЕсли;
	КонецЕсли;
	
	// Заполним реквизит Договор в ТЧ ВыполненныеУслуги.
	МассивСоглашений_ = Новый Массив;
	Для Каждого строкаТЧ_ Из ЭтотОбъект.ВыполненныеУслуги Цикл
		Если Не строкаТЧ_.БезЗаказа Тогда
			РеквизитыДляОчистки_ = Новый Структура("Цена, Сумма, Соглашение, КлючСвязи, СтавкаНДС, СуммаНДС, ПроцентАвтоматическойСкидки, ПроцентРучнойСкидки, СуммаАвтоматическойСкидки, СуммаРучнойСкидки, ИсточникФинансирования, Валюта, Контрагент, Договор");
			ЗаполнитьЗначенияСвойств(строкаТЧ_, РеквизитыДляОчистки_);
		Иначе
			Если РежимЗаписи = РежимЗаписиДокумента.Проведение И Не ЗначениеЗаполнено(строкаТЧ_.Соглашение) Тогда
				Если строкаТЧ_.Номенклатура = строкаТЧ_.НоменклатураСоставаУслуги Тогда // Не СМУ.
					ВызватьИсключение "Для услуги без заказа """ + строкаТЧ_.НоменклатураСоставаУслуги + """ не заполнен реквизит Соглашение.";
				КонецЕсли;
			КонецЕсли;
			Если ЗначениеЗаполнено(строкаТЧ_.Соглашение) И Не ЗначениеЗаполнено(строкаТЧ_.Договор) Тогда
				МассивСоглашений_.Добавить(строкаТЧ_.Соглашение);
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	Для Каждого строкаТЧ_ Из ЭтотОбъект.ВыполненныеМЭУслуги Цикл
		Если РежимЗаписи = РежимЗаписиДокумента.Проведение И Не ЗначениеЗаполнено(строкаТЧ_.Соглашение) Тогда
			ВызватьИсключение "У многоэтапной услуги без заказа """ + строкаТЧ_.Номенклатура + """ не заполнен реквизит Соглашение.";
		КонецЕсли;
		Если ЗначениеЗаполнено(строкаТЧ_.Соглашение) И Не ЗначениеЗаполнено(строкаТЧ_.Договор) Тогда
			МассивСоглашений_.Добавить(строкаТЧ_.Соглашение);
		КонецЕсли;
	КонецЦикла;
	
	Если МассивСоглашений_.Количество() > 0 Тогда
		ДоговораСоответствие_ = ОбщегоНазначения.ЗначенияРеквизитовОбъектов(МассивСоглашений_, "Договор");
		Для Каждого строкаТЧ_ Из ЭтотОбъект.ВыполненныеУслуги Цикл
			Если ЗначениеЗаполнено(строкаТЧ_.Соглашение) И Не ЗначениеЗаполнено(строкаТЧ_.Договор) Тогда
				строкаТЧ_.Договор = ДоговораСоответствие_[строкаТЧ_.Соглашение].Договор;
			КонецЕсли;
		КонецЦикла;
		Для Каждого строкаТЧ_ Из ЭтотОбъект.ВыполненныеМЭУслуги Цикл
			Если ЗначениеЗаполнено(строкаТЧ_.Соглашение) И Не ЗначениеЗаполнено(строкаТЧ_.Договор) Тогда
				строкаТЧ_.Договор = ДоговораСоответствие_[строкаТЧ_.Соглашение].Договор;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	// Добавим исполнителем СМП, если в списке ВыполненныеУслуги присутствуют услуги, номенклатура которых подразумевает выполнение СМП
	Запрос_ = Новый Запрос(
		"ВЫБРАТЬ
		|	МедицинскийДокументВыполненныеУслуги.Номенклатура
		|ИЗ
		|	Документ.МедицинскийДокумент.ВыполненныеУслуги КАК МедицинскийДокументВыполненныеУслуги
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК Н
		|		ПО (МедицинскийДокументВыполненныеУслуги.НоменклатураСоставаУслуги = Н.Ссылка)
		|ГДЕ
		|	Н.ИсполнительМедицинскойУслуги = ЗНАЧЕНИЕ(Перечисление.ИсполнителиМедицинскихУслуг.СМП)
		|	И МедицинскийДокументВыполненныеУслуги.Ссылка = &Ссылка"
	);
	Запрос_.УстановитьПараметр("Ссылка", ЭтотОбъект.Ссылка);
	Выборка_ = Запрос_.Выполнить().Выбрать();
	Если Выборка_.Следующий() Тогда
		СМПНайден_ = Ложь;
		Для Каждого Исполнитель_ Из ЭтотОбъект.Исполнители Цикл
			Если Исполнитель_.Должность.ВидМедицинскойДолжности = Перечисления.ВидыМедицинскихДолжностей.СреднийМедицинскийПерсонал Тогда
				СМПНайден_ = Истина;
				Прервать;
			КонецЕсли;
		КонецЦикла;
		
		Если НЕ СМПНайден_ Тогда
			СМП_ = Обработки.СменноеЗадание.ЗагрузитьНастройкуСМП();
			Если ЗначениеЗаполнено(СМП_) Тогда
				СущСтрока_ = ЭтотОбъект.Исполнители.Найти(СМП_, "Исполнитель");
				Исполнитель_ = ЭтотОбъект.Исполнители.Добавить();
				Исполнитель_.Исполнитель = СМП_;
				Исполнитель_.Должность = Исполнитель_.Исполнитель.Должность;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	Для Каждого Исполнитель_ Из ЭтотОбъект.Исполнители Цикл
		Если Не ЗначениеЗаполнено(Исполнитель_.Должность) Тогда
			Продолжить;
		КонецЕсли;
		ОписаниеДолжности_ = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(
			Исполнитель_.Должность, 
			"Наименование1002,ФедеральныйКод"
		);
		Если Не ЗначениеЗаполнено(ОписаниеДолжности_.Наименование1002) Тогда
			Отказ = Истина;
			СообщенияПользователю.Показать(
				"МедицинскийДокумент_ДляДолжностиНеЗаполненоНаименованиеПоКлассификатору",
				Новый Структура(
					"Должность",
					Исполнитель_.Должность
				)
			);
		КонецЕсли;
		Если Не ЗначениеЗаполнено(ОписаниеДолжности_.ФедеральныйКод) Тогда
			Отказ = Истина;
			СообщенияПользователю.Показать(
				"МедицинскийДокумент_ДляДолжностиНеЗаполненФедеральныйКодПоКлассификатору",
				Новый Структура(
					"Должность",
					Исполнитель_.Должность
				)
			);
		КонецЕсли;
	КонецЦикла;
	
	//Для Каждого Исполнитель_ Из ЭтотОбъект.Исполнители Цикл
	//	Если Исполнитель_.Исполнитель.ФизЛицо.Пустая() Тогда
	//		Отказ = Истина;
	//		СообщенияПользователю.Показать(
	//			"МедицинскийДокумент_УдалениеМедицинскогоДокументаНеДопускается",
	//			Новый Структура(
	//				"Ссылка",
	//				Ссылка
	//			)
	//		);
	//		Возврат;
	//	КонецЕсли;
	//КонецЦикла;
	
	ДополнительныеСвойства.Вставить("ЭтоНовый",    ЭтоНовый());
	ДополнительныеСвойства.Вставить("РежимЗаписи", РежимЗаписи);
	ДополнительныеСвойства.Вставить(
		"УдаляемыеВыполненныеУслуги",
		ОбщиеМеханизмы.СоздатьТаблицуЗначений(
			"УникальныйИдентификаторУслуги: УникальныйИдентификатор,
			|ИсходныйСтатус: ПеречислениеСсылка.СтатусыУслуг,
			|КлючСтрокиСоставаУслуги: Число,
			|Номенклатура: СправочникСсылка.Номенклатура,
			|БезЗаказа: Булево"
		)
	);
	ДополнительныеСвойства.Вставить(
		"УдаляемыеНазначенныеУслуги",
		ОбщиеМеханизмы.СоздатьТаблицуЗначений(
			"УникальныйИдентификаторУслуги: УникальныйИдентификатор,
			|Номенклатура: СправочникСсылка.Номенклатура"
		)
	);
	ДополнительныеСвойства.Вставить(
		"УдаляемыеВыполненныеПриемы",
		ОбщиеМеханизмы.СоздатьТаблицуЗначений(
			"УникальныйИдентификаторПриема: УникальныйИдентификатор"
		)
	);
	ДополнительныеСвойства.Вставить(
		"УдаляемыеНазначенныеПриемы",
		ОбщиеМеханизмы.СоздатьТаблицуЗначений(
			"УникальныйИдентификаторПриема: УникальныйИдентификатор"
		)
	);
	
	// Если не указан текст документа, то читаем его из объекта (понадобится при перепроведении)
	Если НЕ ЭтотОбъект.ДополнительныеСвойства.Свойство("ТекстДокументаПрограммногоСобытия") Тогда	
		CDAДокумент_ = Документы.МедицинскийДокумент.ПолучитьCDAДокумент(ЭтотОбъект);
		ШаблоныМедицинскихДокументовПоликлиника.ОбъектМДДляПрограммныхСобытий(
			ЭтотОбъект, Документы.МедицинскийДокумент.ПолучитьТелоМД(CDAДокумент_)
		);		
	КонецЕсли;
	
	Если ЭтотОбъект.ДополнительныеСвойства.Свойство("ТекстДокументаПрограммногоСобытия") Тогда
		_ПрограммныеСобытия.ОповеститьОПрограммномСобытии(
			"ПередЗаписьюМедицинскогоДокумента",
			ЭтотОбъект, 
			ЭтотОбъект.ДополнительныеСвойства.ТекстДокументаПрограммногоСобытия, 
			ЭтотОбъект.ДополнительныеСвойства.ДанныеПрограммногоСобытия, 
			Отказ, РежимЗаписи, РежимПроведения
		);
		
		Если Отказ Тогда
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	Если РежимЗаписи = РежимЗаписиДокумента.Проведение Тогда
		Найденные_ = ЭтотОбъект.ВыполненныеУслуги.НайтиСтроки(Новый Структура("БезЗаказа", Истина));
		Для Каждого строкаТЧ_ Из Найденные_ Цикл
			Если строкаТЧ_.Номенклатура <> строкаТЧ_.НоменклатураСоставаУслуги Тогда
				// СМУ.
				НайденныеСМУ_ = ЭтотОбъект.ВыполненныеМЭУслуги.НайтиСтроки(Новый Структура("УникальныйИдентификаторУслуги", строкаТЧ_.УникальныйИдентификаторУслуги));
				Если НайденныеСМУ_.Количество() = 0 Тогда
					ВызватьИсключение "В списке выполненных услуг без заказа есть этапы многоэтапной услуги, но нет самой многоэтапной услуги.";
				КонецЕсли;
			КонецЕсли;
		КонецЦикла;
		
		Для Каждого строкаТЧ_ Из ЭтотОбъект.ВыполненныеМЭУслуги Цикл
			НайденныеЭтапы_ = ЭтотОбъект.ВыполненныеУслуги.НайтиСтроки(Новый Структура("УникальныйИдентификаторУслуги", строкаТЧ_.УникальныйИдентификаторУслуги));
			Если НайденныеЭтапы_.Количество() = 0 Тогда
				ВызватьИсключение "В списке выполненных многоэтапных услуг есть услуги для которых в списке выполненных услуг нет этапов.";
			КонецЕсли;
		КонецЦикла;
		
		// Проверка на наличие выполненных услуг
		Если Не ЭтоДокументСтационара()
				И Не ЭтотОбъект.ИзВнешнегоИсточника // Возможно документ загружен в обработке ЗагрузкаВнешнихМД, выполненных услуг в таком случае нет.
				И НЕ ЭтотОбъект.ДополнительныеСвойства.Свойство("ПерепроведениеНаОснованииДокумента")
				И НЕ ЭтотОбъект.ДополнительныеСвойства.Свойство("ОтключитьКонтрольНаличияВыполненныхУслуг")
		Тогда 
			ЕстьВыполненнаяУслуга_ = Ложь;
			Для Каждого Услуга_ Из ЭтотОбъект.ВыполненныеУслуги Цикл 
				Если НЕ Истина = Справочники.Номенклатура.ЯвляетсяПриемом(Услуга_.Номенклатура) Тогда 
					ЕстьВыполненнаяУслуга_ = Истина;
					Прервать;
				КонецЕсли;
			КонецЦикла;
			Если НЕ Истина = ЕстьВыполненнаяУслуга_ Тогда 
				СообщенияПользователю.Показать("МедицинскийДокумент_ОтсутствуютВыполненныеУслуги");
				Отказ = Истина;
				Возврат;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	// При установке готовности документа и перед установкой ЭП проверяем заполнение инструментальных исследований.
	Если Истина 
		И РежимЗаписи = РежимЗаписиДокумента.Проведение 
		И ((ЭтотОбъект.ДополнительныеСвойства.Свойство("СтатусТелаДокумента")
		И ЭтотОбъект.ДополнительныеСвойства.СтатусТелаДокумента = Перечисления.СтатусыМедицинскихДокументов.Готов)
		ИЛИ (ЭтотОбъект.ДополнительныеСвойства.Свойство("ЗаписьПередПодписанием")
		И ЭтотОбъект.ДополнительныеСвойства.ЗаписьПередПодписанием = Истина))
	Тогда
		НоменклатураВыполненныхУслуг_ = ЭтотОбъект.ВыполненныеУслуги.ВыгрузитьКолонку("Номенклатура");
		ТЗИсследования_ = Справочники.ИнструментальноДиагностическиеИсследования.ПолучитьИнструментальныеИсследованияПоНоменклатуре(НоменклатураВыполненныхУслуг_);
		ВыполненныеИсследования_ = ЭтотОбъект.ВыполненныеИнструментальныеИсследования;
		Для Каждого Строка_ Из ВыполненныеИсследования_ Цикл
			Если Не ЗначениеЗаполнено(Строка_.НоменклатураМедицинскихУслуг) Тогда
				ВызватьИсключение "Для инструментального исследования """ + Строка_.Исследование + """ не заполнена номенклатура медицинской услуги.";
			КонецЕсли;
		КонецЦикла;
		Если ТЗИсследования_.Количество() > 0 Тогда
			Для Каждого Номенклатура_ Из НоменклатураВыполненныхУслуг_ Цикл
				ИсследованияПоНоменклатуре_ = ТЗИсследования_.НайтиСтроки(Новый Структура("Номенклатура", Номенклатура_));
				Если ИсследованияПоНоменклатуре_.Количество() = 0 Тогда
					Продолжить;
				КонецЕсли;
				ЗарегистрированоИсследование_ = Ложь;
				Для Каждого Строка_ Из ИсследованияПоНоменклатуре_ Цикл
					ВыполненныеИсследованияПоНоменклатуре_ = ВыполненныеИсследования_.НайтиСтроки(Новый Структура("Исследование", Строка_.Исследование));
					Если ВыполненныеИсследованияПоНоменклатуре_.Количество() > 0 Тогда
						ЗарегистрированоИсследование_ = Истина;
						Прервать;
					КонецЕсли;
				КонецЦикла;
				Если Не ЗарегистрированоИсследование_ Тогда
					Если ИсследованияПоНоменклатуре_.Количество() = 1 Тогда
						ВыбранноеИсследование_ = ИсследованияПоНоменклатуре_[0].Исследование;
						ИонизирующийМетод_ = Справочники.ИнструментальноДиагностическиеИсследования.ВыполняетсяИонизирующимМетодом(ВыбранноеИсследование_);
						Если ИонизирующийМетод_ Тогда
							ВызватьИсключение "Для ионизирующего исследования """ + ВыбранноеИсследование_ + """ не заполнена лучевая нагрузка.";
						Иначе
							НоваяСтрока_ = ЭтотОбъект.ВыполненныеИнструментальныеИсследования.Добавить();
							НоваяСтрока_.Исследование = ВыбранноеИсследование_;
							НоваяСтрока_.НоменклатураМедицинскихУслуг = ИсследованияПоНоменклатуре_[0].НоменклатураМедицинскихУслуг;
						КонецЕсли;
					Иначе
						ВызватьИсключение "Для услуги """ + Номенклатура_ + """ не заполнено инструментально-диагностическое исследование.";
					КонецЕсли;
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
	КонецЕсли;
	
	ПациентМедКарты_ = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ЭтотОбъект.МедицинскаяКарта, "Пациент");
	
	Если ЗначениеЗаполнено(ЭтотОбъект.МедицинскаяКарта) И Не ЗначениеЗаполнено(ЭтотОбъект.Пациент) Тогда
		ЭтотОбъект.Пациент = ПациентМедКарты_;
	КонецЕсли;
	
	Если РежимЗаписи = РежимЗаписиДокумента.ОтменаПроведения Тогда
	
		Справочники.Образцы.КонтрольСтатусаБиоматериала(ЭтотОбъект, ЭтотОбъект.НазначенныеУслуги.ВыгрузитьКолонку("УникальныйИдентификаторУслуги"), Отказ);
		
		Если Отказ = Истина Тогда
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	Если РежимЗаписи = РежимЗаписиДокумента.Проведение Тогда
	
		ДанныеCDA_ = ЗаполнитьДокументПоДаннымCDA();
	
		Если ДанныеCDA_ <> Неопределено Тогда
			
			ШаблонCDA_ = Неопределено;
			ДанныеCDA_.Свойство("ШаблонCDA", ШаблонCDA_);
				
			Если Истина 
				И РежимЗаписи = РежимЗаписиДокумента.Проведение 
				И ((ЭтотОбъект.ДополнительныеСвойства.Свойство("СтатусТелаДокумента")
				И ЭтотОбъект.ДополнительныеСвойства.СтатусТелаДокумента = Перечисления.СтатусыМедицинскихДокументов.Готов)
				ИЛИ (ЭтотОбъект.ДополнительныеСвойства.Свойство("ЗаписьПередПодписанием")
				И ЭтотОбъект.ДополнительныеСвойства.ЗаписьПередПодписанием = Истина))
			Тогда
				
				Если ЗначениеЗаполнено(ШаблонCDA_) Тогда
					ВыполнитьПроверкуСЭМД(Отказ, ШаблонCDA_);
					Если Отказ = Истина Тогда
						Возврат;
					КонецЕсли;
				КонецЕсли;
			КонецЕсли;
			
			Если Истина 
				И РежимЗаписи = РежимЗаписиДокумента.Проведение 
				И ((ЭтотОбъект.ДополнительныеСвойства.Свойство("СтатусТелаДокумента")
				И ЭтотОбъект.ДополнительныеСвойства.СтатусТелаДокумента = Перечисления.СтатусыМедицинскихДокументов.НеГотов))
			Тогда
				
				Если ЗначениеЗаполнено(ШаблонCDA_) Тогда
					ВыполнитьПроверкуСЭМД(Отказ, ШаблонCDA_, Ложь);
					Если Отказ = Истина Тогда
						Возврат;
					КонецЕсли;
				КонецЕсли;
				
			КонецЕсли;
		КонецЕсли;
	
		УстановкаТранзакционнойБлокировки();
		
		Если ЗначениеЗаполнено(ЭтотОбъект.МедицинскаяКарта) И ПациентМедКарты_ <> ЭтотОбъект.Пациент Тогда
			Отказ = Истина;
			СообщенияПользователю.Показать(
				"ОформлениеЗаказов_ПациентКартыИПациентВРеквизитеНеСовпадают",
				Новый Структура("ПациентКарты,Пациент", ЭтотОбъект.МедицинскаяКарта.Пациент, ЭтотОбъект.Пациент),
				Истина
			);
			Возврат;
		КонецЕсли;
		
		Если Исполнители.Количество() > 0 Тогда
			Найденные = Исполнители.НайтиСтроки(Новый Структура("ОтветственныйЗаНазначение",Истина));
			Если Найденные.Количество() <> 1 Тогда
				СообщенияПользователю.ПоказатьСВыводомВЖурналРегистрации("МедицинскийДокумент_ОдинИсполнительДолженБытьОтветственным");
				Отказ = Истина;
				Возврат;
			ИначеЕсли Найденные.Количество() > 0 Тогда
				Если ЗначениеЗаполнено(Найденные[0].УникальныйИдентификаторУслуги) Тогда
					СообщенияПользователю.Показать(
						"МедицинскийДокумент_ДляОтветственногоИсполнителяЗаполнениеРеквизитаУИУНеДопускается"
					);
					Отказ = Истина;
					Возврат;
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
		
		Отбор_ = Новый Структура("СубъектПоказателяЗдоровья");
		Для Каждого строкаТЧ_ Из ЭтотОбъект.ДиагнозыПоМКБ10 Цикл
			Основной_ = 0;
			ЗаполнитьЗначенияСвойств(Отбор_, строкаТЧ_);
			Найденные_ = ЭтотОбъект.ДиагнозыПоМКБ10.НайтиСтроки(Отбор_);
			Для Каждого Найденная_ Из Найденные_ Цикл
				Если Найденная_.ТипДиагноза = Перечисления.ТипыДиагнозовПациента.ОсновноеЗаболевание
					И Найденная_.Действует
				Тогда
					Основной_ = Основной_ + 1;
				КонецЕсли;
			КонецЦикла;
			
			Если Основной_ > 1 Тогда
				СообщенияПользователю.Показать("МедицинскийДокумент_ДолженБытьОдинОсновнойДиагноз");
				Отказ = Истина;
				Возврат;
			КонецЕсли;
		КонецЦикла;
		
		Для Каждого строкаТЧ_ Из ЭтотОбъект.НазначенныеУслуги Цикл
			Если ЗначениеЗаполнено(строкаТЧ_.Соглашение) И Не ЗначениеЗаполнено(строкаТЧ_.Договор) Тогда
				// Заполним договор
				строкаТЧ_.Договор = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(строкаТЧ_.Соглашение, "Договор");
			КонецЕсли;
		КонецЦикла;
		
		Для Каждого строкаТЧ_ Из ЭтотОбъект.ВыполненныеУслуги Цикл 
			Если строкаТЧ_.Количество = 0 Тогда
				строкаТЧ_.Количество = 1;
			КонецЕсли;
		КонецЦикла;

		Для Каждого строкаТЧ_ Из ЭтотОбъект.НазначенныеУслуги Цикл 
			Если строкаТЧ_.Количество = 0 Тогда
				строкаТЧ_.Количество = 1;
			КонецЕсли;
		КонецЦикла;
		
		ПроцедурныйКабинет.УдалитьНеАктуальныеЗаписиИзТаблицыВыбранногоБиоматериала(ЭтотОбъект.НазначенныеУслуги, ЭтотОбъект.ВыбранныйБиоматериал);
		ПроцедурныйКабинет.УдалитьНеАктуальныеЗаписиИзТаблицыИнформацияКБиоматериалу(ЭтотОбъект.ВыбранныйБиоматериал, ЭтотОбъект.ИнформацияКБиоматериалу);

	КонецЕсли;
	
	Если ЭтоПерепроведение Или РежимЗаписи = РежимЗаписиДокумента.ОтменаПроведения Тогда
		СоздатьВременнуюТаблицуВыполненныеУслуги();
	КонецЕсли;
	
	// сохраним данные статусов услуг, до изменения
	Документы.МедицинскийДокумент.СоздатьВременнуюТаблицуСтатусыУслуг(ЭтотОбъект.ВыполненныеУслуги.ВыгрузитьКолонку("УникальныйИдентификаторУслуги"), ЭтотОбъект.ДополнительныеСвойства);
	
	Если ЭтоПерепроведение Тогда
	
		Если КонтрольОтсутствияИзмененийУсловийОплатыУжеОплаченныхУслуг() Тогда
			Отказ = Истина;
		КонецЕсли;
	
		Отбор_ = Новый Структура("УникальныйИдентификаторУслуги,КлючСтрокиСоставаУслуги");
		Для Каждого УслугаСсылки_ Из ЭтотОбъект.Ссылка.ВыполненныеУслуги Цикл
			ЗаполнитьЗначенияСвойств(Отбор_, УслугаСсылки_);
			УслугиОбъекта_ = ЭтотОбъект.ВыполненныеУслуги.НайтиСтроки(Отбор_);
			Если УслугиОбъекта_.Количество() = 0 Тогда
				ЗаполнитьЗначенияСвойств(
					ДополнительныеСвойства.УдаляемыеВыполненныеУслуги.Добавить(), УслугаСсылки_
				);
			КонецЕсли;
		КонецЦикла;

		Отбор_ = Новый Структура("УникальныйИдентификаторУслуги");
		Для Каждого УслугаСсылки_ Из ЭтотОбъект.Ссылка.ВыполненныеМЭУслуги Цикл
			ЗаполнитьЗначенияСвойств(Отбор_, УслугаСсылки_);
			УслугиОбъекта_ = ЭтотОбъект.ВыполненныеМЭУслуги.НайтиСтроки(Отбор_);
			Если УслугиОбъекта_.Количество() = 0 Тогда
				ЗаполнитьЗначенияСвойств(
					ДополнительныеСвойства.УдаляемыеВыполненныеУслуги.Добавить(), УслугаСсылки_
				);
			КонецЕсли;
		КонецЦикла;
		
		// Удалим исполнителей из ТЧ Исполнители для удаленных выполненных услуг.
		Отбор_ = Новый Структура("УникальныйИдентификаторУслуги,КлючСтрокиСоставаУслуги");
		Для Каждого УдаляемаяУслуга_ Из ДополнительныеСвойства.УдаляемыеВыполненныеУслуги Цикл
			ЗаполнитьЗначенияСвойств(Отбор_, УдаляемаяУслуга_);
			ИсполнителиУдаляемойУслуги_ = ЭтотОбъект.Исполнители.НайтиСтроки(Отбор_);
			Для Каждого строкаТЧИсполнителей_ Из ИсполнителиУдаляемойУслуги_ Цикл
				ЭтотОбъект.Исполнители.Удалить(строкаТЧИсполнителей_);
			КонецЦикла;
		КонецЦикла;
		
		Отбор_ = Новый Структура("УникальныйИдентификаторУслуги");
		Для Каждого УслугаСсылки_ Из ЭтотОбъект.Ссылка.НазначенныеУслуги Цикл
			ЗаполнитьЗначенияСвойств(Отбор_, УслугаСсылки_);
			УслугиОбъекта_ = ЭтотОбъект.НазначенныеУслуги.НайтиСтроки(Отбор_);
			Если УслугиОбъекта_.Количество() = 0 Тогда
				ЗаполнитьЗначенияСвойств(
					ДополнительныеСвойства.УдаляемыеНазначенныеУслуги.Добавить(), УслугаСсылки_
				);
			КонецЕсли;
		КонецЦикла;
		
		Отбор_ = Новый Структура("УникальныйИдентификаторПриема");
		Для Каждого ПриемСсылки_ Из ЭтотОбъект.Ссылка.ВыполненныеПриемы Цикл
			ЗаполнитьЗначенияСвойств(Отбор_, ПриемСсылки_);
			ПриемыОбъекта_ = ЭтотОбъект.ВыполненныеПриемы.НайтиСтроки(Отбор_);
			Если ПриемыОбъекта_.Количество() = 0 Тогда
				ЗаполнитьЗначенияСвойств(
					ДополнительныеСвойства.УдаляемыеВыполненныеПриемы.Добавить(), ПриемСсылки_
				);
			КонецЕсли;
		КонецЦикла;
		
		Отбор_ = Новый Структура("УникальныйИдентификаторПриема");
		Для Каждого ПриемСсылки_ Из ЭтотОбъект.Ссылка.НазначенныеПриемы Цикл
			ЗаполнитьЗначенияСвойств(Отбор_, ПриемСсылки_);
			ПриемыОбъекта_ = ЭтотОбъект.НазначенныеПриемы.НайтиСтроки(Отбор_);
			Если ПриемыОбъекта_.Количество() = 0 Тогда
				ЗаполнитьЗначенияСвойств(
					ДополнительныеСвойства.УдаляемыеНазначенныеПриемы.Добавить(), ПриемСсылки_
				);
			КонецЕсли;
		КонецЦикла;
		
		Отбор_ = Новый Структура("УникальныйИдентификаторУслуги");
		Для Каждого УслугаОбъекта_ Из ЭтотОбъект.ВыполненныеУслуги Цикл
			ЗаполнитьЗначенияСвойств(Отбор_, УслугаОбъекта_);
			УслугиСсылки_ = ЭтотОбъект.Ссылка.ВыполненныеУслуги.НайтиСтроки(Отбор_);
			Если УслугиСсылки_.Количество() = 0 Тогда
				УслугаОбъекта_.ИсходныйСтатус = УправлениеЗаказамиУслугами.ПолучитьСтатусУслуги(
					УслугаОбъекта_.УникальныйИдентификаторУслуги
				);
			КонецЕсли;
		КонецЦикла;
		
		Если ЭтоДокументСтационара() Тогда
			Справочники.Образцы.КонтрольСтатусаБиоматериала(ЭтотОбъект, ДополнительныеСвойства.УдаляемыеНазначенныеУслуги.ВыгрузитьКолонку("УникальныйИдентификаторУслуги"), Отказ);
		КонецЕсли;
		
	ИначеЕсли РежимЗаписи = РежимЗаписиДокумента.Проведение Тогда
		Для Каждого Услуга_ из ЭтотОбъект.ВыполненныеУслуги Цикл 
			Услуга_.ИсходныйСтатус = УправлениеЗаказамиУслугами.ПолучитьСтатусУслуги(
				Услуга_.УникальныйИдентификаторУслуги
			);
		КонецЦикла;
	КонецЕсли;

	
	Если РежимЗаписи = РежимЗаписиДокумента.Проведение Тогда
		// Заполним реквизит Организация.
		
		Если Не ЗначениеЗаполнено(ЭтотОбъект.Организация) Тогда
			Отбор_ = Новый Структура("ОтветственныйЗаНазначение",Истина);
			Результат_ = ЭтотОбъект.Исполнители.НайтиСтроки(Отбор_);
			Если Результат_.Количество() > 0 Тогда
				ЭтотОбъект.Организация = ОбщиеМеханизмы.ЗначениеРеквизитаОбъекта(Результат_[0].Исполнитель, "Подразделение.Владелец");
			КонецЕсли;
		КонецЕсли;

		Если Не ЗначениеЗаполнено(ЭтотОбъект.Организация) Тогда
			// Возьмем его из заказа.
			Запрос = Новый Запрос;
			Запрос.Текст =
			"ВЫБРАТЬ РАЗЛИЧНЫЕ
			|	СтатусыУслуг.Заказ.Организация КАК Организация
			|ИЗ
			|	РегистрСведений.СтатусыУслуг КАК СтатусыУслуг
			|ГДЕ
			|	СтатусыУслуг.УникальныйИдентификаторУслуги В(&УникальныеИдентификаторыУслуг)
			|	И СтатусыУслуг.Заказ.Организация <> ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)";
			
			УникальныеИдентификаторыУслуг_ = ЭтотОбъект.ВыполненныеУслуги.ВыгрузитьКолонку("УникальныйИдентификаторУслуги");
			Запрос.УстановитьПараметр("УникальныеИдентификаторыУслуг", УникальныеИдентификаторыУслуг_);
			Организации_ = Запрос.Выполнить().Выгрузить();
			Если Организации_.Количество() > 0 Тогда
				ЭтотОбъект.Организация = Организации_[0].Организация;
			КонецЕсли;
		КонецЕсли;
		
		Если Не ЗначениеЗаполнено(ЭтотОбъект.УсловияОказанияПомощи) И ЗначениеЗаполнено(ЭтотОбъект.МедицинскаяКарта) Тогда
			УсловияОказанияПомощи_ = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ЭтотОбъект.МедицинскаяКарта, "ТипМедицинскойКарты.УсловияОказанияПомощи");
			ЭтотОбъект.УсловияОказанияПомощи = УсловияОказанияПомощи_;
		КонецЕсли;
		Если Не ЗначениеЗаполнено(ЭтотОбъект.УсловияОказанияПомощи) И Не ЗначениеЗаполнено(ЭтотОбъект.МедицинскаяКарта) Тогда
			// МД созданный в приемнике? Найдем МД назначения, возьмем его условия мед помощи
			Запрос_ = Новый Запрос(
				"ВЫБРАТЬ ПЕРВЫЕ 1
				|	ДокументМедицинскийДокумент.УсловияОказанияПомощи
				|ИЗ
				|	РегистрСведений.СтатусыУслуг КАК СтатусыУслуг
				|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.МедицинскийДокумент КАК ДокументМедицинскийДокумент
				|		ПО СтатусыУслуг.Назначение = ДокументМедицинскийДокумент.Ссылка
				|ГДЕ
				|	СтатусыУслуг.УникальныйИдентификаторУслуги В (&СписокУИД)");
			
			Запрос_.УстановитьПараметр("СписокУИД", ЭтотОбъект.ВыполненныеУслуги.ВыгрузитьКолонку("УникальныйИдентификаторУслуги"));
			Выборка_ = Запрос_.Выполнить().Выбрать();
			Если Выборка_.Следующий() Тогда
				ЭтотОбъект.УсловияОказанияПомощи = Выборка_.УсловияОказанияПомощи;
			КонецЕсли;
			
			Если Не ЗначениеЗаполнено(ЭтотОбъект.УсловияОказанияПомощи) Тогда
				ЭтотОбъект.УсловияОказанияПомощи = Перечисления.УсловияМедицинскойПомощи.АмбулаторноПоликлиническая;
			КонецЕсли;
		КонецЕсли;
		
		// Проверим заполнены ли использованные препараты.
		Если ( ЭтотОбъект.CDAДокументы.Количество() = 0 // Для документов с ШМД проверяем препараты только когда указывают готовность.
				Или 
				ЭтотОбъект.ДополнительныеСвойства.Свойство("СтатусТелаДокумента")
					И ЭтотОбъект.ДополнительныеСвойства.СтатусТелаДокумента = Перечисления.СтатусыМедицинскихДокументов.Готов
			)
			И Не Документы.МедицинскийДокумент.ПроверитьЗаполненностьИспользованныхПрепаратов(ЭтотОбъект)
		Тогда
			Отказ = Истина;
		КонецЕсли;
	КонецЕсли;
	
	// Проверим наличие МКБ из ДанныеОнкологии в ДиагнозыПоМКБ10.
	Если РежимЗаписи = РежимЗаписиДокумента.Проведение Или РежимЗаписи = РежимЗаписиДокумента.Запись Тогда
		Для Каждого строкаТЧ_ Из ЭтотОбъект.ДанныеОнкологии Цикл
			Найденные_ = ЭтотОбъект.ДиагнозыПоМКБ10.НайтиСтроки(Новый Структура("МКБ10", строкаТЧ_.МКБ10));
			НайденныеПодозрения_ = ЭтотОбъект.ДиагнозыПоМКБ10.НайтиСтроки(Новый Структура("ПодозрениеНаОнкологию, МКБ10НаКоторыйПодозрение", Истина, строкаТЧ_.МКБ10));
			
			Если Найденные_.Количество() = 0 И НайденныеПодозрения_.Количество() = 0 Тогда
				Отказ = Истина;
				
				ТекстОшибки_ = стрШаблон("Диагноз ""%1"" указанный в данных онкологии обязан присутствовать в табличной части диагнозов.", строкаТЧ_.МКБ10);
				
				Сообщить(ТекстОшибки_);

				ЗаписатьВЖурналРегистрации(ТекстОшибки_, УровеньЖурналаРегистрации.Ошибка);
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	Для Каждого строкаТЧ_ Из ЭтотОбъект.ДиагнозыПоМКБ10 Цикл
		Если Не строкаТЧ_.ПодозрениеНаОнкологию Тогда
			строкаТЧ_.МКБ10НаКоторыйПодозрение = Неопределено;
		КонецЕсли;
	КонецЦикла;
	
	ПроведениеСерверПереопределяемый.ПередЗаписью(ЭтотОбъект, Отказ, РежимЗаписи, РежимПроведения);
	
	// При повторной записи полученного из мобильного приложения документа нужно сменить обработку формирования
	Если (ЭтотОбъект.ОбработкаФормирования = Перечисления.ОбработкиФормированияМД.МобильноеПриложениеФДС 
		ИЛИ ЭтотОбъект.ОбработкаФормирования = Перечисления.ОбработкиФормированияМД.МобильноеПриложениеФМД)
		И НЕ ЭтотОбъект.ДополнительныеСвойства.Свойство("ОбменСМобильнымПриложением")
	Тогда
		ОбработкаФормирования_ = Неопределено;
		Если ЭтотОбъект.ОбработкаФормирования = Перечисления.ОбработкиФормированияМД.МобильноеПриложениеФДС Тогда
			ОбработкаФормирования_ = Перечисления.ОбработкиФормированияМД.ОбработкаФДС;
		ИначеЕсли ЭтотОбъект.ОбработкаФормирования = Перечисления.ОбработкиФормированияМД.МобильноеПриложениеФМД Тогда
			ОбработкаФормирования_ = Перечисления.ОбработкиФормированияМД.ОбработкаФМД;
		КонецЕсли;

		Если ЗначениеЗаполнено(ОбработкаФормирования_) Тогда
			ЭтотОбъект.ОбработкаФормирования = ОбработкаФормирования_;
		КонецЕсли;
	КонецЕсли; 
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	Если ЭтотОбъект.ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	РегистрыСведений.СтатусыМедицинскихДокументов.УстановитьРеквизиты(ЭтотОбъект.Ссылка, ЭтотОбъект.ДополнительныеСвойства);

	Если НазначенныеУслуги.Количество() > 0 Тогда
		Отказ = Не УправлениеЗаказамиУслугами.ПереходСтатусовУслуг(ЭтотОбъект,"НазначенныеУслуги");
		Если Отказ Тогда
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	Если ВыполненныеУслуги.Количество() > 0 Тогда
		Отказ = Не УправлениеЗаказамиУслугами.ПереходСтатусовУслуг(ЭтотОбъект, "ВыполненныеУслуги");
		Если Отказ Тогда
			Возврат;
		КонецЕсли;
	КонецЕсли;

	Если ВыполненныеМЭУслуги.Количество() > 0 Тогда
		Отказ = Не УправлениеЗаказамиУслугами.ПереходСтатусовУслуг(ЭтотОбъект, "ВыполненныеМЭУслуги");
		Если Отказ Тогда
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	Если Не ЭтотОбъект.ДополнительныеСвойства.Свойство("УслугиНеВыполнены") Тогда
		ЭтотОбъект.ДополнительныеСвойства.Вставить("УслугиНеВыполнены", 
				РегистрыСведений.СтатусыМедицинскихДокументов.ПолучитьРеквизитУслугиНеВыполнены(ЭтотОбъект.Ссылка)
		);
	КонецЕсли;
	
	УдалитьДвиженияВыполненныхУслуг(Отказ, ДополнительныеСвойства.УдаляемыеВыполненныеУслуги);
	УдалитьДвиженияНазначенныхУслуг(Отказ, ДополнительныеСвойства.УдаляемыеНазначенныеУслуги);
	
	ПроведениеСервер.ИнициализироватьДополнительныеСвойстваДляПроведения(Ссылка, ДополнительныеСвойства, РежимПроведения);
	
	Документы.МедицинскийДокумент.ИнициализироватьДанныеДокумента(Ссылка, ДополнительныеСвойства);
	
	ПроведениеСерверПереопределяемый.ИнициализироватьДанныеДокумента(Ссылка, ДополнительныеСвойства);
	
	ПроведениеСервер.ПодготовитьНаборыЗаписейКРегистрацииДвижений(ЭтотОбъект);
	
	ВыполнитьДвиженияСменныеЗадания(Отказ);
	ВыполнитьДвиженияМестаВыполненияУслуг(Отказ);
	ВыполнитьДвиженияИсполнителиУслуг(Отказ);
	ВыполнитьДвиженияДиагнозыПоМКБ10(Отказ);
	ВыполнитьДвиженияДиагнозыПоМКБПсихиатрия(Отказ);
	ВыполнитьДвиженияПоказателиЗдоровья(Отказ);
	ВыполнитьДвиженияМедицинскиеПрограммы(Отказ);
	ВыполнитьДвиженияУслугиСтандартовМедицинскойПомощи(Отказ);
	ВыполнитьДвиженияСтатусыБиоматериала(Отказ);
	ВыполнитьДвиженияРезультатыИсследованийЗНО(Отказ);
	ЗарегистрироватьСогласуемыеУслуги(Отказ);
	ЗарегистрироватьСогласуемыеЛекарственныеНазначения(Отказ);
	УдалитьРегистрациюВыполненныхПриемов(
		Отказ,
		ЭтотОбъект.ДополнительныеСвойства.УдаляемыеВыполненныеПриемы.ВыгрузитьКолонку("УникальныйИдентификаторПриема")
	);
	УдалитьРегистрациюВыполненныхПриемов(
		Отказ,
		ЭтотОбъект.ДополнительныеСвойства.УдаляемыеВыполненныеУслуги.ВыгрузитьКолонку("УникальныйИдентификаторУслуги")
	);
	УдалитьРегистрациюНазначенныхПриемов(
		Отказ,
		ЭтотОбъект.ДополнительныеСвойства.УдаляемыеНазначенныеПриемы.ВыгрузитьКолонку("УникальныйИдентификаторПриема")
	);
	УдалитьРегистрациюНазначенныхПриемов(
		Отказ,
		ЭтотОбъект.ДополнительныеСвойства.УдаляемыеНазначенныеУслуги.ВыгрузитьКолонку("УникальныйИдентификаторУслуги")
	);
	ЗарегистрироватьВыполненныеПриемы(Отказ);
	ЗарегистрироватьНазначенныеПриемы(Отказ);
	ЗарегистрироватьКритерииКачества(Отказ);
	ЗарегистрироватьИсследованияПоФСИДИ(Отказ);
	
	Если Истина = Отказ Тогда 
		Возврат;
	КонецЕсли;
	
	УправлениеЗаказамиУслугами.ОтразитьСтатусыУслугФинансовыеДанные(ДополнительныеСвойства, Ссылка);

	ВзаиморасчетыСервер.ОтразитьРасчетыСКлиентами(ДополнительныеСвойства, Движения, Отказ);
	
	ДоходыИРасходыСервер.ОтразитьВыручкуОтРеализации(ДополнительныеСвойства, Движения, Отказ);

	ПроцедурныйКабинет.ЗаписатьВРегистрыВыбранныйБиоматериалИДопИнформациюКНему(ДополнительныеСвойства, Движения, Отказ);
	
	ПроведениеСерверПереопределяемый.ВыполнитьДвиженияРегистров(Ссылка, ДополнительныеСвойства, Движения, Отказ);

	РозничныеПродажи.ОтразитьФискальныеСтроки(ДополнительныеСвойства, Движения, Отказ);
	
	СформироватьСписокРегистровДляКонтроля();
	
	ПроведениеСервер.ЗаписатьНаборыЗаписей(ЭтотОбъект);
	
	ПроведениеСервер.ВыполнитьКонтрольРезультатовПроведения(ЭтотОбъект, Отказ);
	
	ПроведениеСерверПереопределяемый.ВыполнитьКонтрольРезультатовПроведения(ЭтотОбъект, Отказ);
	
	ПроведениеСервер.ОчиститьДополнительныеСвойстваДляПроведения(ДополнительныеСвойства);
	
	Если ЭтотОбъект.ДополнительныеСвойства.Свойство("ТекстДокументаПрограммногоСобытия") Тогда
		_ПрограммныеСобытия.ОповеститьОПрограммномСобытии(
			"ОбработкаПроведенияМедицинскогоДокумента",
			ЭтотОбъект, 
			ЭтотОбъект.ДополнительныеСвойства.ТекстДокументаПрограммногоСобытия, 
			ЭтотОбъект.ДополнительныеСвойства.ДанныеПрограммногоСобытия, 
			Отказ, РежимПроведения
		);
		
		Если Отказ Тогда
			Возврат;
		КонецЕсли;
	КонецЕсли;

	Если Отказ = Ложь Тогда
		Если ЭтоДокументСтационара() Тогда
			ВзаимодействияПоликлиника.СоздатьУведомленияОВыполненииУслугЦИТО(ЭтотОбъект.Ссылка);	
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

////
 // Процедура: ОбработкаУдаленияПроведения
 //   Обработчик удаления проведения.
 //   Изменяет статусы услуг в регистре у услуг из табличной части ВыполненныеУслуги.
 //
 // Параметры:
 //   Отказ {Булево}
 //     Отказ от отмены проведения.
 ///
Процедура ОбработкаУдаленияПроведения(Отказ)
	УдалитьДвиженияВыполненныхУслуг(Отказ, ЭтотОбъект.ВыполненныеУслуги);
	УдалитьДвиженияНазначенныхУслуг(Отказ, ЭтотОбъект.НазначенныеУслуги);
	
	УдалитьДвиженияСтатусыМедицинскихДокументов(Отказ);
	УдалитьРегистрациюСогласуемыхУслуг(Отказ);
	УдалитьРегистрациюСогласуемыхЛекарственныхНазначений(Отказ);
	УдалитьРегистрациюВыполненныхПриемов(Отказ, ЭтотОбъект.ВыполненныеПриемы.ВыгрузитьКолонку("УникальныйИдентификаторПриема"));
	УдалитьРегистрациюВыполненныхПриемов(Отказ, ЭтотОбъект.ВыполненныеУслуги.ВыгрузитьКолонку("УникальныйИдентификаторУслуги"));
	УдалитьРегистрациюНазначенныхПриемов(Отказ, ЭтотОбъект.НазначенныеПриемы.ВыгрузитьКолонку("УникальныйИдентификаторПриема"));
	УдалитьРегистрациюНазначенныхПриемов(Отказ, ЭтотОбъект.НазначенныеУслуги.ВыгрузитьКолонку("УникальныйИдентификаторУслуги"));
	УдалитьДвиженияИсследованияПоФСИДИ(Отказ);

	// Инициализация дополнительных свойств для проведения документа
	ПроведениеСервер.ИнициализироватьДополнительныеСвойстваДляПроведения(Ссылка, ДополнительныеСвойства);
	
	// Подготовка наборов записей
	ПроведениеСервер.ПодготовитьНаборыЗаписейКРегистрацииДвижений(ЭтотОбъект);
	
	// Удалим движения не подчиненных регистров
	УправлениеЗаказамиУслугами.ОтразитьСтатусыУслугФинансовыеДанные(ДополнительныеСвойства, Ссылка);

	// Запись наборов записей
	ПроведениеСервер.ЗаписатьНаборыЗаписей(ЭтотОбъект);
	
	ПроведениеСерверПереопределяемый.ВыполнитьКонтрольРезультатовПроведения(ЭтотОбъект, Отказ);
	
	ПроведениеСервер.ОчиститьДополнительныеСвойстваДляПроведения(ДополнительныеСвойства);
	
	Если ЭтотОбъект.ДополнительныеСвойства.Свойство("ТекстДокументаПрограммногоСобытия") Тогда
		_ПрограммныеСобытия.ОповеститьОПрограммномСобытии(
			"ОбработкаУдаленияПроведенияМедицинскогоДокумента",
			ЭтотОбъект, 
			ЭтотОбъект.ДополнительныеСвойства.ТекстДокументаПрограммногоСобытия, 
			ЭтотОбъект.ДополнительныеСвойства.ДанныеПрограммногоСобытия, 
			Отказ
		);
		
		Если Отказ Тогда
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура СформироватьСписокРегистровДляКонтроля()

	Массив = Новый Массив;

	Массив.Добавить(Движения.РасчетыСКлиентами);

	ДополнительныеСвойства.ДляПроведения.Вставить("РегистрыДляКонтроля", Массив);
	
КонецПроцедуры // СформироватьСписокРегистровДляКонтроля()

Процедура ВыполнитьДвиженияСменныеЗадания(Отказ)
	Перем ОсновнойИсполнитель_, ОсновноеМестоВыполнения_;
	
	ОсновнойИсполнитель_ = Неопределено;
	СМП_ = Неопределено;
	Для Каждого Исполнитель_ Из ЭтотОбъект.Исполнители Цикл
		Если НЕ ЗначениеЗаполнено(ОсновнойИсполнитель_)
				И Исполнитель_.ОтветственныйЗаНазначение = Истина
		Тогда
			ОсновнойИсполнитель_ = Исполнитель_.Исполнитель;
		КонецЕсли;
		Если НЕ ЗначениеЗаполнено(СМП_)
				И Исполнитель_.Должность.ВидМедицинскойДолжности = Перечисления.ВидыМедицинскихДолжностей.СреднийМедицинскийПерсонал
				И Не ЗначениеЗаполнено(Исполнитель_.УникальныйИдентификаторУслуги)
		Тогда
			СМП_ = Исполнитель_.Исполнитель;
		КонецЕсли;
	КонецЦикла;
	Если НЕ ЗначениеЗаполнено(СМП_) Тогда
		СМП_ = ОсновнойИсполнитель_;
	КонецЕсли;
	
	Если ЭтотОбъект.МестаВыполнения.Количество() > 0 Тогда
		ОсновноеМестоВыполнения_ = ЭтотОбъект.МестаВыполнения[0].МестоВыполнения;
	КонецЕсли;
	
	Планирование_ = Неопределено;
	Для Каждого Услуга_ Из ЭтотОбъект.ВыполненныеУслуги Цикл
		Если Не Услуга_.БезЗаказа Тогда
			Планирование_ = ПланированиеУслуг.ПолучитьДанныеПланированияУслуги(
				Услуга_.УникальныйИдентификаторУслуги, Услуга_.КлючСтрокиСоставаУслуги
			);
			Если Планирование_ <> Неопределено Тогда
				Прервать;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;

	Если Планирование_ = Неопределено Тогда
		Если ЭтотОбъект.ВыполненныеПриемы.Количество() > 0 Тогда
			Планирование_ = ПланированиеУслуг.ПолучитьДанныеПланированияПриема(
				ЭтотОбъект.ВыполненныеПриемы[0].УникальныйИдентификаторПриема
			);	
		КонецЕсли;
	КонецЕсли;
	
	Если Планирование_ <> Неопределено Тогда
		Планирование_.Вставить("ДокументПланирования", ЭтотОбъект.Ссылка);	
		Планирование_.Вставить("КлючСтрокиЭтапа", 1);	
		Планирование_.Вставить("КлючСтрокиМестаВыполненияЭтапа", 1);	
	КонецЕсли;
	
	Запрос_ = Новый Запрос(
		"ВЫБРАТЬ
		|	&Ссылка КАК ДокументВыполнения,
		|	МедицинскийДокументВыполненныеУслуги.ДатаВыполнения,
		|	МедицинскийДокументВыполненныеУслуги.КлючСтрокиСоставаУслуги,
		|	МедицинскийДокументВыполненныеУслуги.УникальныйИдентификаторУслуги,
		|	МедицинскийДокументВыполненныеУслуги.Номенклатура,
		|	МедицинскийДокументВыполненныеУслуги.НоменклатураСоставаУслуги,
		|	МедицинскийДокументВыполненныеУслуги.Спецификация КАК СпецификацияМедицинскойУслуги,
		|	МедицинскийДокументВыполненныеУслуги.ПричинаОтменыУслуги,
		|	ВЫБОР МедицинскийДокументВыполненныеУслуги.НоменклатураСоставаУслуги.ИсполнительМедицинскойУслуги
		|		КОГДА ЗНАЧЕНИЕ(Перечисление.ИсполнителиМедицинскихУслуг.СМП)
		|			ТОГДА &СМП
		|		ИНАЧЕ &ОсновнойИсполнитель
		|	КОНЕЦ КАК ОсновнойИсполнитель,
		|	&ОсновноеМестоВыполнения КАК ОсновноеМестоВыполнения,
		|	МедицинскийДокументВыполненныеУслуги.БезЗаказа
		|ИЗ
		|	Документ.МедицинскийДокумент.ВыполненныеУслуги КАК МедицинскийДокументВыполненныеУслуги
		|ГДЕ
		|	МедицинскийДокументВыполненныеУслуги.Ссылка = &Ссылка"
	);
	
	Запрос_.УстановитьПараметр("Ссылка", ЭтотОбъект.Ссылка);
	Запрос_.УстановитьПараметр("ОсновнойИсполнитель", ОсновнойИсполнитель_);
	Запрос_.УстановитьПараметр("СМП", СМП_);
	Запрос_.УстановитьПараметр("ОсновноеМестоВыполнения", ОсновноеМестоВыполнения_);

	Выборка_ = Запрос_.Выполнить().Выбрать();
	
	Пока Выборка_.Следующий() Цикл
		Если Выборка_.ПричинаОтменыУслуги = Справочники.ПричиныОтменыУслугЗаказаПациента.ЗаменаУслугиВМД
			И Выборка_.Номенклатура.ВидМедицинскойУслуги = Перечисления.ВидыМедицинскихУслуг.Многоэтапная
		Тогда
			НаборЗаписей_ = РегистрыСведений.СменныеЗадания.СоздатьНаборЗаписей();
			НаборЗаписей_.Отбор.УникальныйИдентификаторУслуги.Установить(Выборка_.УникальныйИдентификаторУслуги);
			НаборЗаписей_.Записать(Истина);
		Иначе
			НаборЗаписей_ = РегистрыСведений.СменныеЗадания.СоздатьНаборЗаписей();
			НаборЗаписей_.Отбор.УникальныйИдентификаторУслуги.Установить(Выборка_.УникальныйИдентификаторУслуги);
			НаборЗаписей_.Отбор.КлючСтрокиСоставаУслуги.Установить(Выборка_.КлючСтрокиСоставаУслуги);
			НаборЗаписей_.Прочитать();
			Если НаборЗаписей_.Количество() = 0 Тогда
				НовЗапись = НаборЗаписей_.Добавить();
			КонецЕсли;

			Если ЗначениеЗаполнено(НаборЗаписей_[0].ДатаВыполнения) 
				И НаборЗаписей_[0].ДокументВыполнения <> ЭтотОбъект.Ссылка
				И ЗначениеЗаполнено(НаборЗаписей_[0].ДокументВыполнения)
			Тогда
				Отказ = Истина;
				СообщенияПользователю.ПоказатьСВыводомВЖурналРегистрации("МедицинскийДокумент_УслугаУжеВыполнена",
							Новый Структура("Номенклатура",Выборка_.НоменклатураСоставаУслуги)
						);
				Возврат;
			КонецЕсли;
			
			Если Не ЭтотОбъект.ДополнительныеСвойства.УслугиНеВыполнены Тогда

				ЗаполнитьЗначенияСвойств(
					НаборЗаписей_[0], 
					Выборка_, 
					"Номенклатура,НоменклатураСоставаУслуги,СпецификацияМедицинскойУслуги,"
					"ДатаВыполнения,ОсновнойИсполнитель,ОсновноеМестоВыполнения,ДокументВыполнения,"
					"УникальныйИдентификаторУслуги,КлючСтрокиСоставаУслуги"
				);
				
				Если Выборка_.БезЗаказа И Планирование_ <> Неопределено 
					И Не ЗначениеЗаполнено(НаборЗаписей_[0].МедицинскоеРабочееМесто)
				Тогда
					// Используем планирование услуги из заказа.
					Исключения_ = "";
					Если Планирование_.Свойство("Номенклатура") Тогда
						Исключения_ = "Номенклатура";
					КонецЕсли;
					ЗаполнитьЗначенияСвойств(НаборЗаписей_[0], Планирование_, , Исключения_);
				КонецЕсли;
				
				НаборЗаписей_.Записать();
			КонецЕсли;
		КонецЕсли;

	КонецЦикла;
	
	Если ЭтоДокументСтационара() Тогда
		Для Каждого Услуга_ Из ЭтотОбъект.НазначенныеУслуги Цикл
			Если Истина = Услуга_.ОтказОтВмешательства Тогда
				Продолжить;
			КонецЕсли;
			Если Истина = Справочники.Номенклатура.ЯвляетсяПриемом(Услуга_.Номенклатура) Тогда 
				Продолжить;
			КонецЕсли;
			ЗарегистрироватьСменныеЗаданияНазначеннойУслуги(Услуга_);
		КонецЦикла;
	КонецЕсли;

КонецПроцедуры

Процедура ВыполнитьДвиженияМестаВыполненияУслуг(Отказ)
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	МедицинскийДокументМестаВыполнения.МестоВыполнения,
	|	МедицинскийДокументВыполненныеУслуги.УникальныйИдентификаторУслуги,
	|	МедицинскийДокументВыполненныеУслуги.КлючСтрокиСоставаУслуги
	|ИЗ
	|	Документ.МедицинскийДокумент.МестаВыполнения КАК МедицинскийДокументМестаВыполнения,
	|	Документ.МедицинскийДокумент.ВыполненныеУслуги КАК МедицинскийДокументВыполненныеУслуги
	|ГДЕ
	|	МедицинскийДокументМестаВыполнения.Ссылка = &Ссылка
	|	И МедицинскийДокументВыполненныеУслуги.Ссылка = &Ссылка
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	СменныеЗадания.ОсновноеМестоВыполнения,
	|	СменныеЗадания.УникальныйИдентификаторУслуги,
	|	СменныеЗадания.КлючСтрокиСоставаУслуги
	|ИЗ
	|	РегистрСведений.СменныеЗадания КАК СменныеЗадания
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.МедицинскийДокумент.НазначенныеУслуги КАК НазначенныеУслуги
	|		ПО (НазначенныеУслуги.Ссылка = &Ссылка)
	|			И СменныеЗадания.УникальныйИдентификаторУслуги = НазначенныеУслуги.УникальныйИдентификаторУслуги
	|			И (НазначенныеУслуги.Ссылка.МедицинскаяКарта.ТипМедицинскойКарты.УсловияОказанияПомощи = ЗНАЧЕНИЕ(Перечисление.УсловияМедицинскойПомощи.Стационарная))
	|			И (НазначенныеУслуги.Номенклатура.НеТребуетОтметкиИсполнения = ИСТИНА)
	|			И (НазначенныеУслуги.ОтказОтВмешательства = ЛОЖЬ)
	|ГДЕ
	|	СменныеЗадания.ОсновноеМестоВыполнения <> ЗНАЧЕНИЕ(Справочник.МедицинскиеКабинеты.ПустаяСсылка)";
	
	Запрос.УстановитьПараметр("Ссылка", ЭтотОбъект.Ссылка);
	Выборка = Запрос.Выполнить().Выбрать();
	
	Очищенные_ = Новый Соответствие;
	Пока Выборка.Следующий() Цикл

		НаборЗаписей = РегистрыСведений.МестаВыполненияУслуг.СоздатьНаборЗаписей();
		
		Если Очищенные_.Получить(Строка(Выборка.УникальныйИдентификаторУслуги) + Строка(Выборка.КлючСтрокиСоставаУслуги)) = Неопределено Тогда
			НаборЗаписей.Отбор.УникальныйИдентификаторУслуги.Установить(Выборка.УникальныйИдентификаторУслуги);
			НаборЗаписей.Отбор.КлючСтрокиСоставаУслуги.Установить(Выборка.КлючСтрокиСоставаУслуги);
			НаборЗаписей.Записать();
			
			Очищенные_.Вставить(
				Строка(Выборка.УникальныйИдентификаторУслуги) + Строка(Выборка.КлючСтрокиСоставаУслуги),
				Истина
			);
		КонецЕсли;

		Если Не ЭтотОбъект.ДополнительныеСвойства.УслугиНеВыполнены Тогда
			НаборЗаписей.Отбор.УникальныйИдентификаторУслуги.Установить(Выборка.УникальныйИдентификаторУслуги);
			НаборЗаписей.Отбор.КлючСтрокиСоставаУслуги.Установить(Выборка.КлючСтрокиСоставаУслуги);
			НаборЗаписей.Отбор.МестоВыполнения.Установить(Выборка.МестоВыполнения);
			НаборЗаписей.Добавить();
			ЗаполнитьЗначенияСвойств(НаборЗаписей[0], Выборка, 
							"МестоВыполнения,УникальныйИдентификаторУслуги,КлючСтрокиСоставаУслуги");
			НаборЗаписей.Записать();
		КонецЕсли;

	КонецЦикла;
КонецПроцедуры

Процедура ВыполнитьДвиженияИсполнителиУслуг(Отказ)
	Если Истина = Отказ Тогда 
		Возврат;
	КонецЕсли;
	
	НайденныеСтрокиОсновныхИсполнителей_ = ЭтотОбъект.Исполнители.НайтиСтроки(Новый Структура("ОтветственныйЗаНазначение", Истина));

	ВрачиВидыДолжностей_ = Обработки.СменноеЗадание.ВидыДолжностейСоответствующиеИсполнителюВрач();
	
	Для Каждого ВыполненнаяУслуга_ Из ЭтотОбъект.ВыполненныеУслуги Цикл
		НаборЗаписей_ = РегистрыСведений.ИсполнителиУслуг.СоздатьНаборЗаписей();
		НаборЗаписей_.Отбор.УникальныйИдентификаторУслуги.Установить(ВыполненнаяУслуга_.УникальныйИдентификаторУслуги);
		НаборЗаписей_.Отбор.КлючСтрокиСоставаУслуги.Установить(ВыполненнаяУслуга_.КлючСтрокиСоставаУслуги);
		НаборЗаписей_.Записать();

		Если ЭтотОбъект.ДополнительныеСвойства.УслугиНеВыполнены Тогда
			Продолжить;
		КонецЕсли;

		НоменклатураИсполнитель_ = ВыполненнаяУслуга_.НоменклатураСоставаУслуги.ИсполнительМедицинскойУслуги;
		КоличествоИсполнителей_ = 0;
		ДобавленВрач_ = Неопределено;
		ДобавленСМП_ = Неопределено;

		НаборЗаписей_.Очистить();
		НаборЗаписей_.Отбор.УникальныйИдентификаторУслуги.Установить(ВыполненнаяУслуга_.УникальныйИдентификаторУслуги);
		НаборЗаписей_.Отбор.КлючСтрокиСоставаУслуги.Установить(ВыполненнаяУслуга_.КлючСтрокиСоставаУслуги);
		НаборЗаписей_.Добавить();
		ЗаполнитьЗначенияСвойств(
			НаборЗаписей_[0], 
			ВыполненнаяУслуга_, 
			"УникальныйИдентификаторУслуги, КлючСтрокиСоставаУслуги"
		);
		Для Каждого Исполнитель_ Из ЭтотОбъект.Исполнители Цикл
			Если Не ЗначениеЗаполнено(Исполнитель_.УникальныйИдентификаторУслуги)
				Или (Исполнитель_.УникальныйИдентификаторУслуги = ВыполненнаяУслуга_.УникальныйИдентификаторУслуги
					И Исполнитель_.КлючСтрокиСоставаУслуги = ВыполненнаяУслуга_.КлючСтрокиСоставаУслуги)
			Тогда
				Если НоменклатураИсполнитель_ = Перечисления.ИсполнителиМедицинскихУслуг.СМП Тогда
					// Записываем только СМП
					Если Исполнитель_.Должность.ВидМедицинскойДолжности = Перечисления.ВидыМедицинскихДолжностей.СреднийМедицинскийПерсонал Тогда
						ДобавитьИсполнителяВНаборЗаписейРегистра(НаборЗаписей_, Исполнитель_);
						КоличествоИсполнителей_ = КоличествоИсполнителей_ + 1;
					КонецЕсли;
				ИначеЕсли (НоменклатураИсполнитель_ = Перечисления.ИсполнителиМедицинскихУслуг.ВрачСМП
					Или НоменклатураИсполнитель_ = Перечисления.ИсполнителиМедицинскихУслуг.БригадноеВыполнение)
				Тогда
					// Записываем всех исполнителей и СМП
					ДобавитьИсполнителяВНаборЗаписейРегистра(НаборЗаписей_, Исполнитель_);
					КоличествоИсполнителей_ = КоличествоИсполнителей_ + 1;
				Иначе
					// Записываем всех исполнителей, кроме СМП
					Если Исполнитель_.Должность.ВидМедицинскойДолжности <> Перечисления.ВидыМедицинскихДолжностей.СреднийМедицинскийПерсонал Тогда
						ДобавитьИсполнителяВНаборЗаписейРегистра(НаборЗаписей_, Исполнитель_);
						КоличествоИсполнителей_ = КоличествоИсполнителей_ + 1;
					КонецЕсли;
				КонецЕсли;
				
				Если НоменклатураИсполнитель_ = Перечисления.ИсполнителиМедицинскихУслуг.СМП 
						ИЛИ НоменклатураИсполнитель_ = Перечисления.ИсполнителиМедицинскихУслуг.ВрачСМП
				Тогда
					ДобавленСМП_ = (Истина = ДобавленСМП_ 
						ИЛИ Исполнитель_.Должность.ВидМедицинскойДолжности = Перечисления.ВидыМедицинскихДолжностей.СреднийМедицинскийПерсонал
					);
				КонецЕсли;
				
				Если НоменклатураИсполнитель_ = Перечисления.ИсполнителиМедицинскихУслуг.Врач 
						ИЛИ НоменклатураИсполнитель_ = Перечисления.ИсполнителиМедицинскихУслуг.ВрачСМП
				Тогда
					ДобавленВрач_ = (Истина = ДобавленВрач_ 
						ИЛИ ВрачиВидыДолжностей_.Найти(Исполнитель_.Должность.ВидМедицинскойДолжности) <> Неопределено
					);
				КонецЕсли;

			КонецЕсли;
		КонецЦикла;
		
		Если Не ЗначениеЗаполнено(ЭтотОбъект.ПолученОтМО) Тогда// В документах (ЛИС) от внешних МО ТЧ Исполнители может быть пуста.
			Если Ложь = ДобавленСМП_ Тогда 
				Отказ = Истина;
				СообщенияПользователю.Показать("МедицинскийДокумент_СМПНеЗадан");
				Возврат;
			КонецЕсли;
			
			Если Ложь = ДобавленВрач_ Тогда 
				Отказ = Истина;
				СообщенияПользователю.Показать("МедицинскийДокумент_ВрачНеЗадан");
				Возврат;
			КонецЕсли;
		КонецЕсли;
		
		// Если не добавлен ни один из исполнителей надо прописать ОсновногоИсполнителя
		Если КоличествоИсполнителей_ = 0 
				И НайденныеСтрокиОсновныхИсполнителей_.Количество() > 0 
		Тогда
			ДобавитьИсполнителяВНаборЗаписейРегистра(НаборЗаписей_, НайденныеСтрокиОсновныхИсполнителей_[0]);
		КонецЕсли;
	КонецЦикла;

	Если ЭтоДокументСтационара() Тогда
		УИДыУслуг_ = Новый Массив;
		Для Каждого Услуга_ Из ЭтотОбъект.НазначенныеУслуги Цикл
			Если Услуга_.Номенклатура.НеТребуетОтметкиИсполнения = Истина И Услуга_.ОтказОтВмешательства = Ложь Тогда
				АлгоритмыДляКоллекций.ДобавитьУникальное(УИДыУслуг_, Услуга_.УникальныйИдентификаторУслуги);
			КонецЕсли;
		КонецЦикла;
		
		Если УИДыУслуг_.Количество() > 0 Тогда
			Запрос_ = Новый Запрос(
				"ВЫБРАТЬ
				|	РегистрСменныеЗадания.УникальныйИдентификаторУслуги КАК УникальныйИдентификаторУслуги,
				|	РегистрСменныеЗадания.КлючСтрокиСоставаУслуги КАК КлючСтрокиСоставаУслуги,
				|	СправочникМедицинскиеРабочиеМестаИсполнители.Сотрудник КАК Исполнитель,
				|	СправочникСотрудники.Должность
				|ИЗ
				|	РегистрСведений.СменныеЗадания КАК РегистрСменныеЗадания
				|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.МедицинскиеРабочиеМеста.Исполнители КАК СправочникМедицинскиеРабочиеМестаИсполнители
				|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Сотрудники КАК СправочникСотрудники
				|			ПО СправочникМедицинскиеРабочиеМестаИсполнители.Сотрудник = СправочникСотрудники.Ссылка
				|		ПО РегистрСменныеЗадания.МедицинскоеРабочееМесто = СправочникМедицинскиеРабочиеМестаИсполнители.Ссылка
				|ГДЕ
				|	РегистрСменныеЗадания.УникальныйИдентификаторУслуги В(&УИДыУслуг)
				|ИТОГИ ПО
				|	УникальныйИдентификаторУслуги,
				|	КлючСтрокиСоставаУслуги"
			);
			Запрос_.УстановитьПараметр("УИДыУслуг", УИДыУслуг_);
			РезультатЗапроса_ = Запрос_.Выполнить();
			ВыборкаПоУИДам_ = РезультатЗапроса_.Выбрать(
				ОбходРезультатаЗапроса.ПоГруппировкам, "УникальныйИдентификаторУслуги"
			);
			Пока ВыборкаПоУИДам_.Следующий() Цикл
				ВыборкаПоКлючамСтроки_ = ВыборкаПоУИДам_.Выбрать(
					ОбходРезультатаЗапроса.ПоГруппировкам, "КлючСтрокиСоставаУслуги"
				);
				Пока ВыборкаПоКлючамСтроки_.Следующий() Цикл
					НаборЗаписей_ = РегистрыСведений.ИсполнителиУслуг.СоздатьНаборЗаписей();
					НаборЗаписей_.Отбор.УникальныйИдентификаторУслуги.Установить(
						ВыборкаПоКлючамСтроки_.УникальныйИдентификаторУслуги
					);
					НаборЗаписей_.Отбор.КлючСтрокиСоставаУслуги.Установить(
						ВыборкаПоКлючамСтроки_.КлючСтрокиСоставаУслуги
					);
					Если ЭтотОбъект.ДополнительныеСвойства.УслугиНеВыполнены = Ложь Тогда
						Выборка_ = ВыборкаПоКлючамСтроки_.Выбрать();
						Пока Выборка_.Следующий() Цикл
							Запись_ = НаборЗаписей_.Добавить();
							ЗаполнитьЗначенияСвойств(Запись_, Выборка_);
						КонецЦикла;
					КонецЕсли;
					НаборЗаписей_.Записать();
				КонецЦикла;
			КонецЦикла;
		КонецЕсли;
	КонецЕсли;

КонецПроцедуры

Процедура ДобавитьИсполнителяВНаборЗаписейРегистра(НаборЗаписей, Исполнитель)
	НаборЗаписей.Отбор.Исполнитель.Установить(Исполнитель.Исполнитель);
	ЗаполнитьЗначенияСвойств(
		НаборЗаписей[0], 
		Исполнитель, 
		"Исполнитель, Должность, Роль"
	);
	НаборЗаписей.Записать();
КонецПроцедуры

////
 // Процедура: ВыполнитьДвиженияДиагнозыПоМКБ10
 //   Выполняет формирование движений по регистру "ДиагнозыПоМКБ10".
 ///
Процедура ВыполнитьДвиженияДиагнозыПоМКБ10(Отказ)
	Если Истина = Отказ Тогда 
		Возврат;
	КонецЕсли;
	
	Движения.ДиагнозыПоМКБ10.Записывать = Истина;
	Если ЭтотОбъект.ДиагнозыПоМКБ10.Количество() > 0 Тогда
		Для Каждого Диагноз_ Из ЭтотОбъект.ДиагнозыПоМКБ10 Цикл
			Движение_ = Движения.ДиагнозыПоМКБ10.Добавить();
			ЗаполнитьЗначенияСвойств(Движение_, Диагноз_);
			Движение_.МедицинскаяКарта = ЭтотОбъект.МедицинскаяКарта;
			Движение_.Пациент = ЭтотОбъект.Пациент;
			Движение_.Период = ЭтотОбъект.ДатаМД;
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

Процедура ВыполнитьДвиженияДиагнозыПоМКБПсихиатрия(Отказ)
	Если Истина = Отказ Тогда
		Возврат;
	КонецЕсли;
	
	Движения.ДиагнозыПоМКБПсихиатрии.Записывать = Истина;
	Если ЭтотОбъект.ДиагнозыПоМКБПсихиатрии.Количество() > 0 Тогда
		Для Каждого ДиагнозПсихиатрии_ Из ЭтотОбъект.ДиагнозыПоМКБПсихиатрии Цикл
			Движение_ = Движения.ДиагнозыПоМКБПсихиатрии.Добавить();
			ЗаполнитьЗначенияСвойств(Движение_, ДиагнозПсихиатрии_);
			Движение_.МедицинскаяКарта = ЭтотОбъект.МедицинскаяКарта;
			Движение_.Пациент = ЭтотОбъект.Пациент;
			Движение_.Период = ЭтотОбъект.ДатаМД;
		КонецЦикла;
	КонецЕсли;
КонецПроцедуры

////
 // Процедура: ВыполнитьДвиженияПоказателиЗдоровья
 //   Выполняет формирование движений по регистру "ПоказателиЗдоровья".
 ///
Процедура ВыполнитьДвиженияПоказателиЗдоровья(Отказ)
	Если Истина = Отказ Тогда 
		Возврат;
	КонецЕсли;
	
	Движения.ПоказателиЗдоровья.Записывать = Истина;
	
	Для Каждого ПоказательЗдоровья_ Из ЭтотОбъект.ПоказателиЗдоровья Цикл
		Движение_ = Движения.ПоказателиЗдоровья.Добавить();
		Движение_.ВидРезультата = ПоказательЗдоровья_.Показатель;
		Движение_.ЗначениеПоказателяЗдоровья = ПоказательЗдоровья_.Значение;
		Движение_.ЗначениеПоказателяЗдоровьяМинимум = ПоказательЗдоровья_.ЗначениеМинимум;
		Движение_.ЗначениеПоказателяЗдоровьяМаксимум = ПоказательЗдоровья_.ЗначениеМаксимум;
		Движение_.ЕдиницаИзмерения = ПоказательЗдоровья_.ЕдиницаИзмерения;
		Движение_.СубъектПоказателяЗдоровья = ПоказательЗдоровья_.СубъектПоказателяЗдоровья;
		Движение_.Флаг = ПоказательЗдоровья_.Флаг;
		Движение_.МедицинскаяКарта = ЭтотОбъект.МедицинскаяКарта;
		Движение_.Пациент = ЭтотОбъект.Пациент;
		Движение_.Период = ЭтотОбъект.Дата;
		
		//группа крови
		Если Справочники.СправочникиФНСИ.ПолучитьСтруктуруКодированныхПолейПоСинониму("GRP472").МассивОИД.Найти(Движение_.ВидРезультата.codeSystem) <> Неопределено Тогда
			Движение_.НомерЗаписиПоказателя = ПоказательЗдоровья_.НомерСтроки;
		КонецЕсли;

	КонецЦикла;
	
КонецПроцедуры

Процедура ВыполнитьДвиженияМедицинскиеПрограммы(Отказ)
	Если Истина = Отказ Тогда 
		Возврат;
	КонецЕсли;

	Движения.УслугиМедицинскихПрограмм.Записывать = Истина;
	Для Каждого Строка_ Из ЭтотОбъект.МедицинскиеПрограммы Цикл
		ЗаполнитьЗначенияСвойств(Движения.УслугиМедицинскихПрограмм.Добавить(), Строка_);
	КонецЦикла;
КонецПроцедуры

// Добавляет записи в СтатусыБиоматериала
// для назначений которые используют биоматериал связанный с другой услугой.
//
// Параметры:
//  <Параметр1>  - <Тип.Вид> - <описание параметра>
//                 <продолжение описания параметра>
//  <Параметр2>  - <Тип.Вид> - <описание параметра>
//                 <продолжение описания параметра>
//
Процедура ВыполнитьДвиженияСтатусыБиоматериала(Отказ)
	Если Истина = Отказ Тогда 
		Возврат;
	КонецЕсли;

	// Получим список назначенных ПМУ
	Запрос_ = Новый Запрос(
		"ВЫБРАТЬ
		|	НазначенныеУслуги.УникальныйИдентификаторУслуги,
		|	1 КАК КлючСтрокиСоставаУслуги,
		|	НазначенныеУслуги.ИдентификаторБиоматериала
		|ИЗ
		|	Документ.МедицинскийДокумент.НазначенныеУслуги КАК НазначенныеУслуги
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СправочникНоменклатура
		|		ПО НазначенныеУслуги.Номенклатура = СправочникНоменклатура.Ссылка
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Образцы.МедицинскиеУслуги КАК ОбразцыМедицинскиеУслуги
		|		ПО (НазначенныеУслуги.УникальныйИдентификаторУслуги = ОбразцыМедицинскиеУслуги.УникальныйИдентификаторУслуги)
		|			И (ОбразцыМедицинскиеУслуги.КлючСтрокиСоставаУслуги = 1)
		|ГДЕ
		|	НазначенныеУслуги.Ссылка = &Ссылка
		|	И СправочникНоменклатура.ВидНоменклатуры В (&ВидыНоменклатурыЛИС)
		|	И НазначенныеУслуги.ОтказОтВмешательства = ЛОЖЬ
		|	И ОбразцыМедицинскиеУслуги.Ссылка ЕСТЬ NULL
		|	И НазначенныеУслуги.ИдентификаторБиоматериала <> """""
	);
	
	Запрос_.УстановитьПараметр("Ссылка", ЭтотОбъект.Ссылка);
	Запрос_.УстановитьПараметр("ВидыНоменклатурыЛИС", ОбщиеМеханизмыHL7.ВидыНоменклатурыДляОбменаСЛИСПоHL7());
	
	Выборка_ = Запрос_.Выполнить().Выбрать();
	
	Пока Выборка_.Следующий() Цикл
		Справочники.Образцы.СвязатьУслугуСОбразцом(
				Выборка_.УникальныйИдентификаторУслуги,
				Выборка_.КлючСтрокиСоставаУслуги,
				Выборка_.ИдентификаторБиоматериала
		);
	КонецЦикла;

КонецПроцедуры

Процедура ВыполнитьДвиженияРезультатыИсследованийЗНО(Отказ)
	
	Если Истина = Отказ Тогда 
		Возврат;
	КонецЕсли;
	
	Движения.РезультатыИсследованийЗНО.Записывать = Истина;
	
	Для Каждого Онкология_ Из ЭтотОбъект.ДанныеОнкологии Цикл
		Движение_ = Движения.РезультатыИсследованийЗНО.Добавить();
		Движение_.Пациент = ЭтотОбъект.Пациент;
		Движение_.Период  = ЭтотОбъект.ДатаМД;
		ЗаполнитьЗначенияСвойств(Движение_, Онкология_);
		
		Движение_.ДиагнозКлиническийЗаключительный   = Онкология_.МКБ10;
	КонецЦикла;
	
КонецПроцедуры

////
 //Процедура: УстановкаТранзакционнойБлокировки
 //     Устанавливает блокировки на записи регистра СтатусыУслуг.
 //
 // Параметры:
 //
 ///
Процедура УстановкаТранзакционнойБлокировки()

	Если ЗначениеЗаполнено(ЭтотОбъект.Ссылка) Тогда
		РегистрыСведений.СтатусыМедицинскихДокументов.УстановитьТранзакционнуюБлокировку(ЭтотОбъект.Ссылка);
	КонецЕсли;

	УправлениеЗаказамиУслугами.ЗаблокироватьРегистр(
				ВыполненныеУслуги.Выгрузить(,"УникальныйИдентификаторУслуги"),
				"РегистрСведений.СтатусыУслуг",
				"УникальныйИдентификаторУслуги"
			);

	УправлениеЗаказамиУслугами.ЗаблокироватьРегистр(
				НазначенныеУслуги.Выгрузить(,"УникальныйИдентификаторУслуги"),
				"РегистрСведений.СтатусыУслуг",
				"УникальныйИдентификаторУслуги"
			);

	УправлениеЗаказамиУслугами.ЗаблокироватьРегистр(
				ВыполненныеУслуги.Выгрузить(,"УникальныйИдентификаторУслуги"),
				"РегистрСведений.СменныеЗадания",
				"УникальныйИдентификаторУслуги"
			);
			
	УправлениеЗаказамиУслугами.ЗаблокироватьРегистр(
				НазначенныеУслуги.Выгрузить(,"УникальныйИдентификаторУслуги"),
				"РегистрСведений.СменныеЗадания",
				"УникальныйИдентификаторУслуги"
			);
			
	УправлениеЗаказамиУслугами.ЗаблокироватьРегистр(
				ВыполненныеУслуги.Выгрузить(,"УникальныйИдентификаторУслуги"),
				"РегистрСведений.СтатусыУслугФинансовыеДанные",
				"УникальныйИдентификаторУслуги"
			);

	УправлениеЗаказамиУслугами.ЗаблокироватьРегистр(
				НазначенныеУслуги.Выгрузить(,"УникальныйИдентификаторУслуги"),
				"РегистрСведений.СтатусыУслугФинансовыеДанные",
				"УникальныйИдентификаторУслуги"
			);

	УправлениеЗаказамиУслугами.ЗаблокироватьРегистр(
				ВыполненныеПриемы.Выгрузить(,"УникальныйИдентификаторПриема"),
				"РегистрСведений.Приемы",
				"УникальныйИдентификаторПриема"
			);

	УправлениеЗаказамиУслугами.ЗаблокироватьРегистр(
				НазначенныеПриемы.Выгрузить(,"УникальныйИдентификаторПриема"),
				"РегистрСведений.Приемы",
				"УникальныйИдентификаторПриема"
			);

	УправлениеЗаказамиУслугами.ЗаблокироватьРегистр(
				НазначенныеУслуги.Выгрузить(,"УникальныйИдентификаторУслуги"),
				"РегистрСведений.CDAДокументыНазначенныхУслуг",
				"УникальныйИдентификаторУслуги"
			);

	УправлениеЗаказамиУслугами.ЗаблокироватьРегистр(
				ВыполненныеУслуги.Выгрузить(,"УникальныйИдентификаторУслуги"),
				"РегистрСведений.ИсполнителиУслуг",
				"УникальныйИдентификаторУслуги"
			);

	УправлениеЗаказамиУслугами.ЗаблокироватьРегистр(
				НазначенныеУслуги.Выгрузить(,"УникальныйИдентификаторУслуги"),
				"РегистрСведений.ИсполнителиУслуг",
				"УникальныйИдентификаторУслуги"
			);

	УправлениеЗаказамиУслугами.ЗаблокироватьРегистр(
				ВыполненныеУслуги.Выгрузить(,"УникальныйИдентификаторУслуги"),
				"РегистрСведений.МестаВыполненияУслуг",
				"УникальныйИдентификаторУслуги"
			);
			
	УправлениеЗаказамиУслугами.ЗаблокироватьРегистр(
				НазначенныеУслуги.Выгрузить(,"УникальныйИдентификаторУслуги"),
				"РегистрСведений.МестаВыполненияУслуг",
				"УникальныйИдентификаторУслуги"
			);

	УправлениеЗаказамиУслугами.ЗаблокироватьРегистр(
				ВыполненныеУслуги.Выгрузить(,"УникальныйИдентификаторУслуги"),
				"РегистрСведений.УслугиМедицинскихПрограмм",
				"УИДУслуги",
				"УникальныйИдентификаторУслуги"
			);
			
	УправлениеЗаказамиУслугами.ЗаблокироватьРегистр(
				НазначенныеУслуги.Выгрузить(,"УникальныйИдентификаторУслуги"),
				"РегистрСведений.УслугиМедицинскихПрограмм",
				"УИДУслуги",
				"УникальныйИдентификаторУслуги"
			);

	УправлениеЗаказамиУслугами.ЗаблокироватьРегистр(
				НазначенныеУслуги.Выгрузить(,"УникальныйИдентификаторУслуги"),
				"РегистрСведений.УслугиМедицинскихПрограмм",
				"УИДУслуги",
				"УникальныйИдентификаторУслуги"
			);

	УправлениеЗаказамиУслугами.ЗаблокироватьРегистр(
				ВыполненныеУслуги.Выгрузить(,"УникальныйИдентификаторУслуги"),
				"РегистрСведений.УслугиСтандартовМедицинскойПомощи",
				"УИДУслуги",
				"УникальныйИдентификаторУслуги"
			);

			
	УправлениеЗаказамиУслугами.ЗаблокироватьРегистр(
				НазначенныеУслуги.Выгрузить(,"УникальныйИдентификаторУслуги"),
				"РегистрСведений.УслугиСтандартовМедицинскойПомощи",
				"УИДУслуги",
				"УникальныйИдентификаторУслуги"
			);

	УправлениеЗаказамиУслугами.ЗаблокироватьРегистр(
				ВыполненныеУслуги.Выгрузить(,"УникальныйИдентификаторУслуги"),
				"РегистрСведений.СтатусыСогласованияУслуг",
				"УИД",
				"УникальныйИдентификаторУслуги"
			);

			
	УправлениеЗаказамиУслугами.ЗаблокироватьРегистр(
				НазначенныеУслуги.Выгрузить(,"УникальныйИдентификаторУслуги"),
				"РегистрСведений.СтатусыСогласованияУслуг",
				"УИД",
				"УникальныйИдентификаторУслуги"
			);

	Если ЗначениеЗаполнено(ЭтотОбъект.МедицинскаяКарта) Или ЗначениеЗаполнено(ЭтотОбъект.Пациент) Тогда
		МедкартаПациент_ = ОбщиеМеханизмы.СоздатьТаблицуЗначений("МедицинскаяКарта, Пациент");
		НоваяСтрокаТЧ_ = МедкартаПациент_.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрокаТЧ_, ЭтотОбъект);

		УправлениеЗаказамиУслугами.ЗаблокироватьРегистр(
					МедкартаПациент_,
					"РегистрСведений.ДиагнозыПоМКБ10",
					"МедицинскаяКарта,Пациент"
				);
		
		УправлениеЗаказамиУслугами.ЗаблокироватьРегистр(
					МедкартаПациент_,
					"РегистрСведений.ПоказателиЗдоровья",
					"МедицинскаяКарта,Пациент"
				);
	КонецЕсли;

	Если ЗначениеЗаполнено(ЭтотОбъект.Ссылка) Тогда
		Меддок_ = ОбщиеМеханизмы.СоздатьТаблицуЗначений("МедицинскийДокумент");
		НоваяСтрокаТЧ_ = Меддок_.Добавить();
		
		НоваяСтрокаТЧ_.МедицинскийДокумент = ЭтотОбъект.Ссылка;

		УправлениеЗаказамиУслугами.ЗаблокироватьРегистр(
					Меддок_,
					"РегистрСведений.СтатусыМедицинскихДокументов",
					"МедицинскийДокумент"
				);

		УправлениеЗаказамиУслугами.ЗаблокироватьРегистр(
					Меддок_,
					"РегистрСведений.ИсследованияПоФСИДИ",
					"Документ",
					"МедицинскийДокумент"
				);
	КонецЕсли;

	Если ЗначениеЗаполнено(ЭтотОбъект.Ссылка) Тогда
	
		Запрос_ = Новый Запрос;
		Запрос_.Текст =
		"ВЫБРАТЬ
		|	МедицинскийДокументВыполненныеУслуги.УникальныйИдентификаторУслуги
		|ИЗ
		|	Документ.МедицинскийДокумент.ВыполненныеУслуги КАК МедицинскийДокументВыполненныеУслуги
		|ГДЕ
		|	МедицинскийДокументВыполненныеУслуги.Ссылка = &Ссылка";
		Запрос_.УстановитьПараметр("Ссылка", ЭтотОбъект.Ссылка);

		СписокУИД_ = Запрос_.Выполнить().Выгрузить();
		
		УправлениеЗаказамиУслугами.ЗаблокироватьРегистр(
					СписокУИД_,
					"РегистрСведений.СтатусыУслуг",
					"УникальныйИдентификаторУслуги"
				);

		УправлениеЗаказамиУслугами.ЗаблокироватьРегистр(
					СписокУИД_,
					"РегистрСведений.СменныеЗадания",
					"УникальныйИдентификаторУслуги"
				);

		УправлениеЗаказамиУслугами.ЗаблокироватьРегистр(
					СписокУИД_,
					"РегистрСведений.CDAДокументыНазначенныхУслуг",
					"УникальныйИдентификаторУслуги"
				);

		УправлениеЗаказамиУслугами.ЗаблокироватьРегистр(
					СписокУИД_,
					"РегистрСведений.ИсполнителиУслуг",
					"УникальныйИдентификаторУслуги"
				);

		УправлениеЗаказамиУслугами.ЗаблокироватьРегистр(
					СписокУИД_,
					"РегистрСведений.МестаВыполненияУслуг",
					"УникальныйИдентификаторУслуги"
				);

		УправлениеЗаказамиУслугами.ЗаблокироватьРегистр(
					СписокУИД_,
					"РегистрСведений.УслугиМедицинскихПрограмм",
					"УИДУслуги",
					"УникальныйИдентификаторУслуги"
				);

		УправлениеЗаказамиУслугами.ЗаблокироватьРегистр(
					СписокУИД_,
					"РегистрСведений.УслугиСтандартовМедицинскойПомощи",
					"УИДУслуги",
					"УникальныйИдентификаторУслуги"
				);

		УправлениеЗаказамиУслугами.ЗаблокироватьРегистр(
					СписокУИД_,
					"РегистрСведений.СтатусыСогласованияУслуг",
					"УИД",
					"УникальныйИдентификаторУслуги"
				);

		Запрос_ = Новый Запрос;
		Запрос_.Текст =
		"ВЫБРАТЬ
		|	МедицинскийДокументНазначенныеУслуги.УникальныйИдентификаторУслуги
		|ИЗ
		|	Документ.МедицинскийДокумент.НазначенныеУслуги КАК МедицинскийДокументНазначенныеУслуги
		|ГДЕ
		|	МедицинскийДокументНазначенныеУслуги.Ссылка = &Ссылка";
		Запрос_.УстановитьПараметр("Ссылка", ЭтотОбъект.Ссылка);

		СписокУИД_ = Запрос_.Выполнить().Выгрузить();
		
		УправлениеЗаказамиУслугами.ЗаблокироватьРегистр(
					СписокУИД_,
					"РегистрСведений.СтатусыУслуг",
					"УникальныйИдентификаторУслуги"
				);

		УправлениеЗаказамиУслугами.ЗаблокироватьРегистр(
					СписокУИД_,
					"РегистрСведений.СменныеЗадания",
					"УникальныйИдентификаторУслуги"
				);

		УправлениеЗаказамиУслугами.ЗаблокироватьРегистр(
					СписокУИД_,
					"РегистрСведений.CDAДокументыНазначенныхУслуг",
					"УникальныйИдентификаторУслуги"
				);

		УправлениеЗаказамиУслугами.ЗаблокироватьРегистр(
					СписокУИД_,
					"РегистрСведений.ИсполнителиУслуг",
					"УникальныйИдентификаторУслуги"
				);

		УправлениеЗаказамиУслугами.ЗаблокироватьРегистр(
					СписокУИД_,
					"РегистрСведений.МестаВыполненияУслуг",
					"УникальныйИдентификаторУслуги"
				);

		УправлениеЗаказамиУслугами.ЗаблокироватьРегистр(
					СписокУИД_,
					"РегистрСведений.УслугиМедицинскихПрограмм",
					"УИДУслуги",
					"УникальныйИдентификаторУслуги"
				);

		УправлениеЗаказамиУслугами.ЗаблокироватьРегистр(
					СписокУИД_,
					"РегистрСведений.УслугиСтандартовМедицинскойПомощи",
					"УИДУслуги",
					"УникальныйИдентификаторУслуги"
				);

		УправлениеЗаказамиУслугами.ЗаблокироватьРегистр(
					СписокУИД_,
					"РегистрСведений.СтатусыСогласованияУслуг",
					"УИД",
					"УникальныйИдентификаторУслуги"
				);
				
		// Приемы
		Запрос_ = Новый Запрос;
		Запрос_.Текст =
		"ВЫБРАТЬ
		|	ВыполненныеПриемы.УникальныйИдентификаторПриема
		|ИЗ
		|	Документ.МедицинскийДокумент.ВыполненныеПриемы КАК ВыполненныеПриемы
		|ГДЕ
		|	ВыполненныеПриемы.Ссылка = &Ссылка";
		Запрос_.УстановитьПараметр("Ссылка", ЭтотОбъект.Ссылка);

		СписокУИДПриемов_ = Запрос_.Выполнить().Выгрузить();

		УправлениеЗаказамиУслугами.ЗаблокироватьРегистр(
					СписокУИДПриемов_,
					"РегистрСведений.Приемы",
					"УникальныйИдентификаторПриема"
				);


		Запрос_ = Новый Запрос;
		Запрос_.Текст =
		"ВЫБРАТЬ
		|	НазначенныеПриемы.УникальныйИдентификаторПриема
		|ИЗ
		|	Документ.МедицинскийДокумент.НазначенныеПриемы КАК НазначенныеПриемы
		|ГДЕ
		|	НазначенныеПриемы.Ссылка = &Ссылка";
		Запрос_.УстановитьПараметр("Ссылка", ЭтотОбъект.Ссылка);

		СписокУИДПриемов_ = Запрос_.Выполнить().Выгрузить();

		УправлениеЗаказамиУслугами.ЗаблокироватьРегистр(
					СписокУИДПриемов_,
					"РегистрСведений.Приемы",
					"УникальныйИдентификаторПриема"
				);

	 КонецЕсли;


КонецПроцедуры

Процедура СоздатьВременнуюТаблицуВыполненныеУслуги()
	МВТ_ = Новый МенеджерВременныхТаблиц;
	// Поместим во временную таблицу содержимое ТЧ ВыполненныеУслуги, т.к. из этой ТЧ могли быть удалены записи.
	Запрос_ = Новый Запрос;
	Запрос_.МенеджерВременныхТаблиц = МВТ_;
	Запрос_.Текст =
	"ВЫБРАТЬ
	|	МедицинскийДокументВыполненныеУслуги.Ссылка,
	|	МедицинскийДокументВыполненныеУслуги.НомерСтроки,
	|	МедицинскийДокументВыполненныеУслуги.ДатаВыполнения,
	|	МедицинскийДокументВыполненныеУслуги.ИсходныйСтатус,
	|	МедицинскийДокументВыполненныеУслуги.КлючСтрокиСоставаУслуги,
	|	МедицинскийДокументВыполненныеУслуги.Номенклатура,
	|	МедицинскийДокументВыполненныеУслуги.НоменклатураСоставаУслуги,
	|	МедицинскийДокументВыполненныеУслуги.Спецификация,
	|	МедицинскийДокументВыполненныеУслуги.УникальныйИдентификаторУслуги,
	|	МедицинскийДокументВыполненныеУслуги.Количество
	|ПОМЕСТИТЬ втВыполненныеУслуги
	|ИЗ
	|	Документ.МедицинскийДокумент.ВыполненныеУслуги КАК МедицинскийДокументВыполненныеУслуги
	|ГДЕ
	|	МедицинскийДокументВыполненныеУслуги.Ссылка = &Ссылка";
	
	Запрос_.УстановитьПараметр("Ссылка", ЭтотОбъект.Ссылка);
	Запрос_.Выполнить();
	
	ЭтотОбъект.ДополнительныеСвойства.Вставить("МВТВыполненныеУслуги", МВТ_);
КонецПроцедуры

Процедура УдалитьДвиженияСтатусыМедицинскихДокументов(Отказ)
	НаборЗаписей_ = РегистрыСведений.СтатусыМедицинскихДокументов.СоздатьНаборЗаписей();
	НаборЗаписей_.Отбор.МедицинскийДокумент.Установить(ЭтотОбъект.Ссылка);
	НаборЗаписей_.Записать();
КонецПроцедуры

Процедура УдалитьДвиженияИсследованияПоФСИДИ(Отказ)
	НаборЗаписей_ = РегистрыСведений.ИсследованияПоФСИДИ.СоздатьНаборЗаписей();
	НаборЗаписей_.Отбор.Документ.Установить(ЭтотОбъект.Ссылка);
	НаборЗаписей_.Записать();
КонецПроцедуры

Процедура ВосстановитьРеквизитКоличествоВРегистреСтатусыУслуг(СписокУИД)

	Если СписокУИД.Количество() > 0 Тогда
		Запрос_ = Новый Запрос;
		Запрос_.Текст =
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	СтатусыУслуг.УникальныйИдентификаторУслуги,
		|	ЗаказПациентаМедицинскиеУслуги.Количество
		|ИЗ
		|	РегистрСведений.СтатусыУслуг КАК СтатусыУслуг
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ЗаказПациента КАК ДокументЗаказПациента
		|		ПО СтатусыУслуг.Заказ = ДокументЗаказПациента.Ссылка
		|			И ДокументЗаказПациента.Проведен
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ЗаказПациента.МедицинскиеУслуги КАК ЗаказПациентаМедицинскиеУслуги
		|		ПО СтатусыУслуг.УникальныйИдентификаторУслуги = ЗаказПациентаМедицинскиеУслуги.УникальныйИдентификаторУслуги
		|			И ДокументЗаказПациента.Ссылка = ЗаказПациентаМедицинскиеУслуги.Ссылка
		|ГДЕ
		|	СтатусыУслуг.УникальныйИдентификаторУслуги В(&СписокУИД)
		|	И СтатусыУслуг.Количество <> ЗаказПациентаМедицинскиеУслуги.Количество";
		
		Запрос_.УстановитьПараметр("СписокУИД", СписокУИД);
		
		Выборка_ = Запрос_.Выполнить().Выбрать();
		
		Пока Выборка_.Следующий() Цикл
			Набор_ = РегистрыСведений.СтатусыУслуг.СоздатьНаборЗаписей();
			Набор_.Отбор.УникальныйИдентификаторУслуги.Установить(Выборка_.УникальныйИдентификаторУслуги);
			Набор_.Прочитать();
			Набор_[0].Количество = Выборка_.Количество;
			Набор_[0].Документ = ЭтотОбъект.Ссылка;
			Набор_.Записать(Истина);
		КонецЦикла;
	КонецЕсли;

КонецПроцедуры

Процедура УдалитьДвиженияСтатусыУслугФинансовыеДанные(СписокУИД)
	
	МВТ_ = Неопределено;
	
	Если СписокУИД.Количество() > 0 И ЭтотОбъект.ДополнительныеСвойства.Свойство("МВТВыполненныеУслуги", МВТ_) Тогда
		Запрос_ = Новый Запрос;
		Запрос_.МенеджерВременныхТаблиц = МВТ_;
		Запрос_.Текст =
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	МедицинскийДокументВыполненныеУслуги.УникальныйИдентификаторУслуги,
		|	СтатусыУслугФинансовыеДанные.Цена,
		|	СтатусыУслугФинансовыеДанные.Сумма * (ДокументЗаказПациентаМедицинскиеУслуги.Количество / МедицинскийДокументВыполненныеУслуги.Количество) КАК Сумма,
		|	СтатусыУслугФинансовыеДанные.СтавкаНДС КАК СтавкаНДС,
		|	СтатусыУслугФинансовыеДанные.СуммаНДС * (ДокументЗаказПациентаМедицинскиеУслуги.Количество / МедицинскийДокументВыполненныеУслуги.Количество) КАК СуммаНДС,
		|	СтатусыУслугФинансовыеДанные.ПроцентАвтоматическойСкидки КАК ПроцентАвтоматическойСкидки,
		|	СтатусыУслугФинансовыеДанные.СуммаАвтоматическойСкидки * (ДокументЗаказПациентаМедицинскиеУслуги.Количество / МедицинскийДокументВыполненныеУслуги.Количество) КАК СуммаАвтоматическойСкидки,
		|	ДокументЗаказПациента.Ссылка КАК Документ,
		|	МедицинскийДокументВыполненныеУслуги.ИсходныйСтатус,
		|	МедицинскийДокументВыполненныеУслуги.Количество <> ДокументЗаказПациентаМедицинскиеУслуги.Количество КАК КоличествоИзменилось
		|ИЗ
		|	втВыполненныеУслуги КАК МедицинскийДокументВыполненныеУслуги
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.СтатусыУслуг КАК СтатусыУслуг
		|		ПО МедицинскийДокументВыполненныеУслуги.УникальныйИдентификаторУслуги = СтатусыУслуг.УникальныйИдентификаторУслуги
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.СтатусыУслугФинансовыеДанные КАК СтатусыУслугФинансовыеДанные
		|		ПО МедицинскийДокументВыполненныеУслуги.УникальныйИдентификаторУслуги = СтатусыУслугФинансовыеДанные.УникальныйИдентификаторУслуги
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ЗаказПациента КАК ДокументЗаказПациента
		|		ПО СтатусыУслуг.Заказ = ДокументЗаказПациента.Ссылка
		|			И ДокументЗаказПациента.Проведен
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ЗаказПациента.МедицинскиеУслуги КАК ДокументЗаказПациентаМедицинскиеУслуги
		|		ПО ДокументЗаказПациента.Ссылка = ДокументЗаказПациентаМедицинскиеУслуги.Ссылка
		|			И СтатусыУслуг.УникальныйИдентификаторУслуги = ДокументЗаказПациентаМедицинскиеУслуги.УникальныйИдентификаторУслуги
		|ГДЕ
		|	МедицинскийДокументВыполненныеУслуги.УникальныйИдентификаторУслуги В (&СписокУИД)";
		
		Запрос_.УстановитьПараметр("СписокУИД", СписокУИД);
		Выборка_ = Запрос_.Выполнить().Выбрать();
		
		Пока Выборка_.Следующий() Цикл
			Если ЗначениеЗаполнено(Выборка_.ИсходныйСтатус) Тогда
				Если Выборка_.КоличествоИзменилось И Выборка_.Сумма > 0 Тогда
					Набор_ = РегистрыСведений.СтатусыУслугФинансовыеДанные.СоздатьНаборЗаписей();
					Набор_.Отбор.УникальныйИдентификаторУслуги.Установить(Выборка_.УникальныйИдентификаторУслуги);
					Набор_.Прочитать();
					Если Набор_.Количество() > 0 Тогда
						ЗаполнитьЗначенияСвойств(Набор_[0], Выборка_);
						Набор_.Записать(Истина);
					КонецЕсли;
				КонецЕсли;
			Иначе
				// Услуга добавлена мед документом, просто удалим запись.
				Набор_ = РегистрыСведений.СтатусыУслугФинансовыеДанные.СоздатьНаборЗаписей();
				Набор_.Отбор.УникальныйИдентификаторУслуги.Установить(Выборка_.УникальныйИдентификаторУслуги);
				Набор_.Записать(Истина);
			КонецЕсли;
		КонецЦикла;

	КонецЕсли;
	
КонецПроцедуры

Процедура ВыполнитьДвиженияУслугиСтандартовМедицинскойПомощи(Отказ)
	Если Истина = Отказ Тогда 
		Возврат;
	КонецЕсли;
	
	Для Каждого Строка_ Из ЭтотОбъект.СтандартыМедицинскойПомощи Цикл
		Движение_ = ЭтотОбъект.Движения.УслугиСтандартовМедицинскойПомощи.Добавить();
		Движение_.УИДСтандарта = Строка_.УИДСтандарта;
		Движение_.УИДУслуги = Строка_.УИДУслуги;
	КонецЦикла;

КонецПроцедуры

Процедура ЗарегистрироватьСогласуемыеУслуги(Отказ)
	Если Отказ = Истина Тогда
		Возврат;	
	КонецЕсли;
	
	РегистрыСведений.СтатусыСогласованияУслуг.ОтменитьРегистрациюСогласований(ЭтотОбъект);
	Для Каждого Услуга_ Из ЭтотОбъект.НазначенныеУслуги Цикл
		Если Услуга_.ТребуетСогласования = Истина И Услуга_.ОтказОтВмешательства = Ложь Тогда
			РегистрыСведений.СтатусыСогласованияУслуг.ЗарегистрироватьСогласование(
				Услуга_.УникальныйИдентификаторУслуги,
				Услуга_.Номенклатура,
				Перечисления.СтатусыСогласований.ТребуетСогласования,
				ЭтотОбъект,
				ЭтотОбъект.ДатаМД,,
				Услуга_.Количество,,
				Услуга_.СогласованиеПостфактум
			);
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры

Процедура ЗарегистрироватьСогласуемыеЛекарственныеНазначения(Отказ)
	Если Отказ = Истина Тогда
		Возврат;	
	КонецЕсли;
	
	РегистрыСведений.СтатусыСогласованияЛекарственныхНазначений.ОтменитьРегистрациюСогласований(ЭтотОбъект);
	Для Каждого Назначение_ Из ЭтотОбъект.ЛекарственныеНазначения Цикл
		Если Назначение_.ТребуетСогласования = Истина Тогда
			РегистрыСведений.СтатусыСогласованияЛекарственныхНазначений.ЗарегистрироватьСогласование(
				Назначение_.УникальныйИдентификаторНазначения,
				Перечисления.СтатусыСогласований.ТребуетСогласования,
				ЭтотОбъект,
				ЭтотОбъект.ДатаМД,
				Назначение_.СогласованиеПостфактум
			);
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры

Процедура УдалитьРегистрациюСогласуемыхУслуг(Отказ)
	Если Отказ = Истина Тогда
		Возврат;	
	КонецЕсли;
	
	РегистрыСведений.СтатусыСогласованияУслуг.ОтменитьРегистрациюСогласований(ЭтотОбъект);
КонецПроцедуры

Процедура УдалитьРегистрациюСогласуемыхЛекарственныхНазначений(Отказ)
	Если Отказ = Истина Тогда
		Возврат;	
	КонецЕсли;
	
	РегистрыСведений.СтатусыСогласованияЛекарственныхНазначений.ОтменитьРегистрациюСогласований(ЭтотОбъект);
КонецПроцедуры

// Определяет условия оказания помощи документа.
Функция ЭтоДокументСтационара() Экспорт
	Если ОбщиеМеханизмыПовтИсп.ИспользоватьСтационарныеУсловия() <> Истина Тогда
		Возврат Ложь;	
	КонецЕсли;
	
	ЭтоДокументСтационара_ = Ложь;
	УсловияОказанияПомощи_ = ОпределитьУсловияОказанияПомощи();
	Если УсловияОказанияПомощи_ = Перечисления.УсловияМедицинскойПомощи.АмбулаторноПоликлиническая Тогда
		ЭтоДокументСтационара_ = Ложь;
	ИначеЕсли УсловияОказанияПомощи_ = Перечисления.УсловияМедицинскойПомощи.Стационарная Или
		 УсловияОказанияПомощи_ = Перечисления.УсловияМедицинскойПомощи.ДневнойСтационар Или
		 УсловияОказанияПомощи_ = Перечисления.УсловияМедицинскойПомощи.ВПриемномОтделении
	Тогда
		ЭтоДокументСтационара_ = Истина;
	Иначе
		ЭтоДокументСтационара_ = ЭтоДокументПриемногоОтделения();
	КонецЕсли;
	Возврат ЭтоДокументСтационара_;
КонецФункции

Функция ЭтоДокументПриемногоОтделения()
	Если ОбщиеМеханизмыПовтИсп.ИспользоватьСтационарныеУсловия() <> Истина Тогда
		Возврат Ложь;	
	КонецЕсли;
	
	ЭтоДокументПриемногоОтделения_ = Ложь;
	Если ЭтотОбъект.УсловияОказанияПомощи = Перечисления.УсловияМедицинскойПомощи.ВПриемномОтделении Тогда
		ЭтоДокументПриемногоОтделения_ = Истина;	
	Иначе	
		Если ЭтотОбъект.ВыполненныеУслуги.Количество() > 0 Тогда
			Номенклатура_ = ЭтотОбъект.ВыполненныеУслуги[0].Номенклатура;
			Если ЗначениеЗаполнено(Номенклатура_) Тогда
				ХарактерМедицинскойУслуги_ = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(
					Номенклатура_, "ХарактерМедицинскойУслуги"
				);
				Если ХарактерМедицинскойУслуги_ = Перечисления.ХарактерМедицинскихУслуг["ОсмотрВПриемнике"] Тогда
					ЭтоДокументПриемногоОтделения_ = Истина;	
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	Возврат ЭтоДокументПриемногоОтделения_;
КонецФункции

Функция ОпределитьУсловияОказанияПомощи()
	УсловияОказанияПомощи_ = Неопределено;
	Если ЗначениеЗаполнено(ЭтотОбъект.УсловияОказанияПомощи) Тогда
		УсловияОказанияПомощи_ = ЭтотОбъект.УсловияОказанияПомощи;
	Иначе
		Если ЗначениеЗаполнено(ЭтотОбъект.МедицинскаяКарта) Тогда
			УсловияОказанияПомощи_ = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(
				ЭтотОбъект.МедицинскаяКарта, "ТипМедицинскойКарты.УсловияОказанияПомощи"
			);
		КонецЕсли;
	КонецЕсли;
	Возврат УсловияОказанияПомощи_;
КонецФункции

Процедура ЗарегистрироватьСменныеЗаданияНазначеннойУслуги(Услуга)
	Спецификация_ = МеханизмыСпецификаций.ОпределитьСпецификациюПоУмолчанию(
		Услуга.Номенклатура, ЭтотОбъект.ДатаМД, Услуга.МедицинскоеРабочееМесто
	);
	Если ЗначениеЗаполнено(Спецификация_) Тогда
		Если Услуга.Номенклатура.ВидМедицинскойУслуги = Перечисления.ВидыМедицинскихУслуг.Многоэтапная Тогда
			Отбор_ = Новый Структура("УникальныйИдентификаторУслуги", Услуга.УникальныйИдентификаторУслуги);
			Этапы_ = ЭтотОбъект.ЭтапыНазначенныхУслуг.НайтиСтроки(Отбор_);
			Для Каждого Этап_ Из Этапы_ Цикл
				ЗарегистрироватьСменноеЗаданиеНазначеннойУслуги(
					Услуга, Спецификация_, 
					Этап_.Номенклатура, 
					Этап_.КлючСтрокиСоставаУслуги, 
					Этап_.КлючСтрокиЭтапа,
					Этап_.РабочееМесто,
					Этап_.ЗапланированноеВремя
				);
			КонецЦикла;
		Иначе
			Этапы_ = МеханизмыСпецификаций.ПолучитьЭтапыСпецификации(Спецификация_);
			Для Каждого Этап_ Из Этапы_ Цикл
				ЗарегистрироватьСменноеЗаданиеНазначеннойУслуги(
					Услуга, Спецификация_, 
					Этап_.Номенклатура, 
					Этап_.КлючСтрокиСоставаУслуги, 
					Этап_.КлючСтрокиЭтапа
				);
			КонецЦикла;
		КонецЕсли;
	Иначе
		ЗарегистрироватьСменноеЗаданиеНазначеннойУслуги(
			Услуга, Неопределено, Услуга.Номенклатура, 1, 1
		);
	КонецЕсли;
КонецПроцедуры

Процедура ЗарегистрироватьСменноеЗаданиеНазначеннойУслуги(
	Услуга, Спецификация, НоменклатураСоставаУслуги, КлючСтрокиСоставаУслуги, КлючСтрокиЭтапа,
	МедицинскоеРабочееМесто = Неопределено, ЗапланированноеВремя = Неопределено
)
	МедицинскоеРабочееМесто_ = ?(ЗначениеЗаполнено(МедицинскоеРабочееМесто), МедицинскоеРабочееМесто, Услуга.МедицинскоеРабочееМесто);
	ЗапланированноеВремя_ = ?(ЗначениеЗаполнено(ЗапланированноеВремя), ЗапланированноеВремя, Услуга.ЗапланированноеВремя);
	НаборЗаписей_ = РегистрыСведений.СменныеЗадания.СоздатьНаборЗаписей();
	НаборЗаписей_.Отбор.УникальныйИдентификаторУслуги.Установить(Услуга.УникальныйИдентификаторУслуги);
	НаборЗаписей_.Отбор.КлючСтрокиСоставаУслуги.Установить(КлючСтрокиСоставаУслуги);
	НаборЗаписей_.Прочитать();
	Запись_ = ?(НаборЗаписей_.Количество() > 0, НаборЗаписей_[0], НаборЗаписей_.Добавить());

	Если ЗначениеЗаполнено(Запись_.ДокументПланирования) И Запись_.ДокументПланирования <> ЭтотОбъект.Ссылка Тогда
		// Планирование сделано в сетке документом резервирование, не затираем.
		Возврат;
	КонецЕсли;

	Запись_.УникальныйИдентификаторУслуги = Услуга.УникальныйИдентификаторУслуги;
	Запись_.Номенклатура = Услуга.Номенклатура;
	Запись_.МедицинскоеРабочееМесто = МедицинскоеРабочееМесто_;
	Запись_.ЗапланированноеВремя = ЗапланированноеВремя_;
	Запись_.СпецификацияМедицинскойУслуги = Спецификация;
	Запись_.НоменклатураСоставаУслуги = НоменклатураСоставаУслуги;
	Запись_.КлючСтрокиСоставаУслуги = КлючСтрокиСоставаУслуги;
	Запись_.КлючСтрокиЭтапа = КлючСтрокиЭтапа;
	Запись_.ПланированиеНаСмену = (НачалоДня(Запись_.ЗапланированноеВремя) = Запись_.ЗапланированноеВремя);
	Если ЗначениеЗаполнено(МедицинскоеРабочееМесто_) Тогда
		Запись_.ДокументПланирования = ЭтотОбъект.Ссылка;
		Запись_.МедицинскийКабинет = МедицинскиеРабочиеМеста.ПолучитьКабинет(
			МедицинскоеРабочееМесто_, ЗапланированноеВремя_ 
		);
		Если ЗначениеЗаполнено(Спецификация) Тогда
			Запись_.КлючСтрокиМестаВыполненияЭтапа = 
				МеханизмыСпецификаций.ПолучитьКлючСтрокиМестаВыполненияЭтапа(
					Спецификация, КлючСтрокиЭтапа, МедицинскоеРабочееМесто_
				);
		КонецЕсли;
	КонецЕсли;
	
	Если Услуга.Номенклатура.НеТребуетОтметкиИсполнения = Истина Тогда
		Запись_.ДокументВыполнения = ЭтотОбъект.Ссылка;	
		Запись_.ДатаВыполнения = ЭтотОбъект.ДатаМД;
		Если ЗначениеЗаполнено(МедицинскоеРабочееМесто_) Тогда
			Запись_.ОсновнойИсполнитель = МедицинскиеРабочиеМеста.ПолучитьВрача(
				МедицинскоеРабочееМесто_
			);
		Иначе
			Если ЗначениеЗаполнено(Спецификация) Тогда
				РеквизитыМестВыполненияЭтапа_ = МеханизмыСпецификаций.ПолучитьРеквизитыМестВыполненияЭтапа(
					Спецификация, КлючСтрокиЭтапа
				);
				Если РеквизитыМестВыполненияЭтапа_.Количество() > 0 Тогда
					Запись_.МедицинскоеРабочееМесто = РеквизитыМестВыполненияЭтапа_[0].МестоВыполнения;
					Запись_.МедицинскийКабинет = МедицинскиеРабочиеМеста.ПолучитьКабинет(
						РеквизитыМестВыполненияЭтапа_[0].МестоВыполнения, Услуга.ЗапланированноеВремя 
					);
					Запись_.КлючСтрокиМестаВыполненияЭтапа = РеквизитыМестВыполненияЭтапа_[0].КлючСтроки;
					Запись_.ОсновнойИсполнитель = МедицинскиеРабочиеМеста.ПолучитьВрача(
						РеквизитыМестВыполненияЭтапа_[0].МестоВыполнения
					);
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	НаборЗаписей_.Записать();
КонецПроцедуры

Процедура УдалитьДвиженияВыполненныхУслуг(Отказ, Услуги)
	УдалитьДвиженияВыполненныхУслугПоРегиструСтатусовУслуг(Отказ, Услуги);
	УдалитьДвиженияВыполненныхУслугПоРегиструСменныхЗаданий(Отказ, Услуги);
	УдалитьДвиженияУслугПоРегиструМестаВыполненияУслуг(Отказ, Услуги);
	УдалитьДвиженияхУслугПоРегиструИсполнителиУслуг(Отказ, Услуги);
	
	УИДыУслуг_ = Услуги.ВыгрузитьКолонку("УникальныйИдентификаторУслуги");
	УдалитьДвиженияСтатусыУслугФинансовыеДанные(УИДыУслуг_);
	ВосстановитьРеквизитКоличествоВРегистреСтатусыУслуг(УИДыУслуг_);
КонецПроцедуры

Процедура УдалитьДвиженияВыполненныхУслугПоРегиструСтатусовУслуг(Отказ, Услуги)
	Если Отказ = Истина Тогда
		Возврат;	
	КонецЕсли;
	
	УИДыУслуг_ = Услуги.ВыгрузитьКолонку("УникальныйИдентификаторУслуги");
	ЗначенияРеквизитовУслуг_ = УправлениеЗаказамиУслугами.ПолучитьЗначенияРеквизитовУслуг(
		УИДыУслуг_, "Документ, СтатусУслуги"
	);
	НоменклатураУслуг_ = Услуги.ВыгрузитьКолонку("Номенклатура");
	ВидыУслуг_ = ОбщегоНазначения.ЗначениеРеквизитаОбъектов(НоменклатураУслуг_, "ВидМедицинскойУслуги");
	Для Каждого Услуга_ Из Услуги Цикл
		ЗначенияРеквизитовУслуги_ = ЗначенияРеквизитовУслуг_[Услуга_.УникальныйИдентификаторУслуги];
		Если ЗначенияРеквизитовУслуги_.СтатусУслуги = Перечисления.СтатусыУслуг.Выполнена Или
			 ЗначенияРеквизитовУслуги_.СтатусУслуги = Перечисления.СтатусыУслуг.ВыполненаЧастично Или
			 ЗначенияРеквизитовУслуги_.СтатусУслуги = Перечисления.СтатусыУслуг.ОтложеноВключениеВРеестр
		Тогда
			НаборЗаписей_ = РегистрыСведений.СтатусыУслуг.СоздатьНаборЗаписей();
			НаборЗаписей_.Отбор.УникальныйИдентификаторУслуги.Установить(Услуга_.УникальныйИдентификаторУслуги);
			Если ЗначениеЗаполнено(Услуга_.ИсходныйСтатус) Тогда
				Статус_ = Услуга_.ИсходныйСтатус;
				Если ВидыУслуг_[Услуга_.Номенклатура] = Перечисления.ВидыМедицинскихУслуг.Многоэтапная Тогда
					Статус_ = Перечисления.СтатусыУслуг.НаВыполнение;
					СменныеЗадания_ = ПолучитьСменныеЗаданияУслуги(Услуга_.УникальныйИдентификаторУслуги);
					Для Каждого СменноеЗадание_ Из СменныеЗадания_ Цикл
						Если СменноеЗадание_.КлючСтрокиСоставаУслуги <> Услуга_.КлючСтрокиСоставаУслуги И
							 ЗначениеЗаполнено(СменноеЗадание_.ДатаВыполнения)
						Тогда
							Статус_ = Перечисления.СтатусыУслуг.ВыполненаЧастично;
							Прервать;
						КонецЕсли;
					КонецЦикла;
				КонецЕсли;
				НаборЗаписей_.Прочитать();
				__ПРОВЕРКА__(НаборЗаписей_.Количество() > 0, "Не найдена услуга в регистре статусов услуг");
				НаборЗаписей_[0].СтатусУслуги = Статус_;
				НаборЗаписей_[0].МедицинскийДокумент = Неопределено;
				Если Статус_ = Перечисления.СтатусыУслуг.Назначена Тогда
					НаборЗаписей_[0].Заказ = Неопределено;
				КонецЕсли;
			Иначе	
				НаборЗаписей_.ДополнительныеСвойства.Вставить(
					"СтатусыУслугИсключитьДокументыПриПроверке", 
					ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(ЭтотОбъект.Ссылка)
				);
			КонецЕсли;
			
			// Копируем доп совйства. Среди них могут присутствовать свойства переданные из форм ФМД
			// для управления процессом записи (форма услуг ФМД).
			Для Каждого Свойство_ Из ЭтотОбъект.ДополнительныеСвойства Цикл
				НаборЗаписей_.ДополнительныеСвойства.Вставить(Свойство_.Ключ, Свойство_.Значение);
			КонецЦикла;
			
			НаборЗаписей_.Записать();
		Иначе
			ИмяСообщения_ = "Услуги_НевозможноИзменитьСтатусУслуги";
			ПараметрыСообщения_ = Новый Структура;
			ПараметрыСообщения_.Вставить("Документ", ЗначенияРеквизитовУслуги_.Документ);
			ПараметрыСообщения_.Вставить("Статус", ЗначенияРеквизитовУслуги_.СтатусУслуги);
			ПараметрыСообщения_.Вставить("Номенклатура", Услуга_.Номенклатура);
			СообщенияПользователю.ПоказатьСВыводомВЖурналРегистрации(ИмяСообщения_, ПараметрыСообщения_);
			Отказ = Истина;
			Прервать;
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры

Процедура УдалитьДвиженияВыполненныхУслугПоРегиструСменныхЗаданий(Отказ, Услуги)
	Если Отказ = Истина Тогда
		Возврат;	
	КонецЕсли;
	
	Для Каждого Услуга_ Из Услуги Цикл
		НаборЗаписей_ = РегистрыСведений.СменныеЗадания.СоздатьНаборЗаписей();
		НаборЗаписей_.Отбор.УникальныйИдентификаторУслуги.Установить(Услуга_.УникальныйИдентификаторУслуги);
		НаборЗаписей_.Отбор.КлючСтрокиСоставаУслуги.Установить(Услуга_.КлючСтрокиСоставаУслуги);
		Если Услуга_.БезЗаказа = Ложь Тогда
			НаборЗаписей_.Прочитать();
			__ПРОВЕРКА__(НаборЗаписей_.Количество() > 0, "Не найдена услуга в регистре сменных заданий");
			НаборЗаписей_[0].ОсновноеМестоВыполнения = Неопределено;
			НаборЗаписей_[0].ОсновнойИсполнитель = Неопределено;
			НаборЗаписей_[0].ДатаВыполнения = Неопределено;
			НаборЗаписей_[0].ДокументВыполнения = Неопределено;
		КонецЕсли;
		НаборЗаписей_.Записать();
	КонецЦикла;
КонецПроцедуры

Функция ПолучитьСменныеЗаданияУслуги(УИДУслуги)
	Запрос_ = Новый Запрос(
		"ВЫБРАТЬ
		|	СменныеЗадания.УникальныйИдентификаторУслуги,
		|	СменныеЗадания.КлючСтрокиСоставаУслуги,
		|	СменныеЗадания.ДатаВыполнения,
		|	СменныеЗадания.ДокументВыполнения
		|ИЗ
		|	РегистрСведений.СменныеЗадания КАК СменныеЗадания
		|ГДЕ
		|	СменныеЗадания.УникальныйИдентификаторУслуги = &УникальныйИдентификаторУслуги"
	);
	Запрос_.УстановитьПараметр("УникальныйИдентификаторУслуги", УИДУслуги);
	Результат_ = Запрос_.Выполнить();
	Возврат Результат_.Выгрузить();
КонецФункции

Процедура УдалитьДвиженияНазначенныхУслуг(Отказ, Услуги)
	УдалитьДвиженияНазначенныхУслугПоРегиструСтатусовУслуг(Отказ, Услуги);
	
	Если ЭтоДокументСтационара() Тогда
		УдалитьДвиженияНазначенныхУслугПоРегиструСменныхЗаданий(Отказ, Услуги);
		УдалитьДвиженияУслугПоРегиструМестаВыполненияУслуг(Отказ, Услуги);
		УдалитьДвиженияхУслугПоРегиструИсполнителиУслуг(Отказ, Услуги);
	КонецЕсли;
КонецПроцедуры

Процедура УдалитьДвиженияНазначенныхУслугПоРегиструСтатусовУслуг(Отказ, Услуги)
	Если Отказ = Истина Тогда
		Возврат;	
	КонецЕсли;
	
	УИДыУслуг_ = Услуги.ВыгрузитьКолонку("УникальныйИдентификаторУслуги");
	ЗначенияРеквизитовУслуг_ = УправлениеЗаказамиУслугами.ПолучитьЗначенияРеквизитовУслуг(
		УИДыУслуг_, "СтатусУслуги, Документ"
	);
	Для Каждого Услуга_ Из Услуги Цикл
		ЗначенияРеквизитовУслуги_ = ЗначенияРеквизитовУслуг_[Услуга_.УникальныйИдентификаторУслуги];
		Если Истина = Справочники.УслугиПациентов.ЯвляетсяПриемом(Услуга_.УникальныйИдентификаторУслуги) Тогда 
			Продолжить;
		КонецЕсли;
		
		Если ЗначенияРеквизитовУслуги_.СтатусУслуги = Перечисления.СтатусыУслуг.ОтмененЗаказ Тогда
			НаборЗаписей_ = РегистрыСведений.СтатусыУслуг.СоздатьНаборЗаписей();
			НаборЗаписей_.Отбор.УникальныйИдентификаторУслуги.Установить(Услуга_.УникальныйИдентификаторУслуги);
			НаборЗаписей_.Прочитать();
			НаборЗаписей_[0].Документ = ЭтотОбъект.Ссылка;
			НаборЗаписей_[0].СтатусУслуги = Перечисления.СтатусыУслуг.Отменена;
			НаборЗаписей_.Записать();
		ИначеЕсли ЗначенияРеквизитовУслуги_.СтатусУслуги = Перечисления.СтатусыУслуг.Отменена Тогда
			Продолжить;
		ИначеЕсли ЗначенияРеквизитовУслуги_.Документ = ЭтотОбъект.Ссылка Тогда
			НаборЗаписей_ = РегистрыСведений.СтатусыУслуг.СоздатьНаборЗаписей();
			НаборЗаписей_.Отбор.УникальныйИдентификаторУслуги.Установить(Услуга_.УникальныйИдентификаторУслуги);
			НаборЗаписей_.Записать();
		Иначе
			ИмяСообщения_ = "Услуги_НевозможноИзменитьСтатусУслуги";
			ПараметрыСообщения_ = Новый Структура;
			ПараметрыСообщения_.Вставить("Документ", ЗначенияРеквизитовУслуги_.Документ);
			ПараметрыСообщения_.Вставить("Статус", ЗначенияРеквизитовУслуги_.СтатусУслуги);
			ПараметрыСообщения_.Вставить("Номенклатура", Услуга_.Номенклатура);
			СообщенияПользователю.ПоказатьСВыводомВЖурналРегистрации(ИмяСообщения_, ПараметрыСообщения_);
			Отказ = Истина;
			Прервать;
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры

Процедура УдалитьДвиженияНазначенныхУслугПоРегиструСменныхЗаданий(Отказ, Услуги)
	Если Отказ = Истина Тогда
		Возврат;	
	КонецЕсли;
	
	Для Каждого Услуга_ Из Услуги Цикл
		НаборЗаписей = РегистрыСведений.СменныеЗадания.СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.УникальныйИдентификаторУслуги.Установить(Услуга_.УникальныйИдентификаторУслуги);
		НаборЗаписей.Записать();
	КонецЦикла;
КонецПроцедуры

Процедура УдалитьДвиженияУслугПоРегиструМестаВыполненияУслуг(Отказ, Услуги)
	Если Отказ = Истина Тогда
		Возврат;	
	КонецЕсли;
	
	Для Каждого Услуга_ Из Услуги Цикл
		НаборЗаписей_ = РегистрыСведений.МестаВыполненияУслуг.СоздатьНаборЗаписей();
		НаборЗаписей_.Отбор.УникальныйИдентификаторУслуги.Установить(Услуга_.УникальныйИдентификаторУслуги);
		Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(Услуга_, "КлючСтрокиСоставаУслуги") Тогда
			НаборЗаписей_.Отбор.КлючСтрокиСоставаУслуги.Установить(Услуга_.КлючСтрокиСоставаУслуги);
		КонецЕсли;
		НаборЗаписей_.Записать();
	КонецЦикла;
КонецПроцедуры

Процедура УдалитьДвиженияхУслугПоРегиструИсполнителиУслуг(Отказ, Услуги)
	Если Отказ = Истина Тогда
		Возврат;	
	КонецЕсли;
	
	Для Каждого Услуга_ Из Услуги Цикл
		НаборЗаписей_ = РегистрыСведений.ИсполнителиУслуг.СоздатьНаборЗаписей();
		НаборЗаписей_.Отбор.УникальныйИдентификаторУслуги.Установить(Услуга_.УникальныйИдентификаторУслуги);
		Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(Услуга_, "КлючСтрокиСоставаУслуги") Тогда
			НаборЗаписей_.Отбор.КлючСтрокиСоставаУслуги.Установить(Услуга_.КлючСтрокиСоставаУслуги);
		КонецЕсли;
		НаборЗаписей_.Записать();
	КонецЦикла;
КонецПроцедуры

Процедура ЗарегистрироватьВыполненныеПриемы(Отказ)
	Если Отказ = Истина Тогда
		Возврат;	
	КонецЕсли;
	
	ОсновноеМестоВыполнения_ = ?(
		ЭтотОбъект.МестаВыполнения.Количество() > 0, 
		ЭтотОбъект.МестаВыполнения[0].МестоВыполнения, 
		Неопределено
	);
	СтрокаИсполнителя_ = ЭтотОбъект.Исполнители.Найти(Истина, "ОтветственныйЗаНазначение");
	ОсновнойИсполнитель_ = ?(
		СтрокаИсполнителя_ <> Неопределено, СтрокаИсполнителя_.Исполнитель, Неопределено
	);
	Для Каждого Прием_ Из ЭтотОбъект.ВыполненныеПриемы Цикл
		НаборЗаписей_ = РегистрыСведений.Приемы.СоздатьНаборЗаписей();
		НаборЗаписей_.Отбор.УникальныйИдентификаторПриема.Установить(Прием_.УникальныйИдентификаторПриема);
		НаборЗаписей_.Прочитать();
		__ПРОВЕРКА__(НаборЗаписей_.Количество() > 0, "Нет зарегистрированного приема");
		НаборЗаписей_[0].ДокументВыполнения = ЭтотОбъект.Ссылка;
		НаборЗаписей_[0].ДатаВыполнения = Прием_.ДатаВыполнения;
		НаборЗаписей_[0].ОсновноеМестоВыполнения = ОсновноеМестоВыполнения_;
		НаборЗаписей_[0].ОсновнойИсполнитель = ОсновнойИсполнитель_;
		НаборЗаписей_.Записать();
	КонецЦикла;
	
	Для Каждого Прием_ Из ЭтотОбъект.ВыполненныеУслуги Цикл
		НаборЗаписей_ = РегистрыСведений.Приемы.СоздатьНаборЗаписей();
		НаборЗаписей_.Отбор.УникальныйИдентификаторПриема.Установить(Прием_.УникальныйИдентификаторУслуги);
		НаборЗаписей_.Прочитать();
		Если НаборЗаписей_.Количество() = 0 Тогда
			// Это не прием, а услуга.
			Продолжить;
		КонецЕсли;
		НаборЗаписей_[0].ДокументВыполнения = ЭтотОбъект.Ссылка;
		НаборЗаписей_[0].ДатаВыполнения = Прием_.ДатаВыполнения;
		НаборЗаписей_[0].ОсновноеМестоВыполнения = ОсновноеМестоВыполнения_;
		НаборЗаписей_[0].ОсновнойИсполнитель = ОсновнойИсполнитель_;
		НаборЗаписей_.Записать();
	КонецЦикла;
	
КонецПроцедуры

Процедура УдалитьРегистрациюВыполненныхПриемов(Отказ, УникальныеИдентификаторыПриемов)
	Если Отказ = Истина Тогда
		Возврат;	
	КонецЕсли;
	
	Для Каждого УникальныйИдентификаторПриема_ Из УникальныеИдентификаторыПриемов Цикл
		НаборЗаписей_ = РегистрыСведений.Приемы.СоздатьНаборЗаписей();
		НаборЗаписей_.Отбор.УникальныйИдентификаторПриема.Установить(УникальныйИдентификаторПриема_);
		НаборЗаписей_.Прочитать();
		Если НаборЗаписей_.Количество() > 0 Тогда
			НаборЗаписей_[0].ДокументВыполнения = Неопределено;
			НаборЗаписей_[0].ДатаВыполнения = Неопределено;
			НаборЗаписей_[0].ОсновноеМестоВыполнения = Неопределено;
			НаборЗаписей_[0].ОсновнойИсполнитель = Неопределено;
			НаборЗаписей_.Записать();
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры

Процедура ЗарегистрироватьНазначенныеПриемы(Отказ)
	Если Отказ = Истина Тогда
		Возврат;
	КонецЕсли;
	
	НазначенныеПриемы_ = Новый Массив();
	Для Каждого Прием_ Из ЭтотОбъект.НазначенныеПриемы Цикл
		НазначенныеПриемы_.Добавить(Прием_);
	КонецЦикла;
	
	УслугиЯвляющиесяПриемами_ = Новый Массив;
	Для Каждого Прием_ Из ЭтотОбъект.НазначенныеУслуги Цикл
		Если Истина = Справочники.Номенклатура.ЯвляетсяПриемом(Прием_.Номенклатура) Тогда 
			УслугиЯвляющиесяПриемами_.Добавить(Прием_);
		КонецЕсли;
	КонецЦикла;
	Если УслугиЯвляющиесяПриемами_.Количество() > 0 Тогда
		ОграничениеТаблицы_ = ЭтотОбъект.НазначенныеУслуги.Выгрузить(УслугиЯвляющиесяПриемами_);
		ОграничениеТаблицы_.Колонки.УникальныйИдентификаторУслуги.Имя = "УникальныйИдентификаторПриема";
		Для Каждого Прием_ Из ОграничениеТаблицы_ Цикл
			НазначенныеПриемы_.Добавить(Прием_);
		КонецЦикла;
	КонецЕсли;
	
	Для Каждого Прием_ Из НазначенныеПриемы_ Цикл
		НаборЗаписей_ = РегистрыСведений.Приемы.СоздатьНаборЗаписей();
		НаборЗаписей_.Отбор.УникальныйИдентификаторПриема.Установить(Прием_.УникальныйИдентификаторПриема);
		Если ЭтоПерепроведение Тогда
			НаборЗаписей_.Прочитать();	
		КонецЕсли;
		Запись_ = ?(НаборЗаписей_.Количество() > 0, НаборЗаписей_[0], НаборЗаписей_.Добавить());
		Запись_.УникальныйИдентификаторПриема = Прием_.УникальныйИдентификаторПриема;
		Запись_.ДокументЗаказа = ЭтотОбъект.Ссылка;
		Запись_.МедицинскаяКарта = ЭтотОбъект.МедицинскаяКарта;
		Запись_.Пациент = ЭтотОбъект.Пациент;
		Запись_.Номенклатура = Прием_.Номенклатура;
		Если ЗначениеЗаполнено(Прием_.МедицинскоеРабочееМесто) Тогда
			Запись_.ДокументПланирования = ЭтотОбъект.Ссылка;
			Запись_.МедицинскоеРабочееМесто = Прием_.МедицинскоеРабочееМесто;
			Запись_.ЗапланированноеВремя = Прием_.ЗапланированноеВремя;
			Запись_.ЧислоТалонов = Прием_.ЧислоТалонов;
			Запись_.МедицинскийКабинет = МедицинскиеРабочиеМеста.ПолучитьКабинет(
				Прием_.МедицинскоеРабочееМесто, Прием_.ЗапланированноеВремя
			);
		Иначе
			Если Запись_.ДокументПланирования = ЭтотОбъект.Ссылка Тогда
				Запись_.ДокументПланирования = Неопределено;
				Запись_.МедицинскоеРабочееМесто = Неопределено;
				Запись_.ЗапланированноеВремя = Неопределено;
				Запись_.ЧислоТалонов = 0;
				Запись_.МедицинскийКабинет = Неопределено;
			КонецЕсли;
		КонецЕсли;
		НаборЗаписей_.Записать();
	КонецЦикла;
	
КонецПроцедуры

Процедура УдалитьРегистрациюНазначенныхПриемов(Отказ, УникальныеИдентификаторыПриемов)
	Если Отказ = Истина Тогда
		Возврат;	
	КонецЕсли;
	
	Для Каждого УникальныйИдентификаторПриема_ Из УникальныеИдентификаторыПриемов Цикл
		НаборЗаписей_ = РегистрыСведений.Приемы.СоздатьНаборЗаписей();
		НаборЗаписей_.Отбор.УникальныйИдентификаторПриема.Установить(УникальныйИдентификаторПриема_);
		НаборЗаписей_.Записать();
	КонецЦикла;
КонецПроцедуры

Процедура ЗарегистрироватьКритерииКачества(Отказ)
	Если Отказ = Истина Тогда
		Возврат;	
	КонецЕсли;
	
	Движения.КритерииКачества.Записывать = Истина;
	Для Каждого Диагноз_ Из ЭтотОбъект.ДиагнозыПоМКБ10 Цикл
		Если ЗначениеЗаполнено(Диагноз_.КритерииКачества) И 
			 ЗначениеЗаполнено(Диагноз_.КритерииКачестваРезультатОценки)  И
			 Диагноз_.Действует
		Тогда
			Движение_ = Движения.КритерииКачества.Добавить();
			Движение_.Период = ЭтотОбъект.Дата;
			Движение_.МКБ10 = Диагноз_.МКБ10;
			Движение_.КритерииКачества = Диагноз_.КритерииКачества;
			Движение_.КритерииКачестваРезультатОценки = Диагноз_.КритерииКачестваРезультатОценки;
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры

Процедура ЗарегистрироватьИсследованияПоФСИДИ(Отказ)
	Если Отказ = Истина Тогда
		Возврат;	
	КонецЕсли;
		
	НаборЗаписей_ = РегистрыСведений.ИсследованияПоФСИДИ.СоздатьНаборЗаписей();
	НаборЗаписей_.Отбор.Документ.Установить(ЭтотОбъект.Ссылка);
	НаборЗаписей_.Отбор.Пациент.Установить(ЭтотОбъект.Пациент);
	Для Каждого Строка_ Из ЭтотОбъект.ВыполненныеИнструментальныеИсследования Цикл
		НовЗапись_ = НаборЗаписей_.Добавить();
		НовЗапись_.Пациент = ЭтотОбъект.Пациент;
		НовЗапись_.Документ = ЭтотОбъект.Ссылка;
		НовЗапись_.Исследование = Строка_.Исследование;
		НовЗапись_.Доза = Строка_.ЛучеваяНагрузка;
		Если Строка_.ЛучеваяНагрузка > 0 Тогда
			НовЗапись_.ЕдиницаИзмерения = "мЗв";
		КонецЕсли;
		НовЗапись_.Дата = ЭтотОбъект.ДатаМД;
	КонецЦикла;
	НаборЗаписей_.Записать(Истина);
КонецПроцедуры

Функция ПользовательСредиИсполнителей(ТЧИсполнители, Пользователь)
	МассивСотрудников_ = РегистрыСведений.СотрудникиПользователя.ПолучитьСотрудниковПользователя(
		Пользователь
	);
	Для Каждого СтрокаТЧИсполнители_ Из ТЧИсполнители Цикл 
		Если МассивСотрудников_.Найти(СтрокаТЧИсполнители_.Исполнитель) <> Неопределено Тогда 
			Возврат Истина;
		КонецЕсли;
	КонецЦикла;
	
	Возврат Ложь;
КонецФункции

Функция КонтрольОтсутствияИзмененийУсловийОплатыУжеОплаченныхУслуг()
	Результат_ = Ложь;
	Если ЗначениеЗаполнено(ЭтотОбъект.Ссылка) Тогда
		
		ИзменившиесяУслуги_ = Новый Массив;
		НоменклатураУслуг_ = Новый Соответствие;
		
		Для Каждого строкаТЧ_ Из ЭтотОбъект.ВыполненныеУслуги Цикл
			Если строкаТЧ_.БезЗаказа Тогда
				Найденные_ = ЭтотОбъект.Ссылка.ВыполненныеУслуги.НайтиСтроки(
					Новый Структура("УникальныйИдентификаторУслуги", строкаТЧ_.УникальныйИдентификаторУслуги));
					
				Если Найденные_.Количество() > 0 И строкаТЧ_.Сумма <> Найденные_[0].Сумма Тогда
					ИзменившиесяУслуги_.Добавить(Строка(строкаТЧ_.УникальныйИдентификаторУслуги));
					НоменклатураУслуг_.Вставить(Строка(строкаТЧ_.УникальныйИдентификаторУслуги), строкаТЧ_.Номенклатура);
				КонецЕсли;
			КонецЕсли;
		КонецЦикла;
		
		Если ИзменившиесяУслуги_.Количество() > 0 Тогда
			Запрос_ = Новый Запрос;
			Запрос_.Текст =
				"ВЫБРАТЬ
				|	РасчетыСКлиентами.ЗаказКлиента КАК ЗаказКлиента,
				|	РасчетыСКлиентами.Регистратор КАК Регистратор
				|ИЗ
				|	РегистрНакопления.РасчетыСКлиентами КАК РасчетыСКлиентами
				|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ЧекККМВозврат КАК ЧекККМВозврат
				|		ПО РасчетыСКлиентами.Регистратор = ЧекККМВозврат.ЧекККМ
				|			И ЧекККМВозврат.Проведен
				|ГДЕ
				|	РасчетыСКлиентами.ЗаказКлиента В(&СписокУИД)
				|	И РасчетыСКлиентами.Регистратор ССЫЛКА Документ.ЧекККМ
				|	И ЧекККМВозврат.Ссылка ЕСТЬ NULL";
				
			Запрос_.УстановитьПараметр("СписокУИД", ИзменившиесяУслуги_);
			Выборка_ = Запрос_.Выполнить().Выбрать();
			
			Пока Выборка_.Следующий() Цикл
				
				Сообщить("В документе изменены условия оплаты услуги """
					+ НоменклатураУслуг_.Получить(Выборка_.ЗаказКлиента)
					+ """, которая уже была оплачена документом: " + Выборка_.Регистратор);
					
				Результат_ = Истина;
			КонецЦикла;
		КонецЕсли;
	КонецЕсли;
	
	Возврат Результат_;
КонецФункции

Процедура ВыполнитьПроверкуСЭМД(Отказ, ШаблонCDA, ДокументГотов = Истина)
	Если ШаблонCDA = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если ДокументГотов 
		И НЕ ЭтотОбъект.ДополнительныеСвойства.Свойство("ОтключитьКонтрольНаличияВыполненныхУслуг") 
	Тогда
		Если ШаблоныМедицинскихДокументовПоликлиника.ЭтоСЭМДНаправлениеНаМСЭ(ШаблонCDA) Тогда
			
		ИначеЕсли ШаблоныМедицинскихДокументовПоликлиника.ЭтоСЭМДПротоколКонсультации(ШаблонCDA) Тогда
			ВыполнитьПроверкуТипаКонсультации(Отказ);
		ИначеЕсли ШаблоныМедицинскихДокументовПоликлиника.ЭтоСЭМДПротоколИнструментальногоИсследования(ШаблонCDA) Тогда
			ВыполнитьПроверкуИнструментальныхИсследований(Отказ);
		ИначеЕсли ШаблоныМедицинскихДокументовПоликлиника.ЭтоСЭМДПротоколТелемедицинскойКонсультации(ШаблонCDA) Тогда
			ВыполнитьПроверкуТипаКонсультации(Отказ);
		КонецЕсли;
	КонецЕсли;
	
	Если ШаблоныМедицинскихДокументовПоликлиника.ЭтоСЭМДПротоколПатологоАнатомическогоВскрытияПлода(ШаблонCDA) Тогда
		ЭтотОбъект.ДополнительныеСвойства.Вставить("ЗаписатьДажеЕслиКартаЗакрыта", Истина);
	КонецЕсли;
КонецПроцедуры

Процедура ВыполнитьПроверкуТипаКонсультации(Отказ)
	ВыполненныеУслуги_ = ЭтотОбъект.ВыполненныеУслуги;
	Для Каждого Услуга_ Из ВыполненныеУслуги_ Цикл
		ТипКонсультации_ = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Услуга_.НоменклатураСоставаУслуги, "ТипКонсультации");
		Если ЗначениеЗаполнено(ТипКонсультации_) Тогда
			Возврат;
		КонецЕсли;
	КонецЦикла;
	Отказ = Истина;
	СообщенияПользователю.Показать(
		"МедицинскийДокумент_ОтсутствуетУслугаСТипомКонсультации"
	);
КонецПроцедуры

Процедура ВыполнитьПроверкуИнструментальныхИсследований(Отказ)
	Если ЭтотОбъект.ВыполненныеИнструментальныеИсследования.Количество() = 0 Тогда
		Отказ = Истина;
		СообщенияПользователю.Показать(
			"МедицинскийДокумент_НеЗаполненыИнструментальныеИсследования"
		);
	КонецЕсли;
КонецПроцедуры

Процедура ЗаписатьВЖурналРегистрации(Сообщение, Уровень = Неопределено)

	ЗаписьЖурналаРегистрации(
		"МедицинскийДокумент.МодульОбъекта",
		?(Уровень = Неопределено, УровеньЖурналаРегистрации.Информация, Уровень),
		,
		,
		Сообщение
	);
	
КонецПроцедуры

Функция ЗаполнитьДокументПоДаннымCDA()

	Результат_ = Неопределено;
	
	НомерCDA_ = 1;
	
	ТекущаяУниверсальнаяДата_ = ТекущаяУниверсальнаяДата();
	
	Для Каждого СтрокаТЧ_ Из ЭтотОбъект.CDAДокументы Цикл
	
		Если СтрокаТЧ_.Актуально И ЗначениеЗаполнено(СтрокаТЧ_.ТелоМедДокумента) Тогда
		
			ЗначенияРеквизитовТребуютЗаполнения_ = Ложь;// Избегаем повторный парсинг CDA без необходимости.
			
			Если Не ЗначениеЗаполнено(ЭтотОбъект.Ссылка) Тогда
				// Записывается новый документ.
				ЗначенияРеквизитовТребуютЗаполнения_ = Истина;
				
			Иначе
				ДатаМодификацииCDA_ = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(СтрокаТЧ_.ТелоМедДокумента, "ДатаМодификацииУниверсальная");
				
				Если ДатаМодификацииCDA_ > СтрокаТЧ_.ДатаАктуализацииУниверсальная Тогда
					// CDA изменился
					ЗначенияРеквизитовТребуютЗаполнения_ = Истина;
				КонецЕсли;
			КонецЕсли;
			
			Если ЗначенияРеквизитовТребуютЗаполнения_ Тогда
				ДанныеCDA_ = ПолучитьЗначенияИзCDA(СтрокаТЧ_.ТелоМедДокумента);
				
				Если ДанныеCDA_ <> Неопределено Тогда
					Если НомерCDA_ = 1 И ЗначениеЗаполнено(ДанныеCDA_.ЗаголовокМД) Тогда
						ЭтотОбъект.Заголовок = ДанныеCDA_.ЗаголовокМД;
						
						Результат_ = ДанныеCDA_;
					КонецЕсли;
				
					ЗаполнитьЗначенияСвойств(СтрокаТЧ_, ДанныеCDA_);
					
					СтрокаТЧ_.ДатаАктуализацииУниверсальная = ТекущаяУниверсальнаяДата_;
				КонецЕсли;
			КонецЕсли;
			
			НомерCDA_ = НомерCDA_ + 1;
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат Результат_;
	
КонецФункции

Функция ПолучитьЗначенияИзCDA(ТелоМедДокумента)
	Расширение_ = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ТелоМедДокумента, "Расширение");
	
	Если Расширение_ = "cda" Тогда
		СЭМД_ = Документы.МедицинскийДокумент.ПолучитьТелоМД(ТелоМедДокумента);
		
		Если ЗначениеЗаполнено(СЭМД_) Тогда

			Результат_ = ФедеральныеВебСервисыРазборСЭМД.ПолучитьДанныеИзСЭМД(СЭМД_, "ДанныеДляСтатусыМедицинскихДокументов");
			
			Возврат Результат_["ДанныеДляСтатусыМедицинскихДокументов"];
		КонецЕсли;
	КонецЕсли;
	
	Возврат Неопределено;
КонецФункции

#КонецОбласти

#КонецЕсли


