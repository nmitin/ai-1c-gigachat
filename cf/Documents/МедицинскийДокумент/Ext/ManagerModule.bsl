#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// проверяет, принадлежность медицинского документа указанному типу
//
// Параметры:
//    МедицинскийДокумент - ДокументСсылка.МедициснкийДокумент - медицинский документ, который проверяем
//    ТипыДокумента       - Строка, Массив                     - АтрибутCode из регистра СтатусыМедицинскихДокументов
//    codeSystem          - Строка                             - codeSystem из регистра СтатусыМедицинскихДокументов
//
// Возврат:
//    Булево - Истина, если документ принадлежит указанному типу, Ложь не принадлежит
//
Функция ПринадлежитТипу(Знач МедицинскийДокумент, ТипыДокумента, Знач  codeSystem = "1.2.643.5.1.13.13.11.1522") Экспорт
	
	Если ТипЗнч(ТипыДокумента) = Тип("Строка") Тогда
		ТипыДокументовЗапрос_ = СтрРазделить(ТипыДокумента, ",", Ложь);
	Иначе
		ТипыДокументовЗапрос_ = ТипыДокумента;
	КонецЕсли;
	
	Запрос_ = Новый Запрос;
	Запрос_.Текст = "ВЫБРАТЬ ПЕРВЫЕ 1
	                |	СтатусыМедицинскихДокументов.МедицинскийДокумент КАК МедицинскийДокумент
	                |ИЗ
	                |	РегистрСведений.СтатусыМедицинскихДокументов КАК СтатусыМедицинскихДокументов
	                |ГДЕ
	                |	СтатусыМедицинскихДокументов.МедицинскийДокумент = &Ссылка
	                |	И СтатусыМедицинскихДокументов.codeSystem = &codeSystem
	                |	И СтатусыМедицинскихДокументов.АтрибутCode В (&ТипыДокументаМассивСтрок)";
	
	Запрос_.УстановитьПараметр("Ссылка", МедицинскийДокумент);
	Запрос_.УстановитьПараметр("codeSystem", codeSystem);
	Запрос_.УстановитьПараметр("ТипыДокументаМассивСтрок", ТипыДокументовЗапрос_);
	
	Выборка_ = Запрос_.Выполнить().Выбрать();
	
	Возврат Выборка_.Следующий();
	
КонецФункции

// Возвращает некоторые данные документа для РЭМД (ВИМИС).
//
// Параметры:
//  ДокументСсылка		 - ДокументСсылка.МедицинскийДокумент, СправочникСсылка.Файлы	 - МедДокумент или ссыдка на CDA-файл.
//  ТребуемыеРеквизиты - Строка, Неопределено	 - Список требуемых свойств для возвращаемой структуры
// 
// Возвращаемое значение:
//  Структура - Структура с данными по документу
//
Функция ПолучитьСвойстваДокументаДляРЭМД(ДокументСсылка, ТребуемыеРеквизиты = Неопределено) Экспорт
	Если НЕ ЗначениеЗаполнено(ТребуемыеРеквизиты) Тогда 
		ТребуемыеРеквизиты = 
			"Пациент,МедицинскаяКарта,Дата,ДатаПодписи,Автор,Подписант,Организация,Подразделение,КодТипаДокументаРЭМД,Описание,ТипДокументаВИМИС,
			|СписокСотрудниковОбязанныхПодписать,СтруктурированныеДанныеДокумента"
		;
	КонецЕсли;
	
	Если ТипЗнч(ДокументСсылка) = Тип("СправочникСсылка.Файлы") Тогда
		// Ссылка на тело МД.
		МДСсылка_ = НайтиМДПоCDAДокументу(ДокументСсылка);
		Если Не ЗначениеЗаполнено(МДСсылка_) Тогда
			ВызватьИсключение стрШаблон("Не удалось найти документ МедицинскийДокумент по ссылке на cda файл: %1", ДокументСсылка);
		КонецЕсли;
		CDAСсылка_ = ДокументСсылка;
	Иначе
		МДСсылка_ = ДокументСсылка;
		CDAСсылка_ = Неопределено; // Возможно, тут стоит получить основной CDA.
	КонецЕсли;
	
	Результат_ = Новый Структура(ТребуемыеРеквизиты);
	
	СтруктураПолученияРеквизитов_ = Новый Структура();
	РеквизитыВыполненияМД_ = Неопределено;
	Если Результат_.Свойство("Пациент") Тогда
		СтруктураПолученияРеквизитов_.Вставить("Пациент");
	КонецЕсли;
	
	Если Результат_.Свойство("МедицинскаяКарта") Тогда
		СтруктураПолученияРеквизитов_.Вставить("МедицинскаяКарта");
	КонецЕсли;

	Если Результат_.Свойство("Описание") Тогда
		Если ЗначениеЗаполнено(CDAСсылка_) Тогда
			Запрос_ = Новый Запрос;
			Запрос_.Текст =
				"ВЫБРАТЬ
				|	СтатусыМедицинскихДокументов.ЗаголовокМД КАК ЗаголовокМД
				|ИЗ
				|	РегистрСведений.СтатусыМедицинскихДокументов КАК СтатусыМедицинскихДокументов
				|ГДЕ
				|	СтатусыМедицинскихДокументов.ТелоМедДокумента = &ТелоМедДокумента";
			Запрос_.УстановитьПараметр("ТелоМедДокумента", CDAСсылка_);
			Выборка_ = Запрос_.Выполнить().Выбрать();
			Если Выборка_.Следующий() Тогда
				Результат_.Описание = Выборка_.ЗаголовокМД;
			КонецЕсли;
		Иначе
			// Старое поведений для обратной совместимости.
			СтруктураПолученияРеквизитов_.Вставить("Описание", "Заголовок");
		КонецЕсли;
	КонецЕсли;

	Если Результат_.Свойство("Дата") Тогда
		Если Неопределено = РеквизитыВыполненияМД_ Тогда 
			РеквизитыВыполненияМД_ = ШаблоныМедицинскихДокументовПереопределяемый.ПолучитьРеквизитыВыполнения(МДСсылка_);
		КонецЕсли;
		Результат_.Дата = РеквизитыВыполненияМД_.ДатаВыполнения;
	КонецЕсли;
	
	Если Результат_.Свойство("ДатаПодписи") Тогда
		Если Неопределено = РеквизитыВыполненияМД_ Тогда 
			РеквизитыВыполненияМД_ = ШаблоныМедицинскихДокументовПереопределяемый.ПолучитьРеквизитыВыполнения(МДСсылка_);
		КонецЕсли;
		Результат_.ДатаПодписи = РеквизитыВыполненияМД_.ДатаВыполнения;
	КонецЕсли;
	
	Если Результат_.Свойство("Автор") Тогда 
		Если Неопределено = РеквизитыВыполненияМД_ Тогда 
			РеквизитыВыполненияМД_ = ШаблоныМедицинскихДокументовПереопределяемый.ПолучитьРеквизитыВыполнения(МДСсылка_);
		КонецЕсли;
		Результат_.Автор = РеквизитыВыполненияМД_.Исполнитель;
	КонецЕсли;

	Если Результат_.Свойство("Подписант") Тогда 
		Если Неопределено = РеквизитыВыполненияМД_ Тогда 
			РеквизитыВыполненияМД_ = ШаблоныМедицинскихДокументовПереопределяемый.ПолучитьРеквизитыВыполнения(МДСсылка_);
		КонецЕсли;
		Результат_.Подписант = РеквизитыВыполненияМД_.Исполнитель;
	КонецЕсли;

	Если Результат_.Свойство("СписокСотрудниковОбязанныхПодписать") Тогда 
		Результат_.СписокСотрудниковОбязанныхПодписать = ПолучитьСписокСотрудниковОбязанныхПодписатьМД(МДСсылка_);
	КонецЕсли;

	Если Результат_.Свойство("Организация") Или Результат_.Свойство("СтруктурированныеДанныеДокумента") Тогда
		СтруктураПолученияРеквизитов_.Вставить("ПолученОтМО");
		СтруктураПолученияРеквизитов_.Вставить("Организация");
	КонецЕсли;

	Если Результат_.Свойство("Подразделение") Тогда
		СтруктураПолученияРеквизитов_.Вставить("МедицинскаяКарта");
		СтруктураПолученияРеквизитов_.Вставить("Пациент");
		СтруктураПолученияРеквизитов_.Вставить("Дата");
		СтруктураПолученияРеквизитов_.Вставить("УсловияОказанияПомощи");
	КонецЕсли;

	Если ЗначениеЗаполнено(СтруктураПолученияРеквизитов_) Тогда 
		РеквизитыДокумента_ = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(МДСсылка_, СтруктураПолученияРеквизитов_);
		ЗаполнитьЗначенияСвойств(Результат_, РеквизитыДокумента_);
	КонецЕсли;
	
	Если Результат_.Свойство("Организация") Тогда
		Результат_.Организация = ВыбратьЗаполненное(РеквизитыДокумента_.ПолученОтМО, РеквизитыДокумента_.Организация);
	КонецЕсли;

	Если Результат_.Свойство("КодТипаДокументаРЭМД") Тогда
		Результат_.КодТипаДокументаРЭМД = КодТипаДокументаРЭМД(МДСсылка_, CDAСсылка_);
	КонецЕсли;

	Если Результат_.Свойство("Подразделение") Тогда
		Если ОбщиеМеханизмыПовтИсп.ИспользоватьСтационарныеУсловия()
			И (РеквизитыДокумента_.УсловияОказанияПомощи = Перечисления.УсловияМедицинскойПомощи.Стационарная
				Или РеквизитыДокумента_.УсловияОказанияПомощи = Перечисления.УсловияМедицинскойПомощи.ДневнойСтационар)
		Тогда
			
			Результат_.Подразделение = Пациенты.ПодразделениеСтационарногоПациента(
				РеквизитыДокумента_.Пациент,
				РеквизитыДокумента_.Дата,
				РеквизитыДокумента_.МедицинскаяКарта
			);
			
		Иначе
			// Возьмем подразделение исполнителя.
			Запрос_ = Новый Запрос(
				"ВЫБРАТЬ
				|	МедицинскийДокументИсполнители.Исполнитель.Подразделение КАК Подразделение
				|ИЗ
				|	Документ.МедицинскийДокумент.Исполнители КАК МедицинскийДокументИсполнители
				|ГДЕ
				|	МедицинскийДокументИсполнители.Ссылка = &Ссылка
				|	И МедицинскийДокументИсполнители.ОтветственныйЗаНазначение"
			);
			Запрос_.УстановитьПараметр("Ссылка", МДСсылка_);
			Выборка_ = Запрос_.Выполнить().Выбрать();
			Если Выборка_.Следующий() И ЗначениеЗаполнено(Выборка_.Подразделение) Тогда
				Результат_.Подразделение =  Выборка_.Подразделение;
				
				Если ЗначениеЗаполнено(Результат_.Подразделение) Тогда
					Результат_.Подразделение = Справочники.СтруктураПредприятия.ПолучитьПодразделениеПоОтделению(Результат_.Подразделение);
				КонецЕсли;

			КонецЕсли;
		КонецЕсли;
	КонецЕсли;

	Если Результат_.Свойство("ТипДокументаВИМИС") Тогда
		Запрос_ = Новый Запрос(
			"ВЫБРАТЬ
			|	ТД.АтрибутCode КАК АтрибутCode,
			|	ТД.codeSystem КАК codeSystem,
			|	ТД.ТипДокументаВИМИС КАК ТипДокументаВИМИС
			|ПОМЕСТИТЬ втТипыДокументов
			|ИЗ
			|	&ТипыДокументов КАК ТД
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ ПЕРВЫЕ 1
			|	СтатусыМД.АтрибутCode КАК АтрибутCode,
			|	СтатусыМД.codeSystem КАК codeSystem,
			|	ТипыВИМИС.ТипДокументаВИМИС КАК ТипДокументаВИМИС
			|ИЗ
			|	РегистрСведений.СтатусыМедицинскихДокументов КАК СтатусыМД
			|		ЛЕВОЕ СОЕДИНЕНИЕ втТипыДокументов КАК ТипыВИМИС
			|		ПО (ТипыВИМИС.АтрибутCode = СтатусыМД.АтрибутCode)
			|			И (ТипыВИМИС.codeSystem = СтатусыМД.codeSystem)
			|ГДЕ
			|	(&НеОтборПоCDA
			|			ИЛИ СтатусыМД.ТелоМедДокумента = &CDAСсылка)
			|	И (&НеОтборПоМД
			|			ИЛИ СтатусыМД.МедицинскийДокумент = &МДСсылка)"
		);
		Запрос_.УстановитьПараметр("ТипыДокументов", Перечисления.ТипДокументаВИМИС.ТаблицаТиповСЭМДпоТипуВИМИС());
		Запрос_.УстановитьПараметр("НеОтборПоCDA", НЕ ЗначениеЗаполнено(CDAСсылка_));
		Запрос_.УстановитьПараметр("CDAСсылка", CDAСсылка_);
		Запрос_.УстановитьПараметр("НеОтборПоМД", НЕ ЗначениеЗаполнено(МДСсылка_));
		Запрос_.УстановитьПараметр("МДСсылка", МДСсылка_);

		Выборка_ = Запрос_.Выполнить().Выбрать();
		Если Выборка_.Следующий() Тогда
			Результат_.ТипДокументаВИМИС = Выборка_.ТипДокументаВИМИС;
			Если НЕ ЗначениеЗаполнено(Выборка_.АтрибутCode) ИЛИ НЕ ЗначениеЗаполнено(Выборка_.codeSystem) Тогда 
#Если НЕ ВнешнееСоединение Тогда
				ФедеральныеВебСервисыВИМИСТриггерныеТочки.ЗаписатьВЖурналРегистрации(
					СтрШаблон("Не удалось получить <code,codeSystem> для документа '%1', файл '%2'.", МДСсылка_, CDAСсылка_), 
					УровеньЖурналаРегистрации.Ошибка, 
					МДСсылка_
				);
#КонецЕсли
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	Если Результат_.Свойство("СтруктурированныеДанныеДокумента") Тогда
		
		Если Результат_.Свойство("КодТипаДокументаРЭМД") Тогда
			КодТипаДокументаРЭМД_ = Результат_.КодТипаДокументаРЭМД;
		Иначе
			КодТипаДокументаРЭМД_ = КодТипаДокументаРЭМД(МДСсылка_, CDAСсылка_);
		КонецЕсли;
		
		Если КодТипаДокументаРЭМД_ = "5"
			Или КодТипаДокументаРЭМД_ = "6"
			Или КодТипаДокументаРЭМД_ = "7"
			Или КодТипаДокументаРЭМД_ = "227"
			Или ЗначениеЗаполнено(РеквизитыДокумента_.ПолученОтМО) // Внешний МД, загруженный из файла.
			Или Не ЗначениеЗаполнено(КодТипаДокументаРЭМД_) // Регистрируется в Регионе (РИЭМК), без регистраци в РЭМД. Важно передать ТипМД.
		Тогда
			// PDF документы.
			Автор_ = Неопределено;
			Результат_.Свойство("Автор", Автор_);
			
			Результат_.СтруктурированныеДанныеДокумента = СтруктурированныеДанныеДокумента(МДСсылка_, CDAСсылка_, КодТипаДокументаРЭМД_, Автор_);
			
		КонецЕсли;
		
	КонецЕсли;

	
	Возврат Результат_;
КонецФункции

// Возвращает структурированные данные для отправки в РМИС и РЭМД. См. ФедеральныеВебСервисыРЭМД.ОписаниеСтруктурированныхДанныхДокумента.
Функция СтруктурированныеДанныеДокумента(ДокументСсылка, ТелоМедДокумента, КодТипаДокументаРЭМД, Автор)

	РеквизитыДокумента_ = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ДокументСсылка,
		"ПолученОтМО.КодOIDМедицинскойОрганизации, Организация.КодOIDМедицинскойОрганизации, ДиагнозыПоМКБ10, CDAДокументы");
	
	СтруктурированныеДанныеДокумента_ = ФедеральныеВебСервисыРЭМД.ОписаниеСтруктурированныхДанныхДокумента();
	СтруктурированныеДанныеДокумента_.КодТипаРЭМДДокумента = КодТипаДокументаРЭМД;
	
	Если ЗначениеЗаполнено(ТелоМедДокумента) Тогда
		CDAДокументы_ = РеквизитыДокумента_.CDAДокументы.Выгрузить();
		НайденныеСтроки_ = CDAДокументы_.НайтиСтроки(Новый Структура("ТелоМедДокумента", ТелоМедДокумента));
		СтрокаCDAДокументы_ = НайденныеСтроки_[0];
		
		Если ЗначениеЗаполнено(СтрокаCDAДокументы_.ТипМД) Тогда
			ОписаниеТипаМД_ = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(СтрокаCDAДокументы_.ТипМД, "КодМинздрава, codeSystem");
			СтруктурированныеДанныеДокумента_.ТипМД.code = ОписаниеТипаМД_.КодМинздрава;
			СтруктурированныеДанныеДокумента_.ТипМД.codeSystem = ОписаниеТипаМД_.codeSystem;
		КонецЕсли;
	КонецЕсли;
	
	КодOIDМедицинскойОрганизации_ = ВыбратьЗаполненное(
		РеквизитыДокумента_.ОрганизацияКодOIDМедицинскойОрганизации, РеквизитыДокумента_.ПолученОтМОКодOIDМедицинскойОрганизации);
	
	Если ЗначениеЗаполнено(КодOIDМедицинскойОрганизации_) Тогда
		СтруктурированныеДанныеДокумента_.OIDОрганизации = КодOIDМедицинскойОрганизации_;
	КонецЕсли;

	Если Автор = Неопределено Тогда
		РеквизитыВыполненияМД_ = ШаблоныМедицинскихДокументовПереопределяемый.ПолучитьРеквизитыВыполнения(ДокументСсылка);
		Автор_ = РеквизитыВыполненияМД_.Исполнитель;
	Иначе
		Автор_ = Автор;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Автор_) Тогда
		ДанныеСотрудника_ = Справочники.Сотрудники.ПолучитьДанныеСотрудника(
			Автор_, Новый Структура("ФИО"));

		СтруктурированныеДанныеДокумента_.Показатели.Вставить("Автор", ФедеральныеВебСервисыРЭМД.ОписаниеПоказателяСтруктурированныхДанных());
		
		СтруктурированныеДанныеДокумента_.Показатели.Автор.Показатели.Вставить("СНИЛС", ФедеральныеВебСервисыРЭМД.ОписаниеПоказателяСтруктурированныхДанных());
		СтруктурированныеДанныеДокумента_.Показатели.Автор.Показатели.СНИЛС.ЗначениеПоказателя = АлгоритмыСтроковые.УдалитьСимволыКроме(
			ДанныеСотрудника_.ФИО.СтраховойНомерПФР, "0123456789");
			
		СтруктурированныеДанныеДокумента_.Показатели.Автор.Показатели.Вставить("Фамилия", ФедеральныеВебСервисыРЭМД.ОписаниеПоказателяСтруктурированныхДанных());
		СтруктурированныеДанныеДокумента_.Показатели.Автор.Показатели.Фамилия.ЗначениеПоказателя = ДанныеСотрудника_.ФИО.Фамилия;

		СтруктурированныеДанныеДокумента_.Показатели.Автор.Показатели.Вставить("Имя", ФедеральныеВебСервисыРЭМД.ОписаниеПоказателяСтруктурированныхДанных());
		СтруктурированныеДанныеДокумента_.Показатели.Автор.Показатели.Имя.ЗначениеПоказателя = ДанныеСотрудника_.ФИО.Имя;

		СтруктурированныеДанныеДокумента_.Показатели.Автор.Показатели.Вставить("Отчество", ФедеральныеВебСервисыРЭМД.ОписаниеПоказателяСтруктурированныхДанных());
		СтруктурированныеДанныеДокумента_.Показатели.Автор.Показатели.Отчество.ЗначениеПоказателя = ДанныеСотрудника_.ФИО.Отчество;

	КонецЕсли;
	
	ДиагнозыПоМКБ10_ = РеквизитыДокумента_.ДиагнозыПоМКБ10.Выгрузить();
	
	СтрокаТЗОсновногоДиагноза_ = ДиагнозыПоМКБ10_.НайтиСтроки(Новый Структура("ТипДиагноза", Перечисления.ТипыДиагнозовПациента.ОсновноеЗаболевание));
	НомерСтрокиОсновногоДиагноза_ = Неопределено;
	
	Если СтрокаТЗОсновногоДиагноза_.Количество() > 0 Тогда
		НомерСтрокиОсновногоДиагноза_ = СтрокаТЗОсновногоДиагноза_[0].НомерСтроки;
		
		Если ЗначениеЗаполнено(СтрокаТЗОсновногоДиагноза_[0].МКБ10) Тогда
			РеквизитыМКБ10_ = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(СтрокаТЗОсновногоДиагноза_[0].МКБ10, "Код, НаименованиеПолное, Наименование");
			
			СтруктурированныеДанныеДокумента_.Показатели.Вставить("ОсновнойДиагноз", ФедеральныеВебСервисыРЭМД.ОписаниеПоказателяСтруктурированныхДанных());
			СтруктурированныеДанныеДокумента_.Показатели.ОсновнойДиагноз.ЗначениеПоказателя = ФедеральныеВебСервисыРЭМД.ОписаниеЗначенияПоказателяСтруктурированныхДанных();
			СтруктурированныеДанныеДокумента_.Показатели.ОсновнойДиагноз.ЗначениеПоказателя.code = СокрЛП(РеквизитыМКБ10_.Код);
			СтруктурированныеДанныеДокумента_.Показатели.ОсновнойДиагноз.ЗначениеПоказателя.codeSystem = "1.2.643.5.1.13.13.11.1005";
			СтруктурированныеДанныеДокумента_.Показатели.ОсновнойДиагноз.ЗначениеПоказателя.displayName = СокрЛП(ВыбратьЗаполненное(РеквизитыМКБ10_.НаименованиеПолное, РеквизитыМКБ10_.Наименование));
		КонецЕсли;

	КонецЕсли;
	
	СтруктурированныеДанныеДокумента_.Показатели.Вставить("Диагнозы", ФедеральныеВебСервисыРЭМД.ОписаниеПоказателяСтруктурированныхДанных());
	СтруктурированныеДанныеДокумента_.Показатели.Диагнозы.ЗначениеПоказателя = Новый Массив;
	
	Для Каждого СтрокаДиагноза_ Из ДиагнозыПоМКБ10_ Цикл
		Если СтрокаДиагноза_.НомерСтроки <> НомерСтрокиОсновногоДиагноза_ И ЗначениеЗаполнено(СтрокаДиагноза_.МКБ10) Тогда
			
			РеквизитыМКБ10_ = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(СтрокаДиагноза_.МКБ10, "Код, НаименованиеПолное, Наименование");
			
			Показатель_ = ФедеральныеВебСервисыРЭМД.ОписаниеПоказателяСтруктурированныхДанных();
			
			Показатель_.ЗначениеПоказателя = ФедеральныеВебСервисыРЭМД.ОписаниеЗначенияПоказателяСтруктурированныхДанных();
			Показатель_.ЗначениеПоказателя.code = СокрЛП(РеквизитыМКБ10_.Код);
			Показатель_.ЗначениеПоказателя.codeSystem = "1.2.643.5.1.13.13.11.1005";
			Показатель_.ЗначениеПоказателя.displayName = СокрЛП(ВыбратьЗаполненное(РеквизитыМКБ10_.НаименованиеПолное, РеквизитыМКБ10_.Наименование));
			
			Если ЗначениеЗаполнено(СтрокаДиагноза_.ТипДиагноза) Тогда
				
				РеквизитыТипаДиагноза_ = Перечисления.ТипыДиагнозовПациента.ПолучитьОписаниеЭлементаCodeПоСсылке(СтрокаДиагноза_.ТипДиагноза);
				
				Показатель_.Показатели.Вставить("ВидНозологическойЕдиницы", ФедеральныеВебСервисыРЭМД.ОписаниеПоказателяСтруктурированныхДанных());
				Показатель_.Показатели.ВидНозологическойЕдиницы.ЗначениеПоказателя = ФедеральныеВебСервисыРЭМД.ОписаниеЗначенияПоказателяСтруктурированныхДанных();
				ЗаполнитьЗначенияСвойств(Показатель_.Показатели.ВидНозологическойЕдиницы.ЗначениеПоказателя, РеквизитыТипаДиагноза_);
			КонецЕсли;
			
			СтруктурированныеДанныеДокумента_.Показатели.Диагнозы.ЗначениеПоказателя.Добавить(Показатель_);
		КонецЕсли;
	КонецЦикла;
	
	Если КодТипаДокументаРЭМД = "227" Тогда
		ДобавитьПоказателиЗдоровьяВСтруктурированныеДанныеДокумента(ДокументСсылка, СтруктурированныеДанныеДокумента_);
	КонецЕсли;
	
	Возврат СтруктурированныеДанныеДокумента_;
КонецФункции

// Определяет код типа РЭМД мед. документа
//
// Параметры:
//  МДСсылка  - ДокументСсылка.МедицинскийДокумент - Ссылка на мед. документ.
//  CDAСсылка  - СправочникСсылка.Файлы - Ссылка на файл тела документа.
//  ТипМД  - СправочникСсылка.ТипыМД - В этот аргумент будет записан соответствующий тип МД.
//
// Возвращаемое значение:
//   Строка   - код типа РЭМД.
//
Функция КодТипаДокументаРЭМД(МДСсылка, CDAСсылка, ТипМД = Неопределено) Экспорт

	ДанныеОТипеДокумента_ = КешВызова.ПолучитьЗначение("Документы.МедицинскийДокумент.КодТипаДокументаРЭМДСлужебный", МДСсылка, CDAСсылка);
	
	ТипМД = ДанныеОТипеДокумента_.ТипМД;
	
	Возврат ДанныеОТипеДокумента_.КодТипаРЭМД;
	
КонецФункции

#Область Печать

// Переопределяет настройки печати для объекта.
//
// Параметры:
//  Настройки - см. УправлениеПечатью.НастройкиПечатиОбъекта.
//
Процедура ПриОпределенииНастроекПечати(Настройки) Экспорт
	
	Настройки.ПриДобавленииКомандПечати = Истина;
	
КонецПроцедуры

// Заполняет список команд печати.
// 
// Параметры:
//   КомандыПечати - ТаблицаЗначений - состав полей см. в функции УправлениеПечатью.СоздатьКоллекциюКомандПечати.
//
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
	
	ФормированиеПечатныхФормМП.ДобавитьКомандыПечати_ПечатныеФормыУслуг(КомандыПечати);
	
КонецПроцедуры

// Сформировать печатные формы объектов
//
// ВХОДЯЩИЕ:
//   МассивОбъектов - Массив - Массив ссылок на объекты которые нужно распечатать
//
// ИСХОДЯЩИЕ:
//   КоллекцияПечатныхФорм - Таблица значений - Сформированные табличные документы
//   ОшибкиПечати          - Список значений  - Ошибки печати  (значение - ссылка на объект, представление - текст ошибки)
//   ОбъектыПечати         - Список значений  - Объекты печати (значение - ссылка на объект, представление - имя области в которой был выведен объект).
//   ПараметрыВывода       - Структура        - Параметры сформированных табличных документов.
//
Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт
	
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "КомплектДокументов") Тогда
		
		КоллекцияПечатныхФорм.Очистить();
		СформироватьКомплектПечатныхФорм(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати);
		
	ИначеЕсли УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "ПФ_MXL_НаправлениеНаИсследование") Тогда
		
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(
			КоллекцияПечатныхФорм,
			"ПФ_MXL_НаправлениеНаИсследование",
			"Направление на исследование",
			ПолучитьТабличныйДокумент(МассивОбъектов, ОбъектыПечати, ПараметрыПечати)
		);
		
	Иначе
		
		НазначенияДляПечати = Новый Массив;
		Для каждого Объект Из МассивОбъектов Цикл
			Если ТипЗнч(Объект) = Тип("ДокументСсылка.МедицинскийДокумент") Тогда
				Для Каждого СтрТЧ Из Объект.ЛекарственныеНазначения Цикл
					Если НЕ ПараметрыПечати.Свойство("ВыбранныеУслуги") ИЛИ
						 ПараметрыПечати.ВыбранныеУслуги.Найти(СтрТЧ.УникальныйИдентификаторНазначения) <> Неопределено
					Тогда
						НазначенияДляПечати.Добавить(СтрТЧ.УникальныйИдентификаторНазначения);
					КонецЕсли;
				КонецЦикла;
			КонецЕсли;
		КонецЦикла;
		ПечатьПоТорговомуНаименованию_ = 
			?(ПараметрыПечати.Свойство("ПечатьПоТорговомуНаименованию"), ПараметрыПечати.ПечатьПоТорговомуНаименованию, Ложь);
		
		ПровереннаяНоменклатураИСМНН = ПолучитьСМННДляПечатиРецептов(НазначенияДляПечати, КоллекцияПечатныхФорм);
		
		АлгоритмыДляКоллекций.ПрисоединитьУникальные(ПровереннаяНоменклатураИСМНН, ПолучитьНоменклатуруДляПечатиРецептов(НазначенияДляПечати));
		
		Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "ПФ_MXL_Форма_148_1_у_88") Тогда
			
			УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(КоллекцияПечатныхФорм, 
				"ПФ_MXL_Форма_148_1_у_88", "Рецепт (форма 148-1/у-88)", 
				ПолучитьФорму_148_1_у_88_или_107_1_у(ПровереннаяНоменклатураИСМНН, ОбъектыПечати, "ПФ_MXL_Форма_148_1_у_88", ПечатьПоТорговомуНаименованию_)
			);
			
		ИначеЕсли УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "ПФ_MXL_Форма_107_1_у") Тогда
		
			УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(КоллекцияПечатныхФорм, 
				"ПФ_MXL_Форма_107_1_у", "Рецепт (форма 107-1/у)", 
				ПолучитьФорму_148_1_у_88_или_107_1_у(ПровереннаяНоменклатураИСМНН, ОбъектыПечати, "ПФ_MXL_Форма_107_1_у", ПечатьПоТорговомуНаименованию_)
			);
			
		ИначеЕсли УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "ПФ_MXL_Форма_148_1_у_04_л") Тогда
			
			Если КоллекцияПечатныхФорм.Количество() > 0 Тогда
				КоллекцияПечатныхФорм.Получить(0).Экземпляров = 3;
			КонецЕсли;
			УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(КоллекцияПечатныхФорм, 
				"ПФ_MXL_Форма_148_1_у_04_л", "Рецепт (форма 148-1/у-04 (л))", 
				ПолучитьФорму_148_1_у_04_л(ПровереннаяНоменклатураИСМНН, ОбъектыПечати, "ПФ_MXL_Форма_148_1_у_04_л", ПечатьПоТорговомуНаименованию_)
			);
			
		КонецЕсли;
		
	КонецЕсли;

КонецПроцедуры

////
 // Функция: ПолучитьТабличныйДокумент
 //   Формирует и возвращает печатную форму направления на исследование.
 //
 // Параметры:
 //   МассивОбъектов - Массив
 //     Массив уникальных идентификаторов услуг.
 //   ОбъектыПечати.
 //     Объекты печати.
 //   РежимПечатиМногоНаправление - Булево
 //     Если Истина, то для каждой услуги будет сформировано отдельное направление,
 //     иначе - для всех услуг - одно направление.
 //
 //  Возврат: 
 //    Табличный документ.
 ///
Функция ПолучитьТабличныйДокумент(МассивОбъектов, ОбъектыПечати, ПараметрыПечати) Экспорт
	
	УслугиДляПечати = Новый Массив;
	РежимПечатиМногоНаправлений = ?(
									ПараметрыПечати.Свойство("ОтдельноеНаправлениеДляКаждойУслуги"),
									ПараметрыПечати.ОтдельноеНаправлениеДляКаждойУслуги,
									Ложь
									);
	
	Для каждого Объект Из МассивОбъектов Цикл
		Если ТипЗнч(Объект) = Тип("ДокументСсылка.МедицинскийДокумент") Тогда
			ИмяТЧ = "НазначенныеУслуги";
			УслугиТЧ = Объект[ИмяТЧ].ВыгрузитьКолонку("УникальныйИдентификаторУслугиСсылка");
			ОбщегоНазначенияКлиентСервер.ДополнитьМассив(УслугиДляПечати, УслугиТЧ, Истина);
		ИначеЕсли ТипЗнч(Объект) = Тип("ДокументСсылка.ЗаказПациента") Тогда
			ИмяТЧ = "МедицинскиеУслуги";
			УслугиТЧ = Объект[ИмяТЧ].ВыгрузитьКолонку("УникальныйИдентификаторУслугиСсылка");
			ОбщегоНазначенияКлиентСервер.ДополнитьМассив(УслугиДляПечати, УслугиТЧ, Истина);
		ИначеЕсли ТипЗнч(Объект) = Тип("СправочникСсылка.УслугиПациентов") Тогда
			УслугиДляПечати.Добавить(Объект);
		КонецЕсли;
	КонецЦикла;
	
	УчитыватьИерархию = ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить("ОбщаяФорма.ПерсональныеНастройки", "ИерархияПечати",Истина);
	
	Если РежимПечатиМногоНаправлений Тогда

		// Лабораторные услуги печатаем группируя по иерархии номенклатуры, вне зависмости от настройки пользователя ИерархияПечати,
		// т.к. при отправке заявки в ЛИС услуги всегда разбиваются по иерархии, в направлении улуги необходимо группировать аналогично,
		// иначе QR код в направлении не будет содержать нужного значения.
		ГруппыУслуг_ = ОбщиеМеханизмыHL7.РазделитьЛабораторныеИПрочиеУслуги(УслугиДляПечати);
	
		ТабличныйДокумент = ТабличныйДокументНаправленияНаИсследования(ГруппыУслуг_.ЛабораторныеУслуги, Истина, Истина);
		
		ПрочиеУслугиТД_ = ТабличныйДокументНаправленияНаИсследования(ГруппыУслуг_.ПрочиеУслуги, Истина, УчитыватьИерархию);
		
		ТабличныйДокумент.Вывести(ПрочиеУслугиТД_);
	Иначе
		ТабличныйДокумент = ТабличныйДокументНаправленияНаИсследования(УслугиДляПечати, Ложь, УчитыватьИерархию);
	КонецЕсли;

	НомерСтрокиНачало = ТабличныйДокумент.ВысотаТаблицы + 1;
	
	УправлениеПечатью.ЗадатьОбластьПечатиДокумента(
		ТабличныйДокумент, НомерСтрокиНачало, ОбъектыПечати, 
		МассивОбъектов[0]
	);

	Возврат ТабличныйДокумент;
	
КонецФункции

// https://its.1c.ru/db/metod8dev/content/5963/hdoc - документация по ВК.
Функция ПолучитьШтрихкод(Штрихкод, ШиринаШтрихкода, ВысотаШтрихкода)
	
	ПараметрыШтрихкода_ = ГенерацияШтрихкода.ПараметрыГенерацииШтрихкода();
	ПараметрыШтрихкода_.Штрихкод = Штрихкод;
	ПараметрыШтрихкода_.ТипКода = 16; // QR код
	ПараметрыШтрихкода_.ОтображатьТекст =  Ложь;
	
	Картинка_ = Штрихкодирование.ПолучитьКартинкуШтрихкода(, ПараметрыШтрихкода_);
	
	Если Картинка_ = Неопределено Тогда
		Картинка_ = Новый Картинка;
	КонецЕсли;
	
	Возврат Картинка_;
	
КонецФункции

// Состав комплекта печатных форм
Функция КомплектПечатныхФорм() Экспорт
	
	Возврат Справочники.УслугиПациентов.КомплектПечатныхФорм();
	
КонецФункции

// Формирует комплект печатных форм мед документа.
Функция СформироватьКомплектПечатныхФорм(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати) Экспорт
	
	ФормированиеПечатныхФормМП.СформироватьКомплектПечатныхФормМедицинскихУслуг(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати);
	
КонецФункции

#КонецОбласти

// Проверяет, есть ли в МД услуги со спецификациями, содержащими лекарственные средства расходумые
// при выполненнии услуг.
Функция ПроверитьЗаполненностьИспользованныхПрепаратов(МД) Экспорт
	Спецификации_ = ПолучитьДанныеДляВыбораИспользованныхПрепаратовИМатериалов(МД, Истина, Истина, Ложь);
	
	Запрос_ = Новый Запрос;
	Запрос_.Текст =
		"ВЫБРАТЬ
		|	СпецификацииМедицинскихУслугЛекарственныеСредства.Номенклатура КАК Номенклатура,
		|	СпецификацииМедицинскихУслугЛекарственныеСредства.ТорговоеНаименование КАК ТорговоеНаименование,
		|	СпецификацииМедицинскихУслугЛекарственныеСредства.ДействующиеВеществаМНН КАК ДействующиеВеществаМНН,
		|	СпецификацииМедицинскихУслугЛекарственныеСредства.ФормаВыпуска КАК ФормаВыпуска,
		|	СпецификацииМедицинскихУслугЛекарственныеСредства.Препарат КАК Препарат
		|ИЗ
		|	Справочник.СпецификацииМедицинскихУслуг.ЛекарственныеСредства КАК СпецификацииМедицинскихУслугЛекарственныеСредства
		|ГДЕ
		|	СпецификацииМедицинскихУслугЛекарственныеСредства.Ссылка В(&МассивСпецификаций)";
		
	Запрос_.УстановитьПараметр("МассивСпецификаций", Спецификации_);
	Выборка_ = Запрос_.Выполнить().Выбрать();
	
	Пока Выборка_.Следующий() Цикл
		Отбор_ = Новый Структура("Номенклатура,ТорговоеНаименование,ДействующиеВеществаМНН,ФормаВыпуска, Препарат");
		
		Для Каждого КлючЗначение_ Из Отбор_ Цикл
			Если Не ЗначениеЗаполнено(Выборка_[КлючЗначение_.Ключ]) Тогда
				Отбор_.Удалить(КлючЗначение_.Ключ);
			КонецЕсли;
		КонецЦикла;
		
		ЗаполнитьЗначенияСвойств(Отбор_, Выборка_);
		
		Найденные_ = МД.ВыполненныеУслугиИспользованныеПрепараты.НайтиСтроки(Отбор_);
		Если Найденные_.Количество() = 0 Тогда
			Сообщить("Не указано использование при выполнении услуги препарата: "
				+ ВыбратьЗаполненное(Выборка_.Препарат, Выборка_.Номенклатура, Выборка_.ТорговоеНаименование, Выборка_.ДействующиеВеществаМНН)
			);
			Возврат Ложь;
		КонецЕсли;
	КонецЦикла;
	
	Возврат Истина;
КонецФункции

// Подготавливает данные для формы обработки ФормированиеМедицинскихДокументов ВводДанныхОбИспользованныхПрепаратах.
// Возвращает массив структур с полями (Пациент,НоменклатураСоставаУслуги,УникальныйИдентификаторУслуги,КлючСтрокиСоставаУслуги,Спецификация)
Функция ПолучитьДанныеДляВыбораИспользованныхПрепаратовИМатериалов(МД, ТолькоСпецификации = Ложь, ВключатьПрепараты = Истина, ВключатьМатериалы = Ложь) Экспорт
	
	ВсеСпецификации_ = Новый Массив;

	МассивНоменклатурЭтаповДляПолученияСпецификаций_ = Новый Массив;
	
	Для Каждого строкаТЧ_ Из МД.ВыполненныеУслуги Цикл
		Если строкаТЧ_.Номенклатура <> строкаТЧ_.НоменклатураСоставаУслуги Тогда
			МассивНоменклатурЭтаповДляПолученияСпецификаций_.Добавить(строкаТЧ_.НоменклатураСоставаУслуги);
		ИначеЕсли ЗначениеЗаполнено(строкаТЧ_.Спецификация) Тогда
			ВсеСпецификации_.Добавить(строкаТЧ_.Спецификация);
		КонецЕсли;
	КонецЦикла;
	
	ТаблицаСпецификацийЭтапов_ = Неопределено;
	Если МассивНоменклатурЭтаповДляПолученияСпецификаций_.Количество() > 0 Тогда

		ОбластьДанных_ = Неопределено;
		Если ОбщегоНазначения.РазделениеВключено()
			И ОбщегоНазначения.ИспользованиеРазделителяСеанса()
		Тогда
			ОбластьДанных_ = РаботаВМоделиСервиса.ЗначениеРазделителяСеанса();
		КонецЕсли;
		
		ТаблицаСпецификацийЭтапов_ = МеханизмыСпецификаций.ОпределитьТаблицуСпецификаций(МассивНоменклатурЭтаповДляПолученияСпецификаций_, МД.ДатаМД, ОбластьДанных_);
		
		СпецификацииЭтапов_ = ТаблицаСпецификацийЭтапов_.ВыгрузитьКолонку("СпецификацияМедицинскойУслуги");
		
		АлгоритмыДляКоллекций.Присоединить(ВсеСпецификации_, СпецификацииЭтапов_);

	КонецЕсли;
	
	// Проверим содержит ли хоть одна спецификация лекарственные средства или материалы.
	Запрос_ = Новый Запрос;
	Запрос_.Текст =
		"ВЫБРАТЬ
		|	СпецификацииМедицинскихУслугЛекарственныеСредства.Ссылка КАК Ссылка,
		|	ИСТИНА КАК ЭтоЛС
		|ИЗ
		|	Справочник.СпецификацииМедицинскихУслуг.ЛекарственныеСредства КАК СпецификацииМедицинскихУслугЛекарственныеСредства
		|ГДЕ
		|	СпецификацииМедицинскихУслугЛекарственныеСредства.НомерСтроки = 1
		|	И СпецификацииМедицинскихУслугЛекарственныеСредства.Ссылка В(&МассивСпецификаций)
		|	И &ВключатьПрепараты
		|
		|ОБЪЕДИНИТЬ
		|
		|ВЫБРАТЬ
		|	СпецификацииМедицинскихУслугСоставУслугиМатериалы.Ссылка КАК Ссылка,
		|	ЛОЖЬ КАК ЭтоЛС
		|ИЗ
		|	Справочник.СпецификацииМедицинскихУслуг.СоставУслугиМатериалы КАК СпецификацииМедицинскихУслугСоставУслугиМатериалы
		|ГДЕ
		|	СпецификацииМедицинскихУслугСоставУслугиМатериалы.НомерСтроки = 1
		|	И СпецификацииМедицинскихУслугСоставУслугиМатериалы.Ссылка В(&МассивСпецификаций)
		|	И &ВключатьМатериалы";
		
	Запрос_.УстановитьПараметр("ВключатьПрепараты", ВключатьПрепараты);
	Запрос_.УстановитьПараметр("ВключатьМатериалы", ВключатьМатериалы);
	Запрос_.УстановитьПараметр("МассивСпецификаций", ВсеСпецификации_);
	СпецификацииСЛекСредствамиИлиМатериалами_ = Запрос_.Выполнить().Выгрузить();
	
	Если ТолькоСпецификации = Истина Тогда
		СпецификацииСЛекСредствамиИлиМатериалами_.Свернуть("Ссылка");
		Возврат СпецификацииСЛекСредствамиИлиМатериалами_.ВыгрузитьКолонку("Ссылка");
	КонецЕсли;
	
	Результат_ = Новый Массив;
	ПоляСтруктуры_ = "Пациент,НоменклатураСоставаУслуги,УникальныйИдентификаторУслуги,КлючСтрокиСоставаУслуги,Спецификация, ЭтоЛС, ЭтоМатериал";

	Для Каждого строкаТЧ_ Из МД.ВыполненныеУслуги Цикл
	
		
		Если строкаТЧ_.Номенклатура <> строкаТЧ_.НоменклатураСоставаУслуги Тогда
		
			Если ТаблицаСпецификацийЭтапов_ <> Неопределено Тогда
				// Найдем спецификацию.
				Спецификации_ = ТаблицаСпецификацийЭтапов_.НайтиСтроки(Новый Структура("Номенклатура", строкаТЧ_.НоменклатураСоставаУслуги));

				Если Спецификации_.Количество() > 0 Тогда
					НайденныеЛС_ = СпецификацииСЛекСредствамиИлиМатериалами_.НайтиСтроки(
						Новый Структура("Ссылка, ЭтоЛС", Спецификации_[0].СпецификацияМедицинскойУслуги, Истина));
					НайденныеМат_ = СпецификацииСЛекСредствамиИлиМатериалами_.НайтиСтроки(
						Новый Структура("Ссылка, ЭтоЛС", Спецификации_[0].СпецификацияМедицинскойУслуги, Ложь));
					
					Если НайденныеЛС_.Количество() > 0 Или НайденныеМат_.Количество() > 0 Тогда
						Описание_ = Новый Структура(ПоляСтруктуры_);
						Описание_.Пациент = МД.Пациент;
						Описание_.НоменклатураСоставаУслуги = строкаТЧ_.НоменклатураСоставаУслуги;
						Описание_.УникальныйИдентификаторУслуги = строкаТЧ_.УникальныйИдентификаторУслуги;
						Описание_.КлючСтрокиСоставаУслуги = строкаТЧ_.КлючСтрокиСоставаУслуги;
						Описание_.Спецификация = Спецификации_[0].СпецификацияМедицинскойУслуги;
						Описание_.ЭтоЛС = НайденныеЛС_.Количество() > 0;
						Описание_.ЭтоМатериал = НайденныеМат_.Количество() > 0;
						
						Результат_.Добавить(Описание_);
					КонецЕсли;
				КонецЕсли;
			КонецЕсли;

		ИначеЕсли ЗначениеЗаполнено(строкаТЧ_.Спецификация) Тогда
		
			НайденныеЛС_ = СпецификацииСЛекСредствамиИлиМатериалами_.НайтиСтроки(
				Новый Структура("Ссылка, ЭтоЛС", строкаТЧ_.Спецификация, Истина));
			НайденныеМат_ = СпецификацииСЛекСредствамиИлиМатериалами_.НайтиСтроки(
				Новый Структура("Ссылка, ЭтоЛС", строкаТЧ_.Спецификация, Ложь));
					
			Если НайденныеЛС_.Количество() > 0 Или НайденныеМат_.Количество() > 0 Тогда
			
				Описание_ = Новый Структура(ПоляСтруктуры_);
				Описание_.Пациент = МД.Пациент;
				Описание_.НоменклатураСоставаУслуги = строкаТЧ_.Номенклатура;
				Описание_.УникальныйИдентификаторУслуги = строкаТЧ_.УникальныйИдентификаторУслуги;
				Описание_.КлючСтрокиСоставаУслуги = строкаТЧ_.КлючСтрокиСоставаУслуги;
				Описание_.Спецификация = строкаТЧ_.Спецификация;
				Описание_.ЭтоЛС = НайденныеЛС_.Количество() > 0;
				Описание_.ЭтоМатериал = НайденныеМат_.Количество() > 0;
				
				Результат_.Добавить(Описание_);
				
			КонецЕсли;
			
		КонецЕсли;
	КонецЦикла;
	
	Если Результат_.Количество() > 0 Тогда
		Возврат Результат_;
	Иначе
		Возврат Неопределено;
	КонецЕсли;
	
КонецФункции

#Область РаботаСТеломМД
// Получает текст документа из справочника файлы.
Функция ПолучитьТелоМД(ФайлСсылка) Экспорт
	Если ЗначениеЗаполнено(ФайлСсылка) Тогда
		ДанныеФайла_ = РаботаСФайламиСлужебныйВызовСервера.ДанныеФайлаИДвоичныеДанные(ФайлСсылка);
		ТелоМД_ = ОбщиеМеханизмыКлиентСервер.ДвоичныеДанныеВСтроку(ДанныеФайла_.ДвоичныеДанные);
	
		Возврат ТелоМД_;
	Иначе
		Возврат Неопределено;
	КонецЕсли;
КонецФункции

// Записывает текст документа в справочник файлы, если
// требуется создает новый элемент справочника.
Функция ЗаписатьТелоМД(ФайлСсылка, ТекстДокумента, Владелец = Неопределено, ХранитьВерсии = Истина, Комментарий = Неопределено, ИмяФайла = Неопределено, Расширение = Неопределено, МДОбъектИлиСсылка = Неопределено) Экспорт
	ВремяНачалаЗамера_ = ОценкаПроизводительности.НачатьЗамерВремени();
	СведенияОФайле_ = РаботаСФайламиКлиентСервер.СведенияОФайле("ФайлСВерсией");
	
	Если ТипЗнч(ТекстДокумента) = Тип("ДвоичныеДанные") Тогда
		ДД_ = ТекстДокумента;
	Иначе
		ДД_ = ОбщиеМеханизмыКлиентСервер.СтрокаВДвоичныеДанные(ТекстДокумента);
	КонецЕсли;
	
	СведенияОФайле_.АдресВременногоХранилищаФайла = ПоместитьВоВременноеХранилище(ДД_);
	СведенияОФайле_.Размер = ДД_.Размер();
	СведенияОФайле_.РасширениеБезТочки = ВыбратьЗаполненное(Расширение, "cda");
	Если СведенияОФайле_.РасширениеБезТочки = "cda" Тогда 
		СведенияОФайле_.Кодировка = КодировкаИзОбъявленияXML(ДД_);
	КонецЕсли;
	Если ЗначениеЗаполнено(Комментарий) Тогда
		СведенияОФайле_.НоваяВерсияКомментарий = Комментарий;// используется если создается новая версия
		СведенияОФайле_.Комментарий = Комментарий;// если обновляется существующая
	КонецЕсли;

	Если Не ЗначениеЗаполнено(ФайлСсылка) Тогда
		СведенияОФайле_.АдресВременногоХранилищаТекста = Новый ХранилищеЗначения("");
		Если ЗначениеЗаполнено(ИмяФайла) Тогда
			СведенияОФайле_.ИмяБезРасширения = ИмяФайла;
		Иначе
			СведенияОФайле_.ИмяБезРасширения = ПолучитьИмяДляCDAФайла(МДОбъектИлиСсылка);
		КонецЕсли;
		
		ФайлСсылка_ = РаботаСФайламиСлужебныйВызовСервера.СоздатьФайлСВерсией(Владелец, СведенияОФайле_);
	Иначе
		
		СведенияОФайле_.ХранитьВерсии = ХранитьВерсии;
		СведенияОФайле_.ИмяБезРасширения = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ФайлСсылка, "Наименование");

		Версия_ = РаботаСФайламиСлужебный.ОбновитьВерсиюФайла(ФайлСсылка, СведенияОФайле_);
		Если Версия_ <> Неопределено Тогда // cda должен иметь какие-нибудь изменения.
			РаботаСФайламиСлужебный.ОбновитьВерсиюВФайле(ФайлСсылка, Версия_, Новый ХранилищеЗначения(""));
		КонецЕсли;
		
		ФайлСсылка_ = ФайлСсылка;
	КонецЕсли;
	
	ОценкаПроизводительности.ЗакончитьЗамерВремени(
		"ЗаписьТелаМедицинскогоДокументаВФайл", 
		ВремяНачалаЗамера_
	);
	
	Возврат ФайлСсылка_;
КонецФункции

// Формирует наименование для элемента справочника Файлы в который планируется записать CDA.
// Параметры
// СмещениеПорядковогоНомера - Число - используется для получения нескольких имен до добавления в ТЧ.
Функция ПолучитьИмяДляCDAФайла(МДОбъектИлиСсылка = Неопределено, СмещениеПорядковогоНомера = Неопределено) Экспорт
	
	Если МДОбъектИлиСсылка = Неопределено Тогда
		Возврат Строка(Новый УникальныйИдентификатор);
	КонецЕсли;
	
	Результат_ = Строка(МДОбъектИлиСсылка);
	
	// Определим порядковый номер тела документа.
	Если ТипЗнч(МДОбъектИлиСсылка) = Тип("ДокументОбъект.МедицинскийДокумент") Тогда
	
		Найденные_ = МДОбъектИлиСсылка.CDAДокументы.НайтиСтроки(
			Новый Структура("Актуально,ТелоМедДокумента", Истина, Справочники.Файлы.ПустаяСсылка())
		);// Обработка АРМ врача при открытии добавляет строку в ТЧ с пустой ссылкой на файл и заполненной ссылкой на ШМД, выбранный при открытии.

		Номер_ = МДОбъектИлиСсылка.CDAДокументы.Количество() - Найденные_.Количество() + 1;
	Иначе
		CDAДокументыТЗ_ = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(МДОбъектИлиСсылка, "CDAДокументы").Выгрузить();

		Найденные_ = CDAДокументыТЗ_.НайтиСтроки(
			Новый Структура("Актуально,ТелоМедДокумента", Истина, Справочники.Файлы.ПустаяСсылка())
		);
		
		Номер_ = CDAДокументыТЗ_.Количество() - Найденные_.Количество() + 1;
	КонецЕсли;
	
	Если СмещениеПорядковогоНомера <> Неопределено Тогда
		Номер_ = Номер_ + СмещениеПорядковогоНомера;
	КонецЕсли;
	
	Результат_ = ОбщегоНазначенияКлиентСервер.ЗаменитьНедопустимыеСимволыВИмениФайла(Результат_ + "(" + Номер_ + ")");
	
	Возврат Результат_;
КонецФункции

// Выбирает владельца для файла CDA для МД без пациента (анонимный пациент).
Функция ВыбратьВладельцаДляCDA(МДОбъектИлиСсылка, Владелец = Неопределено) Экспорт
	Если ЗначениеЗаполнено(Владелец) Тогда
		Возврат Владелец;
	КонецЕсли;
	
	Если ТипЗнч(МДОбъектИлиСсылка) = Тип("ДокументСсылка.МедицинскийДокумент") Тогда
		Запрос_ = Новый Запрос;
		Запрос_.Текст =
			"ВЫБРАТЬ
			|	СтатусыУслуг.Заказ
			|ИЗ
			|	Документ.МедицинскийДокумент.ВыполненныеУслуги КАК МедицинскийДокументВыполненныеУслуги
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.СтатусыУслуг КАК СтатусыУслуг
			|		ПО МедицинскийДокументВыполненныеУслуги.УникальныйИдентификаторУслуги = СтатусыУслуг.УникальныйИдентификаторУслуги
			|ГДЕ
			|	МедицинскийДокументВыполненныеУслуги.Ссылка = &Ссылка
			|	И МедицинскийДокументВыполненныеУслуги.НомерСтроки = 0
			|
			|ОБЪЕДИНИТЬ ВСЕ
			|
			|ВЫБРАТЬ
			|	РегистрСведенийПриемы.ДокументЗаказа
			|ИЗ
			|	Документ.МедицинскийДокумент.ВыполненныеПриемы КАК МедицинскийДокументВыполненныеПриемы
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.Приемы КАК РегистрСведенийПриемы
			|		ПО МедицинскийДокументВыполненныеПриемы.УникальныйИдентификаторПриема = РегистрСведенийПриемы.УникальныйИдентификаторПриема
			|ГДЕ
			|	МедицинскийДокументВыполненныеПриемы.Ссылка = &Ссылка
			|	И МедицинскийДокументВыполненныеПриемы.НомерСтроки = 0";
			
		Запрос_.УстановитьПараметр("Ссылка", МДОбъектИлиСсылка);
		Выборка_ = Запрос_.Выполнить().Выбрать();
		
		Если Выборка_.Следующий() Тогда
			Возврат Выборка_.Заказ;
		Иначе
			Возврат Неопределено;
		КонецЕсли;
	Иначе
		Если МДОбъектИлиСсылка.ВыполненныеУслуги.Количество() > 0 Тогда
			Запрос_ = Новый Запрос;
			Запрос_.Текст =
				"ВЫБРАТЬ
				|	СтатусыУслуг.Заказ
				|ИЗ
				|	РегистрСведений.СтатусыУслуг КАК СтатусыУслуг
				|ГДЕ
				|	СтатусыУслуг.УникальныйИдентификаторУслуги = &УникальныйИдентификаторУслуги";

			Запрос_.УстановитьПараметр("УникальныйИдентификаторУслуги", МДОбъектИлиСсылка.ВыполненныеУслуги[0].УникальныйИдентификаторУслуги);
			Выборка_ = Запрос_.Выполнить().Выбрать();
			
			Если Выборка_.Следующий() Тогда
				Возврат Выборка_.Заказ;
			Иначе
				Возврат Неопределено;
			КонецЕсли;
		ИначеЕсли МДОбъектИлиСсылка.ВыполненныеПриемы.Количество() > 0 Тогда
			Запрос_ = Новый Запрос;
			Запрос_.Текст =
				"ВЫБРАТЬ
				|	РегистрСведенийПриемы.ДокументЗаказа
				|ИЗ
				|	РегистрСведений.Приемы КАК РегистрСведенийПриемы
				|ГДЕ
				|	РегистрСведенийПриемы.УникальныйИдентификаторПриема = &УникальныйИдентификаторПриема";

			Запрос_.УстановитьПараметр("УникальныйИдентификаторПриема", МДОбъектИлиСсылка.ВыполненныеПриемы[0].УникальныйИдентификаторПриема);
			Выборка_ = Запрос_.Выполнить().Выбрать();
			
			Если Выборка_.Следующий() Тогда
				Возврат Выборка_.ДокументЗаказа;
			Иначе
				Возврат Неопределено;
			КонецЕсли;
		Иначе
			Возврат Неопределено;
		КонецЕсли;
	КонецЕсли; 
КонецФункции

// Возвращает ссылку на CDA документ из МД (УСТАРЕЛА - использовать ПолучитьВсеCDAДокумента).
Функция ПолучитьCDAДокумент(МДОбъектИлиСсылка, ПолучитьВнешнийФайл = Ложь, OutПодписанЭП = Неопределено, ИмяФайла = Неопределено) Экспорт
	Если ТипЗнч(МДОбъектИлиСсылка) = Тип("ДокументСсылка.МедицинскийДокумент") Тогда
		Запрос_ = Новый Запрос(
			"ВЫБРАТЬ
			|	МедицинскийДокументCDAДокументы.ТелоМедДокумента КАК ТелоМедДокумента,
			|	Файлы.Расширение КАК ТекущаяВерсияРасширение,
			|	МедицинскийДокументCDAДокументы.ПодписанЭП КАК ПодписанЭП
			|ИЗ
			|	Документ.МедицинскийДокумент.CDAДокументы КАК МедицинскийДокументCDAДокументы
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Файлы КАК Файлы
			|		ПО МедицинскийДокументCDAДокументы.ТелоМедДокумента = Файлы.Ссылка
			|ГДЕ
			|	МедицинскийДокументCDAДокументы.Актуально
			|	И МедицинскийДокументCDAДокументы.Ссылка = &Ссылка
			|	И (&НеОтборПоИмениФайла
			|			ИЛИ Файлы.Наименование = &ИмяФайла)
			|
			|УПОРЯДОЧИТЬ ПО
			|	МедицинскийДокументCDAДокументы.НомерСтроки"
		);
		Запрос_.УстановитьПараметр("Ссылка", МДОбъектИлиСсылка);
		Запрос_.УстановитьПараметр("НеОтборПоИмениФайла", НЕ ЗначениеЗаполнено(ИмяФайла));
		Запрос_.УстановитьПараметр("ИмяФайла", ИмяФайла);
		
		Выборка_ = Запрос_.Выполнить().Выбрать();
		
		Если Выборка_.Следующий() Тогда
			Если ПолучитьВнешнийФайл Тогда
				Если Выборка_.ТекущаяВерсияРасширение <> "cda" Тогда
					OutПодписанЭП = Выборка_.ПодписанЭП;
					Возврат Выборка_.ТелоМедДокумента;
				Иначе
					Возврат Справочники.Файлы.ПустаяСсылка();
				КонецЕсли;
			Иначе
				Если Выборка_.ТекущаяВерсияРасширение = "cda" Тогда
					OutПодписанЭП = Выборка_.ПодписанЭП;
					Возврат Выборка_.ТелоМедДокумента;
				Иначе
					Возврат Справочники.Файлы.ПустаяСсылка();
				КонецЕсли;
			КонецЕсли;
		Иначе
			Возврат Справочники.Файлы.ПустаяСсылка();
		КонецЕсли;
	Иначе
		Найденные_ = ?(
			МДОбъектИлиСсылка <> Неопределено, 
			МДОбъектИлиСсылка.CDAДокументы.НайтиСтроки(Новый Структура("Актуально", Истина)),
			Новый Массив()
		);
		
		Для Каждого строкаТЧ_ Из Найденные_ Цикл
			РеквизитыФайла_ = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(строкаТЧ_.ТелоМедДокумента, "Расширение,Наименование");
			
			Если Не ЗначениеЗаполнено(ИмяФайла) Или ИмяФайла = РеквизитыФайла_.Наименование Тогда
				Если ПолучитьВнешнийФайл Тогда
					Если РеквизитыФайла_.Расширение <> "cda" Тогда
						OutПодписанЭП = строкаТЧ_.ПодписанЭП;
						Возврат строкаТЧ_.ТелоМедДокумента;
					КонецЕсли;
				Иначе
					Если РеквизитыФайла_.Расширение = "cda" Тогда
						OutПодписанЭП = строкаТЧ_.ПодписанЭП;
						Возврат строкаТЧ_.ТелоМедДокумента;
					КонецЕсли;
				КонецЕсли;
			КонецЕсли;
		КонецЦикла;
		
		Возврат Справочники.Файлы.ПустаяСсылка();
	КонецЕсли;
КонецФункции

// Возвращает ссылки на все CDA
//
// Параметры:
//  МДОбъектИлиСсылка					 - ДокументСсылка.МедицинскийДокумент, ДокументОбъектСсылка.МедицинскийДокумент	 - МедДокумент
//  ВключаяВнешниеФайлы				 - Булево	 - Включать ли вв выборку внешние файлы
//  OutСоответствиеПодписанЭП	 - Соответствие, Неопределено	 - Выходной параметр, указывающий какие из файлов CDA подписаны.
//  ШаблонМедицинскогоДокумента	 - СправочникСсылка.ШаблоныМедицинскихДокументов	 - Возвращать только ссылки на файлы CDA заданного ШМД.
// 
// Возвращаемое значение:
//  Массив - Массив СправочникСсылка.Файлы ссылок на CDA.
//
Функция ПолучитьВсеCDAДокумента(Знач МДОбъектИлиСсылка, Знач ВключаяВнешниеФайлы = Ложь, OutСоответствиеПодписанЭП = Неопределено, Знач ШаблонМедицинскогоДокумента = Неопределено) Экспорт
	Результат_ = Новый Массив();
	Если ТипЗнч(OutСоответствиеПодписанЭП) <> Тип("Соответствие") Тогда 
		OutСоответствиеПодписанЭП = Новый Соответствие();
	КонецЕсли;
	
	Если ТипЗнч(МДОбъектИлиСсылка) = Тип("ДокументСсылка.МедицинскийДокумент") Тогда
		Запрос_ = Новый Запрос(
			"ВЫБРАТЬ
			|	МедицинскийДокументCDAДокументы.ТелоМедДокумента КАК ТелоМедДокумента,
			|	Файлы.Расширение КАК ТекущаяВерсияРасширение,
			|	МедицинскийДокументCDAДокументы.ПодписанЭП КАК ПодписанЭП,
			|	МедицинскийДокументCDAДокументы.ШаблонМедицинскогоДокумента КАК ШаблонМедицинскогоДокумента
			|ИЗ
			|	Документ.МедицинскийДокумент.CDAДокументы КАК МедицинскийДокументCDAДокументы
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Файлы КАК Файлы
			|		ПО МедицинскийДокументCDAДокументы.ТелоМедДокумента = Файлы.Ссылка
			|ГДЕ
			|	МедицинскийДокументCDAДокументы.Актуально
			|	И МедицинскийДокументCDAДокументы.Ссылка = &Ссылка
			|
			|УПОРЯДОЧИТЬ ПО
			|	МедицинскийДокументCDAДокументы.НомерСтроки"
		);
		
		Запрос_.УстановитьПараметр("Ссылка", МДОбъектИлиСсылка);
		
		Выборка_ = Запрос_.Выполнить().Выбрать();
		
		Пока Выборка_.Следующий() Цикл
			Если Выборка_.ТекущаяВерсияРасширение <> "cda" Тогда
				Если ВключаяВнешниеФайлы <> Истина Тогда 
					Продолжить;
				КонецЕсли;
			КонецЕсли;
			
			Если ЗначениеЗаполнено(ШаблонМедицинскогоДокумента) И Выборка_.ШаблонМедицинскогоДокумента <> ШаблонМедицинскогоДокумента Тогда
				Продолжить;
			КонецЕсли;
			
			Результат_.Добавить(Выборка_.ТелоМедДокумента);
			OutСоответствиеПодписанЭП.Вставить(Выборка_.ТелоМедДокумента, Выборка_.ПодписанЭП);
		КонецЦикла;
	Иначе
		Найденные_ = ?(
			МДОбъектИлиСсылка <> Неопределено, 
			МДОбъектИлиСсылка.CDAДокументы.НайтиСтроки(Новый Структура("Актуально", Истина)),
			Новый Массив()
		);
		
		Для Каждого строкаТЧ_ Из Найденные_ Цикл
			Если НЕ ЗначениеЗаполнено(строкаТЧ_.ТелоМедДокумента) Тогда 
				Продолжить;
			КонецЕсли;
			РеквизитыФайла_ = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(строкаТЧ_.ТелоМедДокумента, "Расширение,Наименование");
			Если РеквизитыФайла_.Расширение <> "cda" Тогда 
				Если ВключаяВнешниеФайлы <> Истина Тогда 
					Продолжить;
				КонецЕсли;
			КонецЕсли;
			
			Если ЗначениеЗаполнено(ШаблонМедицинскогоДокумента) И строкаТЧ_.ШаблонМедицинскогоДокумента <> ШаблонМедицинскогоДокумента Тогда
				Продолжить;
			КонецЕсли;

			Результат_.Добавить(строкаТЧ_.ТелоМедДокумента);
			OutСоответствиеПодписанЭП.Вставить(строкаТЧ_.ТелоМедДокумента, строкаТЧ_.ПодписанЭП);
		КонецЦикла;
	КонецЕсли;
	
	Возврат Результат_;
КонецФункции

// Возвращает список всех тел медицинского документа с их свойствами.
//
// Параметры:
//  МедицинскийДокумент  - ДокументСсылка.МедицинскийДокумент - МедицинскийДокумент список файлов которого необходимо получить
//
// Возвращаемое значение:
//   ТаблицаЗначений   - Таблица тел документа и их свойства.
//
Функция СписокCDAДокументов(МедицинскийДокумент) Экспорт
	Запрос_ = Новый Запрос;
	Запрос_.Текст =
		"ВЫБРАТЬ
		|	МДCDAДокументы.ТелоМедДокумента КАК ТелоМедДокумента,
		|	МДCDAДокументы.ПодписанЭП КАК ПодписанЭП,
		|	МДCDAДокументы.ШаблонМедицинскогоДокумента КАК ШаблонМедицинскогоДокумента,
		|	ЕСТЬNULL(СтатусыМД.codeSystem, МДCDAДокументы.codeSystem) КАК codeSystem,
		|	ЕСТЬNULL(СтатусыМД.АтрибутCode, МДCDAДокументы.АтрибутCode) КАК АтрибутCode,
		|	ЕСТЬNULL(СтатусыМД.ТипМД, МДCDAДокументы.ТипМД) КАК ТипМД,
		|	ЕСТЬNULL(СтатусыМД.ДатаМД, МДCDAДокументы.ДатаМД) КАК ДатаМД,
		|	ЕСТЬNULL(СтатусыМД.ЗаголовокМД, МДCDAДокументы.ЗаголовокМД) КАК ЗаголовокМД,
		|	СтатусыМД.СтатусТелаДокумента,
		|	Файлы.Расширение КАК РасширениеФайла,
		|	Файлы.Расширение = ""cda"" КАК ЭтоCDA
		|ИЗ
		|	Документ.МедицинскийДокумент.CDAДокументы КАК МДCDAДокументы
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Файлы КАК Файлы
		|		ПО МДCDAДокументы.ТелоМедДокумента = Файлы.Ссылка
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СтатусыМедицинскихДокументов КАК СтатусыМД
		|		ПО МДCDAДокументы.Ссылка = СтатусыМД.МедицинскийДокумент
		|			И МДCDAДокументы.ТелоМедДокумента = СтатусыМД.ТелоМедДокумента
		|ГДЕ
		|	МДCDAДокументы.Актуально
		|	И МДCDAДокументы.Ссылка = &МедицинскийДокумент
		|
		|УПОРЯДОЧИТЬ ПО
		|	МДCDAДокументы.НомерСтроки";
		
	Запрос_.УстановитьПараметр("МедицинскийДокумент", МедицинскийДокумент);
	Результат_ = Запрос_.Выполнить().Выгрузить();
	
	Возврат Результат_;
КонецФункции

// Возвращает ссылку на CDA документ из МД.
Функция ПолучитьШаблонМедицинскогоДокумента(МДОбъектИлиСсылка, СсылкаФайлCDA = Неопределено) Экспорт
	Если ТипЗнч(МДОбъектИлиСсылка) = Тип("ДокументСсылка.МедицинскийДокумент") Тогда
		Запрос_ = Новый Запрос(
			"ВЫБРАТЬ
			|	МДCDAДокументы.ШаблонМедицинскогоДокумента КАК ШаблонМедицинскогоДокумента,
			|	МДCDAДокументы.ТелоМедДокумента КАК ТелоМедДокумента
			|ИЗ
			|	Документ.МедицинскийДокумент.CDAДокументы КАК МДCDAДокументы
			|ГДЕ
			|	МДCDAДокументы.Актуально
			|	И МДCDAДокументы.Ссылка = &Ссылка
			|	И МДCDAДокументы.ШаблонМедицинскогоДокумента <> ЗНАЧЕНИЕ(Справочник.ШаблоныМедицинскихДокументов.ПустаяСсылка)
			|
			|УПОРЯДОЧИТЬ ПО
			|	МДCDAДокументы.НомерСтроки"
		);
		Запрос_.УстановитьПараметр("Ссылка", МДОбъектИлиСсылка);
		Выборка_ = Запрос_.Выполнить().Выбрать();
		
		Пока Выборка_.Следующий() Цикл
			Если Не ЗначениеЗаполнено(СсылкаФайлCDA) Или Выборка_.ТелоМедДокумента = СсылкаФайлCDA Тогда
				Возврат Выборка_.ШаблонМедицинскогоДокумента;
			КонецЕсли;
		КонецЦикла;
	Иначе
		Найденные_ = ?(
			МДОбъектИлиСсылка <> Неопределено, 
			МДОбъектИлиСсылка.CDAДокументы.НайтиСтроки(Новый Структура("Актуально", Истина)),
			Новый Массив()
		);
		
		Для Каждого Найденный_ Из Найденные_ Цикл 
			Если ЗначениеЗаполнено(Найденный_.ШаблонМедицинскогоДокумента)
				И (Не ЗначениеЗаполнено(СсылкаФайлCDA) Или Найденный_.ТелоМедДокумента = СсылкаФайлCDA)
			Тогда 
				Возврат Найденный_.ШаблонМедицинскогоДокумента;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	Возврат Справочники.ШаблоныМедицинскихДокументов.ПустаяСсылка();
КонецФункции

// Помещает в МД ссылку на CDA.
Функция УстановитьCDAДокумент(МДОбъект, ФайлСсылка, ШаблонМедицинскогоДокумента = Неопределено, ОбязательноЗаполнятьШМД = Ложь, ПодписанЭП = Неопределено, ТипМД = '05710422152926') Экспорт
	
	
	
	
	Результат_ = Неопределено;
	
	Найденные_ = МДОбъект.CDAДокументы.НайтиСтроки(
		Новый Структура("Актуально,ТелоМедДокумента", Истина, ВыбратьЗаполненное(ФайлСсылка, Справочники.Файлы.ПустаяСсылка()))
	);// ФайлСсылка могут передать Неопределено.
	
	Если Найденные_.Количество() = 0 И ЗначениеЗаполнено(ФайлСсылка) Тогда
		// Если найдется строка с пустой ссылкой, тогда ее и заполним.
		Найденные_ = МДОбъект.CDAДокументы.НайтиСтроки(
			Новый Структура("Актуально,ТелоМедДокумента", Истина, Справочники.Файлы.ПустаяСсылка())
		);// Обработка АРМ врача при открытии добавляет строку в ТЧ с пустой ссылкой на файл и заполненной ссылкой на ШМД, выбранный при открытии.
	КонецЕсли;
	
	Если Найденные_.Количество() > 0 Тогда
	
		Результат_ = Найденные_[0];
		
		Результат_.ТелоМедДокумента = ФайлСсылка;
		
		Если ТипМД <> МагическиеКонстанты.НезаполненноеЗначение() Тогда
			Результат_.ТипМД = ТипМД;
		КонецЕсли;

		Если ОбязательноЗаполнятьШМД Или ЗначениеЗаполнено(ШаблонМедицинскогоДокумента) Тогда
			Результат_.ШаблонМедицинскогоДокумента = ШаблонМедицинскогоДокумента;
		КонецЕсли;
		
		Если ПодписанЭП <> Неопределено Тогда
			Результат_.ПодписанЭП = ПодписанЭП;
		КонецЕсли;
	Иначе
		Результат_ = МДОбъект.CDAДокументы.Добавить();
		Результат_.ТелоМедДокумента = ФайлСсылка;
		Результат_.ШаблонМедицинскогоДокумента = ШаблонМедицинскогоДокумента;
		Результат_.Актуально = Истина;
		
		Если ТипМД <> МагическиеКонстанты.НезаполненноеЗначение() Тогда
			Результат_.ТипМД = ТипМД;
		КонецЕсли;
		
		Если ПодписанЭП <> Неопределено Тогда
			Результат_.ПодписанЭП = ПодписанЭП;
		КонецЕсли;
	КонецЕсли;
	
	Возврат Результат_;
КонецФункции

// Снимает актуальность cda документа.
Процедура СброситьCDAДокумент(МДОбъект) Экспорт
	Найденные_ = МДОбъект.CDAДокументы.НайтиСтроки(Новый Структура("Актуально", Истина));
	Для Каждого строкаТЧ_ Из Найденные_ Цикл
		строкаТЧ_.Актуально = Ложь;
	КонецЦикла;
КонецПроцедуры

// Функция - Получить ссылку по элементу id
//
// Параметры:
//  СтруктураПараметров	 - Структура	 - структура с параметрами
//  - root - Строка - глобально уникальный идентификатор OID, 
// 					  присвоенный организации или классификатору, 
//					либо системе идентификации объектов, находящихся в ведении этой организации.
//  - extension - Строка - уникальный идентификатор документа в информационной системе.
// 
// Возвращаемое значение:
//  ДокументСсылка.МедицинскийДокумент - медицинский документ, соответствующий заданному элементу root.
//
Функция ПолучитьСсылкуПоЭлементуId(СтруктураПараметров) Экспорт
	Если СтроковыеФункцииКлиентСервер.ЭтоУникальныйИдентификатор(СтруктураПараметров.extension) Тогда
		УИД_ = Новый УникальныйИдентификатор(СтруктураПараметров.extension);
		Возврат Документы.МедицинскийДокумент.ПолучитьССылку(УИД_);
	КонецЕсли;
	Возврат Неопределено;
КонецФункции

// По ссылке на тело МД (ссылка на справочник Файлы) находит МД.
Функция НайтиМДПоCDAДокументу(ФайлСсылка, ИспользоватьКеширование = Истина) Экспорт

	Если ИспользоватьКеширование Тогда
		Возврат КешСеанса.ПолучитьЗначение("Документы.МедицинскийДокумент.НайтиМДПоCDAДокументу", ФайлСсылка, Ложь);
	КонецЕсли;

	Если Не ЗначениеЗаполнено(ФайлСсылка) Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Запрос_ = Новый Запрос;
	Запрос_.Текст =
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	CDA.Ссылка КАК Ссылка
		|ИЗ
		|	Документ.МедицинскийДокумент.CDAДокументы КАК CDA
		|ГДЕ
		|	CDA.ТелоМедДокумента = &ТелоМедДокумента";
		
	Запрос_.УстановитьПараметр("ТелоМедДокумента", ФайлСсылка);
	Выборка_ = Запрос_.Выполнить().Выбрать();
	
	Если Выборка_.Следующий() Тогда
		Возврат Выборка_.Ссылка;
	КонецЕсли;
	
	Возврат Неопределено;
КонецФункции

// Вернет код минздрава вида документа РЭМД создаваемого в см. ДобавитьCDAНаправленийВоВспомомгательныеКабинеты.
Функция ТипРЭМДНаправленияВоВспомогательныеКабинеты() Экспорт
	Возврат "85";
КонецФункции

// Добавляет в ТЧ CDAДокументы дополнительные автосоздаваемые CDA Направлений во вспомогательные кабинеты.
//
// Параметры:
//  МедицинскийДокумент - ДокументОбъект.МедицинскийДокумент, ДокументСсылка.МедицинскийДокумент - Медицинский документ.
//                                                                                                 Если передана ссылка, будут пересозданы СЭМД уже имеющиеся в МД.
//  СоздаватьШапку - Булево	 - Если Истина, СЭМД будут созданы с шапкой, если Ложь шапка не будет создана.
//
Процедура ДобавитьCDAНаправленийВоВспомомгательныеКабинеты(МедицинскийДокумент, СоздаватьШапку = Истина) Экспорт
	// Подписанный документ не изменяем.
	Если Не ДокументИмеетЭЦП(МедицинскийДокумент) Тогда
		
		ВремяНачалаЗамера_ = ОценкаПроизводительности.НачатьЗамерВремени();
		
		ЗначенияПараметровШаблона_ = Неопределено;
		
		МассивCDA_ = ФедеральныеВебСервисыСЭМД.СоздатьСЭМД_НаправлениеВоВспомогательныйКабинет(МедицинскийДокумент, СоздаватьШапку, ЗначенияПараметровШаблона_);
		
		Если МассивCDA_.Количество() > 0 Тогда 
			
			Если ТипЗнч(МедицинскийДокумент) = Тип("ДокументОбъект.МедицинскийДокумент") Тогда
				МДОбъект_ = МедицинскийДокумент;
				Пациент_ = МедицинскийДокумент.Пациент;
				МДСсылка_ = МедицинскийДокумент.Ссылка;
				CDAДокументы_ = МедицинскийДокумент.CDAДокументы;
				ПереданаСсылка_ = Ложь;
			Иначе
				МДОбъект_ = Неопределено;
				МДСсылка_ = МедицинскийДокумент;
				РеквизитыПоСсылке_ = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(МедицинскийДокумент, "Пациент, CDAДокументы");
				Пациент_ = РеквизитыПоСсылке_.Пациент;
				CDAДокументы_ = РеквизитыПоСсылке_.CDAДокументы.Выгрузить();
				ПереданаСсылка_ = Истина;
			КонецЕсли;
			
			ВладелецCDA_ = Документы.МедицинскийДокумент.ВыбратьВладельцаДляCDA(МедицинскийДокумент, Пациент_);
			
			// Составим соответствие порядкового номера СЭМД-а с именем файла.
			// Найдем ранее созданные файлы cda и сопоставим с вновь созданными СЭМД направлений по идентификатору набора версий.
			Номер_ = 0;
			ПорядковыйНомерИмяФайла_ = Новый Соответствие;
			ИспользованныеНомераСтрокCDAДокументы_ = Новый Соответствие;
			СмещениеПорядковогоНомераДляИмениФайла_ = 0;
			
			Для Каждого ПараметрыИзСЭМД_ Из ЗначенияПараметровШаблона_ Цикл
				
				ИдентификаторНабораВерсий_ = ПараметрыИзСЭМД_.Получить("ДокументИдентификаторНабораВерсий");// значение из setId/@extension
				УстарелИдентификаторНабораВерсий_ = ПараметрыИзСЭМД_.Получить("УстарелДокументНаборВерсийExtension");//Для обратной совместимости.
				
				Найден_ = Ложь;
				
				Если ЗначениеЗаполнено(ИдентификаторНабораВерсий_) Тогда // Если параметр шаблона СЭМД не заполнен, нет смысла искать ранее созданный файл.
					Для Каждого СтрокаТЧ_ Из CDAДокументы_ Цикл
						
						// На случай если вдруг setId/@extension в разных СЭМД повторяется (чего быть не должно), избегаем использование одних и тех же файлов cda.
						Если ИспользованныеНомераСтрокCDAДокументы_.Получить(СтрокаТЧ_.НомерСтроки) = Неопределено Тогда
							ИмяФайла_ = Строка(СтрокаТЧ_.ТелоМедДокумента);
							
							Если ИдентификаторНабораВерсий_ = СтрокаТЧ_.ИдентификаторНабораВерсий
								Или стрНайти(ИмяФайла_, ИдентификаторНабораВерсий_) > 0 
																						
								Или (ЗначениеЗаполнено(УстарелИдентификаторНабораВерсий_)
										И (УстарелИдентификаторНабораВерсий_ = СтрокаТЧ_.ИдентификаторНабораВерсий
											Или стрНайти(ИмяФайла_, УстарелИдентификаторНабораВерсий_) > 0)
									)
							Тогда
								ПорядковыйНомерИмяФайла_.Вставить(
									Номер_,
									Новый Структура("ИмяФайла, ФайлСсылка, ИдентификаторНабораВерсий", ИмяФайла_, СтрокаТЧ_.ТелоМедДокумента, ИдентификаторНабораВерсий_)
								);
								Найден_ = Истина;
								ИспользованныеНомераСтрокCDAДокументы_.Вставить(СтрокаТЧ_.НомерСтроки, Истина);
								Прервать;
							КонецЕсли;
						КонецЕсли;
						
					КонецЦикла;
				КонецЕсли;
				
				Если Найден_ = Ложь Тогда

					НМУКодМинздрава_ = ПараметрыИзСЭМД_.Получить("НМУКодМинздрава");

					Если Не ПереданаСсылка_ Тогда
						// Ранее созданный файл для СЭМД не найден, будет создан новый. Сформируем имя для нового файла.
						ИмяФайла_ = ОбщегоНазначенияКлиентСервер.ЗаменитьНедопустимыеСимволыВИмениФайла(
							стрШаблон("%1(Направление на %2)", ПолучитьИмяДляCDAФайла(МДОбъект_, СмещениеПорядковогоНомераДляИмениФайла_), НМУКодМинздрава_)
						);
						ПорядковыйНомерИмяФайла_.Вставить(
							Номер_,
							Новый Структура("ИмяФайла, ФайлСсылка, ИдентификаторНабораВерсий", ИмяФайла_, Неопределено, ИдентификаторНабораВерсий_)
						);
						СмещениеПорядковогоНомераДляИмениФайла_ = СмещениеПорядковогоНомераДляИмениФайла_ + 1;
					Иначе
						
						
						ЗаписьЖурналаРегистрации(
							НСтр("ru='ДобавитьCDAНаправленийВоВспомомгательныеКабинеты'", ОбщегоНазначения.КодОсновногоЯзыка()),
							УровеньЖурналаРегистрации.Предупреждение,
							,
							МДСсылка_,
							стрШаблон("1a11d9fa-278e-45fc-9764-5b88a38735d4: При добавлении шапок в ранее созданные СЭМДы направлений
								|обнаружена необходимость добавления нового СЭМД направления для НМУ %1. Медицинский документ: %2",
								НМУКодМинздрава_,
								МДСсылка_
							)
						);
					КонецЕсли;
				КонецЕсли;
				
				Номер_ = Номер_ + 1;
				
			КонецЦикла;
			
			// Записываем СЭМДы направлений.
			Номер_ = 0;
			СсылкиНаCDAФайлы_ = Новый Соответствие;
			Для Каждого CDA_ Из МассивCDA_ Цикл
				ИмяФайлаCDA_ = ПорядковыйНомерИмяФайла_.Получить(Номер_);
				
				Если ИмяФайлаCDA_ <> Неопределено Тогда //Если в аргументе передана ссылка на МД, новые СЭМД не записываем.
					CDAДокумент_ = Документы.МедицинскийДокумент.ЗаписатьТелоМД(ИмяФайлаCDA_.ФайлСсылка,
						CDA_, ВладелецCDA_,,,ИмяФайлаCDA_.ИмяФайла
					);
					
					Если Не ПереданаСсылка_ Тогда
						Документы.МедицинскийДокумент.УстановитьCDAДокумент(МДОбъект_, CDAДокумент_);
					КонецЕсли;
					
					СсылкиНаCDAФайлы_.Вставить(CDAДокумент_, Истина);
				КонецЕсли;
				
				Номер_ = Номер_ + 1;
			КонецЦикла;
			
			
			// Снимаем актуальность для СЭМД удаленных назначений.
			Для Каждого СтрокаТЧ_ Из CDAДокументы_ Цикл
				
				ИмяФайла_ = Строка(СтрокаТЧ_.ТелоМедДокумента);
				
				Если СсылкиНаCDAФайлы_.Получить(СтрокаТЧ_.ТелоМедДокумента) = Неопределено
					И (стрНайти(ИмяФайла_, "#направление#") > 0 // Старый вариант имени файла.
						Или стрНайти(ИмяФайла_, "Направление на") > 0
						Или ОбщегоНазначения.ЗначениеРеквизитаОбъекта(СтрокаТЧ_.ТелоМедДокумента, "ВладелецФайла") = МДСсылка_)// Проверка ВладелецФайла для обратной совместимости, надо будет удалить.
				Тогда 
					Если Не ПереданаСсылка_ Тогда
						СтрокаТЧ_.Актуально = Ложь;
					Иначе
						
						
						ЗаписьЖурналаРегистрации(
							НСтр("ru='ДобавитьCDAНаправленийВоВспомомгательныеКабинеты'", ОбщегоНазначения.КодОсновногоЯзыка()),
							УровеньЖурналаРегистрации.Предупреждение,
							,
							МДСсылка_,
							стрШаблон("a07fe31f-9c37-4f3e-96db-f4f73d8c3957: При добавлении шапок в ранее созданные СЭМДы направлений
								|обнаружена необходимость удаления неактуального СЭМД направления %1. Медицинский документ: %2",
								ИмяФайла_,
								МДСсылка_
							)
						);
					КонецЕсли;
				КонецЕсли;
			КонецЦикла;

			ОценкаПроизводительности.ЗакончитьЗамерВремени(
				"ФормированиеCDAНаправленийВоВспомомгательныеКабинеты", 
				ВремяНачалаЗамера_
			);
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

// Проверяет по спецификации услуги возможность выполнять ее определенной должностью
//
// Параметры:
//  МедицинскийДокумент  - ДокументОбъект.МедицинскийДокумент, ДанныеФормыСтруктура - Мед. документ.
//                 
//  Сотрудник  - СправочникСсылка.Сотрудники - Проверяемый сотрудник.
//                 Если не указан, будут проверены все исполнители в МД.
//                 
//  УникальныйИдентификаторУслуги  - УникальныйИдентификатор - УИД услуги, если проверять надо не для всех выполненых услуг в МД.
//
// Возвращаемое значение:
//   Булево   - Истина если реазрешено выполнять, иначе Ложь.
//
Функция ПроверитьДолжностиИсполнителейНаПравоВыполнятьУслуги(МедицинскийДокумент, Сотрудник = Неопределено, УникальныйИдентификаторУслуги = Неопределено) Экспорт

	Результат_ = Истина;
	
	КонтролироватьДолжностьИсполнителейНаПравоВыполненияУслуги_ = КешСеанса.ПолучитьЗначение("Константы.КонтролироватьДолжностьИсполнителейНаПравоВыполненияУслуги.Получить");
	
	Если КонтролироватьДолжностьИсполнителейНаПравоВыполненияУслуги_ Тогда
	
		ПроверяемыеУслуги_ = Новый Соответствие;
		
		Для Каждого ВыполненнаяУслуга_ Из МедицинскийДокумент.ВыполненныеУслуги Цикл
			Если ЗначениеЗаполнено(ВыполненнаяУслуга_.Спецификация)
				И (Не ЗначениеЗаполнено(УникальныйИдентификаторУслуги)
					Или ВыполненнаяУслуга_.УникальныйИдентификаторУслуги = УникальныйИдентификаторУслуги)
				И ВыполненнаяУслуга_.Номенклатура = ВыполненнаяУслуга_.НоменклатураСоставаУслуги // Многоэтапные пока не проверяем.
			Тогда

				ДопустимыеИсполнители_ = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ВыполненнаяУслуга_.Спецификация, "Исполнители").Выгрузить();

				Если ДопустимыеИсполнители_.Количество() > 0 Тогда
					ПроверяемыеУслуги_.Вставить(
						ВыполненнаяУслуга_.Номенклатура,
						Новый Структура("Должности, УникальныйИдентификаторУслуги",
							ДопустимыеИсполнители_.ВыгрузитьКолонку("ДолжностьИсполнителя"),
							ВыполненнаяУслуга_.УникальныйИдентификаторУслуги)
					);
				КонецЕсли;
				
			КонецЕсли;
		КонецЦикла;
		
		Если ПроверяемыеУслуги_.Количество() > 0 Тогда

			Если ЗначениеЗаполнено(Сотрудник) Тогда
				ДолжностьСотрудника_ = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Сотрудник, "Должность");
				
				Если ЗначениеЗаполнено(ДолжностьСотрудника_) Тогда

					Для Каждого Услуга_ Из ПроверяемыеУслуги_ Цикл
						
						Если Услуга_.Значение.Должности.Найти(ДолжностьСотрудника_) = Неопределено Тогда
							
							Сообщить(
								стрШаблон(
									"Спецификация услуги '%1' не содержит в своем списке исполнителей должность '%2'. Услуга не разрешена к выполнению сотрудником '%3'.",
									Услуга_.Ключ, ДолжностьСотрудника_, Сотрудник
								)
							);
							
							Результат_ = Ложь;
						КонецЕсли;
						
					КонецЦикла;

				КонецЕсли;
				
			Иначе
				Для Каждого СтрокаТЧИсполнители_ Из МедицинскийДокумент.Исполнители Цикл
					Если ЗначениеЗаполнено(СтрокаТЧИсполнители_.Должность) Тогда
						
						Для Каждого Услуга_ Из ПроверяемыеУслуги_ Цикл
						
							Если Не ЗначениеЗаполнено(СтрокаТЧИсполнители_.УникальныйИдентификаторУслуги)
								Или СтрокаТЧИсполнители_.УникальныйИдентификаторУслуги = Услуга_.Значение.УникальныйИдентификаторУслуги
							Тогда

								Если Услуга_.Значение.Должности.Найти(СтрокаТЧИсполнители_.Должность) = Неопределено Тогда
									
									Сообщить(
										стрШаблон(
											"Спецификация услуги '%1' не содержит в своем списке исполнителей должность '%2'. Услуга не разрешена к выполнению сотрудником из списка исполнителей '%3'.",
											Услуга_.Ключ, СтрокаТЧИсполнители_.Должность, СтрокаТЧИсполнители_.Исполнитель
										)
									);
									
									Результат_ = Ложь;
								КонецЕсли;

							КонецЕсли;
								
						КонецЦикла;
					КонецЕсли;
				КонецЦикла;
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат Результат_;
	
КонецФункции

#КонецОбласти

#Область ФорматированиеШапкиСЭМД

Функция СформироватьДанныеЗаполненияДляШапкиСЭМД(СсылкаМД, ЕстьИзмененияДанных) Экспорт 
	Результат_ = Новый Соответствие;
	
	АвторМД_ = ШаблоныМедицинскихДокументовПереопределяемый.ПолучитьАвтораМД(СсылкаМД).Автор;
	ШМДСсылка_ = ШаблоныМедицинскихДокументовПоликлиника.ПолучитьШМДуМД(СсылкаМД);
	СтруктураДанных_ = Новый Структура("Документ,Автор", СсылкаМД, АвторМД_);
	Результат_ = Обработки.ФормированиеШапки.СформироватьОсновныеДанныеЗаполнения(СтруктураДанных_, ШМДСсылка_.ТипРЭМД);
	
	ОрганизацияOID_ = Результат_.Получить("ОрганизацияOID");
	Исполнители_ = ФедеральныеВебСервисыСЭМД.ПолучитьДанныеИсполнителей(СсылкаМД, ОрганизацияOID_, АвторМД_);
	Результат_.Вставить("Исполнители", Исполнители_);
	
	Организация_ = СсылкаМД.Организация;
	Владелец_ = ФедеральныеВебСервисыСЭМД.ПолучитьДанныеОрганизации(Организация_, "Владелец",ШМДСсылка_.ТипРЭМД);
	АвторПодразделениеOID_ = Результат_.Получить("АвторПодразделениеOID");
	Если ЗначениеЗаполнено(АвторПодразделениеOID_) Тогда
		Владелец_.Вставить("ВладелецПодразделениеOID", АвторПодразделениеOID_);
	КонецЕсли;
	ОбщегоНазначенияКлиентСервер.ДополнитьСоответствие(Результат_, Владелец_);
	
	ДанныеДокумента_ = ПолучитьДанныеДокумента(СсылкаМД, ОрганизацияOID_, "Документ");
	ОбщегоНазначенияКлиентСервер.ДополнитьСоответствие(Результат_, ДанныеДокумента_, Истина);
	
	ВыполненныеУслуги_ = СсылкаМД.ВыполненныеУслуги.Выгрузить();
	УИДУслугиСсылка_ = Неопределено;
	Для Каждого СтрокаВыполненныхУслуг_ Из ВыполненныеУслуги_ Цикл 
		Если ЗначениеЗаполнено(СтрокаВыполненныхУслуг_.ПричинаОтменыУслуги) Тогда 
			Продолжить;
		КонецЕсли;
		УИДУслугиСсылка_ = СтрокаВыполненныхУслуг_.УникальныйИдентификаторУслугиСсылка;
		Если ЗначениеЗаполнено(УИДУслугиСсылка_) Тогда 
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	Если ЗначениеЗаполнено(УИДУслугиСсылка_) Тогда
		ДанныеОплаты_ = ФедеральныеВебСервисыСЭМД.ПолучитьДанныеОплаты(УИДУслугиСсылка_,,ШМДСсылка_.ТипРЭМД);
		ОбщегоНазначенияКлиентСервер.ДополнитьСоответствие(Результат_, ДанныеОплаты_, Ложь);
	Иначе
		ДанныеМД_ = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(СсылкаМД, "УсловияОказанияПомощи,МедицинскаяКарта,ДатаМД,Пациент,Номер");
		УсловияПомощи_ = ДанныеМД_.УсловияОказанияПомощи;
		Если Ложь 
			ИЛИ УсловияПомощи_ = Перечисления.УсловияМедицинскойПомощи.ВПриемномОтделении
			ИЛИ УсловияПомощи_ = Перечисления.УсловияМедицинскойПомощи.Стационарная
		Тогда
			Соглашение_ = Неопределено;
			Если ОбщегоНазначения.ПодсистемаСуществует("ПрикладныеПодсистемы.Медицина.Стационар") Тогда
				ОбщийМодульОбщегоНазначенияСтационар_ = ОбщегоНазначения.ОбщийМодуль("ОбщегоНазначенияСтационар");
				Соглашение_ = ОбщийМодульОбщегоНазначенияСтационар_.ПолучитьСоглашениеПациента(ДанныеМД_.МедицинскаяКарта, ДанныеМД_.ДатаМД);
				КонецЕсли;
			
			Если ЗначениеЗаполнено(Соглашение_) Тогда
				ДанныеОплаты_ = ФедеральныеВебСервисыСЭМД.ПолучитьДанныеОплатыПоУточненнымПараметрам(
					Соглашение_,
					Неопределено,
					ДанныеМД_.Пациент,
					ДанныеМД_.ДатаМД,
					ДанныеМД_.Номер,,
					ШМДСсылка_.ТипРЭМД
				);
				ОбщегоНазначенияКлиентСервер.ДополнитьСоответствие(Результат_, ДанныеОплаты_, Ложь);
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	Назначение_ = ПолучитьНазначение(СсылкаМД);
	ОбщегоНазначенияКлиентСервер.ДополнитьСоответствие(Результат_, Назначение_, Ложь);
	
	Результат_.Вставить("ФактНаличияПодписи", "S");
	
	Возврат Результат_;
КонецФункции

Функция ПолучитьДанныеДокумента(СсылкаМД, ОрганизацияOID, Префикс)
	ДанныеСсылки_ = ОбщиеМеханизмыHL7.Документ2OID(СсылкаМД, ОрганизацияOID);
	РеквизитыВыполнения_ = ШаблоныМедицинскихДокументовПереопределяемый.ПолучитьРеквизитыВыполнения(СсылкаМД);
	
	Результат_ = Новый Структура();
	ДатаДокумента_ = ШаблоныМедицинскихДокументовПоликлиника.ФорматЛокальнойДатыСоСмещением(РеквизитыВыполнения_.ДатаВыполнения, "ДФ=ггггММддЧЧмм");
	Результат_.Вставить(Префикс + "Дата", ДатаДокумента_);
	Результат_.Вставить(Префикс + "ДатаПодписи", ДатаДокумента_);
	Результат_.Вставить(Префикс + "НаборВерсийRoot", ОрганизацияOID + ".100.1.1.50");
	Результат_.Вставить(Префикс + "НаборВерсийExtension", СсылкаМД.УникальныйИдентификатор());	
	Результат_.Вставить(Префикс + "НомерКарты");
	
	МедКарта_ = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(СсылкаМД, "МедицинскаяКарта");
	Если ЗначениеЗаполнено(МедКарта_) Тогда
		Результат_.Вставить(Префикс + "НомерКарты", МедКарта_.НомерКартыПредставление);
	КонецЕсли;
	
	Результат_.Вставить(Префикс + "Root", ОрганизацияOID + ".100.1.1.51");
	Результат_.Вставить(Префикс + "Extension", Новый УникальныйИдентификатор);
	Результат_.Вставить(Префикс + "НомерВерсии", "1");
	
	Результат_.Вставить(Префикс + "ТипКонсультацииCode", "");
	Услуги_ = СсылкаМД.ВыполненныеУслуги;
	ТипКонсультацииЗаполнен_ = Ложь;
	Для Каждого Услуга_ Из Услуги_ Цикл
		Если ЗначениеЗаполнено(Услуга_.ПричинаОтменыУслуги) Тогда 
			Продолжить;
		КонецЕсли;
		
		ВидМедицинскойУслуги_ = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Услуга_.Номенклатура, "ВидМедицинскойУслуги");
		Если ВидМедицинскойУслуги_ = Перечисления.ВидыМедицинскихУслуг.Многоэтапная Тогда
			ЭтапыУслуги_ = ШаблоныМедицинскихДокументовПереопределяемый.ПолучитьЭтапыУслуги(Услуга_.Номенклатура);
			Для Каждого Этап_ Из ЭтапыУслуги_ Цикл
				ТипКонсультации_ = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Этап_.Номенклатура, "ТипКонсультации");
				Если ЗначениеЗаполнено(ТипКонсультации_) Тогда
					Результат_.Вставить(Префикс +"ТипКонсультацииCode", ФормированиеСЭМД.codeТегПоСсылке("code", ТипКонсультации_));
					ТипКонсультацииЗаполнен_ = Истина;
					Прервать;
				КонецЕсли;
			КонецЦикла;
			Если ТипКонсультацииЗаполнен_ Тогда
				Прервать;
			КонецЕсли;
		Иначе
			ТипКонсультации_ = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Услуга_.Номенклатура, "ТипКонсультации");
			Если ЗначениеЗаполнено(ТипКонсультации_) Тогда
				Результат_.Вставить(Префикс +"ТипКонсультацииCode", ФормированиеСЭМД.codeТегПоСсылке("code", ТипКонсультации_));
				Прервать;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	Возврат Результат_;
КонецФункции

Функция ПолучитьНазначение(СсылкаМД)
	Результат_ = АлгоритмыДляКоллекций.СоздатьСоответствие(
		"НазначениеМД",                Неопределено,
		"НазначениеОрганизация",       Неопределено,
		"НазначениеТипМДТег",          Неопределено,
		"НазначениеТипМДКод",          Неопределено,
		"НазначениеТипМДНаименование", Неопределено,
		"НазначениеТипМДСсылка",       Неопределено,
		"НазначениеRoot",              Неопределено,
		"НазначениеExtension",         Неопределено,
		"ЕстьНаправивишийСотрудник",   Ложь,
		"НазначениеАвторСотрудникИмя",             Неопределено,
		"НазначениеАвторСотрудникСНИЛС",           Неопределено,
		"НазначениеАвторОрганизацияНаименование",  Неопределено,
		"НазначениеАвторОрганизацияOID",           Неопределено,
		"НазначениеАвторОрганизацияТелефон",       Неопределено,
		"НазначениеАвторСотрудникФамилия",         Неопределено,
		"НазначениеАвторПодразделениеOID",         Неопределено,
		"НазначениеАвторОрганизацияАдресСубъектРФ_noCD", Неопределено,
		"НазначениеАвторСотрудникId",              Неопределено,
		"НазначениеАвторОрганизацияАдресAOGUID",   Неопределено,
		"НазначениеАвторСотрудникОтчество",        Неопределено,
		"НазначениеАвторОрганизацияАдресHOUSEGUID",Неопределено,
		"НазначениеАвторСотрудникДолжностьCode",   Неопределено,
		"НазначениеАвторОрганизацияАдрес",         Неопределено,
		"НазначениеАвторОрганизацияАдресИндекс",   Неопределено,
		"НазначениеРЭМДExtension",                 Неопределено,
		"НазначениеАвторОрганизацияАдресСтранаCode", Неопределено,
		"НазначениеАвторОрганизацияАдресСубъектРФCD", Неопределено,
		"НазначениеАвторОрганизацияАдресCode", Неопределено
	);
	
	МассивУИДУслуг_ = СсылкаМД.ВыполненныеУслуги.Выгрузить().ВыгрузитьКолонку("УникальныйИдентификаторУслуги");
	Назначения_ = УправлениеЗаказамиУслугами.ПолучитьНазначенияУслуг(МассивУИДУслуг_);
	Если (Назначения_.Количество() > 0) Тогда
		Назначение_ = Назначения_[0].МДСсылка;
		Организация_ = Назначения_[0].Организация;
	Иначе
		Назначение_ = Неопределено;
		Организация_ = Неопределено;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Назначение_) Тогда
		Результат_["Назначение"] = Назначение_;
		Результат_["НазначениеОрганизация"] = Организация_;
		ШМДСсылка_ = ШаблоныМедицинскихДокументовПоликлиника.ПолучитьШМДуМД(Назначение_);
		Результат_["НазначениеТипМДТег"] = ФормированиеСЭМД.codeТегПоСсылке(
			"code", 
			ОбщиеМеханизмы.ЗначениеРеквизитаОбъекта(ШМДСсылка_, "ТипМД"),
			"SLM365"
		);
		СвойстваТипаМД_ = ОбщиеМеханизмы.ЗначенияРеквизитовОбъекта(ШМДСсылка_, "ТипМД.КодМинздрава,ТипМД.Наименование,ТипМД.Ссылка");
		Результат_["НазначениеТипМДКод"] = СвойстваТипаМД_.ТипМДКодМинздрава;
		Результат_["НазначениеТипМДНаименование"] = СвойстваТипаМД_.ТипМДНаименование;
		Результат_["НазначениеТипМДСсылка"] = СвойстваТипаМД_.ТипМДСсылка;
		
		СтруктураМДНазначение_ = ОбщиеМеханизмыHL7.СсылкаИлиУИД2OID(
			Назначение_,
			Организация_,
			".51",
			Истина
		);
		Результат_["НазначениеRoot"] = СтруктураМДНазначение_.root;
		Результат_["НазначениеExtension"] = СтруктураМДНазначение_.extension;
		
		АвторМД_ = ШаблоныМедицинскихДокументовПереопределяемый.ПолучитьАвтораМД(Назначение_).Автор;
		Если ЗначениеЗаполнено(АвторМД_) Тогда 
			Результат_["ЕстьНаправивишийСотрудник"] = Истина;
			ОбщегоНазначенияКлиентСервер.ДополнитьСоответствие(
				Результат_, 
				ФедеральныеВебСервисыСЭМД.ПолучитьДанныеСотрудника(АвторМД_, Организация_, "НазначениеАвтор"),
				Ложь
			);
			ОбщегоНазначенияКлиентСервер.ДополнитьСоответствие(
				Результат_, 
				ФедеральныеВебСервисыСЭМД.ПолучитьДанныеОрганизации(Организация_, "НазначениеАвтор"),
				Ложь
			)
		КонецЕсли;
	КонецЕсли;

	Возврат Результат_;
КонецФункции

#КонецОбласти

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

// Определяет код типа РЭМД мед. документа
Функция КодТипаДокументаРЭМДСлужебный(МДСсылка, CDAСсылка) Экспорт

	КодТипаРЭМД_ = Неопределено;

	ТипМД_ = Неопределено;
	
	Если ЗначениеЗаполнено(CDAСсылка) Тогда
		Запрос_ = Новый Запрос(
			"ВЫБРАТЬ
			|	ТипыРЭМД.КодМинздрава КАК КодТипаДокументаРЭМД,
			|	ШМДВидыСоздаваемыхДокументов.Ссылка КАК ШМД,
			|	Файлы.ВладелецФайла КАК ВладелецФайла,
			|	ШаблоныМедицинскихДокументов.ТипМД КАК ТипМД,
			|	МедицинскийДокументCDAДокументы.ТипМД КАК ТипМДИзТЧ
			|ИЗ
			|	Документ.МедицинскийДокумент.CDAДокументы КАК МедицинскийДокументCDAДокументы
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.СтатусыМедицинскихДокументов КАК СтатусыМД
			|		ПО МедицинскийДокументCDAДокументы.Ссылка = СтатусыМД.МедицинскийДокумент
			|			И МедицинскийДокументCDAДокументы.ТелоМедДокумента = СтатусыМД.ТелоМедДокумента
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ШаблоныМедицинскихДокументов.ВидыСоздаваемыхДокументов КАК ШМДВидыСоздаваемыхДокументов
			|		ПО МедицинскийДокументCDAДокументы.ШаблонМедицинскогоДокумента = ШМДВидыСоздаваемыхДокументов.Ссылка
			|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ШаблоныМедицинскихДокументов КАК ШаблоныМедицинскихДокументов
			|		ПО ШМДВидыСоздаваемыхДокументов.Ссылка = ШаблоныМедицинскихДокументов.Ссылка
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ТипыМДРегистрируемыхВРЭМД КАК ТипыРЭМД
			|		ПО (ШМДВидыСоздаваемыхДокументов.ТипРЭМД = ТипыРЭМД.Ссылка)
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ТипыМД КАК ВидыМД
			|		ПО (ШМДВидыСоздаваемыхДокументов.ТипМД = ВидыМД.Ссылка)
			|			И (ВидыМД.КодМинздрава = СтатусыМД.АтрибутCode)
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Файлы КАК Файлы
			|		ПО МедицинскийДокументCDAДокументы.ТелоМедДокумента = Файлы.Ссылка
			|ГДЕ
			|	МедицинскийДокументCDAДокументы.Ссылка = &Ссылка
			|	И МедицинскийДокументCDAДокументы.ТелоМедДокумента = &CDAСсылка
			|
			|ОБЪЕДИНИТЬ ВСЕ
			|
			|ВЫБРАТЬ
			|	ТипыМД.КодМинздрава КАК КодТипаДокументаРЭМД,
			|	ШаблоныМедицинскихДокументов.Ссылка КАК ШМД,
			|	Файлы.ВладелецФайла КАК ВладелецФайла,
			|	ШаблоныМедицинскихДокументов.ТипМД КАК ТипМД,
			|	МедицинскийДокументCDAДокументы.ТипМД КАК ТипМДИзТЧ
			|ИЗ
			|	Документ.МедицинскийДокумент.CDAДокументы КАК МедицинскийДокументCDAДокументы
			|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ШаблоныМедицинскихДокументов КАК ШаблоныМедицинскихДокументов
			|		ПО МедицинскийДокументCDAДокументы.ШаблонМедицинскогоДокумента = ШаблоныМедицинскихДокументов.Ссылка
			|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ТипыМДРегистрируемыхВРЭМД КАК ТипыМД
			|		ПО (ШаблоныМедицинскихДокументов.ТипРЭМД = ТипыМД.Ссылка)
			|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Файлы КАК Файлы
			|		ПО МедицинскийДокументCDAДокументы.ТелоМедДокумента = Файлы.Ссылка
			|ГДЕ
			|	МедицинскийДокументCDAДокументы.Ссылка = &Ссылка
			|	И МедицинскийДокументCDAДокументы.ТелоМедДокумента = &CDAСсылка"
		);
		Запрос_.УстановитьПараметр("Ссылка", МДСсылка);
		Запрос_.УстановитьПараметр("CDAСсылка", CDAСсылка);
		Выборка_ = Запрос_.Выполнить().Выбрать();
		Если Выборка_.Следующий() Тогда
			
			ТипМД_ = ВыбратьЗаполненное(Выборка_.ТипМДИзТЧ, Выборка_.ТипМД);
			
			Если ЗначениеЗаполнено(Выборка_.КодТипаДокументаРЭМД) Тогда 
				КодТипаРЭМД_ = Выборка_.КодТипаДокументаРЭМД;
			Иначе
				// Возможно, это автосозданный CDA.
				Если НЕ ЗначениеЗаполнено(Выборка_.ШМД)
					И ЭтоНаправлениеВоВспомогательныеКабинеты(ТипМД_)
				Тогда 
					// Считаем, что это Направление во вспом кабинет.
					КодТипаРЭМД_ = ТипРЭМДНаправленияВоВспомогательныеКабинеты();
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
	Иначе
		// Старое поведение для обратной совместимости.
		Запрос_ = Новый Запрос(
			"ВЫБРАТЬ
			|	ТипыМД.КодМинздрава КАК КодМинздрава
			|ИЗ
			|	Документ.МедицинскийДокумент.CDAДокументы КАК МедицинскийДокументCDAДокументы
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ТипыМДРегистрируемыхВРЭМД КАК ТипыМД
			|		ПО МедицинскийДокументCDAДокументы.ШаблонМедицинскогоДокумента.ТипРЭМД = ТипыМД.Ссылка
			|ГДЕ
			|	МедицинскийДокументCDAДокументы.Ссылка = &Ссылка
			|	И МедицинскийДокументCDAДокументы.Актуально
			|
			|УПОРЯДОЧИТЬ ПО
			|	МедицинскийДокументCDAДокументы.НомерСтроки"
		);// т.к. в будущем будут МД с несколькими телами, берем первое.
		Запрос_.УстановитьПараметр("Ссылка", МДСсылка);
		Выборка_ = Запрос_.Выполнить().Выбрать();
		Если Выборка_.Следующий() Тогда
			КодТипаРЭМД_ = Выборка_.КодМинздрава;
		КонецЕсли;
	КонецЕсли;
	
	Возврат Новый Структура("КодТипаРЭМД, ТипМД", КодТипаРЭМД_, ТипМД_);
	
КонецФункции

#КонецОбласти

#Область ВерсионированиеОбъектов

// СтандартныеПодсистемы.ВерсионированиеОбъектов

// Определяет настройки объекта для подсистемы ВерсионированиеОбъектов.
//
// Параметры:
//  Настройки - Структура - настройки подсистемы.
Процедура ПриОпределенииНастроекВерсионированияОбъектов(Настройки) Экспорт

КонецПроцедуры

#КонецОбласти

#Область ВспомогательныеПроцедурыИФункции

Функция ТекстЗапросаАналитикаУчетаПоПартнерам()
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	КлючиАналитикиТЗ.НомерСтроки КАК НомерСтроки,
	|	КлючиАналитикиТЗ.Ключ КАК КлючАналитики,
	|	КлючиАналитикиТЗ.ТипТЧ КАК ТипТЧ
	|ПОМЕСТИТЬ втАналитикаУчетаПоПартнерам
	|ИЗ
	|	&КлючиАналитикиТЗ КАК КлючиАналитикиТЗ";

	Возврат ТекстЗапроса;
	
КонецФункции

// Функция формирует текст запроса для таблицы расчетов с клиентами.
//
// Возвращаемое значение:
//	Строка - Текст запроса
//
Функция ТекстЗапросаТаблицаЦеновыеСоглашения()
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ЦеновыеСоглашения.Соглашение,
	|	ЦеновыеСоглашения.ЦеновоеСоглашение
	|ПОМЕСТИТЬ втЦеновыеСоглашения
	|ИЗ
	|	&ЦеновыеСоглашения КАК ЦеновыеСоглашения";
	
	Возврат ТекстЗапроса;
КонецФункции

// Функция формирует текст запроса для таблицы расчетов с клиентами.
//
// Возвращаемое значение:
//	Строка - Текст запроса
//
Функция ТекстЗапросаТаблицаРасчетыСКлиентами()
	
	СтационарныеУсловия_ = ОбщиеМеханизмыПовтИсп.ИспользоватьСтационарныеУсловия();
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	&Период КАК Период,
	|	&Период КАК ДатаРегистратора,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
	|	втАналитикаУчетаПоПартнерам.КлючАналитики КАК АналитикаУчетаПоПартнерам,
	|	ВЫБОР КОГДА ДанныеДокумента.ПланЛеченияУслуги = ЗНАЧЕНИЕ(Документ.ПланЛечения.ПустаяСсылка)
	|		ТОГДА ПРЕДСТАВЛЕНИЕ(ДанныеДокумента.УникальныйИдентификаторУслуги) 
	|	ИНАЧЕ ДанныеДокумента.ПланЛеченияУслуги КОНЕЦ КАК ЗаказКлиента,
	|	СтатусыУслуг.Соглашение.Валюта КАК Валюта,
	|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПланированиеПоЗаказуКлиента) КАК ХозяйственнаяОперация,
	|	НЕОПРЕДЕЛЕНО КАК ФормаОплаты,
	|	РегистрСведенийСтатусыУслугФинансовыеДанные.Сумма * (ДанныеДокумента.Количество / СтатусыУслуг.Количество) КАК Сумма,
	|	ВЫБОР
	|		КОГДА ДанныеДокумента.Количество = СтатусыУслуг.Количество
	|				И ДанныеДокумента.Количество <> ДанныеЗаказа.Количество
	|			ТОГДА РегистрСведенийСтатусыУслугФинансовыеДанные.Сумма * (1 - ДанныеЗаказа.Количество / ДанныеДокумента.Количество)
	|		ИНАЧЕ -РегистрСведенийСтатусыУслугФинансовыеДанные.Сумма * (1 - ДанныеДокумента.Количество / СтатусыУслуг.Количество)
	|	КОНЕЦ КАК КОплате,
	|	ДанныеДокумента.Ссылка КАК РасчетныйДокумент
	|ИЗ
	|	Документ.МедицинскийДокумент.ВыполненныеУслуги КАК ДанныеДокумента
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втСтатусыУслуг КАК СтатусыУслуг
	|		ПО ДанныеДокумента.УникальныйИдентификаторУслуги = СтатусыУслуг.УникальныйИдентификаторУслуги
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ЗаказПациента.МедицинскиеУслуги КАК ДанныеЗаказа
	|		ПО (СтатусыУслуг.Заказ = ДанныеЗаказа.Ссылка)
	|			И ДанныеДокумента.УникальныйИдентификаторУслуги = ДанныеЗаказа.УникальныйИдентификаторУслуги
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.СтатусыУслугФинансовыеДанные КАК РегистрСведенийСтатусыУслугФинансовыеДанные
	|		ПО (СтатусыУслуг.УникальныйИдентификаторУслуги = РегистрСведенийСтатусыУслугФинансовыеДанные.УникальныйИдентификаторУслуги)
	|		ЛЕВОЕ СОЕДИНЕНИЕ втАналитикаУчетаПоПартнерам КАК втАналитикаУчетаПоПартнерам
	|		ПО ДанныеДокумента.НомерСтроки = втАналитикаУчетаПоПартнерам.НомерСтроки
	|			И втАналитикаУчетаПоПартнерам.ТипТЧ = 1
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.СпецификацииМедицинскихУслуг.СоставУслуги КАК СоставУслуги
	|		ПО ДанныеДокумента.Спецификация = СоставУслуги.Ссылка
	|			И ДанныеДокумента.КлючСтрокиСоставаУслуги = СоставУслуги.КлючСтроки
	|ГДЕ
	|	ДанныеДокумента.Ссылка = &Ссылка
	|	И НЕ ДанныеДокумента.БезЗаказа
	|	И ДанныеДокумента.ПричинаОтменыУслуги = ЗНАЧЕНИЕ(Справочник.ПричиныОтменыУслугЗаказаПациента.ПустаяСсылка)
	|	И СтатусыУслуг.ИсточникФинансирования В ИЕРАРХИИ (ЗНАЧЕНИЕ(Справочник.ИсточникиФинансирования.ПЛТ))
	|	И (СоставУслуги.Ссылка ЕСТЬ NULL
	|			ИЛИ СоставУслуги.НомерСтроки = 1)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	&Период,
	|	&Период,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход),
	|	втАналитикаУчетаПоПартнерам.КлючАналитики,
	|	ПРЕДСТАВЛЕНИЕ(ДанныеДокумента.УникальныйИдентификаторУслуги),
	|	СтатусыУслуг.Соглашение.Валюта,
	|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПланированиеПоЗаказуКлиента),
	|	НЕОПРЕДЕЛЕНО,
	|	0,
	|	-РегистрСведенийСтатусыУслугФинансовыеДанные.Сумма,
	|	ДанныеДокумента.Ссылка
	|ИЗ
	|	Документ.МедицинскийДокумент.ВыполненныеУслуги КАК ДанныеДокумента
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.СтатусыУслуг КАК СтатусыУслуг
	|		ПО ДанныеДокумента.УникальныйИдентификаторУслуги = СтатусыУслуг.УникальныйИдентификаторУслуги
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.СтатусыУслугФинансовыеДанные КАК РегистрСведенийСтатусыУслугФинансовыеДанные
	|		ПО (СтатусыУслуг.УникальныйИдентификаторУслуги = РегистрСведенийСтатусыУслугФинансовыеДанные.УникальныйИдентификаторУслуги)
	|		ЛЕВОЕ СОЕДИНЕНИЕ втАналитикаУчетаПоПартнерам КАК втАналитикаУчетаПоПартнерам
	|		ПО ДанныеДокумента.НомерСтроки = втАналитикаУчетаПоПартнерам.НомерСтроки
	|			И втАналитикаУчетаПоПартнерам.ТипТЧ = 1
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.СпецификацииМедицинскихУслуг.СоставУслуги КАК СоставУслуги
	|		ПО ДанныеДокумента.Спецификация = СоставУслуги.Ссылка
	|			И ДанныеДокумента.КлючСтрокиСоставаУслуги = СоставУслуги.КлючСтроки
	|ГДЕ
	|	ДанныеДокумента.Ссылка = &Ссылка
	|	И НЕ ДанныеДокумента.БезЗаказа
	|	И ДанныеДокумента.ПричинаОтменыУслуги <> ЗНАЧЕНИЕ(Справочник.ПричиныОтменыУслугЗаказаПациента.ПустаяСсылка)
	|	И СтатусыУслуг.ИсточникФинансирования В ИЕРАРХИИ (ЗНАЧЕНИЕ(Справочник.ИсточникиФинансирования.ПЛТ))
	|	И (СоставУслуги.Ссылка ЕСТЬ NULL
	|			ИЛИ СоставУслуги.НомерСтроки = 1)
	|	И ДанныеДокумента.ПланЛеченияУслуги = ЗНАЧЕНИЕ(Документ.ПланЛечения.ПустаяСсылка)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	&Период,
	|	&Период,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход),
	|	втАналитикаУчетаПоПартнерам.КлючАналитики,
	|	ПРЕДСТАВЛЕНИЕ(ДанныеДокумента.УникальныйИдентификаторУслуги),
	|	ДанныеДокумента.Валюта,
	|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПланированиеПоЗаказуКлиента),
	|	НЕОПРЕДЕЛЕНО,
	|	ДанныеДокумента.Сумма,
	|	ДанныеДокумента.Сумма,
	|	ДанныеДокумента.Ссылка
	|ИЗ
	|	Документ.МедицинскийДокумент.ВыполненныеУслуги КАК ДанныеДокумента
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втАналитикаУчетаПоПартнерам КАК втАналитикаУчетаПоПартнерам
	|		ПО ДанныеДокумента.НомерСтроки = втАналитикаУчетаПоПартнерам.НомерСтроки
	|			И втАналитикаУчетаПоПартнерам.ТипТЧ = 1
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.МедицинскийДокумент.ВыполненныеМЭУслуги КАК МедицинскийДокументВыполненныеМЭУслуги
	|		ПО ДанныеДокумента.УникальныйИдентификаторУслуги = МедицинскийДокументВыполненныеМЭУслуги.УникальныйИдентификаторУслуги
	|			И ДанныеДокумента.Ссылка = МедицинскийДокументВыполненныеМЭУслуги.Ссылка
	|ГДЕ
	|	ДанныеДокумента.Ссылка = &Ссылка
	|	И ДанныеДокумента.ПричинаОтменыУслуги = ЗНАЧЕНИЕ(Справочник.ПричиныОтменыУслугЗаказаПациента.ПустаяСсылка)
	|	И ДанныеДокумента.БезЗаказа
	|	И ДанныеДокумента.ИсточникФинансирования В ИЕРАРХИИ (ЗНАЧЕНИЕ(Справочник.ИсточникиФинансирования.ПЛТ))
	|	И ДанныеДокумента.ПланЛеченияУслуги = ЗНАЧЕНИЕ(Документ.ПланЛечения.ПустаяСсылка)
	|	И МедицинскийДокументВыполненныеМЭУслуги.УникальныйИдентификаторУслуги ЕСТЬ NULL
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	&Период,
	|	&Период,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход),
	|	втАналитикаУчетаПоПартнерам.КлючАналитики,
	|	ДанныеДокумента.ПланЛеченияУслуги,
	|	ДанныеДокумента.Валюта,
	|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПланированиеПоЗаказуКлиента),
	|	НЕОПРЕДЕЛЕНО,
	|	ДанныеДокумента.Сумма,
	|	0,
	|	ДанныеДокумента.Ссылка
	|ИЗ
	|	Документ.МедицинскийДокумент.ВыполненныеУслуги КАК ДанныеДокумента
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втАналитикаУчетаПоПартнерам КАК втАналитикаУчетаПоПартнерам
	|		ПО ДанныеДокумента.НомерСтроки = втАналитикаУчетаПоПартнерам.НомерСтроки
	|			И втАналитикаУчетаПоПартнерам.ТипТЧ = 1
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.МедицинскийДокумент.ВыполненныеМЭУслуги КАК МедицинскийДокументВыполненныеМЭУслуги
	|		ПО ДанныеДокумента.УникальныйИдентификаторУслуги = МедицинскийДокументВыполненныеМЭУслуги.УникальныйИдентификаторУслуги
	|			И ДанныеДокумента.Ссылка = МедицинскийДокументВыполненныеМЭУслуги.Ссылка
	|ГДЕ
	|	ДанныеДокумента.Ссылка = &Ссылка
	|	И ДанныеДокумента.ПричинаОтменыУслуги = ЗНАЧЕНИЕ(Справочник.ПричиныОтменыУслугЗаказаПациента.ПустаяСсылка)
	|	И ДанныеДокумента.БезЗаказа
	|	И ДанныеДокумента.ИсточникФинансирования В ИЕРАРХИИ (ЗНАЧЕНИЕ(Справочник.ИсточникиФинансирования.ПЛТ))
	|	И МедицинскийДокументВыполненныеМЭУслуги.УникальныйИдентификаторУслуги ЕСТЬ NULL
	|	И ДанныеДокумента.ПланЛеченияУслуги <> ЗНАЧЕНИЕ(Документ.ПланЛечения.ПустаяСсылка)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	&Период,
	|	&Период,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход),
	|	втАналитикаУчетаПоПартнерам.КлючАналитики,
	|	ВЫБОР
	|		КОГДА СправочникДоговорыКонтрагентов.ПорядокРасчетов = ЗНАЧЕНИЕ(Перечисление.ПорядокРасчетов.ПоСоглашениям)
	|			ТОГДА ДанныеДокумента.Соглашение
	|		ИНАЧЕ СправочникДоговорыКонтрагентов.Ссылка
	|	КОНЕЦ,
	|	ДанныеДокумента.Валюта,
	|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПланированиеПоЗаказуКлиента),
	|	НЕОПРЕДЕЛЕНО,
	|	ДанныеДокумента.Сумма,
	|	0,
	|	ДанныеДокумента.Ссылка
	|ИЗ
	|	Документ.МедицинскийДокумент.ВыполненныеУслуги КАК ДанныеДокумента
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втАналитикаУчетаПоПартнерам КАК втАналитикаУчетаПоПартнерам
	|		ПО ДанныеДокумента.НомерСтроки = втАналитикаУчетаПоПартнерам.НомерСтроки
	|			И втАналитикаУчетаПоПартнерам.ТипТЧ = 1
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ДоговорыКонтрагентов КАК СправочникДоговорыКонтрагентов
	|		ПО ДанныеДокумента.Договор = СправочникДоговорыКонтрагентов.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.МедицинскийДокумент.ВыполненныеМЭУслуги КАК МедицинскийДокументВыполненныеМЭУслуги
	|		ПО ДанныеДокумента.УникальныйИдентификаторУслуги = МедицинскийДокументВыполненныеМЭУслуги.УникальныйИдентификаторУслуги
	|			И ДанныеДокумента.Ссылка = МедицинскийДокументВыполненныеМЭУслуги.Ссылка
	|ГДЕ
	|	ДанныеДокумента.Ссылка = &Ссылка
	|	И ДанныеДокумента.БезЗаказа
	|	И ДанныеДокумента.ПричинаОтменыУслуги = ЗНАЧЕНИЕ(Справочник.ПричиныОтменыУслугЗаказаПациента.ПустаяСсылка)
	|	И МедицинскийДокументВыполненныеМЭУслуги.УникальныйИдентификаторУслуги ЕСТЬ NULL
	|	И СправочникДоговорыКонтрагентов.ВидДоговора = ЗНАЧЕНИЕ(Перечисление.ВидыДоговоров.ДепозитныйДоговор)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	&Период,
	|	&Период,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход),
	|	втАналитикаУчетаПоПартнерам.КлючАналитики,
	|	ПРЕДСТАВЛЕНИЕ(ДанныеДокумента.УникальныйИдентификаторУслуги),
	|	ДанныеДокумента.Валюта,
	|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПланированиеПоЗаказуКлиента),
	|	НЕОПРЕДЕЛЕНО,
	|	ДанныеДокумента.Сумма,
	|	ДанныеДокумента.Сумма,
	|	ДанныеДокумента.Ссылка
	|ИЗ
	|	Документ.МедицинскийДокумент.ВыполненныеМЭУслуги КАК ДанныеДокумента
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втАналитикаУчетаПоПартнерам КАК втАналитикаУчетаПоПартнерам
	|		ПО ДанныеДокумента.НомерСтроки = втАналитикаУчетаПоПартнерам.НомерСтроки
	|			И втАналитикаУчетаПоПартнерам.ТипТЧ = 2
	|ГДЕ
	|	ДанныеДокумента.Ссылка = &Ссылка
	|	И ДанныеДокумента.ПричинаОтменыУслуги = ЗНАЧЕНИЕ(Справочник.ПричиныОтменыУслугЗаказаПациента.ПустаяСсылка)
	|	И ДанныеДокумента.ИсточникФинансирования В ИЕРАРХИИ (ЗНАЧЕНИЕ(Справочник.ИсточникиФинансирования.ПЛТ))
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	&Период,
	|	&Период,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход),
	|	втАналитикаУчетаПоПартнерам.КлючАналитики,
	|	ВЫБОР
	|		КОГДА СправочникДоговорыКонтрагентов.ПорядокРасчетов = ЗНАЧЕНИЕ(Перечисление.ПорядокРасчетов.ПоСоглашениям)
	|			ТОГДА ДанныеДокумента.Соглашение
	|		ИНАЧЕ СправочникДоговорыКонтрагентов.Ссылка
	|	КОНЕЦ,
	|	ДанныеДокумента.Валюта,
	|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПланированиеПоЗаказуКлиента),
	|	НЕОПРЕДЕЛЕНО,
	|	ДанныеДокумента.Сумма,
	|	0,
	|	ДанныеДокумента.Ссылка
	|ИЗ
	|	Документ.МедицинскийДокумент.ВыполненныеМЭУслуги КАК ДанныеДокумента
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втАналитикаУчетаПоПартнерам КАК втАналитикаУчетаПоПартнерам
	|		ПО ДанныеДокумента.НомерСтроки = втАналитикаУчетаПоПартнерам.НомерСтроки
	|			И втАналитикаУчетаПоПартнерам.ТипТЧ = 2
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ДоговорыКонтрагентов КАК СправочникДоговорыКонтрагентов
	|		ПО ДанныеДокумента.Договор = СправочникДоговорыКонтрагентов.Ссылка
	|ГДЕ
	|	ДанныеДокумента.Ссылка = &Ссылка
	|	И ДанныеДокумента.ПричинаОтменыУслуги = ЗНАЧЕНИЕ(Справочник.ПричиныОтменыУслугЗаказаПациента.ПустаяСсылка)
	|	И СправочникДоговорыКонтрагентов.ВидДоговора = ЗНАЧЕНИЕ(Перечисление.ВидыДоговоров.ДепозитныйДоговор)";


	Если СтационарныеУсловия_ Тогда
		ТекстЗапроса = ТекстЗапроса + Символы.ПС + "ОБЪЕДИНИТЬ ВСЕ" + Символы.ПС +
		"ВЫБРАТЬ
		|	&Период КАК Период,
		|	&Период КАК ДатаРегистратора,
		|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
		|	РегистрАналитикаУчетаПоПартнерам.КлючАналитики КАК АналитикаУчетаПоПартнерам,
		|	ВЫБОР
		|		КОГДА СправочникДоговорыКонтрагентов.ПорядокРасчетов = ЗНАЧЕНИЕ(Перечисление.ПорядокРасчетов.ПоСоглашениям)
		|			ТОГДА ДанныеДокумента.Соглашение
		|		ИНАЧЕ ДанныеДокумента.Договор
		|	КОНЕЦ КАК ЗаказКлиента,
		|	СправочникСоглашенияСКлиентами.Валюта КАК Валюта,
		|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РеализацияКлиенту) КАК ХозяйственнаяОперация,
		|	НЕОПРЕДЕЛЕНО КАК ФормаОплаты,
		|	ЦеныНоменклатурыПоСоглашениям.Цена * ДанныеДокумента.Количество КАК Сумма,
		|	0 КАК КОплате,
		|	ДанныеДокумента.Ссылка КАК РасчетныйДокумент
		|ИЗ
		|	Документ.МедицинскийДокумент.НазначенныеУслуги КАК ДанныеДокумента
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.МедицинскийДокумент КАК ДокументМедицинскийДокумент
		|		ПО ДанныеДокумента.Ссылка = ДокументМедицинскийДокумент.Ссылка
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.СтатусыУслуг КАК СтатусыУслуг
		|		ПО ДанныеДокумента.УникальныйИдентификаторУслуги = СтатусыУслуг.УникальныйИдентификаторУслуги
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.СоглашенияСКлиентами КАК СправочникСоглашенияСКлиентами
		|		ПО (СтатусыУслуг.Соглашение = СправочникСоглашенияСКлиентами.Ссылка)
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ДоговорыКонтрагентов КАК СправочникДоговорыКонтрагентов
		|		ПО (СтатусыУслуг.Договор = СправочникДоговорыКонтрагентов.Ссылка)
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втЦеновыеСоглашения КАК втЦеновыеСоглашения
		|		ПО (СтатусыУслуг.Соглашение = втЦеновыеСоглашения.Соглашение)
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ЦеныНоменклатурыПоСоглашениям КАК ЦеныНоменклатурыПоСоглашениям
		|		ПО (втЦеновыеСоглашения.ЦеновоеСоглашение = ЦеныНоменклатурыПоСоглашениям.Соглашение)
		|			И (СтатусыУслуг.Номенклатура = ЦеныНоменклатурыПоСоглашениям.Номенклатура)
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаПоПартнерам КАК РегистрАналитикаУчетаПоПартнерам
		|		ПО (СправочникДоговорыКонтрагентов.Организация = РегистрАналитикаУчетаПоПартнерам.Организация)
		|			И (СправочникДоговорыКонтрагентов.Партнер = РегистрАналитикаУчетаПоПартнерам.Партнер)
		|			И (СправочникДоговорыКонтрагентов.Контрагент = РегистрАналитикаУчетаПоПартнерам.Контрагент)
		|ГДЕ
		|	ДанныеДокумента.Ссылка = &Ссылка
		|	И ДанныеДокумента.ОтказОтВмешательства = ЛОЖЬ
		|	И СправочникДоговорыКонтрагентов.ВидДоговора = ЗНАЧЕНИЕ(Перечисление.ВидыДоговоров.ДепозитныйДоговор)
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	&Период,
		|	&Период,
		|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход),
		|	втАналитикаУчетаПоПартнерам.КлючАналитики,
		|	ДанныеДокумента.ПланЛеченияУслуги,
		|	СтатусыУслуг.Соглашение.Валюта,
		|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПланированиеПоЗаказуКлиента),
		|	НЕОПРЕДЕЛЕНО,
		|	РегистрСведенийСтатусыУслугФинансовыеДанные.Сумма,
		|	0,
		|	ДанныеДокумента.Ссылка
		|ИЗ
		|	Документ.МедицинскийДокумент.ВыполненныеУслуги КАК ДанныеДокумента
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втАналитикаУчетаПоПартнерам КАК втАналитикаУчетаПоПартнерам
		|		ПО ДанныеДокумента.НомерСтроки = втАналитикаУчетаПоПартнерам.НомерСтроки
		|			И втАналитикаУчетаПоПартнерам.ТипТЧ = 1
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.СтатусыУслуг КАК СтатусыУслуг
		|		ПО ДанныеДокумента.УникальныйИдентификаторУслуги = СтатусыУслуг.УникальныйИдентификаторУслуги
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.СтатусыУслугФинансовыеДанные КАК РегистрСведенийСтатусыУслугФинансовыеДанные
		|		ПО (СтатусыУслуг.УникальныйИдентификаторУслуги = РегистрСведенийСтатусыУслугФинансовыеДанные.УникальныйИдентификаторУслуги)
		|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.МедицинскийДокумент.ВыполненныеМЭУслуги КАК МедицинскийДокументВыполненныеМЭУслуги
		|		ПО ДанныеДокумента.УникальныйИдентификаторУслуги = МедицинскийДокументВыполненныеМЭУслуги.УникальныйИдентификаторУслуги
		|			И ДанныеДокумента.Ссылка = МедицинскийДокументВыполненныеМЭУслуги.Ссылка
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.СоглашенияСКлиентами КАК СправочникСоглашенияСКлиентами
		|		ПО (СтатусыУслуг.Соглашение = СправочникСоглашенияСКлиентами.Ссылка)
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ДоговорыКонтрагентов КАК СправочникДоговорыКонтрагентов
		|		ПО (СтатусыУслуг.Договор = СправочникДоговорыКонтрагентов.Ссылка)
		|ГДЕ
		|	ДанныеДокумента.Ссылка = &Ссылка
		|	И (ДанныеДокумента.Ссылка.УсловияОказанияПомощи = ЗНАЧЕНИЕ(Перечисление.УсловияМедицинскойПомощи.Стационарная)
		|		ИЛИ ДанныеДокумента.Ссылка.УсловияОказанияПомощи = ЗНАЧЕНИЕ(Перечисление.УсловияМедицинскойПомощи.Стационарная)
		|		ИЛИ ДанныеДокумента.Ссылка.УсловияОказанияПомощи = ЗНАЧЕНИЕ(Перечисление.УсловияМедицинскойПомощи.Стационарная))
		|	И ДанныеДокумента.ПричинаОтменыУслуги = ЗНАЧЕНИЕ(Справочник.ПричиныОтменыУслугЗаказаПациента.ПустаяСсылка)
		|	И НЕ ДанныеДокумента.БезЗаказа
		|	И ЕСТЬNULL(СправочникДоговорыКонтрагентов.ВидДоговора, НЕОПРЕДЕЛЕНО) <> ЗНАЧЕНИЕ(Перечисление.ВидыДоговоров.ДепозитныйДоговор)
		|	И СтатусыУслуг.ИсточникФинансирования В ИЕРАРХИИ (ЗНАЧЕНИЕ(Справочник.ИсточникиФинансирования.ПЛТ))
		|	И МедицинскийДокументВыполненныеМЭУслуги.УникальныйИдентификаторУслуги ЕСТЬ NULL
		|	И ДанныеДокумента.ПланЛеченияУслуги <> ЗНАЧЕНИЕ(Документ.ПланЛечения.ПустаяСсылка)";
		
	КонецЕсли;

	Возврат ТекстЗапроса;
	
КонецФункции // ТекстЗапросаТаблицаРасчетыСКлиентами()

Функция ТекстЗапросаТаблицаСтатусыУслугФинансовыеДанные()
	ТекстЗапрос_ =
	"ВЫБРАТЬ
	|	МедицинскийДокументВыполненныеУслуги.УникальныйИдентификаторУслуги,
	|	ВЫБОР
	|		КОГДА СтатусыУслугФинансовыеДанные.Цена ЕСТЬ NULL
	|			ТОГДА ЦеныНоменклатурыПоСоглашениям.Цена
	|		ИНАЧЕ СтатусыУслугФинансовыеДанные.Цена
	|	КОНЕЦ КАК Цена,
	|	ВЫБОР
	|		КОГДА СтатусыУслугФинансовыеДанные.Сумма ЕСТЬ NULL
	|			ТОГДА ВЫБОР
	|					КОГДА СправочникИсточникиФинансирования.ВестиСуммовойУчет
	|						ТОГДА ЦеныНоменклатурыПоСоглашениям.Цена
	|					ИНАЧЕ 0
	|				КОНЕЦ * МедицинскийДокументВыполненныеУслуги.Количество
	|		ИНАЧЕ СтатусыУслугФинансовыеДанные.Сумма * (МедицинскийДокументВыполненныеУслуги.Количество / СтатусыУслуг.Количество)
	|	КОНЕЦ КАК Сумма,
	|	ВЫБОР
	|		КОГДА СтатусыУслугФинансовыеДанные.СтавкаНДС ЕСТЬ NULL
	|			ТОГДА ВЫБОР
	|					КОГДА СправочникИсточникиФинансирования.ВестиСуммовойУчет
	|						ТОГДА СправочникНоменклатура.СтавкаНДС
	|					ИНАЧЕ НЕОПРЕДЕЛЕНО
	|				КОНЕЦ
	|		ИНАЧЕ СтатусыУслугФинансовыеДанные.СтавкаНДС
	|	КОНЕЦ КАК СтавкаНДС,
	|	ВЫБОР
	|		КОГДА СтатусыУслугФинансовыеДанные.СуммаНДС ЕСТЬ NULL
	|			ТОГДА ВЫБОР
	|					КОГДА СправочникИсточникиФинансирования.ВестиСуммовойУчет
	|						ТОГДА ВЫБОР
	|								КОГДА СправочникНоменклатура.СтавкаНДС = &НДС10
	|									ТОГДА ЦеныНоменклатурыПоСоглашениям.Цена * МедицинскийДокументВыполненныеУслуги.Количество * 0.1 / (0.1 + 1)
	|								КОГДА СправочникНоменклатура.СтавкаНДС = &НДС18
	|									ТОГДА ЦеныНоменклатурыПоСоглашениям.Цена * МедицинскийДокументВыполненныеУслуги.Количество * 0.18 / (0.18 + 1)
	|								КОГДА СправочникНоменклатура.СтавкаНДС = &НДС20
	|									ТОГДА ЦеныНоменклатурыПоСоглашениям.Цена * МедицинскийДокументВыполненныеУслуги.Количество * 0.2 / (0.2 + 1)
	|								КОГДА СправочникНоменклатура.СтавкаНДС = &НДС5
	|									ТОГДА ЦеныНоменклатурыПоСоглашениям.Цена * МедицинскийДокументВыполненныеУслуги.Количество * 0.05 / (0.05 + 1)
	|								КОГДА СправочникНоменклатура.СтавкаНДС = &НДС7
	|									ТОГДА ЦеныНоменклатурыПоСоглашениям.Цена * МедицинскийДокументВыполненныеУслуги.Количество * 0.07 / (0.07 + 1)
	|								ИНАЧЕ 0
	|							КОНЕЦ
	|					ИНАЧЕ 0
	|				КОНЕЦ
	|		ИНАЧЕ СтатусыУслугФинансовыеДанные.СуммаНДС * (МедицинскийДокументВыполненныеУслуги.Количество / СтатусыУслуг.Количество)
	|	КОНЕЦ КАК СуммаНДС,
	|	ВЫБОР
	|		КОГДА СтатусыУслугФинансовыеДанные.ПроцентАвтоматическойСкидки ЕСТЬ NULL
	|			ТОГДА 0
	|		ИНАЧЕ СтатусыУслугФинансовыеДанные.ПроцентАвтоматическойСкидки
	|	КОНЕЦ КАК ПроцентАвтоматическойСкидки,
	|	ВЫБОР
	|		КОГДА СтатусыУслугФинансовыеДанные.СуммаАвтоматическойСкидки ЕСТЬ NULL
	|			ТОГДА 0
	|		ИНАЧЕ СтатусыУслугФинансовыеДанные.СуммаАвтоматическойСкидки * (МедицинскийДокументВыполненныеУслуги.Количество / СтатусыУслуг.Количество)
	|	КОНЕЦ КАК СуммаАвтоматическойСкидки,
	|	ВЫБОР
	|		КОГДА СтатусыУслугФинансовыеДанные.СуммаБонусныхБалловКСписаниюВВалюте ЕСТЬ NULL
	|			ТОГДА 0
	|		ИНАЧЕ СтатусыУслугФинансовыеДанные.СуммаБонусныхБалловКСписаниюВВалюте * (МедицинскийДокументВыполненныеУслуги.Количество / СтатусыУслуг.Количество)
	|	КОНЕЦ КАК СуммаБонусныхБалловКСписаниюВВалюте,
	|	ВЫБОР
	|		КОГДА СтатусыУслугФинансовыеДанные.ПроцентРучнойСкидки ЕСТЬ NULL
	|			ТОГДА 0
	|		ИНАЧЕ СтатусыУслугФинансовыеДанные.ПроцентРучнойСкидки
	|	КОНЕЦ КАК ПроцентРучнойСкидки,
	|	ВЫБОР
	|		КОГДА СтатусыУслугФинансовыеДанные.СуммаРучнойСкидки ЕСТЬ NULL
	|			ТОГДА 0
	|		ИНАЧЕ СтатусыУслугФинансовыеДанные.СуммаРучнойСкидки * (МедицинскийДокументВыполненныеУслуги.Количество / СтатусыУслуг.Количество)
	|	КОНЕЦ КАК СуммаРучнойСкидки,
	|	МедицинскийДокументВыполненныеУслуги.Ссылка КАК Документ,
	|	МедицинскийДокументВыполненныеУслуги.Количество <> СтатусыУслуг.Количество КАК ИзменилосьКоличество
	|ИЗ
	|	Документ.МедицинскийДокумент.ВыполненныеУслуги КАК МедицинскийДокументВыполненныеУслуги
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втСтатусыУслуг КАК СтатусыУслуг
	|		ПО МедицинскийДокументВыполненныеУслуги.УникальныйИдентификаторУслуги = СтатусыУслуг.УникальныйИдентификаторУслуги
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СтатусыУслугФинансовыеДанные КАК СтатусыУслугФинансовыеДанные
	|		ПО МедицинскийДокументВыполненныеУслуги.УникальныйИдентификаторУслуги = СтатусыУслугФинансовыеДанные.УникальныйИдентификаторУслуги
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втЦеновыеСоглашения КАК втЦеновыеСоглашения
	|		ПО (СтатусыУслуг.Соглашение = втЦеновыеСоглашения.Соглашение)
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ЦеныНоменклатурыПоСоглашениям КАК ЦеныНоменклатурыПоСоглашениям
	|		ПО (втЦеновыеСоглашения.ЦеновоеСоглашение = ЦеныНоменклатурыПоСоглашениям.Соглашение)
	|			И МедицинскийДокументВыполненныеУслуги.Номенклатура = ЦеныНоменклатурыПоСоглашениям.Номенклатура
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СправочникНоменклатура
	|		ПО МедицинскийДокументВыполненныеУслуги.Номенклатура = СправочникНоменклатура.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ИсточникиФинансирования КАК СправочникИсточникиФинансирования
	|		ПО (СтатусыУслуг.ИсточникФинансирования = СправочникИсточникиФинансирования.Ссылка)
	|ГДЕ
	|	МедицинскийДокументВыполненныеУслуги.Ссылка = &Ссылка
	|	И НЕ МедицинскийДокументВыполненныеУслуги.БезЗаказа
	|	И ВЫБОР
	|			КОГДА СтатусыУслугФинансовыеДанные.Сумма ЕСТЬ НЕ NULL 
	|				ТОГДА МедицинскийДокументВыполненныеУслуги.Количество <> СтатусыУслуг.Количество
	|							И СтатусыУслугФинансовыеДанные.Сумма > 0
	|						ИЛИ СтатусыУслугФинансовыеДанные.Документ = &Ссылка
	|			ИНАЧЕ ИСТИНА
	|		КОНЕЦ
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	МедицинскийДокументНазначенныеУслуги.УникальныйИдентификаторУслуги,
	|	ЕСТЬNULL(ПланЛеченияМедицинскиеУслуги.Цена, ЦеныНоменклатурыПоСоглашениям.Цена),
	|	ВЫБОР
	|		КОГДА СправочникИсточникиФинансирования.ВестиСуммовойУчет
	|			ТОГДА ЕСТЬNULL(ПланЛеченияМедицинскиеУслуги.Сумма/ПланЛеченияМедицинскиеУслуги.Количество, ЦеныНоменклатурыПоСоглашениям.Цена)
	|		ИНАЧЕ 0
	|	КОНЕЦ * МедицинскийДокументНазначенныеУслуги.Количество,
	|	ВЫБОР
	|		КОГДА СправочникИсточникиФинансирования.ВестиСуммовойУчет
	|			ТОГДА ЕСТЬNULL(ПланЛеченияМедицинскиеУслуги.СтавкаНДС, СправочникНоменклатура.СтавкаНДС)
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА СправочникИсточникиФинансирования.ВестиСуммовойУчет
	|			ТОГДА ВЫБОР
	|					КОГДА НЕ ПланЛеченияМедицинскиеУслуги.СуммаНДС ЕСТЬ NULL
	|						ТОГДА ПланЛеченияМедицинскиеУслуги.СуммаНДС
	|					КОГДА СправочникНоменклатура.СтавкаНДС = &НДС10
	|						ТОГДА ЦеныНоменклатурыПоСоглашениям.Цена * МедицинскийДокументНазначенныеУслуги.Количество * 0.1 / (0.1 + 1)
	|					КОГДА СправочникНоменклатура.СтавкаНДС = &НДС18
	|						ТОГДА ЦеныНоменклатурыПоСоглашениям.Цена * МедицинскийДокументНазначенныеУслуги.Количество * 0.18 / (0.18 + 1)
	|					КОГДА СправочникНоменклатура.СтавкаНДС = &НДС20
	|						ТОГДА ЦеныНоменклатурыПоСоглашениям.Цена * МедицинскийДокументНазначенныеУслуги.Количество * 0.2 / (0.2 + 1)
	|					КОГДА СправочникНоменклатура.СтавкаНДС = &НДС5
	|						ТОГДА ЦеныНоменклатурыПоСоглашениям.Цена * МедицинскийДокументНазначенныеУслуги.Количество * 0.05 / (0.05 + 1)
	|					КОГДА СправочникНоменклатура.СтавкаНДС = &НДС7
	|						ТОГДА ЦеныНоменклатурыПоСоглашениям.Цена * МедицинскийДокументНазначенныеУслуги.Количество * 0.07 / (0.07 + 1)
	|					ИНАЧЕ 0
	|				КОНЕЦ
	|		ИНАЧЕ 0
	|	КОНЕЦ,
	|	0,
	|	0,
	|	0,
	|	0,
	|	0,
	|	МедицинскийДокументНазначенныеУслуги.Ссылка,
	|	ЛОЖЬ
	|ИЗ
	|	Документ.МедицинскийДокумент.НазначенныеУслуги КАК МедицинскийДокументНазначенныеУслуги
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.МедицинскийДокумент КАК ДокументМедицинскийДокумент
	|		ПО МедицинскийДокументНазначенныеУслуги.Ссылка = ДокументМедицинскийДокумент.Ссылка
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.СтатусыУслуг КАК СтатусыУслуг
	|		ПО МедицинскийДокументНазначенныеУслуги.УникальныйИдентификаторУслуги = СтатусыУслуг.УникальныйИдентификаторУслуги
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втЦеновыеСоглашения КАК втЦеновыеСоглашения
	|		ПО (СтатусыУслуг.Соглашение = втЦеновыеСоглашения.Соглашение)
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ЦеныНоменклатурыПоСоглашениям КАК ЦеныНоменклатурыПоСоглашениям
	|		ПО (втЦеновыеСоглашения.ЦеновоеСоглашение = ЦеныНоменклатурыПоСоглашениям.Соглашение)
	|			И МедицинскийДокументНазначенныеУслуги.Номенклатура = ЦеныНоменклатурыПоСоглашениям.Номенклатура
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СправочникНоменклатура
	|		ПО МедицинскийДокументНазначенныеУслуги.Номенклатура = СправочникНоменклатура.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ИсточникиФинансирования КАК СправочникИсточникиФинансирования
	|		ПО (СтатусыУслуг.ИсточникФинансирования = СправочникИсточникиФинансирования.Ссылка)
	|		ЛЕВОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
	|			ПЛМУ.Ссылка КАК Ссылка,
	|			ПЛМУ.НомерСтроки КАК НомерСтроки,
	|			ПЛМУ.УникальныйИдентификаторУслуги КАК УникальныйИдентификаторУслуги,
	|			ПЛМУ.Номенклатура КАК Номенклатура,
	|			ПЛМУ.Цена КАК Цена,
	|			ПЛМУ.Сумма КАК Сумма,
	|			ПЛМУ.СтавкаНДС КАК СтавкаНДС,
	|			ПЛМУ.СуммаНДС КАК СуммаНДС,
	|			ПЛМУ.ПроцентАвтоматическойСкидки КАК ПроцентАвтоматическойСкидки,
	|			ПЛМУ.ПроцентРучнойСкидки КАК ПроцентРучнойСкидки,
	|			ПЛМУ.СуммаАвтоматическойСкидки КАК СуммаАвтоматическойСкидки,
	|			ПЛМУ.СуммаРучнойСкидки КАК СуммаРучнойСкидки,
	|			ПЛМУ.КлючСвязи КАК КлючСвязи,
	|			ПЛМУ.СпецификацияМедицинскойУслуги КАК СпецификацияМедицинскойУслуги,
	|			ПЛМУ.НомерДоговора КАК НомерДоговора,
	|			ПЛМУ.Количество КАК Количество,
	|			ПЛМУ.Комментарий КАК Комментарий,
	|			ПЛМУ.Соглашение КАК Соглашение,
	|			ПЛМУ.УникальныйИдентификаторУслугиСсылка КАК УникальныйИдентификаторУслугиСсылка
	|		ИЗ
	|			Документ.ПланЛечения.МедицинскиеУслуги КАК ПЛМУ
	|		ГДЕ
	|			ПЛМУ.УникальныйИдентификаторУслуги В
	|					(ВЫБРАТЬ
	|						НУ.УникальныйИдентификаторУслуги
	|					ИЗ
	|						Документ.МедицинскийДокумент.НазначенныеУслуги КАК НУ
	|					ГДЕ
	|						НУ.Ссылка = &Ссылка)) КАК ПланЛеченияМедицинскиеУслуги
	|		ПО МедицинскийДокументНазначенныеУслуги.УникальныйИдентификаторУслуги = ПланЛеченияМедицинскиеУслуги.УникальныйИдентификаторУслуги
	|ГДЕ
	|	МедицинскийДокументНазначенныеУслуги.Ссылка = &Ссылка
	|	И МедицинскийДокументНазначенныеУслуги.ОтказОтВмешательства = ЛОЖЬ
	|	И ДокументМедицинскийДокумент.УсловияОказанияПомощи = ЗНАЧЕНИЕ(Перечисление.УсловияМедицинскойПомощи.Стационарная)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	МедицинскийДокументВыполненныеУслуги.УникальныйИдентификаторУслуги,
	|	МедицинскийДокументВыполненныеУслуги.Цена,
	|	ВЫБОР
	|		КОГДА СправочникИсточникиФинансирования.ВестиСуммовойУчет
	|			ТОГДА МедицинскийДокументВыполненныеУслуги.Сумма
	|		ИНАЧЕ NULL
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА СправочникИсточникиФинансирования.ВестиСуммовойУчет
	|			ТОГДА МедицинскийДокументВыполненныеУслуги.СтавкаНДС
	|		ИНАЧЕ NULL
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА СправочникИсточникиФинансирования.ВестиСуммовойУчет
	|			ТОГДА МедицинскийДокументВыполненныеУслуги.СуммаНДС
	|		ИНАЧЕ NULL
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА СправочникИсточникиФинансирования.ВестиСуммовойУчет
	|			ТОГДА МедицинскийДокументВыполненныеУслуги.ПроцентАвтоматическойСкидки
	|		ИНАЧЕ NULL
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА СправочникИсточникиФинансирования.ВестиСуммовойУчет
	|			ТОГДА МедицинскийДокументВыполненныеУслуги.СуммаАвтоматическойСкидки
	|		ИНАЧЕ NULL
	|	КОНЕЦ,
	|	МедицинскийДокументВыполненныеУслуги.СуммаБонусныхБалловКСписаниюВВалюте,
	|	ВЫБОР
	|		КОГДА СправочникИсточникиФинансирования.ВестиСуммовойУчет
	|			ТОГДА МедицинскийДокументВыполненныеУслуги.ПроцентРучнойСкидки
	|		ИНАЧЕ NULL
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА СправочникИсточникиФинансирования.ВестиСуммовойУчет
	|			ТОГДА МедицинскийДокументВыполненныеУслуги.СуммаРучнойСкидки
	|		ИНАЧЕ NULL
	|	КОНЕЦ,
	|	МедицинскийДокументВыполненныеУслуги.Ссылка,
	|	ЛОЖЬ
	|ИЗ
	|	Документ.МедицинскийДокумент.ВыполненныеУслуги КАК МедицинскийДокументВыполненныеУслуги
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ИсточникиФинансирования КАК СправочникИсточникиФинансирования
	|		ПО МедицинскийДокументВыполненныеУслуги.ИсточникФинансирования = СправочникИсточникиФинансирования.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.МедицинскийДокумент.ВыполненныеМЭУслуги КАК МедицинскийДокументВыполненныеМЭУслуги
	|		ПО МедицинскийДокументВыполненныеУслуги.УникальныйИдентификаторУслуги = МедицинскийДокументВыполненныеМЭУслуги.УникальныйИдентификаторУслуги
	|			И МедицинскийДокументВыполненныеУслуги.Ссылка = МедицинскийДокументВыполненныеМЭУслуги.Ссылка
	|ГДЕ
	|	МедицинскийДокументВыполненныеУслуги.Ссылка = &Ссылка
	|	И МедицинскийДокументВыполненныеУслуги.БезЗаказа
	|	И МедицинскийДокументВыполненныеМЭУслуги.Ссылка ЕСТЬ NULL
	|	И МедицинскийДокументВыполненныеУслуги.ИсходныйСтатус = ЗНАЧЕНИЕ(Перечисление.СтатусыУслуг.ПустаяСсылка)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	МедицинскийДокументВыполненныеМЭУслуги.УникальныйИдентификаторУслуги,
	|	МедицинскийДокументВыполненныеМЭУслуги.Цена,
	|	ВЫБОР
	|		КОГДА СправочникИсточникиФинансирования.ВестиСуммовойУчет
	|			ТОГДА МедицинскийДокументВыполненныеМЭУслуги.Сумма
	|		ИНАЧЕ NULL
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА СправочникИсточникиФинансирования.ВестиСуммовойУчет
	|			ТОГДА МедицинскийДокументВыполненныеМЭУслуги.СтавкаНДС
	|		ИНАЧЕ NULL
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА СправочникИсточникиФинансирования.ВестиСуммовойУчет
	|			ТОГДА МедицинскийДокументВыполненныеМЭУслуги.СуммаНДС
	|		ИНАЧЕ NULL
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА СправочникИсточникиФинансирования.ВестиСуммовойУчет
	|			ТОГДА МедицинскийДокументВыполненныеМЭУслуги.ПроцентАвтоматическойСкидки
	|		ИНАЧЕ NULL
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА СправочникИсточникиФинансирования.ВестиСуммовойУчет
	|			ТОГДА МедицинскийДокументВыполненныеМЭУслуги.СуммаАвтоматическойСкидки
	|		ИНАЧЕ NULL
	|	КОНЕЦ,
	|	МедицинскийДокументВыполненныеМЭУслуги.СуммаБонусныхБалловКСписаниюВВалюте,
	|	ВЫБОР
	|		КОГДА СправочникИсточникиФинансирования.ВестиСуммовойУчет
	|			ТОГДА МедицинскийДокументВыполненныеМЭУслуги.ПроцентРучнойСкидки
	|		ИНАЧЕ NULL
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА СправочникИсточникиФинансирования.ВестиСуммовойУчет
	|			ТОГДА МедицинскийДокументВыполненныеМЭУслуги.СуммаРучнойСкидки
	|		ИНАЧЕ NULL
	|	КОНЕЦ,
	|	МедицинскийДокументВыполненныеМЭУслуги.Ссылка,
	|	ЛОЖЬ
	|ИЗ
	|	Документ.МедицинскийДокумент.ВыполненныеМЭУслуги КАК МедицинскийДокументВыполненныеМЭУслуги
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ИсточникиФинансирования КАК СправочникИсточникиФинансирования
	|		ПО МедицинскийДокументВыполненныеМЭУслуги.ИсточникФинансирования = СправочникИсточникиФинансирования.Ссылка
	|ГДЕ
	|	МедицинскийДокументВыполненныеМЭУслуги.Ссылка = &Ссылка";
	
	Возврат ТекстЗапрос_;
КонецФункции

Функция ТекстЗапросаТаблицаВыручкаОтРеализации()
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ДанныеДокумента.НомерСтроки КАК НомерСтроки,
	|	&Период КАК Период,
	|	СтатусыУслуг.Номенклатура КАК Номенклатура,
	|	СтатусыУслуг.Заказ КАК ЗаказПациента,
	|	втАналитикаУчетаПоПартнерам.КлючАналитики КАК АналитикаУчетаПоПартнерам,
	|	НазначенныеУслугиИсполнители.Исполнитель КАК НазначившийСотрудник,
	|	ЗаказПациентаМедицинскиеУслуги.Ссылка.НаправившаяОрганизация КАК НаправившаяОрганизация,
	|	СтатусыУслуг.Соглашение КАК Соглашение,
	|	ЗаказПациентаМедицинскиеУслуги.Сумма * (ДанныеДокумента.Количество / ЗаказПациентаМедицинскиеУслуги.Количество) КАК Сумма,
	|	ДанныеДокумента.Количество КАК Количество
	|ИЗ
	|	Документ.МедицинскийДокумент.ВыполненныеУслуги КАК ДанныеДокумента
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втСтатусыУслуг КАК СтатусыУслуг
	|		ПО ДанныеДокумента.УникальныйИдентификаторУслуги = СтатусыУслуг.УникальныйИдентификаторУслуги
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ЗаказПациента.МедицинскиеУслуги КАК ЗаказПациентаМедицинскиеУслуги
	|		ПО (СтатусыУслуг.Заказ = ЗаказПациентаМедицинскиеУслуги.Ссылка)
	|			И (ЗаказПациентаМедицинскиеУслуги.Ссылка.Проведен)
	|			И (СтатусыУслуг.УникальныйИдентификаторУслуги = ЗаказПациентаМедицинскиеУслуги.УникальныйИдентификаторУслуги)
	|		ЛЕВОЕ СОЕДИНЕНИЕ втАналитикаУчетаПоПартнерам КАК втАналитикаУчетаПоПартнерам
	|		ПО ДанныеДокумента.НомерСтроки = втАналитикаУчетаПоПартнерам.НомерСтроки
	|			И втАналитикаУчетаПоПартнерам.ТипТЧ = 1
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.МедицинскийДокумент.Исполнители КАК НазначенныеУслугиИсполнители
	|		ПО (СтатусыУслуг.Назначение = НазначенныеУслугиИсполнители.Ссылка)
	|			И (НазначенныеУслугиИсполнители.Ссылка.Проведен)
	|			И (НазначенныеУслугиИсполнители.ОтветственныйЗаНазначение)
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.СпецификацииМедицинскихУслуг.СоставУслуги КАК СоставУслуги
	|		ПО ДанныеДокумента.Спецификация = СоставУслуги.Ссылка
	|			И ДанныеДокумента.КлючСтрокиСоставаУслуги = СоставУслуги.КлючСтроки
	|ГДЕ
	|	ДанныеДокумента.Ссылка = &Ссылка
	|	И ДанныеДокумента.ПричинаОтменыУслуги = ЗНАЧЕНИЕ(Справочник.ПричиныОтменыУслугЗаказаПациента.ПустаяСсылка)
	|	И НЕ ДанныеДокумента.БезЗаказа
	|	И (СоставУслуги.Ссылка ЕСТЬ NULL
	|			ИЛИ СоставУслуги.НомерСтроки = 1)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ДанныеДокумента.НомерСтроки,
	|	&Период,
	|	ДанныеДокумента.Номенклатура,
	|	NULL,
	|	втАналитикаУчетаПоПартнерам.КлючАналитики,
	|	NULL,
	|	NULL,
	|	ДанныеДокумента.Соглашение,
	|	ДанныеДокумента.Сумма,
	|	ДанныеДокумента.Количество
	|ИЗ
	|	Документ.МедицинскийДокумент.ВыполненныеУслуги КАК ДанныеДокумента
	|		ЛЕВОЕ СОЕДИНЕНИЕ втАналитикаУчетаПоПартнерам КАК втАналитикаУчетаПоПартнерам
	|		ПО ДанныеДокумента.НомерСтроки = втАналитикаУчетаПоПартнерам.НомерСтроки
	|			И втАналитикаУчетаПоПартнерам.ТипТЧ = 1
	|ГДЕ
	|	ДанныеДокумента.Ссылка = &Ссылка
	|	И ДанныеДокумента.БезЗаказа
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ДанныеДокумента.НомерСтроки,
	|	&Период,
	|	ДанныеДокумента.Номенклатура,
	|	NULL,
	|	втАналитикаУчетаПоПартнерам.КлючАналитики,
	|	NULL,
	|	NULL,
	|	ДанныеДокумента.Соглашение,
	|	ДанныеДокумента.Сумма,
	|	ДанныеДокумента.Количество
	|ИЗ
	|	Документ.МедицинскийДокумент.ВыполненныеМЭУслуги КАК ДанныеДокумента
	|		ЛЕВОЕ СОЕДИНЕНИЕ втАналитикаУчетаПоПартнерам КАК втАналитикаУчетаПоПартнерам
	|		ПО ДанныеДокумента.НомерСтроки = втАналитикаУчетаПоПартнерам.НомерСтроки
	|			И втАналитикаУчетаПоПартнерам.ТипТЧ = 2
	|ГДЕ
	|	ДанныеДокумента.Ссылка = &Ссылка";
	
	Возврат ТекстЗапроса;
	
КонецФункции // ТекстЗапросаТаблицаРасчетыСКлиентами()

Функция ТекстЗапросаТаблицаДопИнформацияКБиоматериалу()
	ТекстЗапроса_ =
		"ВЫБРАТЬ
		|	&Период КАК Период,
		|	МедицинскийДокументДопИнформацияКБиоматериалу.ВидДопМедИнформации КАК ВидДопМедИнформации,
		|	МедицинскийДокументДопИнформацияКБиоматериалу.Значение КАК Значение
		|ИЗ
		|	Документ.МедицинскийДокумент.ИнформацияКБиоматериалу КАК МедицинскийДокументДопИнформацияКБиоматериалу
		|ГДЕ
		|	МедицинскийДокументДопИнформацияКБиоматериалу.Ссылка = &Ссылка";
	
	Возврат ТекстЗапроса_;
КонецФункции

Функция ТекстЗапросаТаблицаВыбранныйБиоматериал()
	ТекстЗапроса_ =
		"ВЫБРАТЬ
		|	&Период КАК Период,
		|	МедицинскийДокументВыбранныйБиоматериал.УникальныйИдентификаторУслуги КАК УникальныйИдентификаторУслуги,
		|	МедицинскийДокументВыбранныйБиоматериал.КлючСтрокиСоставаУслуги КАК КлючСтрокиСоставаУслуги,
		|	МедицинскийДокументВыбранныйБиоматериал.ВариантВыбораБМ КАК ВариантВыбораБМ,
		|	МедицинскийДокументВыбранныйБиоматериал.Биоматериал КАК Биоматериал,
		|	МедицинскийДокументВыбранныйБиоматериал.Номенклатура КАК Номенклатура
		|ИЗ
		|	Документ.МедицинскийДокумент.ВыбранныйБиоматериал КАК МедицинскийДокументВыбранныйБиоматериал
		|ГДЕ
		|	МедицинскийДокументВыбранныйБиоматериал.Ссылка = &Ссылка";
	
	Возврат ТекстЗапроса_;
КонецФункции

Функция ТекстЗапросаТаблицаФискальныеОперацииСтроки()
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	&Период КАК Период,
	|	ВЫБОР
	|		КОГДА СправочникДоговорыКонтрагентов.ПорядокРасчетов = ЗНАЧЕНИЕ(Перечисление.ПорядокРасчетов.ПоСоглашениям)
	|			ТОГДА СтатусыУслуг.Соглашение
	|		ИНАЧЕ СтатусыУслуг.Договор
	|	КОНЕЦ КАК ОбъектПлатежа,
	|	Услуги.УникальныйИдентификаторУслуги КАК УникальныйИдентификаторУслуги,
	|	Услуги.УникальныйИдентификаторУслугиСсылка КАК УникальныйИдентификаторУслугиСсылка,
	|	ЗНАЧЕНИЕ(Перечисление.ПризнакиСпособаРасчета.Аванс) КАК ПризнакСпособаРасчета
	|ИЗ
	|	Документ.МедицинскийДокумент.ВыполненныеУслуги КАК Услуги
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СтатусыУслуг КАК СтатусыУслуг
	|		ПО Услуги.УникальныйИдентификаторУслугиСсылка = СтатусыУслуг.УникальныйИдентификаторУслугиСсылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ФискальныеОперацииСтроки.СрезПоследних(
	|				&Период,
	|				УникальныйИдентификаторУслуги В
	|					(ВЫБРАТЬ
	|						У.УникальныйИдентификаторУслуги
	|					ИЗ
	|						Документ.МедицинскийДокумент.ВыполненныеУслуги КАК У
	|					ГДЕ
	|						У.Ссылка = &Ссылка)) КАК ФискальныеОперацииСтроки
	|		ПО Услуги.УникальныйИдентификаторУслуги = ФискальныеОперацииСтроки.УникальныйИдентификаторУслуги
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ДоговорыКонтрагентов КАК СправочникДоговорыКонтрагентов
	|		ПО (СтатусыУслуг.Договор = СправочникДоговорыКонтрагентов.Ссылка)
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.СоглашенияСКлиентами КАК СправочникСоглашенияСКлиентами
	|		ПО (СтатусыУслуг.Соглашение = СправочникСоглашенияСКлиентами.Ссылка)
	|ГДЕ
	|	Услуги.Ссылка = &Ссылка
	|	И (ФискальныеОперацииСтроки.ПризнакСпособаРасчета ЕСТЬ NULL
	|			ИЛИ НЕ ФискальныеОперацииСтроки.ПризнакСпособаРасчета = ЗНАЧЕНИЕ(Перечисление.ПризнакиСпособаРасчета.ПередачаСПолнойОплатой))
	|	И НЕ СтатусыУслуг.ИсточникФинансирования В ИЕРАРХИИ (ЗНАЧЕНИЕ(Справочник.ИсточникиФинансирования.ПЛТ))
	|	И (СправочникДоговорыКонтрагентов.ВидДоговора = ЗНАЧЕНИЕ(Перечисление.ВидыДоговоров.ДепозитныйДоговор)
	|			ИЛИ СправочникСоглашенияСКлиентами.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РеализацияПоМедПрограмме))";
	Возврат ТекстЗапроса;
	
КонецФункции

// Инициализирует таблицы значений.
// Таблицы значений сохраняет в свойствах структуры "ДополнительныеСвойства".
Процедура ИнициализироватьДанныеДокумента(ДокументСсылка, СтруктураДополнительныеСвойства) Экспорт
	
	Запрос = Новый Запрос("
	|ВЫБРАТЬ
	|	ДанныеДокумента.Дата КАК Период
	|ИЗ
	|	Документ.МедицинскийДокумент КАК ДанныеДокумента
	|	
	|ГДЕ
	|	ДанныеДокумента.Ссылка = &Ссылка
	|");
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылка);
	Реквизиты = Запрос.Выполнить().Выбрать();
	Реквизиты.Следующий();
	
	ОрганизацияИзНастроек_ = НастройкиПользователей.ПолучитьНастройку("ОсновнаяОрганизация");
	
	КлючиАналитикиТЗ_ = ИнициализироватьКлючиАналитикиУчетаПоПартнерам(Реквизиты, ОрганизацияИзНастроек_, ДокументСсылка);
	
	СтационарныеУсловия_ = ОбщиеМеханизмыПовтИсп.ИспользоватьСтационарныеУсловия();

	МВТ_ = Неопределено;
	
	Запрос = Новый Запрос;
	Если НЕ СтруктураДополнительныеСвойства.Свойство("МВТСтатусыУслуг") Тогда
		СоздатьВременнуюТаблицуСтатусыУслуг(ДокументСсылка, СтруктураДополнительныеСвойства);
	КонецЕсли;
	Запрос.МенеджерВременныхТаблиц = СтруктураДополнительныеСвойства.МВТСтатусыУслуг;
	
	ТекстЗапроса = ТекстЗапросаАналитикаУчетаПоПартнерам()
				+ Символы.ПС + ";" + Символы.ПС
				+ ТекстЗапросаТаблицаЦеновыеСоглашения()
				+ Символы.ПС + ";" + Символы.ПС
				+ ТекстЗапросаТаблицаРасчетыСКлиентами()
				+ Символы.ПС + ";" + Символы.ПС
				+ ТекстЗапросаТаблицаВыручкаОтРеализации()
				+ Символы.ПС + ";" + Символы.ПС
				+ ТекстЗапросаТаблицаСтатусыУслугФинансовыеДанные()
				+ Символы.ПС + ";" + Символы.ПС
				+ ТекстЗапросаТаблицаВыбранныйБиоматериал()
				+ Символы.ПС + ";" + Символы.ПС
				+ ТекстЗапросаТаблицаДопИнформацияКБиоматериалу()
				+ Символы.ПС + ";" + Символы.ПС
				+ ТекстЗапросаТаблицаФискальныеОперацииСтроки();

	Запрос.Текст = ТекстЗапроса;
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылка);
	Запрос.УстановитьПараметр("Период", Реквизиты.Период);
	Запрос.УстановитьПараметр("ОрганизацияИзНастроек", ОрганизацияИзНастроек_);
	Запрос.УстановитьПараметр("НДС10", Перечисления.СтавкиНДС.НДС10);
	Запрос.УстановитьПараметр("НДС18", Перечисления.СтавкиНДС.НДС18);
	Запрос.УстановитьПараметр("НДС20", Перечисления.СтавкиНДС.НДС20);
	Запрос.УстановитьПараметр("НДС5", Перечисления.СтавкиНДС.НДС5);
	Запрос.УстановитьПараметр("НДС7", Перечисления.СтавкиНДС.НДС7);

	Запрос.УстановитьПараметр("КлючиАналитикиТЗ", КлючиАналитикиТЗ_);
	
	ЦеновыеСоглашения_ = ПолучитьСоглашенияУслуг(ДокументСсылка);
	Для Каждого строкаТЗ_ Из ЦеновыеСоглашения_ Цикл
		строкаТЗ_.ЦеновоеСоглашение = РаботаССоглашениями.ПолучитьЦеновоеСоглашение(строкаТЗ_.Соглашение);
	КонецЦикла;
	Запрос.УстановитьПараметр("ЦеновыеСоглашения", ЦеновыеСоглашения_);
	
	МассивРезультатов = Запрос.ВыполнитьПакет();
	
	СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаРасчетыСКлиентами", МассивРезультатов[2].Выгрузить());
	СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаВыручкаОтРеализации", МассивРезультатов[3].Выгрузить());
	СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаСтатусыУслугФинансовыеДанные", МассивРезультатов[4].Выгрузить());
	СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаВыбранныйБиоматериал", МассивРезультатов[5].Выгрузить());
	СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаИнформацияКБиоматериалу", МассивРезультатов[6].Выгрузить());
	СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаФискальныеОперацииСтроки", МассивРезультатов[7].Выгрузить());

КонецПроцедуры // ИнициализироватьДанныеДокумента()

Функция ПолучитьСоглашенияУслуг(МДСсылка)
	Запрос_ = Новый Запрос;
	Запрос_.Текст =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	СтатусыУслуг.Соглашение,
	|	СтатусыУслуг.Соглашение КАК ЦеновоеСоглашение
	|ИЗ
	|	РегистрСведений.СтатусыУслуг КАК СтатусыУслуг
	|ГДЕ
	|	СтатусыУслуг.УникальныйИдентификаторУслуги В
	|			(ВЫБРАТЬ
	|				Документ.МедицинскийДокумент.НазначенныеУслуги.УникальныйИдентификаторУслуги
	|			ИЗ
	|				Документ.МедицинскийДокумент.НазначенныеУслуги
	|			ГДЕ
	|				Документ.МедицинскийДокумент.НазначенныеУслуги.Ссылка = &Ссылка
	|			ОБЪЕДИНИТЬ ВСЕ
	|			ВЫБРАТЬ
	|				Документ.МедицинскийДокумент.ВыполненныеУслуги.УникальныйИдентификаторУслуги
	|			ИЗ
	|				Документ.МедицинскийДокумент.ВыполненныеУслуги
	|			ГДЕ
	|				Документ.МедицинскийДокумент.ВыполненныеУслуги.Ссылка = &Ссылка)";
	Запрос_.УстановитьПараметр("Ссылка", МДСсылка);
	Возврат Запрос_.Выполнить().Выгрузить();
КонецФункции

// Процедура создает ключи аналитики учета по партнерам.
//
// Параметры:
//	Реквизиты - Структура - Реквизиты шапки документа.
//	ДокументСсылка - Текущий документ
//
Функция ИнициализироватьКлючиАналитикиУчетаПоПартнерам(Реквизиты, ОрганизацияИзНастроек, ДокументСсылка)

	Запрос = Новый Запрос(
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ВЫБОР
	|		КОГДА ДанныеДокумента.Ссылка.Организация = ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)
	|			ТОГДА &ОрганизацияИзНастроек
	|		ИНАЧЕ ДанныеДокумента.Ссылка.Организация
	|	КОНЕЦ КАК Организация,
	|	ВЫБОР
	|		КОГДА СтатусыУслуг.Соглашение.Партнер <> ЗНАЧЕНИЕ(Справочник.Партнеры.ПустаяСсылка)
	|			ТОГДА СтатусыУслуг.Соглашение.Партнер
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.Партнеры.ФизЛицо)
	|	КОНЕЦ КАК Партнер,
	|	ДанныеДокумента.Ссылка.Пациент КАК Контрагент,
	|	ДанныеДокумента.НомерСтроки,
	|	1 КАК ТипТЧ
	|ИЗ
	|	Документ.МедицинскийДокумент.ВыполненныеУслуги КАК ДанныеДокумента
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.СтатусыУслуг КАК СтатусыУслуг
	|		ПО ДанныеДокумента.УникальныйИдентификаторУслуги = СтатусыУслуг.УникальныйИдентификаторУслуги
	|ГДЕ
	|	ДанныеДокумента.Ссылка = &Ссылка
	|
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ВЫБОР
	|		КОГДА ДокументМедицинскийДокумент.Организация = ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)
	|			ТОГДА &ОрганизацияИзНастроек
	|		ИНАЧЕ ДокументМедицинскийДокумент.Организация
	|	КОНЕЦ КАК Организация,
	|	ВЫБОР
	|		КОГДА СправочникСоглашенияСКлиентами.Партнер <> ЗНАЧЕНИЕ(Справочник.Партнеры.ПустаяСсылка)
	|			ТОГДА СправочникСоглашенияСКлиентами.Партнер
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.Партнеры.ФизЛицо)
	|	КОНЕЦ КАК Партнер,
	|	ВЫБОР
	|		КОГДА СправочникСоглашенияСКлиентами.Контрагент <> ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка)
	|			ТОГДА СправочникСоглашенияСКлиентами.Контрагент
	|		ИНАЧЕ ДокументМедицинскийДокумент.Пациент
	|	КОНЕЦ,
	|	ДанныеДокумента.НомерСтроки,
	|	1 КАК ТипТЧ
	|ИЗ
	|	Документ.МедицинскийДокумент.ВыполненныеУслуги КАК ДанныеДокумента
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.МедицинскийДокумент КАК ДокументМедицинскийДокумент
	|		ПО ДанныеДокумента.Ссылка = ДокументМедицинскийДокумент.Ссылка
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.СоглашенияСКлиентами КАК СправочникСоглашенияСКлиентами
	|		ПО ДанныеДокумента.Соглашение = СправочникСоглашенияСКлиентами.Ссылка
	|ГДЕ
	|	ДанныеДокумента.Ссылка = &Ссылка
	|
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ВЫБОР
	|		КОГДА ДокументМедицинскийДокумент.Организация = ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)
	|			ТОГДА &ОрганизацияИзНастроек
	|		ИНАЧЕ ДокументМедицинскийДокумент.Организация
	|	КОНЕЦ КАК Организация,
	|	ВЫБОР
	|		КОГДА СправочникСоглашенияСКлиентами.Партнер <> ЗНАЧЕНИЕ(Справочник.Партнеры.ПустаяСсылка)
	|			ТОГДА СправочникСоглашенияСКлиентами.Партнер
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.Партнеры.ФизЛицо)
	|	КОНЕЦ КАК Партнер,
	|	ВЫБОР
	|		КОГДА СправочникСоглашенияСКлиентами.Контрагент <> ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка)
	|			ТОГДА СправочникСоглашенияСКлиентами.Контрагент
	|		ИНАЧЕ ДокументМедицинскийДокумент.Пациент
	|	КОНЕЦ,
	|	ДанныеДокумента.НомерСтроки,
	|	2 КАК ТипТЧ
	|ИЗ
	|	Документ.МедицинскийДокумент.ВыполненныеМЭУслуги КАК ДанныеДокумента
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.МедицинскийДокумент КАК ДокументМедицинскийДокумент
	|		ПО ДанныеДокумента.Ссылка = ДокументМедицинскийДокумент.Ссылка
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.СоглашенияСКлиентами КАК СправочникСоглашенияСКлиентами
	|		ПО ДанныеДокумента.Соглашение = СправочникСоглашенияСКлиентами.Ссылка
	|ГДЕ
	|	ДанныеДокумента.Ссылка = &Ссылка
	|
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	СправочникДоговорыКонтрагентов.Организация КАК Организация,
	|	СправочникДоговорыКонтрагентов.Партнер КАК Партнер,
	|	СправочникДоговорыКонтрагентов.Контрагент КАК Контрагент,
	|	ДанныеДокумента.НомерСтроки,
	|	3 КАК ТипТЧ
	|ИЗ
	|	Документ.МедицинскийДокумент.НазначенныеУслуги КАК ДанныеДокумента
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.СтатусыУслуг КАК СтатусыУслуг
	|		ПО ДанныеДокумента.УникальныйИдентификаторУслуги = СтатусыУслуг.УникальныйИдентификаторУслуги
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ДоговорыКонтрагентов КАК СправочникДоговорыКонтрагентов
	|		ПО СтатусыУслуг.Договор = СправочникДоговорыКонтрагентов.Ссылка
	|ГДЕ
	|	ДанныеДокумента.Ссылка = &Ссылка И &СтационарныеУсловия
	|	И ДанныеДокумента.ОтказОтВмешательства = ЛОЖЬ
	|");

	Запрос.УстановитьПараметр("Ссылка", ДокументСсылка);
	Запрос.УстановитьПараметр("ОрганизацияИзНастроек", ОрганизацияИзНастроек);
	
	СтационарныеУсловия_ = ОбщиеМеханизмыПовтИсп.ИспользоватьСтационарныеУсловия();
	
	Запрос.УстановитьПараметр("СтационарныеУсловия", СтационарныеУсловия_);

	Выборка = Запрос.Выполнить().Выбрать();
	
	КлючиАналитикиТЗ_ = Новый ТаблицаЗначений;
	КлючиАналитикиТЗ_.Колонки.Добавить("НомерСтроки", Новый ОписаниеТипов("Число"));
	КлючиАналитикиТЗ_.Колонки.Добавить("Ключ", Новый ОписаниеТипов("СправочникСсылка.КлючиАналитикиУчетаПоПартнерам"));
	КлючиАналитикиТЗ_.Колонки.Добавить("ТипТЧ", Новый ОписаниеТипов("Число"));
	
	Пока Выборка.Следующий() Цикл
		КлючАналитики_ = РегистрыСведений.АналитикаУчетаПоПартнерам.ЗначениеКлючаАналитики(Выборка);
		
		строкаТЗ_ = КлючиАналитикиТЗ_.Добавить();
		строкаТЗ_.НомерСтроки = Выборка.НомерСтроки;
		строкаТЗ_.Ключ = КлючАналитики_;
		строкаТЗ_.ТипТЧ = Выборка.ТипТЧ;
	КонецЦикла;
	
	Возврат КлючиАналитикиТЗ_;

КонецФункции // ИнициализироватьКлючиАналитикиУчетаПоПартнерам()

Процедура СоздатьВременнуюТаблицуСтатусыУслуг(МассивУИДУслугИлиСсылка, ДополнительныеСвойства) Экспорт
	МВТ_ = Новый МенеджерВременныхТаблиц;
	// Поместим во временную таблицу содержимое регистра СтатусыУслуг, т.к. нам понадобятся её данные до проведения.
	Запрос_ = Новый Запрос;
	Запрос_.МенеджерВременныхТаблиц = МВТ_;
	ТекстЗапроса_ =
	"ВЫБРАТЬ
	|	СтатусыУслуг.УникальныйИдентификаторУслуги,
	|	СтатусыУслуг.ИсточникФинансирования,
	|	СтатусыУслуг.Соглашение,
	|	СтатусыУслуг.Договор,
	|	СтатусыУслуг.Номенклатура,
	|	СтатусыУслуг.Заказ,
	|	СтатусыУслуг.Назначение,
	|	СтатусыУслуг.Количество
	|ПОМЕСТИТЬ втСтатусыУслуг
	|ИЗ
	|		РегистрСведений.СтатусыУслуг КАК СтатусыУслуг
	|ГДЕ
	|	СтатусыУслуг.УникальныйИдентификаторУслуги В (&МассивУИДУслугИлиСсылка)";
	
	Если ТипЗнч(МассивУИДУслугИлиСсылка) <> Тип("Массив") Тогда
		ТекстЗапроса_ = стрЗаменить(ТекстЗапроса_, "&МассивУИДУслугИлиСсылка", "ВЫБРАТЬ МД.УникальныйИдентификаторУслуги ИЗ Документ.МедицинскийДокумент.ВыполненныеУслуги КАК МД ГДЕ МД.Ссылка = &МассивУИДУслугИлиСсылка");
	КонецЕсли;
	
	Запрос_.Текст = ТекстЗапроса_;
	Запрос_.УстановитьПараметр("МассивУИДУслугИлиСсылка", МассивУИДУслугИлиСсылка);
	Запрос_.Выполнить();
	
	ДополнительныеСвойства.Вставить("МВТСтатусыУслуг", МВТ_);
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// Печать рецептов формы 148-1/у-88, 107-1/у, 148-1/у-04 (л)

////
 // Функция: ПолучитьНоменклатуруДляПечатиРецептов
 //   Анализирует входящую номенклатуру: если среди нее есть наркотики, психотропные списка 3 
 //   или безрецептурные, то выводит сообщение и не включает ее в исходящий список.
 //
 // Параметры:
 //   ВходящиеНазначения {Массив}
 //     Список уид назначений, которые нужно проверить.
 //
 // Возврат: {Массив}
 //   Список уид номенклатуры, которая удовлетворяет всем условиям.
 ///
Функция ПолучитьНоменклатуруДляПечатиРецептов(ВходящиеНазначения)
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	СправочникНоменклатура.Ссылка КАК НоменклатураДоступная,
		|	ВТТ_Исходная.УникальныйИдентификаторНазначения КАК УникальныйИдентификаторНазначения,
		|	ВТТ_Исходная.Номенклатура КАК Номенклатура,
		|	ВТТ_Исходная.ТорговоеНаименование КАК ТорговоеНаименование,
		|	ВТТ_Исходная.ФормаВыпуска КАК ФормаВыпуска,
		|	ВЫБОР
		|		КОГДА ПринадлежностьЛекарственныхСредствКСпискам.Наркотические = ЗНАЧЕНИЕ(Перечисление.СпискиНаркотическихВеществ.Список3)
		|			ТОГДА ИСТИНА
		|		ИНАЧЕ ЛОЖЬ
		|	КОНЕЦ КАК ПсихотропныеСписка3,
		|	ВЫБОР
		|		КОГДА НЕ ПринадлежностьЛекарственныхСредствКСпискам.Наркотические = ЗНАЧЕНИЕ(Перечисление.СпискиНаркотическихВеществ.ПустаяСсылка)
		|			ТОГДА ИСТИНА
		|		ИНАЧЕ ЛОЖЬ
		|	КОНЕЦ КАК Наркотические,
		|	ВЫБОР
		|		КОГДА ПринадлежностьЛекарственныхСредствКСпискам.БезрецептурныйОтпуск = ИСТИНА
		|			ТОГДА ИСТИНА
		|		ИНАЧЕ ЛОЖЬ
		|	КОНЕЦ КАК БезрецептурныйОтпуск
		|ИЗ
		|	Справочник.Номенклатура КАК СправочникНоменклатура
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
		|			Исходная.УникальныйИдентификаторНазначения КАК УникальныйИдентификаторНазначения,
		|			Исходная.Номенклатура КАК Номенклатура,
		|			Исходная.ТорговоеНаименование КАК ТорговоеНаименование,
		|			Исходная.ДействующиеВеществаМНН КАК ДействующиеВеществаМНН,
		|			Исходная.ФормаВыпуска КАК ФормаВыпуска,
		|			ВЫБОР
		|				КОГДА НЕ Исходная.Номенклатура = ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)
		|					ТОГДА ИСТИНА
		|				ИНАЧЕ ЛОЖЬ
		|			КОНЕЦ КАК ЕстьНоменклатура,
		|			ВЫБОР
		|				КОГДА НЕ Исходная.ТорговоеНаименование = ЗНАЧЕНИЕ(Справочник.ТорговыеНаименования.ПустаяСсылка)
		|					ТОГДА ИСТИНА
		|				ИНАЧЕ ЛОЖЬ
		|			КОНЕЦ КАК ЕстьТорговоеНаименование,
		|			ВЫБОР
		|				КОГДА НЕ Исходная.ДействующиеВеществаМНН = ЗНАЧЕНИЕ(Справочник.ДействующиеВеществаМНН.ПустаяСсылка)
		|					ТОГДА ИСТИНА
		|				ИНАЧЕ ЛОЖЬ
		|			КОНЕЦ КАК ЕстьДействующиеВеществаМНН,
		|			ВЫБОР
		|				КОГДА НЕ Исходная.ФормаВыпуска = ЗНАЧЕНИЕ(Справочник.ФормыВыпуска.ПустаяСсылка)
		|					ТОГДА ИСТИНА
		|				ИНАЧЕ ЛОЖЬ
		|			КОНЕЦ КАК ЕстьФормаВыпуска
		|		ИЗ
		|			Документ.МедицинскийДокумент.ЛекарственныеНазначения КАК Исходная
		|		ГДЕ
		|			&__УсловиеНаУид__
		|			И (НЕ Исходная.Номенклатура = ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)
		|					ИЛИ НЕ Исходная.ТорговоеНаименование = ЗНАЧЕНИЕ(Справочник.ТорговыеНаименования.ПустаяСсылка)
		|					ИЛИ НЕ Исходная.ДействующиеВеществаМНН = ЗНАЧЕНИЕ(Справочник.ДействующиеВеществаМНН.ПустаяСсылка))) КАК ВТТ_Исходная
		|		ПО (ВЫБОР
		|				КОГДА ВТТ_Исходная.ЕстьНоменклатура
		|					ТОГДА СправочникНоменклатура.Ссылка = ВТТ_Исходная.Номенклатура
		|				КОГДА ВТТ_Исходная.ЕстьТорговоеНаименование
		|					ТОГДА СправочникНоменклатура.ТорговоеНаименование = ВТТ_Исходная.ТорговоеНаименование
		|							И ВЫБОР
		|								КОГДА ВТТ_Исходная.ЕстьФормаВыпуска
		|									ТОГДА СправочникНоменклатура.ФормаВыпуска = ВТТ_Исходная.ФормаВыпуска
		|								ИНАЧЕ ИСТИНА
		|							КОНЕЦ
		|			КОНЕЦ)
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПринадлежностьЛекарственныхСредствКСпискам КАК ПринадлежностьЛекарственныхСредствКСпискам
		|		ПО СправочникНоменклатура.Ссылка = ПринадлежностьЛекарственныхСредствКСпискам.Номенклатура"
	;
	УсловиеНаУид = УправлениеЗаказамиУслугами.СформироватьУсловиеДляСпискаУид(
							ВходящиеНазначения, 
							"Исходная", 
							"УникальныйИдентификаторНазначения", 
							Запрос
						);
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "&__УсловиеНаУид__", "(" + УсловиеНаУид + ")");
	Выборка = Запрос.Выполнить().Выбрать();
	
	НаркотическиеСтр = "";
	БезрецептурныеСтр = "";
	ПсихотропныеСписка3Стр = "";
	
	ИсходящаяНоменклатура = Новый Массив;
	
	Пока Выборка.Следующий() Цикл
		Если ЗначениеЗаполнено(Выборка.Номенклатура) Тогда
			ПредставлениеИсходнойСтроки = "" + Выборка.Номенклатура;
		ИначеЕсли ЗначениеЗаполнено(Выборка.ТорговоеНаименование) Тогда
			ПредставлениеИсходнойСтроки = "" + Выборка.ТорговоеНаименование
				+ ?(ЗначениеЗаполнено(Выборка.ФормаВыпуска), ", " + Выборка.ФормаВыпуска, "");
		Иначе
			ПредставлениеИсходнойСтроки = ""
				+ ?(ЗначениеЗаполнено(Выборка.ФормаВыпуска), ", " + Выборка.ФормаВыпуска, "");
		КонецЕсли;
		Если Выборка.БезрецептурныйОтпуск Тогда
			БезрецептурныеСтр = БезрецептурныеСтр 
				+ ?(СтрДлина(БезрецептурныеСтр) > 0, "; ", "") + ПредставлениеИсходнойСтроки;
		ИначеЕсли Выборка.Наркотические Тогда
			НаркотическиеСтр = НаркотическиеСтр 
				+ ?(СтрДлина(НаркотическиеСтр) > 0, "; ", "") + ПредставлениеИсходнойСтроки;
		ИначеЕсли Выборка.ПсихотропныеСписка3 Тогда
			ПсихотропныеСписка3Стр = ПсихотропныеСписка3Стр 
				+ ?(СтрДлина(ПсихотропныеСписка3Стр) > 0, "; ", "") + ПредставлениеИсходнойСтроки;
		Иначе
			ИсходящаяНоменклатура.Добавить(Выборка.УникальныйИдентификаторНазначения);
		КонецЕсли;
	КонецЦикла;
	
	Если СтрДлина(БезрецептурныеСтр) > 0 Тогда
		СообщенияПользователю.Показать(
			"МедицинскийДокумент_Рецепты_НевозможнаПечатьРецептовДляБезрецептурныхЛС",
			Новый Структура(
				"БезрецептурныеСтр",
				БезрецептурныеСтр
			)
		);
	КонецЕсли;
	Если СтрДлина(НаркотическиеСтр) > 0 Тогда
		СообщенияПользователю.Показать(
			"МедицинскийДокумент_Рецепты_НевозможнаПечатьРецептовДляНаркотическихЛС",
			Новый Структура(
				"НаркотическиеСтр",
				НаркотическиеСтр
			)
		);
	КонецЕсли;
	Если СтрДлина(ПсихотропныеСписка3Стр) > 0 Тогда
		СообщенияПользователю.Показать(
			"МедицинскийДокумент_Рецепты_НевозможнаПечатьРецептовДляПсихотропныхЛССписка3",
			Новый Структура(
				"ПсихотропныеСписка3Стр",
				ПсихотропныеСписка3Стр
			)
		);
	КонецЕсли;
	
	Возврат ИсходящаяНоменклатура;
	
КонецФункции

////
 // Функция: ПолучитьСМННДляПечатиРецептов
 //   Анализирует входящую номенклатуру: если среди нее есть наркотики, психотропные списка 3 
 //   или безрецептурные, то выводит сообщение и не включает ее в исходящий список.
 //
 // Параметры:
 //   ВходящиеНазначения {Массив}
 //     Список уид назначений, которые нужно проверить.
 //   КоллекцияПечатныхФорм {ТаблицаЗначений}
 //     Формы рецепта.
 //
 // Возврат: {Массив}
 //   Список уид СМНН, которые удовлетворяет всем условиям.
 ///
Функция ПолучитьСМННДляПечатиРецептов(ВходящиеНазначения, КоллекцияПечатныхФорм)
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	СправочникСМНН.Ссылка КАК Ссылка,
		|	ВТТ_Исходная.УникальныйИдентификаторНазначения КАК УникальныйИдентификаторНазначения,
		|	ВЫБОР
		|		КОГДА СправочникСМНН.Наркотические = ЗНАЧЕНИЕ(Перечисление.СпискиНаркотическихВеществ.Список3)
		|			ТОГДА ИСТИНА
		|		ИНАЧЕ ЛОЖЬ
		|	КОНЕЦ КАК ПсихотропныеСписка3,
		|	ВЫБОР
		|		КОГДА НЕ СправочникСМНН.Наркотические = ЗНАЧЕНИЕ(Перечисление.СпискиНаркотическихВеществ.ПустаяСсылка)
		|			ТОГДА ИСТИНА
		|		ИНАЧЕ ЛОЖЬ
		|	КОНЕЦ КАК Наркотические,
		|	ВЫБОР
		|		КОГДА НЕ СправочникСМНН.СильнодействующиеИЯды = ЗНАЧЕНИЕ(Перечисление.СпискиСильнодействующихИЯдовитыхВеществ.ПустаяСсылка)
		|			ТОГДА ИСТИНА
		|		ИНАЧЕ ЛОЖЬ
		|	КОНЕЦ КАК СильнодействующиеИЯды,
		|	СправочникСМНН.ЖНВЛП КАК ЖНВЛП,
		|	СправочникСМНН.БезрецептурныйОтпуск КАК БезрецептурныйОтпуск
		|ИЗ
		|	Справочник.СМНН КАК СправочникСМНН
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
		|			Исходная.УникальныйИдентификаторНазначения КАК УникальныйИдентификаторНазначения,
		|			Исходная.СМНН КАК СМНН
		|		ИЗ
		|			Документ.МедицинскийДокумент.ЛекарственныеНазначения КАК Исходная
		|		ГДЕ
		|			&__УсловиеНаУид__
		|			И Исходная.СМНН <> ЗНАЧЕНИЕ(Справочник.СМНН.ПустаяСсылка)) КАК ВТТ_Исходная
		|		ПО СправочникСМНН.Ссылка = ВТТ_Исходная.СМНН"
	;
	УсловиеНаУид = УправлениеЗаказамиУслугами.СформироватьУсловиеДляСпискаУид(
							ВходящиеНазначения, 
							"Исходная", 
							"УникальныйИдентификаторНазначения", 
							Запрос
						);
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "&__УсловиеНаУид__", "(" + УсловиеНаУид + ")");
	Выборка = Запрос.Выполнить().Выбрать();
	
	БезрецептурныеСтр = "";
	НаркотическиеСтр = "";
	ПсихотропныеСписка3Стр = "";
	
	ИсходящиеСМНН = Новый Массив;
	
	Пока Выборка.Следующий() Цикл
		ПредставлениеИсходнойСтроки = "" + Выборка.Ссылка;
		Если Выборка.БезрецептурныйОтпуск
			И НЕ УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "ПФ_MXL_Форма_107_1_у")
		Тогда
			БезрецептурныеСтр = БезрецептурныеСтр 
				+ ?(СтрДлина(БезрецептурныеСтр) > 0, "; ", "") + ПредставлениеИсходнойСтроки;
		ИначеЕсли Выборка.Наркотические
			И НЕ УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "ПФ_MXL_Форма_148_1_у_88")
		Тогда
			НаркотическиеСтр = НаркотическиеСтр 
				+ ?(СтрДлина(НаркотическиеСтр) > 0, "; ", "") + ПредставлениеИсходнойСтроки;
		ИначеЕсли Выборка.ПсихотропныеСписка3
			И НЕ УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "ПФ_MXL_Форма_148_1_у_88")
		Тогда
			ПсихотропныеСписка3Стр = ПсихотропныеСписка3Стр 
				+ ?(СтрДлина(ПсихотропныеСписка3Стр) > 0, "; ", "") + ПредставлениеИсходнойСтроки;
		Иначе
			ИсходящиеСМНН.Добавить(Выборка.УникальныйИдентификаторНазначения);
		КонецЕсли;
	КонецЦикла;
	
	Если СтрДлина(БезрецептурныеСтр) > 0 Тогда
		СообщенияПользователю.Показать(
			"МедицинскийДокумент_Рецепты_НевозможнаПечатьРецептовДляБезрецептурныхЛС",
			Новый Структура(
				"БезрецептурныеСтр",
				БезрецептурныеСтр
			)
		);
	КонецЕсли;
	Если СтрДлина(НаркотическиеСтр) > 0 Тогда
		СообщенияПользователю.Показать(
			"МедицинскийДокумент_Рецепты_НевозможнаПечатьРецептовДляНаркотическихЛС",
			Новый Структура(
				"НаркотическиеСтр",
				НаркотическиеСтр
			)
		);
	КонецЕсли;
	Если СтрДлина(ПсихотропныеСписка3Стр) > 0 Тогда
		СообщенияПользователю.Показать(
			"МедицинскийДокумент_Рецепты_НевозможнаПечатьРецептовДляПсихотропныхЛССписка3",
			Новый Структура(
				"ПсихотропныеСписка3Стр",
				ПсихотропныеСписка3Стр
			)
		);
	КонецЕсли;
	
	Возврат ИсходящиеСМНН;
	
КонецФункции

////
 // Функция: ПолучитьФорму_148_1_у_88_или_107_1_у
 //   Формирует табличный документ по форме 148-1/у-88 или 107-1/у.
 //   
 // Параметры:
 //   МассивОбъектов {Массив}
 //     Массив ссылок на номенклатуры для печати.
 //   ОбъектыПечати.
 //     Объекты печати.
 //   НазваниеМакета {Строка}
 //     Название макета, который нужно напечатать.
 //   ПечатьПоТорговомуНаименованию {Булево}
 //     Печать рецепта по торговому наименованию
 //
 // Возврат: {ТабличныйДокумент}
 //   Сформированный табличный документ.
 ///
Функция ПолучитьФорму_148_1_у_88_или_107_1_у(МассивОбъектов, ОбъектыПечати, НазваниеМакета, ПечатьПоТорговомуНаименованию = Ложь)
	Макет_107_1_у = НазваниеМакета = "ПФ_MXL_Форма_107_1_у";
	Макет_148_1_у_88 = НазваниеМакета = "ПФ_MXL_Форма_148_1_у_88";
	
	ТабДок = Новый ТабличныйДокумент;
	ТабДок.КлючПараметровПечати = "ПараметрыПечати_" + НазваниеМакета;
	ТабДок.Защита = Истина;
	ТабДок.ТолькоПросмотр = Истина;
	ТабДок.ОриентацияСтраницы = ОриентацияСтраницы.Портрет;
	ТабДок.ОтображатьСетку = Ложь;
	ТабДок.ОтображатьЗаголовки = Ложь;
	ТабДок.РазмерСтраницы = "A5";
	ТабДок.АвтоМасштаб = Истина;
	
	Макет = УправлениеПечатью.МакетПечатнойФормы("ОбщийМакет." + НазваниеМакета);
	
	ТекДата_ = Новый Структура("ТекущийГод, ТекущийДень, ТекущаяДата");
	Если Не Макет_107_1_у Тогда
		ТекДата_.ТекущийГод = Год(ТекущаяДатаСеанса());
		ТекДата_.ТекущийДень = День(ТекущаяДатаСеанса());
		ТекДата_.ТекущаяДата = ТекущаяДатаСеанса();
	КонецЕсли;
		
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	МД.МедицинскаяКарта КАК МедицинскаяКарта,
		|	ИсполнителиМД.Исполнитель КАК Врач,
		|	ВЫБОР
		|		КОГДА ДанныеПациентов.Отчество <> """"
		|			ТОГДА ДанныеПациентов.Фамилия + ""."" + ПОДСТРОКА(ДанныеПациентов.Имя, 1, 1) + ""."" + ПОДСТРОКА(ДанныеПациентов.Отчество, 1, 1)
		|		ИНАЧЕ ДанныеПациентов.Фамилия + ""."" + ПОДСТРОКА(ДанныеПациентов.Имя, 1, 1)
		|	КОНЕЦ КАК ФамилияИнициалыПациента,
		|	ДанныеПациентов.ДатаРождения КАК ДатаРожденияПациента,
		|	ВЫБОР
		|		КОГДА РАЗНОСТЬДАТ(ДанныеПациентов.ДатаРождения, МД.Дата, ГОД) - ВЫБОР
		|				КОГДА ДЕНЬГОДА(ДанныеПациентов.ДатаРождения) >= ДЕНЬГОДА(МД.Дата)
		|					ТОГДА 1
		|				ИНАЧЕ 0
		|			КОНЕЦ > 17
		|			ТОГДА 1
		|		ИНАЧЕ 2
		|	КОНЕЦ КАК ТипРецепта,
		|	МД.МедицинскаяКарта.НомерКартыПредставление КАК НомерКарты,
		|	ВЫБОР
		|		КОГДА РегистрСведенийФИОФизическихЛиц.Отчество <> """"
		|			ТОГДА РегистрСведенийФИОФизическихЛиц.Фамилия + ""."" + ПОДСТРОКА(РегистрСведенийФИОФизическихЛиц.Имя, 1, 1) + ""."" + ПОДСТРОКА(РегистрСведенийФИОФизическихЛиц.Отчество, 1, 1)
		|		ИНАЧЕ РегистрСведенийФИОФизическихЛиц.Фамилия + ""."" + ПОДСТРОКА(РегистрСведенийФИОФизическихЛиц.Имя, 1, 1)
		|	КОНЕЦ КАК ФамилияИнициалыВрача,
		|	ЛекарственныеНазначенияМД.УникальныйИдентификаторНазначения КАК УникальныйИдентификаторНазначения,
		|	ЛекарственныеНазначенияМД.Номенклатура КАК Номенклатура,
		|	ЛекарственныеНазначенияМД.СМНН КАК СМНН,
		|	ЛекарственныеНазначенияМД.ТорговоеНаименование КАК ТорговоеНаименование,
		|	ЛекарственныеНазначенияМД.ТорговоеНаименование.ЛатинскаяТранскрипция КАК ТорговоеНаименованиеЛат,
		|	ЛекарственныеНазначенияМД.ДействующиеВеществаМНН КАК ДействующиеВеществаМНН,
		|	ЛекарственныеНазначенияМД.ФормаВыпуска КАК ФормаВыпуска,
		|	ЛекарственныеНазначенияМД.РазоваяДозаНазначено КАК КоличествоЕдиниц,
		|	ЛекарственныеНазначенияМД.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
		|	ЛекарственныеНазначенияМД.СпособПрименения КАК СпособПрименения,
		|	ЛекарственныеНазначенияМД.ГрафикНазначения КАК ГрафикНазначения,
		|	ЛекарственныеНазначенияМД.СуточнаяДозаНазначения КАК Дозировка,
		|	ЛекарственныеНазначенияМД.Инструкция КАК Инструкция,
		|	РАЗНОСТЬДАТ(ЛекарственныеНазначенияМД.ДатаНачалаНазначения, ЛекарственныеНазначенияМД.ДатаОкончанияНазначения, ДЕНЬ) + 1 КАК ОбщееКоличествоДоз,
		|	МД.ДатаМД КАК ДатаМедицинскогоДокумента
		|ИЗ
		|	Документ.МедицинскийДокумент КАК МД
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ДанныеПациентов.СрезПоследних КАК ДанныеПациентов
		|		ПО (ДанныеПациентов.Пациент = МД.Пациент)
		|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.МедицинскийДокумент.ЛекарственныеНазначения КАК ЛекарственныеНазначенияМД
		|		ПО МД.Ссылка = ЛекарственныеНазначенияМД.Ссылка
		|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.МедицинскийДокумент.Исполнители КАК ИсполнителиМД
		|		ПО (ИсполнителиМД.Ссылка = МД.Ссылка)
		|			И (ИсполнителиМД.ОтветственныйЗаНазначение)
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ФИОФизическихЛиц.СрезПоследних КАК РегистрСведенийФИОФизическихЛиц
		|		ПО (ИсполнителиМД.Исполнитель.ФизЛицо = РегистрСведенийФИОФизическихЛиц.ФизическоеЛицо)
		|ГДЕ
		|	&__УсловиеНаУид__"
	;
	УсловиеНаУид = УправлениеЗаказамиУслугами.СформироватьУсловиеДляСпискаУид(
							МассивОбъектов, 
							"ЛекарственныеНазначенияМД", 
							"УникальныйИдентификаторНазначения", 
							Запрос
						);
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "&__УсловиеНаУид__", "(" + УсловиеНаУид + ")");
	
	ЛекНазначенияТЗ_ = Запрос.Выполнить().Выгрузить();
	
	НомерСтроки_ = 0;
	Пока НомерСтроки_ < ЛекНазначенияТЗ_.Количество() Цикл
		
		строкаТЗ_ = ЛекНазначенияТЗ_[НомерСтроки_];
		
		НомерСтрокиНачало = ТабДок.ВысотаТаблицы + 1;
			
		Макет.Параметры.Заполнить(строкаТЗ_);
		ВыделитьЯчейки("ТипРецепта", строкаТЗ_.ТипРецепта, Макет);
		
		Rp = ПодготовитьЗначениеПараметраRpДляМакета(строкаТЗ_, ПечатьПоТорговомуНаименованию);
		
		Макет.Параметры.Заполнить(Новый Структура("Rp,Rp2,Rp3", Rp));

		// Один рецепт (форма 107-1/у) - для трех наименований,
		// поэтому еще два наименования нужно добавить в этот же макет.
		Если Макет_107_1_у = Истина Тогда
			
			ТекущийВрач = строкаТЗ_.Врач;
			ТекущаяМедицинскаяКарта = строкаТЗ_.МедицинскаяКарта;
			
			Для Номер = 2 По 3 Цикл
				Если ЛекНазначенияТЗ_.Количество() > НомерСтроки_ + 1
					И ЛекНазначенияТЗ_[НомерСтроки_ + 1].МедицинскаяКарта = ТекущаяМедицинскаяКарта 
					И ЛекНазначенияТЗ_[НомерСтроки_ + 1].Врач = ТекущийВрач 
				Тогда
				
					НомерСтроки_ = НомерСтроки_ + 1;
					
					строкаТЗ_ = ЛекНазначенияТЗ_[НомерСтроки_];
					
					Rp = ПодготовитьЗначениеПараметраRpДляМакета(строкаТЗ_);
					
					Макет.Параметры.Заполнить(Новый Структура("Rp" + Номер, Rp));
				Иначе
					Прервать;
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
		
		Если Не Макет_107_1_у Тогда
			Макет.Параметры.Заполнить(ТекДата_);
		Иначе
			ДатаМедКарты_ = строкаТЗ_.ДатаМедицинскогоДокумента;
			ТекДата_.ТекущийГод = Год(ДатаМедКарты_);
			ТекДата_.ТекущийДень = День(ДатаМедКарты_);
			СтрокаДаты_ = Формат(ДатаМедКарты_, "ДЛФ=ДД");
			СтрокаМесяца_ = СокрЛ(Сред(СтрокаДаты_, Найти(СтрокаДаты_, " ")));
			ТекДата_.ТекущаяДата = Лев(СтрокаМесяца_, Найти(СтрокаМесяца_, " ")-1);
			Макет.Параметры.Заполнить(ТекДата_);
		КонецЕсли;
			
		ТабДок.Вывести(Макет);
		ТабДок.ВывестиГоризонтальныйРазделительСтраниц();
		УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ТабДок, НомерСтрокиНачало, ОбъектыПечати, ВыбратьЗаполненное(строкаТЗ_.СМНН, строкаТЗ_.Номенклатура));
		
		НомерСтроки_ = НомерСтроки_ + 1;
		
	КонецЦикла;
	
	Возврат ТабДок;
	
КонецФункции

Функция ПодготовитьЗначениеПараметраRpДляМакета(строкаТЗ, ПечатьПоТорговомуНаименованию = Ложь) Экспорт
	НаименованиеПотребительскойЕдиницыИзмерения_ = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(строкаТЗ.СМНН, "НаименованиеПотребительскойЕдиницыИзмерения");
	Rp = "Tab: " + ?(ПечатьПоТорговомуНаименованию,
					строкаТЗ.ТорговоеНаименование,
					?(
						ЗначениеЗаполнено(строкаТЗ.СМНН),
						строкаТЗ.СМНН,
						?(ЗначениеЗаполнено(строкаТЗ.ТорговоеНаименованиеЛат), СокрЛП(строкаТЗ.ТорговоеНаименованиеЛат), "")
					)
				);
	Rp = ?(
				ЗначениеЗаполнено(Rp),
				Rp + " " + ?(ЗначениеЗаполнено(строкаТЗ.ФормаВыпуска), Символы.ПС + " " + строкаТЗ.ФормаВыпуска, ""),
				""
			);
	Signa = "S: " + ?(
					ЗначениеЗаполнено(строкаТЗ.КоличествоЕдиниц),
					Строка(строкаТЗ.КоличествоЕдиниц)
					+ ?(
						ЗначениеЗаполнено(НаименованиеПотребительскойЕдиницыИзмерения_),
						" "+ НаименованиеПотребительскойЕдиницыИзмерения_,
						""
					),
					""
				)
				+ ?(ЗначениеЗаполнено(строкаТЗ.СпособПрименения), " " + строкаТЗ.СпособПрименения, "")
				+ ?(ЗначениеЗаполнено(строкаТЗ.ГрафикНазначения), " " + строкаТЗ.ГрафикНазначения, "");
	Signa = ?(
				ЗначениеЗаполнено(Signa),
				Signa + ?(
					ЗначениеЗаполнено(строкаТЗ.ОбщееКоличествоДоз) И ЗначениеЗаполнено(строкаТЗ.Дозировка),
					", Общее кол-во: " + (строкаТЗ.ОбщееКоличествоДоз*строкаТЗ.Дозировка),
					""
				),
				""
			);
	
	Rp = Rp + ?(ЗначениеЗаполнено(Signa), Символы.ПС + Signa, "");;
	
	Возврат Rp;
КонецФункции

////
 // Процедура: ВыделитьЯчейки
 //   Задает шрифт (жирный, подчеркнутый) для ячеек.
 //
 // Параметры:
 //   ОбщееИмяЯчеек {Строка}
 //     Начальная часть имени ячейки, для которой нужно изменить шрифт.
 //   НомерЯчейки {Число, Булево}
 //     Если передано число, конечная часть имени ячейки, для которой нужно изменить шрифт.	
 //   Макет.
 //     Макет, в котором находится ячейка.
 ///
Процедура ВыделитьЯчейки(ОбщееИмяЯчеек, НомерЯчейки, Макет) Экспорт
	Если Не ЗначениеЗаполнено(НомерЯчейки) Тогда
		Возврат;
	КонецЕсли;

	ИмяЯчейки = ОбщееИмяЯчеек + НомерЯчейки;
	ОбластьЯчеек = Макет.Область(ИмяЯчейки);
	ОбластьЯчеек.Шрифт = Новый Шрифт(ОбластьЯчеек.Шрифт, , 9, Истина, , Истина);

КонецПроцедуры

////
 // Функция: ПолучитьФорму_148_1_у_04_л
 //   Формирует табличный документ по форме 148-1/у-04 (л).
 //   
 // Параметры:
 //   МассивОбъектов {Массив}
 //     Массив ссылок на номенклатуры для печати.
 //   ОбъектыПечати.
 //     Объекты печати.
 //   НазваниеМакета {Строка}
 //     Название макета, который нужно напечатать.
  //   ПечатьПоТорговомуНаименованию {Булево}
 //     Печать рецепта по торговому наименованию
 //
 // Возврат: {ТабличныйДокумент}
 //   Сформированный табличный документ.
 ///
Функция ПолучитьФорму_148_1_у_04_л(МассивОбъектов, ОбъектыПечати, НазваниеМакета, ПечатьПоТорговомуНаименованию)
	
	ТабДок = Новый ТабличныйДокумент;
	ТабДок.КлючПараметровПечати = "ПараметрыПечати_" + НазваниеМакета;
	ТабДок.Защита = Истина;
	ТабДок.ТолькоПросмотр = Истина;
	ТабДок.ОриентацияСтраницы = ОриентацияСтраницы.Портрет;
	ТабДок.ОтображатьСетку = Ложь;
	ТабДок.ОтображатьЗаголовки = Ложь;
	ТабДок.РазмерСтраницы = "A5";
	ТабДок.АвтоМасштаб = Истина;
	
	Макет = УправлениеПечатью.МакетПечатнойФормы("ОбщийМакет." + НазваниеМакета);
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	&ДатаСреза КАК ДатаВыписки,
		|	ДанныеПациентов.Пациент КАК Пациент,
		|	ВЫБОР
		|		КОГДА ДанныеПациентов.Отчество <> """"
		|			ТОГДА ДанныеПациентов.Фамилия + ""."" + ПОДСТРОКА(ДанныеПациентов.Имя, 1, 1) + ""."" + ПОДСТРОКА(ДанныеПациентов.Отчество, 1, 1)
		|		ИНАЧЕ ДанныеПациентов.Фамилия + ""."" + ПОДСТРОКА(ДанныеПациентов.Имя, 1, 1)
		|	КОНЕЦ КАК ФамилияИнициалыПациента,
		|	ДанныеПациентов.ДатаРождения КАК ДатаРождения,
		|	ДанныеПациентов.Пациент.СтраховойНомерПФР КАК СтраховойНомерПФР,
		|	МД.МедицинскаяКарта.НомерКартыПредставление КАК НомерКарты,
		|	ВЫБОР
		|		КОГДА РегистрСведенийФИОФизическихЛиц.Отчество <> """"
		|			ТОГДА РегистрСведенийФИОФизическихЛиц.Фамилия + ""."" + ПОДСТРОКА(РегистрСведенийФИОФизическихЛиц.Имя, 1, 1) + ""."" + ПОДСТРОКА(РегистрСведенийФИОФизическихЛиц.Отчество, 1, 1)
		|		ИНАЧЕ РегистрСведенийФИОФизическихЛиц.Фамилия + ""."" + ПОДСТРОКА(РегистрСведенийФИОФизическихЛиц.Имя, 1, 1)
		|	КОНЕЦ КАК ФамилияИнициалыВрача,
		|	ДиагнозыМД.МКБ10.Код КАК МКБ10,
		|	ПолисыОМСПациентов.Номер КАК НомерПолиса,
		|	ЛекарственныеНазначенияМД.УникальныйИдентификаторНазначения КАК УникальныйИдентификаторНазначения,
		|	ЛекарственныеНазначенияМД.Номенклатура КАК Номенклатура,
		|	ЛекарственныеНазначенияМД.СМНН КАК СМНН,
		|	ЛекарственныеНазначенияМД.ТорговоеНаименование КАК ТорговоеНаименование,
		|	ЛекарственныеНазначенияМД.ТорговоеНаименование.ЛатинскаяТранскрипция КАК ТорговоеНаименованиеЛат,
		|	ЛекарственныеНазначенияМД.ДействующиеВеществаМНН КАК ДействующиеВеществаМНН,
		|	ЛекарственныеНазначенияМД.ФормаВыпуска КАК ФормаВыпуска,
		|	ЛекарственныеНазначенияМД.РазоваяДозаНазначено КАК КоличествоЕдиниц,
		|	ЛекарственныеНазначенияМД.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
		|	ЛекарственныеНазначенияМД.СпособПрименения КАК СпособПрименения,
		|	ЛекарственныеНазначенияМД.ГрафикНазначения КАК ГрафикНазначения,
		|	ЛекарственныеНазначенияМД.СуточнаяДозаНазначения КАК Дозировка,
		|	ЛекарственныеНазначенияМД.Инструкция КАК Инструкция,
		|	РАЗНОСТЬДАТ(ЛекарственныеНазначенияМД.ДатаНачалаНазначения, ЛекарственныеНазначенияМД.ДатаОкончанияНазначения, ДЕНЬ) + 1 КАК ОбщееКоличествоДоз
		|ИЗ
		|	Документ.МедицинскийДокумент КАК МД
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.МедицинскийДокумент.ЛекарственныеНазначения КАК ЛекарственныеНазначенияМД
		|		ПО МД.Ссылка = ЛекарственныеНазначенияМД.Ссылка
		|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.МедицинскийДокумент.Исполнители КАК ИсполнителиМД
		|		ПО (ИсполнителиМД.Ссылка = МД.Ссылка)
		|			И (ИсполнителиМД.ОтветственныйЗаНазначение)
		|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.МедицинскийДокумент.ДиагнозыПоМКБ10 КАК ДиагнозыМД
		|		ПО (ДиагнозыМД.Ссылка = МД.Ссылка)
		|			И (ДиагнозыМД.ТипДиагноза = ЗНАЧЕНИЕ(Перечисление.ТипыДиагнозовПациента.ОсновноеЗаболевание))
		|			И (ДиагнозыМД.Действует)
		|			И (МД.Ссылка.Проведен)
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ДанныеПациентов.СрезПоследних КАК ДанныеПациентов
		|		ПО (ДанныеПациентов.Пациент = МД.Пациент)
		|		ЛЕВОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
		|			МедицинскиеСтраховыеПолисы.Владелец КАК Пациент,
		|			МедицинскиеСтраховыеПолисы.Номер КАК Номер
		|		ИЗ
		|			Справочник.МедицинскиеСтраховыеПолисы КАК МедицинскиеСтраховыеПолисы
		|		ГДЕ
		|			МедицинскиеСтраховыеПолисы.ВАрхиве = ЛОЖЬ
		|			И МедицинскиеСтраховыеПолисы.ПометкаУдаления = ЛОЖЬ
		|			И МедицинскиеСтраховыеПолисы.ДатаВыдачи <= &ДатаСреза
		|			И (&ДатаСреза <= МедицинскиеСтраховыеПолисы.СрокДействия
		|					ИЛИ МедицинскиеСтраховыеПолисы.СрокДействия = ДАТАВРЕМЯ(1, 1, 1))
		|			И МедицинскиеСтраховыеПолисы.Вид.ИсточникФинансирования = ЗНАЧЕНИЕ(Справочник.ИсточникиФинансирования.ОМС)) КАК ПолисыОМСПациентов
		|		ПО (ДанныеПациентов.Пациент = ПолисыОМСПациентов.Пациент)
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ФИОФизическихЛиц.СрезПоследних КАК РегистрСведенийФИОФизическихЛиц
		|		ПО (ИсполнителиМД.Исполнитель.ФизЛицо = РегистрСведенийФИОФизическихЛиц.ФизическоеЛицо)
		|ГДЕ
		|	&__УсловиеНаУид__"
	;
	УсловиеНаУид = УправлениеЗаказамиУслугами.СформироватьУсловиеДляСпискаУид(
							МассивОбъектов, 
							"ЛекарственныеНазначенияМД", 
							"УникальныйИдентификаторНазначения", 
							Запрос
						);
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "&__УсловиеНаУид__", "(" + УсловиеНаУид + ")");
	Запрос.УстановитьПараметр("ДатаСреза", ТекущаяДатаСеанса());
	Выборка = Запрос.Выполнить().Выбрать();
	
	Организация = НастройкиПользователей.ПолучитьНастройку("ОсновнаяОрганизация");
	ОГРН = ?(ЗначениеЗаполнено(Организация), Организация.ОГРН, "");
	
	ПараметрыМакета1 = Новый Структура;
	ДатаВыписки_ = ТекущаяДатаСеанса();
	ЗаполнитьПараметрыМакетаИзСтроки(ПараметрыМакета1, "ОГРН", ОГРН);
	ЗаполнитьПараметрыМакетаИзДаты(ПараметрыМакета1, "ДатаВыписки", ДатаВыписки_);
	
	Если НазваниеМакета = "ПФ_MXL_Форма_148_1_у_04_л" Тогда
		ПараметрыМакета1.Вставить("ДатаВыпискиГод", Формат(ДатаВыписки_, "ДФ=yyyy"));
	КонецЕсли; 
	
	ПредПациент = Неопределено;
	ПараметрыМакета2 = Новый Структура;
	Пока Выборка.Следующий() Цикл
		
		НомерСтрокиНачало = ТабДок.ВысотаТаблицы + 1;
			
		Макет.Параметры.Заполнить(Выборка);
		Макет.Параметры.Заполнить(ПараметрыМакета1);
		
		Если ПредПациент <> Выборка.Пациент Тогда
			ПараметрыМакета2.Очистить();
			ЗаполнитьПараметрыМакетаИзСтроки(ПараметрыМакета2, "СНИЛС", Выборка.СтраховойНомерПФР);
			ЗаполнитьПараметрыМакетаИзСтроки(ПараметрыМакета2, "ПолисОМС",  Выборка.НомерПолиса);
			ЗаполнитьПараметрыМакетаИзДаты(ПараметрыМакета2, "ДатаРождения", Выборка.ДатаРождения);
			ЗаполнитьПараметрыМакетаИзСтроки(ПараметрыМакета2, "МКБ", Выборка.МКБ10);
			ПредПациент = Выборка.Пациент;
		КонецЕсли;
		НаименованиеЛС = "" + Выборка.ТорговоеНаименование 
			+ ?(ЗначениеЗаполнено(Выборка.ТорговоеНаименование) 
					И ЗначениеЗаполнено(Выборка.ФормаВыпуска), ", " + Выборка.ФормаВыпуска, "");
		Rp = "" + ?(ПечатьПоТорговомуНаименованию,
					Выборка.ТорговоеНаименование,
					?(
						ЗначениеЗаполнено(Выборка.СМНН),
						Выборка.СМНН,
						?(ЗначениеЗаполнено(Выборка.ТорговоеНаименованиеЛат), СокрЛП(Выборка.ТорговоеНаименованиеЛат), "")
					)
				);
		Rp = ?(
				ЗначениеЗаполнено(Rp),
				Rp + " "
				+ ?(
					ЗначениеЗаполнено(Выборка.ФормаВыпуска),
					Выборка.ФормаВыпуска,
					""
				),
				""
			);
		ПараметрыМакета2.Вставить("Rp", Rp);
		ПараметрыМакета2.Вставить("НаименованиеЛС", НаименованиеЛС);
		НаименованиеПотребительскойЕдиницыИзмерения_ = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Выборка.СМНН, "НаименованиеПотребительскойЕдиницыИзмерения");
		Signa = "" + ?(
					ЗначениеЗаполнено(Выборка.КоличествоЕдиниц),
					Строка(Выборка.КоличествоЕдиниц)
					+ ?(
						ЗначениеЗаполнено(НаименованиеПотребительскойЕдиницыИзмерения_),
						" "+ НаименованиеПотребительскойЕдиницыИзмерения_,
						""
					),
					""
				)
				+ ?(ЗначениеЗаполнено(Выборка.СпособПрименения), " " + Выборка.СпособПрименения, "")
				+ ?(ЗначениеЗаполнено(Выборка.ГрафикНазначения), " " + Выборка.ГрафикНазначения, "");
		Signa = ?(
					ЗначениеЗаполнено(Signa),
					Signa + ?(
						ЗначениеЗаполнено(Выборка.ОбщееКоличествоДоз) И ЗначениеЗаполнено(Выборка.Дозировка),
						", " + Символы.ПС + "Общее кол-во:" + (Выборка.ОбщееКоличествоДоз*Выборка.Дозировка),
						""
					),
					""
				);;
		ПараметрыМакета2.Вставить("Signa", Signa);
		
		Макет.Параметры.Заполнить(ПараметрыМакета2);
					
		ТабДок.Вывести(Макет);
		ТабДок.ВывестиГоризонтальныйРазделительСтраниц();
		УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ТабДок, НомерСтрокиНачало, ОбъектыПечати, ВыбратьЗаполненное(Выборка.СМНН, Выборка.Номенклатура));
				
	КонецЦикла;	
	
	Возврат ТабДок;

КонецФункции

////
 // Функция: ЗаполнитьПараметрыМакетаИзСтроки
 //   Заполняет параметры. Данные для параметров будут извлечены из строки.
 //   
 // Параметры:
 //   ПараметрыМакета {Структура}
 //     Структура, в которую будут помещены параметры.
 //   ОбщееИмя {Строка}
 //     Имя добавляемых параметров, начинается с этой строки.
 //   СтрокаДанных {Строка}
 //     Каждый символ этой строки - значение параметра.
 //
 // Возврат: {Структура}
 //   Данные для заполнения печаной формы.
 ///
Процедура ЗаполнитьПараметрыМакетаИзСтроки(ПараметрыМакета, ОбщееИмя, СтрокаДанных) Экспорт
	Если Не ЗначениеЗаполнено(СтрокаДанных) Тогда
		Возврат;
	КонецЕсли;
	ИндексПоследнейЯчейки = СтрДлина(СтрокаДанных);
	Для Инд = 1 По ИндексПоследнейЯчейки Цикл
		ИмяПараметра = ОбщееИмя + Инд;
		ПараметрыМакета.Вставить(ИмяПараметра, ВРег(Сред(СтрокаДанных, Инд, 1)));
	КонецЦикла;
КонецПроцедуры

////
 // Функция: РазложитьДату
 //   Добавляет параметры в структуру. Данные для параметров будут извлечены из даты.
 //   
 // Параметры:
 //   пДанныеДляЗаполнения {Структура}
 //     Структура, в которую будут помещены параметры.
 //   пОбщееИмя {Строка}
 //     Имя добавляемых параметров, начинается с этой строки.
 //   пДата {Дата}
 //     Каждый символ даты - значение параметра.
 ///
Процедура ЗаполнитьПараметрыМакетаИзДаты(пДанныеДляЗаполнения, пОбщееИмя, пДата) Экспорт
	Если Не ЗначениеЗаполнено(пДата) Тогда
		Возврат;
	КонецЕсли;
	
	ДеньСтр = Формат(День(пДата), "ЧЦ=2; ЧВН=");
	МесСтр = Формат(Месяц(пДата), "ЧЦ=2; ЧВН=");
	ГодСтр = Формат(Год(пДата), "ЧЦ=4; ЧВН=; ЧГ=0");
	
	ЗаполнитьПараметрыМакетаИзСтроки(пДанныеДляЗаполнения, пОбщееИмя + "Д", ДеньСтр);
	ЗаполнитьПараметрыМакетаИзСтроки(пДанныеДляЗаполнения, пОбщееИмя + "М", МесСтр);
	ЗаполнитьПараметрыМакетаИзСтроки(пДанныеДляЗаполнения, пОбщееИмя + "Г", ГодСтр);
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// Конец печати рецептов формы 148-1/у-88, 107-1/у, 148-1/у-04 (л)
////
 // Процедура: ПолучитьКонтакты
 //   Получить контакты этого объекта.
 ///
Функция ПолучитьКонтакты(Ссылка) Экспорт
	Запрос_ = Новый Запрос(
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	ВыполненныеУслуги.Ссылка.Пациент КАК Пациент,
		|	СтатусыУслуг.Заказ.Основание КАК ОснованиеЗаказа
		|ИЗ
		|	Документ.МедицинскийДокумент.ВыполненныеУслуги КАК ВыполненныеУслуги
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.СтатусыУслуг КАК СтатусыУслуг
		|		ПО ВыполненныеУслуги.УникальныйИдентификаторУслуги = СтатусыУслуг.УникальныйИдентификаторУслуги
		|ГДЕ
		|	ВыполненныеУслуги.Ссылка = &Ссылка"
	);
	Запрос_.УстановитьПараметр("Ссылка", Ссылка);
	Результат_ = Запрос_.Выполнить();
	Выборка_ = Результат_.Выбрать();
	Контакты_ = Неопределено;
	Если Выборка_.Следующий() Тогда
		Если ЗначениеЗаполнено(Выборка_.Пациент) Тогда
			Контакты_ = Новый Массив;
			Контакты_.Добавить(Выборка_.Пациент);
		Иначе	
			Если ВзаимодействияКлиентСервер.ЯвляетсяВзаимодействием(Выборка_.ОснованиеЗаказа) Тогда
				МенеджерОбъекта_ = ОбщегоНазначения.МенеджерОбъектаПоСсылке(Выборка_.ОснованиеЗаказа);
				Контакты_ = МенеджерОбъекта_.ПолучитьКонтакты(Выборка_.ОснованиеЗаказа);
			КонецЕсли;	
		КонецЕсли;
	КонецЕсли;
	Возврат Контакты_;
КонецФункции

// Возвращает представление медицинского документа для вывода пользователю.
//
// Параметры:
//  МедицинскийДокумент  - ДокументСсылка.МедицинскийДокумент - ссылка на документ
//
// Возвращаемое значение:
//   Строка   - Представление медицинского документа
//
Функция ПредставлениеМедицинскогоДокумента(Знач Пациент, Знач МедицинскаяКарта)
	
	Если РегистрыСведений.ДанныеПациентов.ПолучитьИспользованиеИтогов() = Ложь Тогда
		Возврат "";
	КонецЕсли;
	
	Результат = "";
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	МедицинскиеКарты.НомерКартыПредставление КАК МедицинскаяКартаПредставление
		|ИЗ
		|	Справочник.МедицинскиеКарты КАК МедицинскиеКарты
		|ГДЕ
		|	МедицинскиеКарты.Ссылка = &МедицинскаяКарта
		|;
		|ВЫБРАТЬ
		|	ДанныеПациентовСрезПоследних.Фамилия КАК Фамилия,
		|	ДанныеПациентовСрезПоследних.Имя КАК Имя,
		|	ДанныеПациентовСрезПоследних.Отчество КАК Отчество
		|ИЗ
		|	РегистрСведений.ДанныеПациентов.СрезПоследних(, Пациент = &Пациент) КАК ДанныеПациентовСрезПоследних";
	Запрос.УстановитьПараметр("МедицинскаяКарта",	 МедицинскаяКарта);
	Запрос.УстановитьПараметр("Пациент",			 Пациент);
	
	РезультатЗапроса = Запрос.ВыполнитьПакет();
	
	ВыборкаМедицинскаяКарта = РезультатЗапроса[0].Выбрать();
	Выборка					= РезультатЗапроса[1].Выбрать();
	
	Пока Выборка.Следующий() Цикл
		Результат = Результат + Регистратура.ПолучитьФИОПациента(Выборка.Фамилия, Выборка.Имя, Выборка.Отчество, Истина);
	КонецЦикла;
	
	Пока ВыборкаМедицинскаяКарта.Следующий() Цикл
		Результат = Результат + ?(ПустаяСтрока(Результат),"", ", ") +ВыборкаМедицинскаяКарта.МедицинскаяКартаПредставление;
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

// Возвращает кодировку, полученную из двоичных данных файла, если
// файл содержит объявление XML.
//
// Параметры:
//  ДвоичныеДанные - Двоичные данные файла.
//
// Возвращаемое значение:
//  КодировкаXML - Строка - Кодировка файла. Если невозможно прочитать 
//                          объявление XML, возвращает пустую строку.
//
Функция КодировкаИзОбъявленияXML(ДвоичныеДанные)
	
	Результат_ = "";
	БуферДвоичныхДанных_ = ПолучитьБуферДвоичныхДанныхИзДвоичныхДанных(ДвоичныеДанные);
	ПотокВПамяти_ = Новый ПотокВПамяти(БуферДвоичныхДанных_);
	ЧтениеXML_ = Новый ЧтениеXML;
	ЧтениеXML_.ОткрытьПоток(ПотокВПамяти_);
	Попытка
		ЧтениеXML_.ПерейтиКСодержимому();
		Результат_ = ЧтениеXML_.КодировкаXML;
	Исключение
	КонецПопытки;
	ЧтениеXML_.Закрыть();
	ПотокВПамяти_.Закрыть();
	
	Возврат Результат_;
	
КонецФункции

Функция ПолучитьСписокСотрудниковОбязанныхПодписатьМД(МДСсылка)
	
	
	
	
	
	ВидыДолжностей_ = Новый Массив;
	ВидыДолжностей_.Добавить(Перечисления.ВидыМедицинскихДолжностей.Врачи);
	ВидыДолжностей_.Добавить(Перечисления.ВидыМедицинскихДолжностей.РуководителиСтруктурныхПодразделенийУчреждений);
	ВидыДолжностей_.Добавить(Перечисления.ВидыМедицинскихДолжностей.РуководителиУчреждений);
	
	Запрос_ = Новый Запрос;
	Запрос_.Текст =
		"ВЫБРАТЬ
		|	МедицинскийДокументИсполнители.Исполнитель КАК Исполнитель,
		|	МедицинскийДокументИсполнители.Должность КАК Должность
		|ИЗ
		|	Документ.МедицинскийДокумент.Исполнители КАК МедицинскийДокументИсполнители
		|ГДЕ
		|	МедицинскийДокументИсполнители.Ссылка = &Ссылка
		|	И (МедицинскийДокументИсполнители.Должность.ВидМедицинскойДолжности В (&ВидыДолжностей)
		|			ИЛИ МедицинскийДокументИсполнители.ОтветственныйЗаНазначение)";
		
	Запрос_.УстановитьПараметр("Ссылка", МДСсылка);
	Запрос_.УстановитьПараметр("ВидыДолжностей", ВидыДолжностей_);
	
	Результат_ = Запрос_.Выполнить().Выгрузить().ВыгрузитьКолонку("Исполнитель");
	
	Возврат Результат_;
КонецФункции

Функция ДокументИмеетЭЦП(МедицинскийДокумент)

	Если ТипЗнч(МедицинскийДокумент) = Тип("ДокументОбъект.МедицинскийДокумент") Тогда
		
		ПодписанныеCDA_ = МедицинскийДокумент.CDAДокументы.НайтиСтроки(
			Новый Структура("ПодписанЭП,Актуально", Истина, Истина)
		);
		
		Возврат ПодписанныеCDA_.Количество() > 0;
	Иначе
		Запрос_ = Новый Запрос;
		Запрос_.Текст =
			"ВЫБРАТЬ ПЕРВЫЕ 1
			|	МедицинскийДокументCDAДокументы.Ссылка КАК Ссылка
			|ИЗ
			|	Документ.МедицинскийДокумент.CDAДокументы КАК МедицинскийДокументCDAДокументы
			|ГДЕ
			|	МедицинскийДокументCDAДокументы.ПодписанЭП
			|	И МедицинскийДокументCDAДокументы.Актуально
			|	И МедицинскийДокументCDAДокументы.Ссылка = &Ссылка";
			
		Запрос_.УстановитьПараметр("Ссылка", МедицинскийДокумент);
		Выборка_ = Запрос_.Выполнить().Выбрать();
		
		Возврат Выборка_.Следующий();
	КонецЕсли;
КонецФункции

Процедура ДобавитьПоказателиЗдоровьяВСтруктурированныеДанныеДокумента(ДокументСсылка, СтруктурированныеДанныеДокумента)
	
	МедицинаРегион.СтруктурированныеДанныеПоказателейЗдоровья(ДокументСсылка, СтруктурированныеДанныеДокумента);

КонецПроцедуры

Функция ЭтоНаправлениеВоВспомогательныеКабинеты(ТипМД)
	Возврат ЗначениеЗаполнено(ТипМД) И ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ТипМД, "КодМинздрава") = "57";
КонецФункции

 // Формирует табличный документ направления на исследования для список услуг.
 //
 // Параметры:
 //  УслугиДляПечати  - Массив - Список СправочникСсылка.УслугиПациентов
 //  РежимПечатиМногоНаправлений  - Булево - Истина, если требуется печатать каждую услугу в отдельном направлении, иначе Ложь.
 //  УчитыватьИерархию  - Булево - Истина, если требуется группировать услуги по иерархии номенклатуры.
 //
 // Возвращаемое значение:
 //   ТабличныйДокумент   - Сформированный табличный документ.
 //
Функция ТабличныйДокументНаправленияНаИсследования(УслугиДляПечати, РежимПечатиМногоНаправлений, УчитыватьИерархию)
	
	
	ТабличныйДокумент = Новый ТабличныйДокумент;
	ТабличныйДокумент.ИмяПараметровПечати = "ПараметрыПечати_НаправлениеНаИсследование";
	
	Если УслугиДляПечати.Количество() = 0 Тогда
		Возврат ТабличныйДокумент;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	УсловиеНаУид = УправлениеЗаказамиУслугами.СформироватьУсловиеДляСпискаУид(
		УслугиДляПечати,
		"СтатусыУслуг",
		"УникальныйИдентификаторУслугиСсылка",
		Запрос
	);
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	СтатусыУслуг.Номенклатура КАК Номенклатура,
		|	СтатусыУслуг.УникальныйИдентификаторУслугиСсылка КАК УникальныйИдентификаторУслугиСсылка,
		|	СтатусыУслуг.УникальныйИдентификаторУслуги КАК УникальныйИдентификаторУслуги,
		|	СтатусыУслуг.ЦИТО КАК ЦИТО,
		|	ЕСТЬNULL(СтатусыУслуг.Заказ.Дата, СтатусыУслуг.Назначение.Дата) КАК ДатаЗаказа,
		|	СтатусыУслуг.Заказ КАК Заказ,
		|	ЕСТЬNULL(СтатусыУслуг.Заказ.Ответственный, СтатусыУслуг.Назначение.Ответственный) КАК ЗаказОтветственный,
		|	СтатусыУслуг.ЗапланированноеВремя КАК ЗапланированноеВремя,
		|	СтатусыУслуг.Назначение КАК Назначение,
		|	СтатусыУслуг.Соглашение КАК Соглашение,
		|	СтатусыУслуг.МедицинскаяКарта КАК МедКарта,
		|	СтатусыУслуг.Пациент КАК Пациент
		|ПОМЕСТИТЬ ВыбранныеУслуги
		|ИЗ
		|	РегистрСведений.СтатусыУслуг КАК СтатусыУслуг
		|ГДЕ
		|	&__УсловиеНаУид__
		|	И СтатусыУслуг.СтатусУслуги <> ЗНАЧЕНИЕ(Перечисление.СтатусыУслуг.Отменена)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВыбранныеУслуги.Назначение КАК Назначение,
		|	МИНИМУМ(СтатусыУслуг.Соглашение) КАК Соглашение
		|ПОМЕСТИТЬ СоглашенияНазначений
		|ИЗ
		|	ВыбранныеУслуги КАК ВыбранныеУслуги
		|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.МедицинскийДокумент.ВыполненныеУслуги КАК ВыполненныеУслуги
		|		ПО ВыбранныеУслуги.Назначение = ВыполненныеУслуги.Ссылка
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СтатусыУслуг КАК СтатусыУслуг
		|		ПО (ВыполненныеУслуги.УникальныйИдентификаторУслугиСсылка = СтатусыУслуг.УникальныйИдентификаторУслугиСсылка)
		|
		|СГРУППИРОВАТЬ ПО
		|	ВыбранныеУслуги.Назначение
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВыбранныеУслуги.УникальныйИдентификаторУслугиСсылка КАК Услуга,
		|	ВыбранныеУслуги.УникальныйИдентификаторУслуги КАК УникальныйИдентификаторУслуги,
		|	ВыбранныеУслуги.ДатаЗаказа КАК ДатаЗаказа,
		|	ВыбранныеУслуги.Заказ КАК Заказ,
		|	ВыбранныеУслуги.Назначение КАК Назначение,
		|	ВыбранныеУслуги.ЗаказОтветственный КАК ЗаказОтветственный,
		|	ВыбранныеУслуги.ЗапланированноеВремя КАК ЗапланированноеВремя,
		|	ВыбранныеУслуги.Номенклатура.Артикул КАК АртикулНоменклатуры,
		|	ВыбранныеУслуги.Номенклатура КАК Номенклатура,
		|	ВыбранныеУслуги.Номенклатура.Наименование КАК НоменклатураНаименование,
		|	ВыбранныеУслуги.ЦИТО КАК ЦИТО,
		|	ВЫБОР
		|		КОГДА ВыбранныеУслуги.Номенклатура.НаименованиеПолное = """"
		|			ТОГДА ВыбранныеУслуги.Номенклатура.Наименование
		|		ИНАЧЕ ВыбранныеУслуги.Номенклатура.НаименованиеПолное
		|	КОНЕЦ КАК НоменклатураНаименованиеПолное,
		|	ВЫБОР
		|		КОГДА ВыбранныеУслуги.Соглашение = ЗНАЧЕНИЕ(Справочник.СоглашенияСКлиентами.ПустаяСсылка)
		|			ТОГДА СоглашенияНазначений.Соглашение.ИсточникФинансирования
		|		ИНАЧЕ ВыбранныеУслуги.Соглашение.ИсточникФинансирования
		|	КОНЕЦ КАК ИсточникФинансирования,
		|	ВЫБОР
		|		КОГДА ВыбранныеУслуги.Соглашение = ЗНАЧЕНИЕ(Справочник.СоглашенияСКлиентами.ПустаяСсылка)
		|			ТОГДА СоглашенияНазначений.Соглашение
		|		ИНАЧЕ ВыбранныеУслуги.Соглашение
		|	КОНЕЦ КАК Соглашение,
		|	СменныеЗадания.МедицинскоеРабочееМесто КАК РабочееМесто,
		|	СправочникМедицинскиеРабочиеМеста.ВариантВремениПланирования КАК ВариантВремениПланирования,
		|	ВЫБОР КОГДА
		|			СправочникМедицинскиеРабочиеМеста.ВариантВремениПланирования = ЗНАЧЕНИЕ(Перечисление.ВариантыВремениПланирования.НаДату)
		|		ТОГДА ИСТИНА
		|		ИНАЧЕ ЛОЖЬ
		|	КОНЕЦ КАК ВариантВремениПланированияНаДату,
		|	ЕСТЬNULL(МедицинскийДокументИсполнители.Исполнитель, ДокументЗаказПациента.НаправившийВрач) КАК НаправившийСотрудник,
		|	ВЫБОР
		|		КОГДА НЕ МедицинскийДокументИсполнители.Исполнитель.Подразделение.Владелец ЕСТЬ NULL
		|			ТОГДА МедицинскийДокументИсполнители.Исполнитель.Подразделение.Владелец
		|		КОГДА ДокументЗаказПациента.НаправившаяОрганизация <> НЕОПРЕДЕЛЕНО
		|			ТОГДА ДокументЗаказПациента.НаправившаяОрганизация
		|		КОГДА МедицинскийДокументИсполнители.Ссылка.Организация <> ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)
		|			ТОГДА МедицинскийДокументИсполнители.Ссылка.Организация
		|		КОГДА ДокументЗаказПациента.Организация <> ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)
		|			ТОГДА ДокументЗаказПациента.Организация
		|		ИНАЧЕ &ОрганизацияПоУмолчанию
		|	КОНЕЦ КАК НаправившаяОрганизация,
		|	ВыбранныеУслуги.МедКарта КАК МедицинскаяКарта,
		|	ВыбранныеУслуги.Пациент КАК Пациент,
		|	ДанныеПациентов.Фамилия КАК Фамилия,
		|	ДанныеПациентов.Имя КАК Имя,
		|	ДанныеПациентов.Отчество КАК Отчество,
		|	ДанныеПациентов.ДатаРождения КАК ДатаРождения,
		|	Комментарии.Комментарий КАК Комментарий,
		|	#УсловиеВыборка# КАК Отделение
		|ИЗ
		|	ВыбранныеУслуги КАК ВыбранныеУслуги
		|		ЛЕВОЕ СОЕДИНЕНИЕ СоглашенияНазначений КАК СоглашенияНазначений
		|		ПО ВыбранныеУслуги.Назначение = СоглашенияНазначений.Назначение
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СменныеЗадания КАК СменныеЗадания
		|		ПО ВыбранныеУслуги.УникальныйИдентификаторУслугиСсылка = СменныеЗадания.УникальныйИдентификаторУслугиСсылка
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.МедицинскиеРабочиеМеста КАК СправочникМедицинскиеРабочиеМеста
		|		ПО СменныеЗадания.МедицинскоеРабочееМесто = СправочникМедицинскиеРабочиеМеста.Ссылка
		|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.МедицинскийДокумент.Исполнители КАК МедицинскийДокументИсполнители
		|		ПО ВыбранныеУслуги.Назначение = МедицинскийДокументИсполнители.Ссылка
		|			И (МедицинскийДокументИсполнители.ОтветственныйЗаНазначение)
		|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.МедицинскийДокумент.НазначенныеУслуги КАК МДНазначенныеУслуги
		|		ПО ВыбранныеУслуги.Назначение = МДНазначенныеУслуги.Ссылка
		|			И ВыбранныеУслуги.УникальныйИдентификаторУслуги = МДНазначенныеУслуги.УникальныйИдентификаторУслуги
		|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ЗаказПациента КАК ДокументЗаказПациента
		|		ПО ВыбранныеУслуги.Заказ = ДокументЗаказПациента.Ссылка
		|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ЗаказПациента.МедицинскиеУслуги КАК ЗаказМедицинскиеУслуги
		|		ПО ВыбранныеУслуги.Заказ = ЗаказМедицинскиеУслуги.Ссылка
		|			И ВыбранныеУслуги.УникальныйИдентификаторУслуги = ЗаказМедицинскиеУслуги.УникальныйИдентификаторУслуги
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ДанныеПациентов.СрезПоследних КАК ДанныеПациентов
		|		ПО (ДанныеПациентов.Пациент = ВыбранныеУслуги.Пациент)
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.Комментарии.СрезПоследних КАК Комментарии
		|		ПО ВыбранныеУслуги.УникальныйИдентификаторУслуги = Комментарии.УидОбъекта
		|		#УсловиеСоединение#
		|УПОРЯДОЧИТЬ ПО
		|	ЕСТЬNULL(ЗаказМедицинскиеУслуги.НомерСтроки, МДНазначенныеУслуги.НомерСтроки)
		|	&_УсловиеУпорядочивания_"
	;
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "&__УсловиеНаУид__", "(" + УсловиеНаУид + ")");
	УсловиеВыборка = """""";
	УсловиеДокумент = "";
	Если Метаданные.Документы.Найти("Госпитализация")<> Неопределено Тогда 
		УсловиеВыборка = "Госпитализация.ПодразделениеПриемник";
		УсловиеДокумент = "ЛЕВОЕ СОЕДИНЕНИЕ Документ.Госпитализация КАК Госпитализация
						 |		ПО (Госпитализация.МедицинскаяКарта = ВыбранныеУслуги.МедКарта)
						 |			И (Госпитализация.Проведен)";
						 
		
	КонецЕсли;
	
	УсловиеУпорядочивания = "";
	Если УчитыватьИерархию И РежимПечатиМногоНаправлений Тогда
		УсловиеУпорядочивания = "ИТОГИ ПО
								|Номенклатура ТОЛЬКО ИЕРАРХИЯ";
	КонецЕсли;
	
	Запрос.Текст = СтрЗаменить(Запрос.Текст,"#УсловиеВыборка#", УсловиеВыборка);
	Запрос.Текст = СтрЗаменить(Запрос.Текст,"#УсловиеСоединение#", УсловиеДокумент);
	Запрос.Текст = СтрЗаменить(Запрос.Текст,"&_УсловиеУпорядочивания_", УсловиеУпорядочивания);

	ОрганизацияПоУмолчанию_ = Справочники.Организации.ОрганизацияПоУмолчанию();
	Запрос.УстановитьПараметр("ОрганизацияПоУмолчанию", ОрганизацияПоУмолчанию_);
	
	РезультатЗапроса = Запрос.Выполнить();
	Выборка = РезультатЗапроса.Выбрать();
	
	Если Выборка.Следующий() Тогда
		Если Выборка.Номенклатура.ЭтоГруппа Тогда
			Пока Выборка.Следующий() Цикл
				Если Не Выборка.Номенклатура.ЭтоГруппа Тогда
					Прервать;
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
		Макет = УправлениеПечатью.МакетПечатнойФормы("Документ.МедицинскийДокумент.ПФ_MXL_НаправлениеНаИсследование");
		
		ОбластьШапка = Макет.ПолучитьОбласть("Шапка");
		ОбластьШапка.Параметры.Заполнить(Выборка);
		
		// Полис ОМС
		Если ЗначениеЗаполнено(Выборка.Пациент) Тогда
			ПолисыПациента_ = Справочники.МедицинскиеСтраховыеПолисы.ПолисыПациента(Выборка.Пациент);
			Для Каждого строкаТЗ_ Из ПолисыПациента_ Цикл
				Если строкаТЗ_.ЭтоОМС Тогда
					ДанныеПолиса_ = Новый Структура("СерияПолиса,НомерПолиса", строкаТЗ_.СерияПолиса, строкаТЗ_.НомерПолиса);
					ОбластьШапка.Параметры.Заполнить(ДанныеПолиса_);
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;

		// Получаем номер карты 
		ДанныеКарты = Регистратура.ПолучитьДанныеКарты(
			Выборка.Пациент,
			Выборка.МедицинскаяКарта,
			ТекущаяДатаСеанса()
		);
		Парам = Новый Структура("НомерМедицинскойКарты,ДатаРождения");
		Если ДанныеКарты.Количество() > 0 Тогда
			Эл = ДанныеКарты[0];
			Парам.НомерМедицинскойКарты =  Эл.ПолныйНомерКарты
				+ " от " + Формат(Эл.Дата, "ДФ=дд.ММ.гггг")
				+ ", " + Строка(Эл.ТипМедицинскойКарты)
			;
		КонецЕсли;
		Парам.ДатаРождения = Формат(Выборка.ДатаРождения, "ДФ=dd.MM.yyyy");
		ОбластьШапка.Параметры.Заполнить(Парам);
		
		Парам = Новый Структура("Адрес,Телефон");
		ДанныеПациента_ = Регистратура.ПолучитьДанныеПациента(Выборка.Пациент,, Новый Структура("Адрес,Контакты"));
		
		Если ДанныеПациента_.Адрес.Количество() > 0 Тогда
			Парам.Адрес = ДанныеПациента_.Адрес[0].Представление;
		КонецЕсли;
		
		Если ДанныеПациента_.Контакты.Количество() > 0 Тогда
			Телефоны_ = ДанныеПациента_.Контакты.НайтиСтроки(Новый Структура("Тип, Основной", Перечисления.ТипыКонтактнойИнформации.Телефон, Истина));
			Если Телефоны_.Количество() > 0 Тогда
				Парам.Телефон = Телефоны_[0].Представление;
			Иначе
				Телефоны_ = ДанныеПациента_.Контакты.НайтиСтроки(Новый Структура("Тип, Вид", Перечисления.ТипыКонтактнойИнформации.Телефон, Справочники.ВидыКонтактнойИнформации.МобТелефонПациента));
				Если Телефоны_.Количество() > 0 Тогда
					Парам.Телефон = Телефоны_[0].Представление;
				Иначе
					Телефоны_ = ДанныеПациента_.Контакты.НайтиСтроки(Новый Структура("Тип", Перечисления.ТипыКонтактнойИнформации.Телефон));
					Если Телефоны_.Количество() > 0 Тогда
						Парам.Телефон = Телефоны_[0].Представление;
					КонецЕсли;
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;

		ОбластьШапка.Параметры.Заполнить(Парам);
		
		Если ОбщиеМеханизмыПовтИсп.ИспользоватьСтационарныеУсловия()
			И Регистратура.ЭтоСтационарнаяКарта(Выборка.МедицинскаяКарта)
		Тогда
			МодульСтационар_ = ОбщегоНазначения.ОбщийМодуль("ОбщегоНазначенияСтационар");
			Парам = Новый Структура("Отделение", МодульСтационар_.ПолучитьОтделениеПациента(Выборка.Пациент, ,Выборка.МедицинскаяКарта));
			ОбластьШапка.Параметры.Заполнить(Парам);
		Иначе
			Сотрудник_ = РегистрыСведений.СотрудникиПользователя.ПолучитьСотрудникаПользователя(Выборка.ЗаказОтветственный);
			Парам = Новый Структура("Отделение", ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Сотрудник_, "Подразделение"));
			ОбластьШапка.Параметры.Заполнить(Парам);
		КонецЕсли;
		
		Если ОбластьШапка.Области.Найти("КартинкаШтрихкода") <> Неопределено Тогда
			ОбластьШтрихкода_ = ОбластьШапка.Область("КартинкаШтрихкода");
			ОбластьШтрихкода_.РазмерКартинки = РазмерКартинки.РеальныйРазмер;
			ОбластьШтрихкода_.Картинка = ПолучитьШтрихкод(стрЗаменить(Строка(Выборка.УникальныйИдентификаторУслуги),"-",""), 0, 45);
		КонецЕсли;

		ТабличныйДокумент.Вывести(ОбластьШапка); 
		
		ОбластьШапкаТаблицы = Макет.ПолучитьОбласть("ШапкаТаблицы");
		ТабличныйДокумент.Вывести(ОбластьШапкаТаблицы);
		ОбластьСтрока = Макет.ПолучитьОбласть("Строка");
		ОбластьРазделитель_ = Макет.ПолучитьОбласть("Разделитель");

		Первый = Истина;
		НомерУслуги = 0;
		Если РежимПечатиМногоНаправлений Тогда
			ТабДокВременный_ = Новый ТабличныйДокумент;
			Родитель_ = Выборка.Номенклатура.Родитель;
			Пока Первый ИЛИ Выборка.Следующий() Цикл
				Если УчитыватьИерархию Тогда
					Если Выборка.Номенклатура.ЭтоГруппа Тогда
						Продолжить;
					КонецЕсли;
					Если Родитель_ = Выборка.Номенклатура.Родитель 
						И (Выборка.Номенклатура.ХарактерМедицинскойУслуги = ПредопределенноеЗначение("Перечисление.ХарактерМедицинскихУслуг.ЦИТО") 
						ИЛИ Выборка.Номенклатура.ХарактерМедицинскойУслуги = ПредопределенноеЗначение("Перечисление.ХарактерМедицинскихУслуг.Исследование") 
						ИЛИ Первый) Тогда
						Первый = Истина;
					Иначе
						Первый = Ложь;
						Родитель_ = Выборка.Номенклатура.Родитель;

						ТабДокВременный_.Вывести(ОбластьРазделитель_);
						Если Не ТабличныйДокумент.ПроверитьВывод(ТабДокВременный_) Тогда
							ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
						КонецЕсли;
						ТабличныйДокумент.Вывести(ТабДокВременный_);
						ТабДокВременный_.Очистить();
						НомерУслуги = 0;
					КонецЕсли;
				КонецЕсли;
				
				Если Не Первый Тогда
					//ТабличныйДокумент.Вывести(ОбластьРазделитель_);
					ОбластьШапка.Параметры.ИсточникФинансирования = Выборка.ИсточникФинансирования;
					ОбластьШапка.Параметры.Соглашение = Выборка.Соглашение;
					
					Если ОбластьШапка.Области.Найти("КартинкаШтрихкода") <> Неопределено Тогда
						ОбластьШтрихкода_ = ОбластьШапка.Область("КартинкаШтрихкода");
						ОбластьШтрихкода_.РазмерКартинки = РазмерКартинки.РеальныйРазмер;
						ОбластьШтрихкода_.Картинка = ПолучитьШтрихкод(стрЗаменить(Строка(Выборка.УникальныйИдентификаторУслуги),"-",""), 0, 45);
					КонецЕсли;

					ТабДокВременный_.Вывести(ОбластьШапка);
					ТабДокВременный_.Вывести(ОбластьШапкаТаблицы);
				Иначе
					Первый = ЛОЖЬ;
				КонецЕсли;
				ОбластьСтрока.Параметры.Заполнить(Выборка);
				Если Выборка.ВариантВремениПланированияНаДату = ИСТИНА Тогда
					ОбластьСтрока.Параметры.ЗапланированноеВремя = Формат(Выборка.ЗапланированноеВремя, "ДФ=дд.ММ.гггг");
				Иначе
					ОбластьСтрока.Параметры.ЗапланированноеВремя = Выборка.ЗапланированноеВремя;
				КонецЕсли;
				ТабДокВременный_.Вывести(ОбластьСтрока);
				НомерУслуги = НомерУслуги + 1;
				Если Не УчитыватьИерархию Тогда
					ТабДокВременный_.Вывести(ОбластьРазделитель_);
					Если Не ТабличныйДокумент.ПроверитьВывод(ТабДокВременный_) Тогда
						ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
					КонецЕсли;
					ТабличныйДокумент.Вывести(ТабДокВременный_);
					ТабДокВременный_.Очистить();
				КонецЕсли;
			КонецЦикла;
			Если Не ТабличныйДокумент.ПроверитьВывод(ТабДокВременный_) Тогда
				ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
			КонецЕсли;
			ТабличныйДокумент.Вывести(ТабДокВременный_);
			ТабДокВременный_.Очистить();
		Иначе
			Пока Первый Или Выборка.Следующий() Цикл
				
				Первый = Ложь;
				ОбластьСтрока.Параметры.Заполнить(Выборка);
				Если Выборка.ВариантВремениПланированияНаДату = ИСТИНА Тогда
					ОбластьСтрока.Параметры.ЗапланированноеВремя = Формат(Выборка.ЗапланированноеВремя, "ДФ=дд.ММ.гггг");
				Иначе
					ОбластьСтрока.Параметры.ЗапланированноеВремя = Выборка.ЗапланированноеВремя;
				КонецЕсли;
				ТабличныйДокумент.Вывести(ОбластьСтрока);
			КонецЦикла;
		КонецЕсли;
		
	КонецЕсли;
		
	Возврат ТабличныйДокумент;
	
КонецФункции

#КонецОбласти

#Область ОбработчикиСобытий

Процедура ОбработкаПолученияПредставления(Данные, Представление, СтандартнаяОбработка) 
	Если ОбщегоНазначения.ДоступноИспользованиеРазделенныхДанных() Тогда
		Представление = ПредставлениеМедицинскогоДокумента(Данные.Пациент, Данные.МедицинскаяКарта);
		Если Не ПустаяСтрока(Представление) Тогда
			Представление = Представление + стрШаблон(", %1 от %2", СокрЛП(Данные.Номер), Данные.Дата);
			СтандартнаяОбработка = Ложь;
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

Процедура ОбработкаПолученияПолейПредставления(Поля, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	Поля.Добавить("Дата");
	Поля.Добавить("Пациент");
	Поля.Добавить("МедицинскаяКарта");
	Поля.Добавить("Номер");
КонецПроцедуры

#КонецОбласти

#КонецЕсли